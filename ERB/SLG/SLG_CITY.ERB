;-------------------------------------------------
;도시 ARG:0의 정보를 표시
;-------------------------------------------------
@SHOW_CITY_INFO(ARG:0, ARG:1 = 0)
#DIM SPACE
#DIM 행수
#DIM SHOW_UNITS
#DIM UNIT_PAIR,10,2
#DIM OWNER
#DIM CNT_ENEMY
#DIM CNT_UNIT
#DIM TARGET_NUM
SHOW_UNITS = 0

;영토 할양으로부터 불렸을 경우의 행 번호 넘치고를 퇴피하면서 개시 버튼을 표시
IF ARG:1 == 0 && LINECOUNT - FIRST_COLUMN_RIGHT_LINE_SLG < LINES_SHOP_DRAW
	CALL COLUMN_LEFT_MENU_SHOW_SLG(LINECOUNT - FIRST_COLUMN_RIGHT_LINE_SLG)
	;SLG의 오른쪽 컬럼은 아니면 단락선과 개행도 오프로 한다
	CALL COLUMN_RIGHT_LINE
	CALL COLUMN_RIGHT_PRINTL_SLG
ENDIF


;지정된 도시 또는 중계지점이 존재하지 않는 경우
IF GET_RELAYNAME(ARG:0) == "무명"
	PRINT 자세력의 도시, 자세력과 인접하는 도시의 정보를 볼 수가 있습니다
	;이하 빈행
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_CLEAR_SLG
	ELSE
		PRINTL
	ENDIF
	RETURN
ENDIF

;방위 정보가 참조 가능한가 어떤가의 플래그(자세력의 도시, 또는 자세력과 인접하는 도시만 가능. 관전 모드라면 모두 가능)
LOCAL:4 = CFLAG:MASTER:소속 >= 1 && (CITY_OWNER:(ARG:0) == CFLAG:MASTER:소속 || IS_NEIGHBORING_COUNTRY(ARG:0, CFLAG:MASTER:소속) >= 0)
LOCAL:4 = LOCAL:4 || FLAG:관전모드

;도시상의 부대를 10 부대까지 취득
CNT_ENEMY = 0
CNT_UNIT  = 0
OWNER = CITY_OWNER:(ARG:0)
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	FOR LOCAL:1, 0, MAX_UNIT
		IF UNIT_SOLDIER:(LOCAL:0):(LOCAL:1) > 0 && UNIT_POSITION:(LOCAL:0):(LOCAL:1) == ARG:0
			IF CNT_UNIT < 10
				UNIT_PAIR:CNT_UNIT:0 = LOCAL:0
				UNIT_PAIR:CNT_UNIT:1 = LOCAL:1
			ENDIF
			CNT_UNIT++
			SIF OWNER != LOCAL:0
				CNT_ENEMY++
		ENDIF
	NEXT
NEXT

TARGET_NUM = -1
;도시를 목적으로 하는 부대를 취득
FOR LOCAL, 0, MAX_UNIT
	IF UNIT_TARGET:(CFLAG:MASTER:소속):LOCAL == ARG:0
		TARGET_NUM = LOCAL
		BREAK
	ENDIF
NEXT

;중계지점
IF CITY_TYPE:(ARG:0) == 1
	LOCALS:0 = ◆중계지점(%GET_RELAYNAME(ARG:0)%)◆
	PRINTFORM %LOCALS:0%
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF
;도시
ELSE
	LOCAL:6 = CITY_OWNER:(ARG:0)
	LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
	LOCALS:0 = ◆%GET_CITYNAME(ARG:0)%◆
	PRINTFORM %LOCALS:0%
	SPACE = 40
	PRINTFORM %TOSTR_SPACE((SPACE - STRLENS(LOCALS:0)) / 2 * 2)%
	;CALL SPACER( MAX( 0, 23 - STRLENS(LOCALS:0) ) )
	IF LOCAL:7 >= 0
		PRINT 소속:
		SETCOLOR COUNTRY_COLOR:(LOCAL:6)
		PRINTFORM %SNAME(LOCAL:7)%
		PRINTFORM  
		SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:소속, LOCAL:6)
			CASE 0
				SETCOLOR 칼라_경고
				PRINT 적
			CASE 1
				SETCOLOR 칼라_정전
				PRINT 정
			CASE 2
				SETCOLOR 칼라_연합
				PRINT 련
			CASE 3
				SETCOLOR 칼라_동맹
				PRINT 동
			CASE 4
				SETCOLOR 칼라_영구동맹
				PRINT 영
			CASE 5
				SETCOLOR 칼라_시안
				PRINT 자
		ENDSELECT
		PRINT  
		RESETCOLOR
		PRINTFORM 도시수:{GET_OWN_CITY(LOCAL:6)}
	ELSE
		PRINT 소속:
		SETCOLOR COUNTRY_COLOR:0
		LOCALS:0 = ----
		PRINTFORM %LOCALS:0%
		LOCALS:1 = 
		RESETCOLOR
	ENDIF
	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	IF !LOCAL:4 || LOCAL:6 == -1
		PRINTFORM 공:? 방:? 지:? 요:?
	ELSE
		CALL GET_CITY_COMMANDER_ALL(ARG:0)
		CALL SET_BATTLE_MIRROR_VAL(0, RESULT:0, RESULT:1, RESULT:2)
		PRINTFORM 공:
		CALL PRINT_ALPHABET_RANK(랭크_부대, ATTACK_LEVEL(0, ARG:0))
		PRINTFORM  방:
		CALL PRINT_ALPHABET_RANK(랭크_부대, DEFENSE_LEVEL(0, ARG:0))
		PRINTFORM  지:
		CALL PRINT_ALPHABET_RANK(랭크_부대, INTELLIGENCE_LEVEL(0, ARG:0))
		PRINTFORM  요:
		CALL PRINT_ALPHABET_RANK(랭크_부대, MAGIC_LEVEL(0, ARG:0))
	ENDIF

	PRINTFORM %TOSTR_SPACE(1)%

	IF LOCAL:4
		LOCALS:0 = 병 수: %NUM_FORMAT(CITY_SOLDIER:(ARG:0)), 14, LEFT%
	ELSE
		LOCALS:0 = 병 수:????
	ENDIF
	SPACE = 20
	PRINTPLAINFORM %LOCALS:0%
	PRINTFORM %TOSTR_SPACE(SPACE - STRLENS(LOCALS:0))%
	IF LOCAL:4
		LOCALS:0 = %GET_CITY_COMMANDER_NAME(ARG:0)%
		IF LOCALS:0 == ""
			LOCALS:0 = ----
		ENDIF
		LOCALS:0 = 수장:%LOCALS:0%
	ELSE
		LOCALS:0 = 수장:????
	ENDIF
	PRINTPLAINFORM %LOCALS:0%

	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	LOCAL:8 = CITY_ECONOMY:(ARG:0) / 5000 + 100
	LOCALS:0 = 경제:{CITY_ECONOMY:(ARG:0) / 100, 4}/{CITY_ECONOMY_LIMIT:(ARG:0) / 100, 4}
	SPACE = 20
	PRINTFORM %LOCALS:0%
	PRINTFORM %TOSTR_SPACE(SPACE - STRLENS(LOCALS:0))%
	PRINTFORM 방위: %DECIMAL_STRING(CITY_GUARD:(ARG:0), 2)%(도시)
	PRINTFORM ×%DECIMAL_STRING(LOCAL:8, 2)%(경제)
	PRINT ×
	IF FLAG:관전모드 || LOCAL:6 == CFLAG:MASTER:소속
		SELECTCASE COUNTRY_POLICY:(CFLAG:MASTER:소속)
			CASE 정책_방위
				PRINT 1.50
				LOCAL:9 = 15
			CASEELSE
				PRINT 1.00
				LOCAL:9 = 10
		ENDSELECT
		PRINTFORM (정책)=%DECIMAL_STRING(CITY_GUARD:(ARG:0) * LOCAL:8 * LOCAL:9 / 1000, 2)%
	ELSEIF LOCAL:7 < 0
		PRINTFORM 1.00(정책)=%DECIMAL_STRING(CITY_GUARD:(ARG:0) * LOCAL:8 / 100, 2)%
	ELSE
		PRINTFORM ???? (정책)=????
	ENDIF

	IF ARG:1 == 0
		CALL COLUMN_RIGHT_PRINTL_SLG
	ELSE
		PRINTL
	ENDIF

	IF !FLAG:거점메뉴에서지도표시
		IF DAY >= SLG_PP:1
			IF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0), CFLAG:MASTER:소속) <= 0 || CITY_OWNER:(ARG:0)==0
				IF IS_INVADABLE(ARG:0, CFLAG:MASTER:소속)
					PRINTBUTTON "[침공]", 2000+ARG:0
				ELSE
					SETCOLOR 칼라_선택불가
					PRINT [침공]
					RESETCOLOR
				ENDIF
			ELSEIF IS_PASSABLE(ARG:0, CFLAG:MASTER:소속)
			PRINTBUTTON "[이동]", 2000+ARG:0
			ENDIF
		ENDIF
		LOCAL:2 = 0
		IF CFLAG:MASTER:소속 >= 1 && LOCAL:6 == CFLAG:MASTER:소속 && CONFIG:302 == 0
			IF CNT_ENEMY > 0
				SETCOLOR 칼라_적
			FONTSTYLE 4
				LOCAL:2 = 1
			ENDIF
			PRINT  
			PRINTBUTTON "[병 수설정]" , ARG:0 + 200
			PRINT  
			PRINTBUTTON "[수장설정]" , ARG:0 + 400
			PRINT  
			PRINTBUTTON "[방위해제]" , ARG:0 + 800
			PRINT  
			IF CITY_INVESTED:(ARG:0)
				PRINTPLAIN [투자제]
			ELSEIF NOW_INVEST == CALC_MAX_INVEST() && LOCAL:2 != 1
				SETCOLOR 칼라_선택불가
				PRINTPLAINFORM [투자{NOW_INVEST}/{CALC_MAX_INVEST()}]
				RESETCOLOR
			ELSE
				PRINTBUTTON @"[투자{NOW_INVEST}/{CALC_MAX_INVEST()}]", ARG:0 + 1500
			ENDIF
			RESETCOLOR
			FONTREGULAR
		ENDIF
		PRINT  
		PRINTBUTTON "[색변경]", LOCAL:6 + 600
		PRINT  
		IF IS_COUNTRY(LOCAL:6)
			PRINTBUTTON "[세력정보]", LOCAL:6 + 650
			PRINT  
		ENDIF
		IF IS_COUNTRY(LOCAL:6) && !COUNTRY_IS_CLOSED:(LOCAL:6) && IS_COUNTRY(CFLAG:MASTER:소속) && !COUNTRY_IS_CLOSED:(CFLAG:MASTER:소속) && SLG_PP:0 <= DAY && LOCAL:6 != CFLAG:MASTER:소속
			PRINTBUTTON "[외교]", LOCAL:6 + 700
			PRINT 
		ENDIF
	ENDIF
	IF TARGET_NUM >= 0
		PRINT  
		PRINTFORM 부대 진군중
	ENDIF
	LOCAL:12 = 0
	FOR LOCAL:11,0,10
		SIF UNIT_POSITION:(CFLAG:MASTER:소속):(LOCAL:11) == ARG:0 && UNIT_SOLDIER:(CFLAG:MASTER:소속):(LOCAL:11) > 0
			LOCAL:12 ++
	NEXT
	IF LOCAL:12
		PRINT  
		SETCOLOR 칼라_오렌지
		PRINTFORM 부대 주둔중
		RESETCOLOR
	ENDIF

ENDIF

IF ARG:1 == 0
	CALL COLUMN_RIGHT_PRINTL_SLG
ELSE
	PRINTL
ENDIF
PRINTFORM ◆이 장소에 존재하는 부대(5 부대까지 표시)◆
IF ARG:1 == 0
	CALL COLUMN_RIGHT_PRINTL_SLG
ELSE
	PRINTL
ENDIF

FOR LOCAL, 0, MIN(CNT_UNIT,5)
	LOCAL:8 = UNIT_PAIR:(LOCAL:0):0
	LOCAL:1 = UNIT_PAIR:(LOCAL:0):1
	IF LOCAL:8 >= 0
		LOCAL:6 = GET_COUNTRY_BOSS(LOCAL:8)
		IF (!LOCAL:4 || LOCAL:6 == -1) && !IS_STAY_MYUNIT(ARG:0)
			CALL PRINT_UNIT_INFO(LOCAL:8, LOCAL:1, 1)
			SHOW_UNITS++
		ELSE
			CALL PRINT_UNIT_INFO(LOCAL:8, LOCAL:1)
			IF LOCAL:8 == CFLAG:MASTER:소속
				RESETCOLOR
				PRINTBUTTON "[편성]", 2500 + LOCAL:1
			ENDIF
			SHOW_UNITS++
		ENDIF
		IF ARG:1 == 0
			CALL COLUMN_RIGHT_PRINTL_SLG
		ELSE
			PRINTL
		ENDIF
	ENDIF
NEXT

IF !SHOW_UNITS
	CALL COLOR_PRINTL("부대 없음", 칼라_선택불가)
	SHOW_UNITS ++
ENDIF

;이하 빈행
IF ARG:1 == 0
	CALL COLUMN_RIGHT_CLEAR_SLG
ELSE
	PRINTL
ENDIF

@SPACER(ARG:0)
PRINTFORM %TOSTR_SPACE(ARG:0)%
