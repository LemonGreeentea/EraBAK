;-------------------------------------------
;전투 계산으로 사용하는 밀러 변수의 준비
;-------------------------------------------
@SET_BATTLE_MIRROR_VAL(SIDE, CHARA:0 = -1, CHARA:1 = -1, CHARA:2 = -1)
#DIM SIDE
#DIM CHARA, 3
SIF !GROUPMATCH(SIDE, 0, 1)
	THROW SIDE에는 0이나 1을 대입해 주세요

FOR LOCAL, 0, VARSIZE("BATTLE_COMMANDER", 1)
	BATTLE_COMMANDER:SIDE:LOCAL = -1
	BATTLE_무투:SIDE:LOCAL = 0
	BATTLE_방위:SIDE:LOCAL = 0
	BATTLE_지략:SIDE:LOCAL = 0
	BATTLE_정치:SIDE:LOCAL = 0
	BATTLE_가창:SIDE:LOCAL = 0
	BATTLE_요리:SIDE:LOCAL = 0
	BATTLE_신비:SIDE:LOCAL = 0
	BATTLE_무투파워:SIDE:LOCAL = 0
	BATTLE_방위파워:SIDE:LOCAL = 0
	BATTLE_지략파워:SIDE:LOCAL = 0
	BATTLE_신비파워:SIDE:LOCAL = 0
	BATTLE_가창파워:SIDE:LOCAL = 0
	BATTLE_요리파워:SIDE:LOCAL = 0
	BATTLE_SKILL_RATE:SIDE:LOCAL = 0

NEXT
BATTLE_COMMANDER_NUM:SIDE = 0

FOR LOCAL:0, 0, VARSIZE("CHARA")
	SIF CHARA:LOCAL < 0 || CHARA:LOCAL > CHARANUM
		BREAK
	BATTLE_COMMANDER_NUM:SIDE ++
	BATTLE_COMMANDER:SIDE:LOCAL = CHARA:LOCAL
	BATTLE_무투:SIDE:LOCAL = ABL:(CHARA:LOCAL):무투
	BATTLE_방위:SIDE:LOCAL = ABL:(CHARA:LOCAL):방위
	BATTLE_지략:SIDE:LOCAL = ABL:(CHARA:LOCAL):지략
	BATTLE_정치:SIDE:LOCAL = ABL:(CHARA:LOCAL):정치
	BATTLE_가창:SIDE:LOCAL = ABL:(CHARA:LOCAL):가창
	BATTLE_요리:SIDE:LOCAL = ABL:(CHARA:LOCAL):요리
	BATTLE_신비:SIDE:LOCAL = ABL:(CHARA:LOCAL):신비
	BATTLE_무투파워:SIDE:LOCAL = ABL_POWER(BATTLE_무투:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_방위파워:SIDE:LOCAL = ABL_POWER(BATTLE_방위:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_지략파워:SIDE:LOCAL = ABL_POWER(BATTLE_지략:SIDE:LOCAL, CHARA:LOCAL)
	SIF BATTLE_신비:SIDE:LOCAL
		BATTLE_신비파워:SIDE:LOCAL = ABL_POWER(BATTLE_신비:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_가창파워:SIDE:LOCAL = ABL_POWER(BATTLE_가창:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_요리파워:SIDE:LOCAL = ABL_POWER(BATTLE_요리:SIDE:LOCAL, CHARA:LOCAL)
	BATTLE_SKILL_RATE:SIDE:LOCAL = 100
NEXT

;-------------------------------------------------
;BATTLE:SIDE측의 COM_NO 차례가 전투중에 이탈했다
;실제의 부대 데이터로부터는 빗나가지 않기 때문에 주의(별도 FORCE_FREE 쳐라)
;-------------------------------------------------
@BATTLE_COMMANDER_WITHDRAWAL(SIDE, COM_IDX)
#DIM SIDE
#DIM COM_IDX

;최후미를 지정했을 경우는 제로 필(쉬프트 하면 배열외 참조로 죽을 수도 있다)
IF COM_IDX == BATTLE_COMMANDER_NUM:SIDE - 1
	BATTLE_무투:SIDE:COM_IDX = 0
	BATTLE_방위:SIDE:COM_IDX = 0
	BATTLE_지략:SIDE:COM_IDX = 0
	BATTLE_정치:SIDE:COM_IDX = 0
	BATTLE_신비:SIDE:COM_IDX = 0
	BATTLE_요리:SIDE:COM_IDX = 0
	BATTLE_가창:SIDE:COM_IDX = 0
	BATTLE_무투파워:SIDE:COM_IDX = 0
	BATTLE_방위파워:SIDE:COM_IDX = 0
	BATTLE_지략파워:SIDE:COM_IDX = 0
	BATTLE_신비파워:SIDE:COM_IDX = 0
	BATTLE_가창파워:SIDE:COM_IDX = 0
	BATTLE_요리파워:SIDE:COM_IDX = 0
	BATTLE_SKILL_RATE:SIDE:COM_IDX = 0
;최후미 이외를 지정했을 경우는 쉬프트 해 대응
;어차피 COMMANDER_NUM 상한으로 루프 하는이나 여과지 쉬프트원의 값은 방치로 네
ELSE
	FOR LOCAL, COM_IDX, BATTLE_COMMANDER_NUM:SIDE - 1
		BATTLE_무투:SIDE:LOCAL = BATTLE_무투:SIDE:(LOCAL + 1)
		BATTLE_방위:SIDE:LOCAL = BATTLE_방위:SIDE:(LOCAL + 1)
		BATTLE_지략:SIDE:LOCAL = BATTLE_지략:SIDE:(LOCAL + 1)
		BATTLE_정치:SIDE:LOCAL = BATTLE_정치:SIDE:(LOCAL + 1)
		BATTLE_신비:SIDE:LOCAL = BATTLE_신비:SIDE:(LOCAL + 1)
		BATTLE_요리:SIDE:LOCAL = BATTLE_요리:SIDE:(LOCAL + 1)
		BATTLE_가창:SIDE:LOCAL = BATTLE_가창:SIDE:(LOCAL + 1)
		BATTLE_무투파워:SIDE:LOCAL = BATTLE_무투파워:SIDE:(LOCAL + 1)
		BATTLE_방위파워:SIDE:LOCAL = BATTLE_방위파워:SIDE:(LOCAL + 1)
		BATTLE_지략파워:SIDE:LOCAL = BATTLE_지략파워:SIDE:(LOCAL + 1)
		BATTLE_신비파워:SIDE:LOCAL = BATTLE_신비파워:SIDE:(LOCAL + 1)
		BATTLE_가창파워:SIDE:LOCAL = BATTLE_가창파워:SIDE:(LOCAL + 1)
		BATTLE_요리파워:SIDE:LOCAL = BATTLE_요리파워:SIDE:(LOCAL + 1)
		BATTLE_SKILL_RATE:SIDE:LOCAL = BATTLE_SKILL_RATE:SIDE:(LOCAL + 1)
	NEXT
ENDIF

BATTLE_COMMANDER:SIDE:COM_IDX = -1
BATTLE_COMMANDER_NUM:SIDE -= 1

;---------------------------------
;암살의 효과용
;---------------------------------
@BATTLE_KNOCKOUT(대상측, 대상번호)
#DIM 대상측
#DIM 대상번호
BATTLE_무투:대상측:대상번호 = 0
BATTLE_방위:대상측:대상번호 = 0
BATTLE_지략:대상측:대상번호 = 0
BATTLE_정치:대상측:대상번호 = 0
BATTLE_신비:대상측:대상번호 = 0
BATTLE_요리:대상측:대상번호 = 0
BATTLE_가창:대상측:대상번호 = 0
BATTLE_무투파워:대상측:대상번호 = 0
BATTLE_방위파워:대상측:대상번호 = 0
BATTLE_지략파워:대상측:대상번호 = 0
BATTLE_신비파워:대상측:대상번호 = 0
BATTLE_가창파워:대상측:대상번호 = 0
BATTLE_요리파워:대상측:대상번호 = 0
BATTLE_SKILL_RATE:대상측:대상번호 = 0
BATTLE_SKILL_SEALED:대상측:대상번호 = 1



;-------------------------------------------------
;BATTLE:SIDE측의 공격 능력을 결정한다.
;CITY_NO가 1이상으로, 대응하는 CITY_TYPE가 0이라면, 방위 부대로 간주한다.
;-------------------------------------------------
@ATTACK_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM ATTACK
ATTACK = 0

;아무도 없는 경우, 능력치 10의 무장이 한명 있는 것으로 간주한다.
IF BATTLE_COMMANDER_NUM:SIDE == 0
	ATTACK = ABL_POWER(10, -1)
	GOTO BASE_CALCED
ENDIF

;누군가 있는 경우, 대응 능력과 신비로부터 산출한다.
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	ATTACK += BATTLE_무투파워:SIDE:LOCAL
	ATTACK += BATTLE_신비파워:SIDE:LOCAL / 3
NEXT

$BASE_CALCED

ATTACK = ATTACK * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

;방위 부대용의 계산식
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	RETURNF ATTACK / 2 * MAX(CITY_GUARD_RATE(CITY_NO) - 100, 100) / 100
ENDIF

RETURNF MAX(ATTACK / 3, 1)

;-------------------------------------------------
;BATTLE:SIDE측의 방어 능력을 결정한다.
;CITY_NO가 1이상으로, 대응하는 CITY_TYPE가 0이라면, 방위 부대로 간주한다.
;-------------------------------------------------
@DEFENSE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM DEFENSE
DEFENSE = 0

;아무도 없는 경우, 능력치 30의 무장이 한명 있는 것으로 간주한다.
IF BATTLE_COMMANDER_NUM:SIDE == 0
	DEFENSE = ABL_POWER(10, -1)
	GOTO BASE_CALCED
ENDIF

;누군가 있는 경우, 대응 능력과 신비로부터 산출한다.
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	DEFENSE += BATTLE_방위파워:SIDE:LOCAL
	DEFENSE += BATTLE_신비파워:SIDE:LOCAL / 3
NEXT

$BASE_CALCED

DEFENSE = DEFENSE * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

;방위 부대용의 계산식
;무소속 도시라면 70%
IF CITY_NO && CITY_TYPE:CITY_NO == 0
	DEFENSE = DEFENSE * CITY_GUARD_RATE(CITY_NO) / 100
	SIF !IS_COUNTRY(CITY_OWNER:CITY_NO)
		TIMES DEFENSE, 0.70
	RETURNF DEFENSE
ENDIF
RETURNF MAX(DEFENSE / 3, 1)


;-------------------------------------------------
;BATTLE:SIDE측의 지략 능력을 결정한다.
;CITY_NO가 1이상으로, 대응하는 CITY_TYPE가 0이라면, 방위 부대로 간주한다.
;-------------------------------------------------
@INTELLIGENCE_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM INTELLIGENCE
INTELLIGENCE = 0

;아무도 없는 경우, 능력치 10의 무장이 한명 있는 것으로 간주한다.
IF BATTLE_COMMANDER_NUM:SIDE == 0
	INTELLIGENCE = ABL_POWER(10, -1)
	GOTO BASE_CALCED
ENDIF

FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	SIF CURRENT_CHARA < 0
		CONTINUE
	INTELLIGENCE += BATTLE_지략파워:SIDE:LOCAL
NEXT

$BASE_CALCED

INTELLIGENCE = INTELLIGENCE * SUPPORT_LEVEL(SIDE, CITY_NO) / 1000

RETURNF MAX(INTELLIGENCE / 3, 1)


;-------------------------------------------------
;BATTLE:SIDE측의 신비 능력을 결정한다.
;CITY_NO가 1이상으로, 대응하는 CITY_TYPE가 0이라면, 방위 부대로 간주한다.
;-------------------------------------------------
@MAGIC_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM MAGIC
MAGIC = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;아마 없다
	SIF CURRENT_CHARA < 0
		CONTINUE
	MAGIC += BATTLE_신비파워:SIDE:LOCAL
NEXT
RETURNF MAGIC

;-------------------------------------------------
;BATTLE:SIDE측의 신비 능력을 결정한다.
;CITY_NO가 1이상으로, 대응하는 CITY_TYPE가 0이라면, 방위 부대로 간주한다.
;-------------------------------------------------
@SUPPORT_LEVEL(SIDE, CITY_NO = 0)
#FUNCTION
#DIM SIDE
#DIM CITY_NO
#DIM CURRENT_CHARA
#DIM SONG_SUPPORT
#DIM FOOD_SUPPORT
#DIM SUPPORT
SUPPORT = 0
SONG_SUPPORT = 0
FOOD_SUPPORT = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:SIDE
	CURRENT_CHARA = BATTLE_COMMANDER:SIDE:(LOCAL:0)
	;아마 없다
	SIF CURRENT_CHARA < 0
		CONTINUE
	SONG_SUPPORT += BATTLE_가창파워:SIDE:LOCAL
	FOOD_SUPPORT += BATTLE_요리파워:SIDE:LOCAL
NEXT
;능력치 100이 3명 있었을 경우에 대한 비율
;1000분율로 돌려줄 필요가 있으므로 반환치는 1000을 걸쳐 둔다
SONG_SUPPORT = SONG_SUPPORT * 1000 / (ABL_POWER(100, -1) * 3)
FOOD_SUPPORT = FOOD_SUPPORT * 1000 / (ABL_POWER(100, -1) * 3)
SUPPORT = 1000 + SONG_SUPPORT + FOOD_SUPPORT
RETURNF SUPPORT

;-------------------------------------------------
;연합 토벌대상을 격파했을 때의 처리
;-------------------------------------------------
@SLG_COMBAT_BEAT_TREATY_TARGET(세력, 토벌대상)
#DIM 세력
#DIM 토벌대상
#DIM 연합번호
#DIM 보장금
#DIM 루프세력
연합번호 = FINDELEMENT(TREATY_U_TARGET, 토벌대상)
FOR LOCAL, 0, MAX_TREATY_COUNTRY
	SIF TREATY_U_COUNTRY:연합번호:LOCAL == 세력
		BREAK
NEXT

SIF LOCAL == MAX_TREATY_COUNTRY
	RETURN

보장금 = RAND(3000, 5000)

PRINTFORML 연합 토벌대상인 %ANAME(GET_COUNTRY_BOSS(토벌대상))%를 격파한 것으로, 연합 참가국으로부터의 %ANAME(GET_COUNTRY_BOSS(세력))%의 평가가 향상했다
CALL ICPRINT(@"또, 보장금으로 해서 돈<{보장금}>가 주어졌다", "L", 칼라_주의)
FOR LOCAL, 0, MAX_TREATY_COUNTRY
	루프세력 = TREATY_U_COUNTRY:연합번호:LOCAL
	SIF IS_COUNTRY(루프세력) && 루프세력 != 세력
		CALL CHANGE_RELATION_C_TO_C(루프세력, 세력, RAND(10, 30), RAND(10, 30) * -1)
NEXT
MONEY:세력 += 보장금


;-------------------------------------------------
;ARG:0 세력의 ARG:1 부대가 ARG:2의 정보를 PRINT 하는 함수
;ARG:3이 진이라면?? 를 표시
;-------------------------------------------------
@PRINT_UNIT_INFO_COMBAT(ARG:0, ARG:1, ARG:2, ARG:3 = 0, ARG:4 = 1)
IF ARG:3
	CALL COLOR_PRINT(@"?? %"???? 부대", 10, LEFT% ★:%"??", 4, LEFT% 용병:%"????", 6, LEFT%공격:%"????", 5, LEFT%방어:%"????", 5, LEFT%지략:%"????", 5, LEFT%", 칼라_선택불가)
	RETURN
ENDIF
LOCAL:1 = GET_COUNTRY_BOSS(ARG:0)
LOCALS:1 = %SNAME(LOCAL:1)%
LOCALS:2 = %GET_COMMANDER_NAME_COMBAT(ARG:2, " ")%
;너무 길면 반각에
SIF STRLENS(LOCALS:2) >= 25
	LOCALS:2 = %TOHALF(LOCALS:2)%
SETCOLOR COUNTRY_COLOR:(ARG:0)
;부대 능력의 준비
PRINTFORM %LOCALS:1, MAX_CHARANAME_LENGTH / 2, LEFT% 부대:
IF ARG:4
	SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:소속, ARG:0)
		CASE 0
			IF IS_COUNTRY(CFLAG:MASTER:소속)
				SETCOLOR 칼라_경고
				PRINT 적
			ELSE
				PRINT   
			ENDIF
		CASE 1
			SETCOLOR 칼라_정전
			PRINT 정
		CASE 2
			SETCOLOR 칼라_연합
			PRINT 련
		CASE 3
			SETCOLOR 칼라_동맹
			PRINT 동
		CASE 4
			SETCOLOR 칼라_영구동맹
			PRINT 영
		CASE 5
			SETCOLOR 칼라_시안
			PRINT 자
	ENDSELECT
	SETCOLOR COUNTRY_COLOR:(ARG:0)
	PRINT  
ENDIF
PRINTFORM  %LOCALS:2, 25, LEFT%
PRINTFORM ★:{SUM_STARS_COMBAT(ARG:2), 4, LEFT}
PRINTFORM 용병:{BATTLE_SOL:(ARG:2), 6, LEFT}
PRINTFORM 공격:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_ATK:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   방어:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_DEF:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   지략:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_INT:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   신비:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_MAG:(ARG:2))
SETCOLOR COUNTRY_COLOR:(ARG:0)
PRINTFORM   피로:
PRINTFORM {UNIT_TIRED_COUNT:(ARG:0):(ARG:1), 2, LEFT}
RESETCOLOR

;-------------------------------------------------
;ARG:0 정력의 ARG:1 부대로 ARG:2 정력의 ARG:3 부대의 전력차이를 표시하는 함수 ARG:3이 부라면 ARG:2를 도시 번호로 간주한다
;-------------------------------------------------
@PRINT_COMBAT_INFO(ARG:0, ARG:1, ARG:2, ARG:3)
#DIM 세력색, 2
#DIM 병수, 2
#DIM 공격력, 2
#DIM 방어력, 2
#DIM 지략, 2
세력색:0 = COUNTRY_COLOR:(ARG:0)
세력색:1 = ARG:3 >= 0 ? COUNTRY_COLOR:(ARG:2) # COUNTRY_COLOR:(CITY_OWNER:(ARG:2))

PRINTFORM 용병 수:
CALL PRINT_VERSUSBAR(BATTLE_SOL:0, BATTLE_SOL:1, 20, 세력색:0, 세력색:1)
PRINTL
PRINTFORM 공격:
CALL PRINT_VERSUSBAR(BATTLE_ATK:0, BATTLE_ATK:1, 20, 세력색:0, 세력색:1)
PRINTL
PRINTFORM 방어:
CALL PRINT_VERSUSBAR(BATTLE_DEF:0, BATTLE_DEF:1, 20, 세력색:0, 세력색:1)
PRINTL
PRINTFORM 지략:
CALL PRINT_VERSUSBAR(BATTLE_INT:0, BATTLE_INT:1, 20, 세력색:0, 세력색:1)
PRINTL


;-------------------------------------------------
;도시 ARG:0의 정보를 PRINT 하는 함수
;-------------------------------------------------
@PRINT_CITY_INFO_COMBAT(ARG:0, ARG:1, ARG:2 = 1)
LOCAL:1 = CITY_OWNER:(ARG:0)
LOCAL:2 = GET_COUNTRY_BOSS(LOCAL:1)
LOCALS:1 = %(LOCAL:2 != -1 ? SNAME(LOCAL:2) # "무소속")%
LOCALS:2 = %GET_COMMANDER_NAME_COMBAT(ARG:1, " ")%
;너무 길면 반각에
SIF STRLENS(LOCALS:2) >= 25
	LOCALS:2 = %TOHALF(LOCALS:2)%
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM %LOCALS:1, MAX_CHARANAME_LENGTH / 2, LEFT% 부대:
IF ARG:2
	SELECTCASE CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:소속, LOCAL:1)
		CASE 0
			IF IS_COUNTRY(CFLAG:MASTER:소속)
				SETCOLOR 칼라_경고
				PRINT 적
			ELSE
				PRINT   
			ENDIF
		CASE 1
			SETCOLOR 칼라_정전
			PRINT 정
		CASE 2
			SETCOLOR 칼라_연합
			PRINT 련
		CASE 3
			SETCOLOR 칼라_동맹
			PRINT 동
		CASE 4
			SETCOLOR 칼라_영구동맹
			PRINT 영
		CASE 5
			SETCOLOR 칼라_시안
			PRINT 자
	ENDSELECT
	SETCOLOR COUNTRY_COLOR:(LOCAL:1)
ENDIF
PRINTFORM  %LOCALS:2, 25, LEFT%
PRINTFORM ★:{SUM_STARS_COMBAT(ARG:1), 4, LEFT}
PRINTFORM 용병:{BATTLE_SOL:(ARG:1), 6, LEFT}
PRINTFORM 공격:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_ATK:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   방어:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_DEF:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   지략:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_INT:(ARG:1))
SETCOLOR COUNTRY_COLOR:(LOCAL:1)
PRINTFORM   신비:
CALL PRINT_ALPHABET_RANK(랭크_부대, BATTLE_MAG:(ARG:1))
RESETCOLOR

;-------------------------------------------------
;ARG:0측의 ARG:1 부대의 쇼우의 수를 돌려주는 함수
;-------------------------------------------------
@SUM_STARS_COMBAT(ARG:0)
#FUNCTION
LOCAL:2 = 0
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:(ARG:0)
	LOCAL:1 = BATTLE_COMMANDER:(ARG:0):LOCAL
	SIF LOCAL:1 >= 0
		LOCAL:2 += TMP_CHARA_STARS:(LOCAL:1)
NEXT
RETURNF LOCAL:2


;-------------------------------------------------
;ARG:0측 부대의 장의 이름을 돌려주는 함수 ARGS:2=복수의 장이 있는 경우에 간을 단락짓는 문자
;-------------------------------------------------
@GET_COMMANDER_NAME_COMBAT(ARG:0, ARGS:2 = "·")
#FUNCTIONS
LOCALS:0 = 
FOR LOCAL:0, 0, BATTLE_COMMANDER_NUM:(ARG:0)
	LOCAL:1 = BATTLE_COMMANDER:(ARG:0):LOCAL
	IF LOCAL:1 >= 0
		SIF BATTLE_SKILL_SEALED:(ARG:0):LOCAL && ALLSAMES(0, BATTLE_무투파워:(ARG:0):(LOCAL), BATTLE_지략파워:(ARG:0):(LOCAL), BATTLE_방위파워:(ARG:0):(LOCAL))
			CONTINUE
		SIF LOCAL:0 >= 1
			LOCALS:0 += @"%ARGS:2%"
		LOCALS:0 += @"%SNAME(LOCAL:1)%"
		IF CFLAG:(LOCAL:1):행동불능상태 == 행동불능_임월
			LOCALS:0 += "(림)"
		ELSEIF TALENT:(LOCAL:1):임신
			LOCALS:0 += "(임)"
		ELSEIF CFLAG:(LOCAL:1):행동불능상태 == 행동불능_육아
			LOCALS:0 += "(육)"
		ENDIF
	ENDIF
NEXT
RETURNF LOCALS:0
