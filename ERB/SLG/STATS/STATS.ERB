;통계 정보에 관한 처리

;-------------------------------------------------
;통계 정보를 생으로 출력한다. 디버그용.
;-------------------------------------------------
@STATS_OUTPUT_RAW
CALL SINGLE_DRAWLINE
;읽고 쓰기 위치
PRINTFORML STATS_TIME = {STATS_TIME}
PRINTFORML STATS_STOPPED = {STATS_STOPPED}

;군주 ID배열
FOR LOCAL:0, 0, STATS_COUNTRIES
	PRINTFORM {STATS_BOSS_ID_LIST:(LOCAL:0)},
NEXT
PRINTL
;경제력 데이터
FOR LOCAL:0, 0, STATS_DURATION
	FOR LOCAL:1, 0, STATS_COUNTRIES
		PRINTFORM {STATS_GDP:(LOCAL:1):(LOCAL:0)},
	NEXT
	PRINTL
NEXT
CALL SINGLE_DRAWLINE

;-------------------------------------------------
;통계 정보를 정형해 출력한다.
;그래프화의 방법이 생각해내지 못했기 때문에, 적어도 엑셀에 붙일 수 있는 형식에서.
;호출은 CALL STATS_OUTPUT(STATS_GDP)같이, 표시하는 데이터를 인수에 있다.
;-------------------------------------------------
@STATS_OUTPUT(DATA_ARR)
#DIM REF DATA_ARR,0,0
#DIM COUNTRY_COUNT

IF STATS_STOPPED
	PRINTL 통계 관계의 처리가 정지되고 있습니다.
	RETURN
ENDIF

CALL SINGLE_DRAWLINE

;출력하는 세력수를 카운트 한다
COUNTRY_COUNT = 0
FOR LOCAL:0, 1, STATS_COUNTRIES
	IF STATS_BOSS_ID_LIST:(LOCAL:0) <= 0
		BREAK
	ENDIF
	COUNTRY_COUNT++
NEXT

;세력명을 출력(1 번째는 기수인 것으로 날린다)
PRINT  ,
FOR LOCAL:0, 1, 1 + COUNTRY_COUNT
	;ID_TO_CHARA의 영향을 잘 모르고 있는 채 쓰고 있으므로
	;혹시 여기서 NAME:(-1)가 될지도 모른다
	PRINTFORM %ANAME(ID_TO_CHARA( STATS_BOSS_ID_LIST:(LOCAL:0) ) )%,
NEXT
PRINTL

;데이터를 기록 기간 1주만큼 출력
FOR LOCAL:1, 0, STATS_DURATION
	;제로기 이전이라면 출력하지 않는다
	IF DATA_ARR:0:(STATS_TIME) < 1
		CALL STATS_TIME_PLUS
		CONTINUE
	ENDIF
	FOR LOCAL:0, 0, COUNTRY_COUNT + 1
		PRINTFORM {DATA_ARR:(LOCAL:0):(STATS_TIME)},
	NEXT
	PRINTL
	CALL STATS_TIME_PLUS
NEXT

;-------------------------------------------------
;통계 정보의 기억
;매기 종료시에 호출한다.
;-------------------------------------------------
@STATS_SAVE
#DIM POS

IF STATS_STOPPED
	RETURN
ENDIF

;기입처의 1행의 준비 처리
CALL STATS_SAVE_PREPROCESS(STATS_GDP)
CALL STATS_SAVE_PREPROCESS(STATS_ARMY)
CALL STATS_SAVE_PREPROCESS(STATS_WEALTH)

;어느 열에 기입할까 찾는다
FOR LOCAL:0, 1, MAX_COUNTRY
	;세력이 없으면 CONTINUE
	SIF !IS_COUNTRY(LOCAL:0)
		CONTINUE
	;군주 ID배열에 검색을 건다
	POS = FINDELEMENT(STATS_BOSS_ID_LIST, COUNTRY_BOSS:(LOCAL:0), 1, VARSIZE("STATS_BOSS_ID_LIST"))

	;발견되지 않았으면 군주 ID배열에의 신규 등록을 시도한다
	IF POS < 0
		POS = STATS_ENLIST_BOSS_ID(COUNTRY_BOSS:(LOCAL:0))
		;그런데도 기입해 앞을 얻을 수 없으면 CONTINUE
		IF POS < 0
			CONTINUE
		ENDIF
;		DEBUGPRINTFORML 【통계 추가】 %NAME:ID_TO_CHARA(COUNTRY_BOSS:(LOCAL:0))%
	ENDIF
	;통계 데이터의 기입
	STATS_GDP:(POS):(STATS_TIME) = GET_SUM_ECONOMY(LOCAL:0)
	STATS_ARMY:(POS):(STATS_TIME) = GET_SUM_SOLDIER(LOCAL:0)
	STATS_WEALTH:(POS):(STATS_TIME) = MONEY:(LOCAL:0)
NEXT
;기입처의 행을 1개진행한다
CALL STATS_TIME_PLUS

;-------------------------------------------------
;통계 정보를 기입하는 사전 처리
;-------------------------------------------------
@STATS_SAVE_PREPROCESS(ARR)
#DIM REF ARR,0,0
;줄머리에 기수를 기록
ARR:0:(STATS_TIME) = DAY+1
;행의 나머지를 제로로 묻는다
FOR LOCAL:0, 1, STATS_COUNTRIES
	ARR:(LOCAL:0):(STATS_TIME) = 0
NEXT

;-------------------------------------------------
;[각 세력의 군주의 ID의 배열]의 빈자리를 찾아(ARG:0)를 등록한다
;0차례 요소는 기수의 기록에 사용하므로 손대지 않는다.
;성공하면 그 위치를, 실패하면-1을 돌려준다
;-------------------------------------------------
@STATS_ENLIST_BOSS_ID(ARG:0)
#FUNCTION
FOR LOCAL:0, 1, STATS_COUNTRIES
	IF STATS_BOSS_ID_LIST:(LOCAL:0) <= 0
		STATS_BOSS_ID_LIST:(LOCAL:0) = ARG:0
		RETURNF LOCAL:0
	ENDIF
NEXT
RETURNF -1

;-------------------------------------------------
;읽고 쓰기처의 이동
;-------------------------------------------------
@STATS_TIME_PLUS(ARG:0 = 1)
STATS_TIME += ARG:0
;범위 위에 나왔다
IF STATS_TIME >= STATS_DURATION
	STATS_TIME %= STATS_DURATION
ENDIF
;범위아래에 나왔다
WHILE STATS_TIME < 0
	STATS_TIME += STATS_DURATION
WEND

;------------------------------------------------
;조약 관계의 출력
;------------------------------------------------
@TREATY_OUTPUT(ARG:0)

SELECTCASE ARG:0
CASE 0
	PRINTL 【기간】  【대상】
	FOR LOCAL:0, 0, MAX_TREATY_C
		SIF !TREATY_C_TERM:(LOCAL:0)
			CONTINUE
		PRINTFORM {TREATY_C_TERM:(LOCAL:0), 8, RIGHT}　
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			SIF TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1) >= 1
			PRINTFORM %ANAME(ID_TO_CHARA(COUNTRY_BOSS:(TREATY_C_COUNTRY:(LOCAL:0):(LOCAL:1))))%　
		NEXT
		PRINTL
	NEXT
CASE 1
	PRINTL 【기간】  【대상】
	FOR LOCAL:0, 0, MAX_TREATY_A
		SIF !TREATY_A_TERM:(LOCAL:0)
			CONTINUE
		PRINTFORM {TREATY_A_TERM:(LOCAL:0), 8, RIGHT}　
		FOR LOCAL:1, 0, MAX_TREATY_COUNTRY
			SIF TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1) >= 1
			PRINTFORM %ANAME(ID_TO_CHARA(COUNTRY_BOSS:(TREATY_A_COUNTRY:(LOCAL:0):(LOCAL:1))))%　
		NEXT
		PRINTL
	NEXT

ENDSELECT
