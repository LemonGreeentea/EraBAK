[SKIPSTART]
MapLibrary
==========
개요
----
  eratohoK의 맵 추가를 편하게 하는 것을 목적으로 만든 것. 정해진 서식에서 지도를 써 그것을 행 마다 함수에 보내는 것으로 지도를 실제로 표시하는 것과 같이 ERB에 기술할 수가 있다.

사용법, 사용예
--------------
MAP_SAMPLE.ERB를 참조

밖에서 사용하기 위해서(때문에) 준비된 함수군
------------------------------
+ @DRAWMAP_INIT(MENU_TYPE = 0, EMPTY_LINES = 0, AUTO_READ_CITYNAMES = 0)
+ @DRAWMAP_LINE_S(LINE, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
+ @DRAWMAP_END()
	+ INIT를 읽고 나서 LINE_S를 좋아하는 회수 읽어 END를 부르면 맵을 표시할 수 있다!
+ @REGISTER_ROUTE_S(TAR_CITYNAME, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
+ @CITY_ECONOMY_S(ARGS:0, ARG:0)
+ @CITY_GUARD_S(ARGS:0, ARG:0)
	+ _S가 붙어 있지 않은 것을 도시명으로부터 자동으로 GET_CITYNUMBER를 사용하는 설정할 수 있도록(듯이)한 것

예상되는 문제
--------------
+ 즉석에 만들었기 때문에 이 파일을 이해하는 것이 큰 일
+ AUTO_READ_CITYNAMES를 이용하면 CPU에 문제가 있는 환경이라면 조금 부하가 나올 가능성이 있다
+ 흘려 넣어지는 대량의 DEBUGPRINT
[SKIPEND]


;이 라이브라리의 설정 부분을 전부 이 함수에 내던지고 싶다
;ARG
	;MENU_TYPE           줄머리에 표시하는 메뉴를 선택한다
	;EMPTY_LINES         지도의 앞에 빈행을 출력할 때 빈행의 행수를 설정한다
	;AUTO_READ_CITYNAMES 도시명을 CITY_SHORT_NAME로부터 자동적으로 읽어낸다
@DRAWMAP_INIT(MENU_TYPE = 0, EMPTY_LINES = 0, AUTO_READ_CITYNAMES = 0)
#DIM MENU_TYPE
#DIM EMPTY_LINES
#DIM AUTO_READ_CITYNAMES
#DIM LINE
IF MENU_TYPE != 0
	DRAWMAP_MENUBARTYPE = MENU_TYPE
	DRAWMAP_LINECOUNT = 0
ELSE
	DRAWMAP_MENUBARTYPE = 0
	DRAWMAP_LINECOUNT = -1
ENDIF

IF EMPTY_LINES > 0
	FOR DRAWMAP_LINECOUNT, 0, EMPTY_LINES
		CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, DRAWMAP_LINECOUNT)
		PRINTL
	NEXT
ENDIF

IF AUTO_READ_CITYNAMES
	DRAWMAP_AUTO_READ_CITYNAMES = 1
ELSE
	DRAWMAP_AUTO_READ_CITYNAMES = 0
ENDIF

;맵 표시의 뒤처리를 하는 함수  세트로 호출하는 것
@DRAWMAP_END()
#DIM LINE
IF INRANGE(DRAWMAP_LINECOUNT, 0, MENU_HEIGHT - 1)
	FOR LINE, DRAWMAP_LINECOUNT, MENU_HEIGHT
		CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, LINE)
		PRINTL
	NEXT
ENDIF
DRAWMAP_LINECOUNT = -1

@DRAWMAP_LINE(LINE, CITIES:0 = -1, CITIES:1 = -1, CITIES:2 = -1, CITIES:3 = -1, CITIES:4 = -1, CITIES:5 = -1, CITIES:6 = -1, CITIES:7 = -1, CITIES:8 = -1, CITIES:9 = -1)
#DIMS LINE
#DIM CITIES,10
#DIMS CITY
#DIM NEXT_CITY
#DIM CITIES_SIZE
#DIM READING_POSITION
#DIMS NOW_READING
#DIM LINE_LENGTH
#DIM CITY_NAME_READING
#DIM CITY_ID
LINE_LENGTH = STRLENSU(LINE)
NEXT_CITY = 0
CITY '= ""
CITY_NAME_READING = 0

;메뉴 표시
IF DRAWMAP_MENUBARTYPE != 0
	CALL DRAWMAP_PRINT_MENU(DRAWMAP_MENUBARTYPE, DRAWMAP_LINECOUNT)
	DRAWMAP_LINECOUNT++
ENDIF

;DEBUGPRINTL
;DEBUGPRINTFORML start Line = %LINE%
;입력된 문자열으로부터 1문자씩 읽어들인다
SETFONT 맵_폰트
FOR READING_POSITION, 0, LINE_LENGTH
	NOW_READING '= SUBSTRINGU(LINE, READING_POSITION, 1)
	;DEBUGPRINTFORM Read %NOW_READING% Is 

	;읽어들인 문자에 의해 중계지점의 기호, 도시 혹은 지역명이 되는 문자, 그 이외의 3 패턴에 분기
	IF NOW_READING  == "◇" || NOW_READING == "●"
		;중계지점이라면 즉석에서 도시 번호를 쏟는다  만약 외로 에러가 나와 있으면 중계지점에 새어나오므로 참고로 하자
		;todo:Cities:NextCity가-1인 경우의 예외를 추가한다
		CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
		;DEBUGPRINTFORM Way Point Number = {CITIES:NEXT_CITY}
		NEXT_CITY++
	ELSEIF STRCOUNT(NOW_READING, @"\\w")
		;문자열이었던 경우는 연속한 문자열을 모아두어 문자열이 종료하면 통합 처리하는 플래그를 세운다
		CITY += NOW_READING
		CITY_NAME_READING = 1
		;DEBUGPRINTFORM City or Area So Memory = %CITY%
	ELSE
		IF CITY_NAME_READING
			;문자열이 종료한 후의 처리  도시명이나 지역명인지를 검사한다
			;DEBUGPRINTFORM NEXT Piece Of City or Area And Last Is 
			IF DRAWMAP_AUTO_READ_CITYNAMES
				CITY_ID = FINDELEMENT(CITY_NAME_SHORT, CITY, 1, CITY_NUM + 1, 1)
				IF CITY_ID != -1
					;도시명이었던 경우는 버튼을 표시한다  도시명인가 어떤가는 지도에 실제로 표시하는 문자열인 CITY_NAME_SHORT를 사용한다  표기 흔들림은 판별 할 수 없기에 주의
					CALL DRAWMAP_CITY_BUTTON(CITY_ID)
					;DEBUGPRINTFORM City Name = %CITY% Number = {CITY_ID}
					PRINTS NOW_READING
				ELSE
					;도시명이 아닌 경우는 이탤릭으로 지역명을 표시한다  지역명의 다음에 가로줄 이외의 기호가 있으면 지도가 무너지므로 주의
					FONTSTYLE 2
					PRINTS CITY
					PRINTS NOW_READING
					FONTSTYLE 0
					;DEBUGPRINTFORM Area Name = %CITY% 
					;SIF CITIES:NEXT_CITY != -1
						;DEBUGPRINTFORM Is Not In CityShortNames
				ENDIF
			ELSE
				IF CITIES:NEXT_CITY != -1 && CITY == CITY_NAME_SHORT:(CITIES:NEXT_CITY)
					;도시명이었던 경우는 버튼을 표시한다  도시명인가 어떤가는 지도에 실제로 표시하는 문자열인 CITY_NAME_SHORT를 사용한다  표기 흔들림은 판별 할 수 없기에 주의
					CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
					;DEBUGPRINTFORM City Name = %CITY% Number = {CITIES:NEXT_CITY}
					NEXT_CITY++
					PRINTS NOW_READING
				ELSE
					;도시명이 아닌 경우는 이탤릭으로 지역명을 표시한다  지역명의 다음에 가로줄 이외의 기호가 있으면 지도가 무너지므로 주의
					FONTSTYLE 2
					PRINTS CITY
					PRINTS NOW_READING
					FONTSTYLE 0
					;DEBUGPRINTFORM Area Name = %CITY% 
					;SIF CITIES:NEXT_CITY != -1
						;DEBUGPRINTFORM Is Not %CITY_NAME_SHORT:(CITIES:NEXT_CITY)%
					ENDIF
			ENDIF
			CITY_NAME_READING = 0
			CITY '= ""
		ELSE
			;DEBUGPRINTFORM Way or Else
			PRINTS NOW_READING
		ENDIF
	ENDIF
	;DEBUGPRINTL
NEXT
;DEBUGPRINTL Line Is End
;행의 마지막에 문자열이 존재하는 경우의 뒤처리  귀찮기 때문에 코피페로
IF CITY_NAME_READING
	;DEBUGPRINTFORM Last Is 
	IF CITIES:NEXT_CITY != -1 && CITY == CITY_NAME_SHORT:(CITIES:NEXT_CITY)
		CALL DRAWMAP_CITY_BUTTON(CITIES:NEXT_CITY)
		;DEBUGPRINTFORM City Name = %CITY% Number = {CITIES:NEXT_CITY}
		NEXT_CITY++
	ELSE
		FONTSTYLE 2
		PRINTS CITY
		FONTSTYLE 0
		;DEBUGPRINTFORM Area Name = %CITY% 
		;SIF CITIES:NEXT_CITY != -1
			;DEBUGPRINTFORM Is Not %CITY_NAME_SHORT:(CITIES:NEXT_CITY)%
	ENDIF
	CITY_NAME_READING = 0
	CITY '= ""
	;DEBUGPRINTL
ENDIF
PRINTL
SETFONT
;DEBUGPRINTL end
RETURN

@DRAWMAP_LINE_S(LINE, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
#DIMS LINE
#DIM CITIES, 10
#DIMS CITYNAMES, 10
VARSET CITIES
CITIES:0 = GET_CITYNUMBER(CITYNAMES:0), GET_CITYNUMBER(CITYNAMES:1), GET_CITYNUMBER(CITYNAMES:2), GET_CITYNUMBER(CITYNAMES:3), GET_CITYNUMBER(CITYNAMES:4), GET_CITYNUMBER(CITYNAMES:5), GET_CITYNUMBER(CITYNAMES:6), GET_CITYNUMBER(CITYNAMES:7), GET_CITYNUMBER(CITYNAMES:8), GET_CITYNUMBER(CITYNAMES:9)
CALL DRAWMAP_LINE(LINE, CITIES:0 == -1 ? -1 # CITIES:0, CITIES:1 == -1 ? -1 # CITIES:1, CITIES:2 == -1 ? -1 # CITIES:2, CITIES:3 == -1 ? -1 # CITIES:3, CITIES:4 == -1 ? -1 # CITIES:4, CITIES:5 == -1 ? -1 # CITIES:5, CITIES:6 == -1 ? -1 # CITIES:6, CITIES:7 == -1 ? -1 # CITIES:7, CITIES:8 == -1 ? -1 # CITIES:8, CITIES:9 == -1 ? -1 # CITIES:9)


@REGISTER_ROUTE_S(TAR_CITYNAME, CITYNAMES:0 = "", CITYNAMES:1 = "", CITYNAMES:2 = "", CITYNAMES:3 = "", CITYNAMES:4 = "", CITYNAMES:5 = "", CITYNAMES:6 = "", CITYNAMES:7 = "", CITYNAMES:8 = "", CITYNAMES:9 = "")
#DIMS TAR_CITYNAME
#DIM CITIES, 10
#DIM TAR_CITY
#DIMS CITYNAMES, 10
VARSET CITIES
CITIES:0 = GET_CITYNUMBER(CITYNAMES:0), GET_CITYNUMBER(CITYNAMES:1), GET_CITYNUMBER(CITYNAMES:2), GET_CITYNUMBER(CITYNAMES:3), GET_CITYNUMBER(CITYNAMES:4), GET_CITYNUMBER(CITYNAMES:5), GET_CITYNUMBER(CITYNAMES:6), GET_CITYNUMBER(CITYNAMES:7), GET_CITYNUMBER(CITYNAMES:8), GET_CITYNUMBER(CITYNAMES:9)
TAR_CITY = GET_CITYNUMBER(TAR_CITYNAME)
CALL REGISTER_ROUTE(TAR_CITY, CITIES:0 == -1 ? -99 # CITIES:0, CITIES:1 == -1 ? -99 # CITIES:1, CITIES:2 == -1 ? -99 # CITIES:2, CITIES:3 == -1 ? -99 # CITIES:3, CITIES:4 == -1 ? -99 # CITIES:4, CITIES:5 == -1 ? -99 # CITIES:5, CITIES:6 == -1 ? -99 # CITIES:6, CITIES:7 == -1 ? -99 # CITIES:7, CITIES:8 == -1 ? -99 # CITIES:8, CITIES:9 == -1 ? -99 # CITIES:9)

@CITY_ECONOMY_S(ARGS:0, ARG:0)
SIF GET_CITYNUMBER(ARGS:0) == -1
	THROW @CITY_ECONOMY_S에게 건네진 도시명%ARGS:0%는 존재하지 않습니다
CITY_ECONOMY:GET_CITYNUMBER(ARGS:0) = ARG:0

@CITY_GUARD_S(ARGS:0, ARG:0)
SIF GET_CITYNUMBER(ARGS:0) == -1
	THROW @CITY_GUARD_S에게 건네진 도시명%ARGS:0%는 존재하지 않습니다
CITY_GUARD:GET_CITYNUMBER(ARGS:0) = ARG:0

@DRAWMAP_PRINT_MENU(MENU_TYPE, MENU_LINE)
#DIM MENU_TYPE
#DIM MENU_LINE
SELECTCASE MENU_TYPE
	CASE DRAWMAP_MENUBARTYPE_SLGMENU
		CALL COLUMN_LEFT_MENU_SHOW_SLG(MENU_LINE)
	CASE DRAWMAP_MENUBARTYPE_DEBUGMENU
		CALL PRINT_MAPDEBUG_MENU(MENU_LINE)
ENDSELECT

@DRAWMAP_CITY_BUTTON(CITY_ID)
#DIM CITY_ID
SELECTCASE DRAWMAP_MENUBARTYPE
	CASE DRAWMAP_MENUBARTYPE_DEBUGMENU
		CALL DRAW_CITY_BUTTON_MAPDEBUG(CITY_ID)
	CASEELSE
		CALL DRAWCITY_BUTTON(CITY_ID)
ENDSELECT
