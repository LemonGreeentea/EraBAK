; ARG:0에 침공한다
@INVADE(ARG:0)
#DIM 세력
#DIM 부대공격
#DIM 부대지략
#DIM 부대방어
#DIM 도시공격
#DIM 도시지략
#DIM 도시방어
VARSET 부대공격
VARSET 부대방어
VARSET 부대지략
VARSET 도시공격
VARSET 도시방어
VARSET 도시지략
CLEARLINE 1
PRINTL 

SIF !IS_INVADABLE(ARG:0, CFLAG:MASTER:소속)
	RETURN

IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}기까지는 침공을 실시할 수가 없습니다
	RETURN 
ENDIF

	
세력 = CITY_OWNER:(ARG:0)
IF IS_COUNTRY(CITY_OWNER:(ARG:0))
	;부대 능력을 계산
	CALL TMP_CREATE_IS_FREE_MAP
	LOCALS:0 = 
	LOCAL:2 = 0
	CALL CHECK_UNIT_COMMANDER_BEST(세력)
	FOR LOCAL, 0, 3
		LOCAL:1 = UNIT_COMMANDER_BEST:LOCAL
		IF LOCAL:1 >= 0
			SIF LOCAL:2 != 0
				LOCALS:0 = %LOCALS:0%·
			LOCALS:0 = %LOCALS:0%%ANAME(LOCAL:1)%
		ENDIF
		LOCAL:2 ++
	NEXT
	SIF LOCALS:0 != ""
		LOCALS:0 = <%LOCALS:0%>
	CALL SET_BATTLE_MIRROR_VAL(0, UNIT_COMMANDER_BEST:0, UNIT_COMMANDER_BEST:1, UNIT_COMMANDER_BEST:2)
	부대공격 = ATTACK_LEVEL(0, 0)
	부대방어 = DEFENSE_LEVEL(0, 0)
	부대지략 = INTELLIGENCE_LEVEL(0, 0)

	;도시 능력을 계산
	LOCALS:1 = 
	LOCAL:2 = 0
	CALL CHECK_CITY_COMMANDER_BEST(ARG:0, 1)
	FOR LOCAL, 0, 2
		LOCAL:1 = CITY_COMMANDER_BEST:LOCAL
		IF LOCAL:1 >= 0
			SIF LOCAL:2 != 0
				LOCALS:1 = %LOCALS:1%·
			LOCALS:1 = %LOCALS:1%%ANAME(LOCAL:1)%
		ENDIF
		LOCAL:2 ++
	NEXT
	SIF LOCALS:1 != ""
		LOCALS:1 = <%LOCALS:1%>
	CALL SET_BATTLE_MIRROR_VAL(0, CITY_COMMANDER_BEST:0, CITY_COMMANDER_BEST:1)
	도시공격 = ATTACK_LEVEL(0, ARG:0)
	도시방어 = DEFENSE_LEVEL(0, ARG:0)
	도시지략 = INTELLIGENCE_LEVEL(0, ARG:0)
ENDIF

$INPUT_LOOP
CALL SINGLE_DRAWLINE
PRINTL ●침공하는 부대를 선택해 주세요
IF IS_COUNTRY(CITY_OWNER:(ARG:0))
	PRINTFORM 현시점일 수 있는 최강의 부대:%LOCALS:0, 32, LEFT% 
	PRINTFORM 공:
	CALL PRINT_ALPHABET_RANK(랭크_부대, 부대공격)
	PRINTFORM   방:
	CALL PRINT_ALPHABET_RANK(랭크_부대, 부대방어)
	PRINTFORM   지
	CALL PRINT_ALPHABET_RANK(랭크_부대, 부대지략)
	PRINTL
	PRINTFORM 현시점일 수 있는 최강의 수장:%LOCALS:1, 32, LEFT% 
	PRINTFORM 공:
	CALL PRINT_ALPHABET_RANK(랭크_부대, 도시공격)
	PRINTFORM   방:
	CALL PRINT_ALPHABET_RANK(랭크_부대, 도시방어)
	PRINTFORM   지
	CALL PRINT_ALPHABET_RANK(랭크_부대, 도시지략)
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
CALL LISTUP_FORCE(CFLAG:MASTER:소속, "CHECK_FORCE_SELECTABLE_INVADE")
CALL SINGLE_DRAWLINE
PRINTBUTTON "20[신규부대]", 20
PRINTL
PRINTBUTTON " 0[돌아온다]", 0
INPUT
LOCAL:10 = RESULT
IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
	IF CHECK_FORCE_SELECTABLE_INVADE_F(CFLAG:MASTER:소속, LOCAL:10 - 1)
		$EXECUTE_FORCE
		CALL FORCE_SELECT_DEPARTURE_POINT(ARG:0, LOCAL:10 - 1)
		;작성한지 얼마 안된 부대에서, 「그만둔다」로 돌아왔다면 소재가 없기에 지워 둔다
		SIF RESULT == 0 && UNIT_POSITION:(CFLAG:MASTER:소속):(LOCAL:10 - 1) == 0
			CALL CLEAR_UNIT(CFLAG:MASTER:소속, LOCAL:10 - 1, 1)
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF LOCAL:10 == 0
ELSEIF LOCAL:10 == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(CFLAG:MASTER:소속):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(CFLAG:MASTER:소속, LOCAL:0, 1)
			IF RESULT
				LOCAL:10 = LOCAL:0 + 1
				GOTO EXECUTE_FORCE
			ENDIF
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW 더 이상 부대를 편성할 수 없습니다
	ENDIF
	RESTART
ELSEIF 100 <= LOCAL:10 && LOCAL:10 < MAX_UNIT + 100
	CALL UNIT_SETTING(CFLAG:MASTER:소속, LOCAL:10 - 100, 1)
	IF RESULT == 1
		LOCAL:10 -= 99
		GOTO EXECUTE_FORCE
	ENDIF
	RESTART
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; ARG:0으로 이동한다
;-------------------------------------------------
@MOVETO(ARG:0)
CLEARLINE 1
PRINTL 
IF DAY < SLG_PP:1
	PRINTFORMW {SLG_PP:1}기까지는 이동을 실시할 수가 없습니다
	RETURN
ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:소속) < 2
	RETURN
ENDIF
	
$INPUT_LOOP
CALL SINGLE_DRAWLINE
PRINTL ●이동하는 부대를 선택해 주세요
CALL SINGLE_DRAWLINE
CALL LISTUP_FORCE(CFLAG:MASTER:소속, "CHECK_FORCE_SELECTABLE_MOVETO", ARG:0)
CALL SINGLE_DRAWLINE
PRINTBUTTON "20[신규부대]", 20
PRINTL
PRINTBUTTON " 0[돌아온다]", 0
INPUT
LOCAL:10 = RESULT
IF LOCAL:10 >=1 && LOCAL:10 <= MAX_UNIT
	IF CHECK_FORCE_SELECTABLE_MOVETO_F(CFLAG:MASTER:소속, LOCAL:10 - 1, ARG:0)
		IF CHECK_COUNTRY_RELATION_F(CFLAG:MASTER:소속, CITY_OWNER:(UNIT_POSITION:(CFLAG:MASTER:소속):(LOCAL:10 - 1))) >= 2
			$EXECUTE_MOVETO
			UNIT_POSITION:(CFLAG:MASTER:소속):(LOCAL:10 - 1) = ARG:0
			PRINTFORML 부대{LOCAL:10}가 %GET_RELAYNAME(ARG:0)%에 이동했습니다
			IF UNIT_TARGET:(CFLAG:MASTER:소속):(LOCAL:10 - 1) != 0
				UNIT_TARGET:(CFLAG:MASTER:소속):(LOCAL:10 - 1) = 0
				PRINTL 현재의 이동 목표는 해제됩니다
			ENDIF
		ELSE
			UNIT_TARGET:(CFLAG:MASTER:소속):(LOCAL:10 - 1) = ARG:0
			PRINTFORML 부대{LOCAL:10}의 이동 목표를 %GET_RELAYNAME(ARG:0)%로 설정합니다
		ENDIF
		
		WAIT
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF LOCAL:10 == 20
	LOCAL:1 = 0
	FOR LOCAL:0, 0, MAX_UNIT
		IF UNIT_SOLDIER:(CFLAG:MASTER:소속):(LOCAL:0) <= 0
			LOCAL:1 = 1
			CALL CREATE_UNIT(CFLAG:MASTER:소속, LOCAL:0, 1)
			IF RESULT
				LOCAL:10 = LOCAL:0 + 1
				GOTO EXECUTE_MOVETO
			ENDIF
			BREAK
		ENDIF
	NEXT
	IF !LOCAL:1
		PRINTW 더 이상 부대를 편성할 수 없습니다
	ENDIF
	RESTART
ELSEIF LOCAL:10 == 0
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
; ARG :0의 세력의 부대를 선택 가능 리스트업
; ARGS:1조건함수
; ARG :2 임의의 인수
;  0=선택 불가（표시 없음）1=선택가능 -1=선택 불가（칼라_선택불가）-2=선택 불가（칼라_경고）
;-------------------------------------------------
@LISTUP_FORCE(ARG:0, ARGS:1, ARG:2=0)
LOCAL:20 = GETCOLOR()
RESETCOLOR
FOR LOCAL:0, 0, MAX_UNIT
	RESETCOLOR
	RESULT = 0
	SIF UNIT_SOLDIER:(ARG:0):(LOCAL:0) <= 0
		CONTINUE
	TRYCALLFORM %ARGS:1%, ARG:0, LOCAL:0, ARG:2
	
	IF RESULT != 0
		SELECTCASE RESULT
			CASE -1
				SETCOLOR 칼라_선택불가
			CASE -2
				SETCOLOR 칼라_경고
		ENDSELECT
	
		LOCALS:0  = {LOCAL:0 + 1, 2}[  부대{LOCAL:0 + 1, 2}]
		LOCALS:0 += " "
		IF RESULT > 0
			PRINTBUTTON LOCALS:0, LOCAL:0 +1
			PRINT 
			PRINTBUTTON "[편성]", LOCAL:0 + 100
		ELSE
			PRINTFORM %LOCALS:0%
			PRINT 
			PRINTPLAIN [편성]
		ENDIF
		CALL SHOW_UNIT_INFO(CFLAG:MASTER:소속, LOCAL:0, 24)
	ENDIF
NEXT
SETCOLOR LOCAL:20

;-------------------------------------------------
; ARG:0의 세력의 ARG:1번 부대가 침공으로 선택 가능한가 체크한다
; REUTNRN
;  0=선택 불가（표시 없음）1=선택가능 -1=선택 불가（칼라_선택불가）-2=선택 불가（칼라_경고）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_INVADE(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT


@CHECK_FORCE_SELECTABLE_INVADE_F(ARG:0, ARG:1, ARG:2)
#FUNCTION

SIF CHECK_COUNTRY_RELATION_F(ARG:0, CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1))) >= 2 && !IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(ARG:1))
	RETURNF 1

RETURNF 0

;-------------------------------------------------
; ARG:0의 세력의 ARG:1번 부대가 ARG:2에의 이동으로 선택 가능한가 체크한다
; REUTNRN
;  0=선택 불가（표시 없음）1=선택가능 -1=선택 불가（칼라_선택불가）-2=선택 불가（칼라_경고）
;-------------------------------------------------
@CHECK_FORCE_SELECTABLE_MOVETO(ARG:0, ARG:1, ARG:2)
RESULT= CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
RETURN RESULT
@CHECK_FORCE_SELECTABLE_MOVETO_F(ARG:0, ARG:1, ARG:2)
#FUNCTION
IF UNIT_SOLDIER:(ARG:0):(ARG:1) > 0
	IF UNIT_POSITION:(ARG:0):(ARG:1) == ARG:2
		RETURNF -1
	;자신의 위치에 적이 있으면 안 돼
	ELSEIF IS_STAY_ENEMY_UNIT(UNIT_POSITION:(ARG:0):(ARG:1))
		RETURNF -1
	;관계 2이상의 나라에 있으면 흰색
	ELSEIF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(UNIT_POSITION:(ARG:0):(ARG:1)), ARG:0) >= 2
		RETURNF 1
	;목적지가 연결되고 있어 목적지가 관계 2이상의 나라나 중계지점이라면 흰색
	ELSEIF IS_ROUTE(UNIT_POSITION:(ARG:0):(ARG:1), ARG:2) && (CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:2), ARG:0) >= 2 || CITY_TYPE:(ARG:2) == 1)
		RETURNF 1
	;그렇지 않았으면 재
	ELSE
		RETURNF -1
	ENDIF
	RETURNF 0
ENDIF


;-------------------------------------------------
; ARG:0에 ARG:1 부대가 침공한다

;-------------------------------------------------
@FORCE_SELECT_DEPARTURE_POINT(ARG:0, ARG:1)
#DIM POINTER
#DIM POINTER2
#DIM ARR,30
#DIM ARR2,30
#DIM FROM
POINTER = 0
POINTER2 = 0
FOR LOCAL:0, 0, MAX_CITY
	; 조건
	; LOCAL:0이 중계지가 아니다
	; LOCAL:0이 ARG:0으로 인접
	; LOCAL:0이 통행 가능（LOCAL:0의 지배국과의 관계가 2이상）
	
	LOCAL:1 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0),   CFLAG:MASTER:소속)
	LOCAL:2 = CHECK_COUNTRY_RELATION_F(CITY_OWNER:(LOCAL:0), CFLAG:MASTER:소속)
	IF CITY_TYPE:(LOCAL:0) == 0 && IS_ROUTE(ARG:0, LOCAL:0) && (LOCAL:1 <= 0 || CITY_OWNER:(ARG:0) == 0) && LOCAL:2 >= 2
		IF IS_ROUTE(ARG:0, LOCAL:0) == 1
			ARR:POINTER = LOCAL:0
			POINTER++
		ELSE
			ARR2:POINTER2 = LOCAL:0
			POINTER2++
		ENDIF
	ENDIF
NEXT

IF (POINTER + POINTER2) == 0
	PRINT 이 메세지가 표시되면 보고를 부탁합니다
ELSEIF (POINTER + POINTER2) == 1
	IF POINTER == 1
		FROM = ARR:0
	ELSE
		FROM = ARR2:0
	ENDIF
ELSE
	CALL SINGLE_DRAWLINE
	PRINTL ●출발 지점을 선택해 주세요
	CALL SINGLE_DRAWLINE
	IF POINTER
		PRINTL 1 턴으로 진군 가능
		PRINT   
		FOR LOCAL:0, 0, POINTER
			LOCAL:1 = ARR:(LOCAL:0)
			SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:소속):(ARG:1)
				SETCOLOR 칼라_오렌지
			LOCALS:0 = [{LOCAL:0+1}] %GET_CITYNAME(ARR:(LOCAL:0))%
			PRINTBUTTON LOCALS:0, LOCAL:0 + 1
			RESETCOLOR
			PRINT   
		NEXT
		PRINTL 
		PRINTL 
	ENDIF
	
	IF POINTER2
		PRINTL 중계지를 경유
		PRINT   
		FOR LOCAL:0, 0, POINTER2
			LOCAL:1 = ARR2:POINTER2
			SIF LOCAL:1 == UNIT_POSITION:(CFLAG:MASTER:소속):(ARG:1)
				SETCOLOR 칼라_오렌지
			LOCALS:0 = [{LOCAL:0+101}] %GET_CITYNAME(ARR2:(LOCAL:0))%
			PRINTBUTTON LOCALS:0, LOCAL:0 + 101
			RESETCOLOR
			PRINT   
		NEXT
		PRINTL 
		PRINTL 
	ENDIF
	PRINTBUTTON "[0] 그만둔다", 0
	PRINTL 
	PRINTL 
	$INPUT_LOOP2
	INPUT
	LOCAL:11 = RESULT
	
	IF POINTER && INRANGE(LOCAL:11, 1, 0+POINTER)
		FROM = ARR:(LOCAL:11 - 1)
	ELSEIF POINTER2 && INRANGE(LOCAL:11, 101, 100+POINTER2)
		FROM = ARR2:(LOCAL:11 - 101)
	ELSEIF LOCAL:11 == 0
		RETURN 0
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP2
	ENDIF
ENDIF
UNIT_TARGET:(CFLAG:MASTER:소속):(ARG:1)   = ARG:0
IF FROM != UNIT_POSITION:(CFLAG:MASTER:소속):(ARG:1)
	UNIT_POSITION:(CFLAG:MASTER:소속):(ARG:1) = FROM
	PRINTFORML 부대{ARG:1 + 1}를 %GET_CITYNAME(FROM)%에 이동했습니다
	CALL SET_COMMANDER_COOLTIME(CFLAG:MASTER:소속, ARG:1)
ENDIF
PRINTFORML 부대{ARG:1 + 1}의 이동 목표를 %GET_RELAYNAME(ARG:0)%로 설정합니다
IF IS_ROUTE(ARG:0, FROM) ==2
	SETCOLOR 칼라_주의
	PRINTFORML 중계지점을 경유해 진군 합니다
	RESETCOLOR
ENDIF
WAIT
RETURN 1
