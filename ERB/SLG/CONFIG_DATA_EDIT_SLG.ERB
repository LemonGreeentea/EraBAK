;콘피그로부터 실시하는 강제 데이터 조작 세력도의 변경

;-------------------------------------------------
;각 커맨드가 선택 가능한가 어떤가를 조사한다
;-------------------------------------------------
@CONFIG_DATA_EDIT_SLG
#DIM MODE
#DIM T_COUNTRY
#DIM T_CITY
#DIM LIST_COUNTRY, MAX_COUNTRY
#DIM LIST_POINT
#DIM LIST_MAX
#DIM FIRST_LINE
;설명서
PRINTL ※세력도의 변경을 실시하면, 종료시로 이하의 변경이 이루어집니다
PRINTL   ◎지배 도시를 갖지 않는 세력이 소멸해, 시중들고 있던 사관이 모두 방랑 상태가 된다
PRINTL   ◎전세력의 모든 부대가 해산되어 병수가 최대수의 것 1/2으로 변경된다
PRINTL   ◎전도시의 수장이 해제되어 방위병수가 500이 된다
CALL ASK_YN("속행", "중지")
IF RESULT == 1
	RETURN
ENDIF

CALL SINGLE_DRAWLINE

MODE = 0
T_COUNTRY = 0
T_CITY = 0
LIST_POINT = 0

;국가 리스트의 작성
LIST_COUNTRY:0 = 0
LIST_MAX = 1
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LIST_COUNTRY:(LIST_MAX) = LOCAL:0
		LIST_MAX ++
	ENDIF
NEXT

FIRST_LINE = LINECOUNT
$SHOW_LOOP

;커맨드의 선택 가부 판정
CALL CHECK_SAVE_SLG

;맵과 커맨드 버튼을 표시
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP(0)
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE

CALL PRINTBUTTON_EX(" 0[도시 정보의 표시]", 0, 1, MODE == 0)
PRINT     
CALL PRINTBUTTON_EX(" 1[지배 도시의 추가]", 1, 1, MODE == 1)
IF MODE != 1
	SETCOLOR 칼라_선택불가
ENDIF
IF T_COUNTRY == 0
	PRINTFORM (대상세력:무소속----
ELSE
	LOCALS:0 = %ANAME(GET_COUNTRY_BOSS(T_COUNTRY))%
	PRINTFORM (대상세력:%LOCALS:0%
	FOR LOCAL:0, 0, 10 - STRLENS(LOCALS:0)
		PRINT -
	NEXT
ENDIF
CALL PRINTBUTTON_EX("2[변경]", 2, MODE == 1)
PRINT   
CALL PRINTBUTTON_EX("3[통일]", 3, MODE == 1 && T_COUNTRY != 0)
IF MODE != 1
	SETCOLOR 칼라_선택불가
ENDIF
PRINTL )
RESETCOLOR

;도시의 정보를 표시
CALL SHOW_CITY_INFO_EDIT(T_CITY)

CALL PRINTBUTTON_EX("99[종료]", 99, 1)
PRINTL 

$INPUT_LOOP
INPUT

REDRAW 0

IF RESULT == 99
	;지배 도시를 가지지 않는 세력을 삭제
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) == 0 && COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(특수세력_도둑)
			FOR LOCAL:1, 0, CHARANUM
				IF CFLAG:(LOCAL:1):소속 == LOCAL:0
					CALL CHANGE_COUNTRY(LOCAL:1, 0)
				ENDIF
			NEXT
			CALL DESTROY_COUNTRY(LOCAL:0)
		;도둑 세력은 도시수에 응해 외교 금지
		ELSEIF COUNTRY_EVENT_ID:(LOCAL:0) == SP_COUNTRY_ID:(특수세력_도둑) && GET_OWN_CITY(LOCAL:0) == 0
			COUNTRY_IS_CLOSED:(LOCAL:0) = 1
		ELSEIF COUNTRY_EVENT_ID:(LOCAL:0) == SP_COUNTRY_ID:(특수세력_도둑) && GET_OWN_CITY(LOCAL:0) > 0
			COUNTRY_IS_CLOSED:(LOCAL:0) = 0
		ENDIF
	NEXT
	;모든 부대를 해산＆병 수를 최대치의 반에 조정
	FOR LOCAL:0, 1, MAX_COUNTRY
		IF IS_COUNTRY(LOCAL:0)
			CALL CLEAR_ALL_UNIT(LOCAL:0)
			COUNTRY_SOLDIER:(LOCAL:0) = GET_SUM_ECONOMY(LOCAL:0) / 20
		ENDIF
	NEXT
	;전도시의 방위해제
	FOR LOCAL:0, 1, MAX_CITY
		IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "무명"
			LOCAL:1 = CITY_OWNER:(LOCAL:0)
			COUNTRY_SOLDIER:(LOCAL:1) = MAX(COUNTRY_SOLDIER:(LOCAL:1) - 500, 0)
			CITY_SOLDIER:(LOCAL:0) = 500
			CITY_COMMANDER:(LOCAL:0) = 0
		ENDIF
	NEXT
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && MODE != 0
	MODE = 0
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 1 && MODE != 1
	MODE = 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 2
	LIST_POINT ++
	IF LIST_POINT >= LIST_MAX
		LIST_POINT = 0
	ENDIF
	T_COUNTRY = LIST_COUNTRY:LIST_POINT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 3 && T_COUNTRY != 0
	FOR LOCAL:0, 1, MAX_CITY
		IF CITY_TYPE:(LOCAL:0) == 0 && GET_CITYNAME(LOCAL:0) != "무명"
			CITY_OWNER:(LOCAL:0) = T_COUNTRY
		ENDIF
	NEXT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 10
	CALL EDIT_SET_ECONOMY(T_CITY)
	GOTO SHOW_LOOP
ELSEIF RESULT == 11
	CALL EDIT_SET_GUARD_RATE(T_CITY)
	GOTO SHOW_LOOP
ELSEIF RESULT >= 1000 && RESULT < 1000 + MAX_CITY
	LOCAL:2 = RESULT - 1000
	IF CITY_TYPE:(LOCAL:2) == 0 && GET_CITYNAME(LOCAL:2) != "무명"
		IF MODE == 0
			T_CITY = LOCAL:2
		ELSE
			CITY_OWNER:(LOCAL:2) = T_COUNTRY
		ENDIF
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;에디트용의 도시 정보 표시 함수
;-------------------------------------------------
@SHOW_CITY_INFO_EDIT(ARG:0)
IF ARG:0 == 0
	PRINTL 
	PRINTL 
	RETURN
ENDIF

LOCAL:6 = CITY_OWNER:(ARG:0)
LOCAL:7 = GET_COUNTRY_BOSS(LOCAL:6)
LOCALS:0 = ◆%GET_CITYNAME(ARG:0)%◆
PRINTFORM %LOCALS:0%%TOSTR_SPACE((10 - STRLENS(LOCALS:0)) / 2 * 2)% 소속:
IF LOCAL:7 >= 0
	SETCOLOR COUNTRY_COLOR:(LOCAL:6)
	LOCALS:0 = %ANAME(LOCAL:7)%
	PRINTFORM %LOCALS:0%
	RESETCOLOR
ELSE
	LOCALS:0 = ----
	PRINTFORM %LOCALS:0%
ENDIF
PRINTL 
LOCAL:8 = CITY_ECONOMY:(ARG:0) * 3 / 10000 + 100
PRINTFORM           경제 규모:{CITY_ECONOMY:(ARG:0) / 100, 4}/{CITY_ECONOMY_LIMIT:(ARG:0) / 100, 4}--
PRINTBUTTON "10[변경]", 10
PRINTFORM      방위배율:%DECIMAL_STRING(CITY_GUARD:(ARG:0), 2)%(도시)--
PRINTBUTTON "11[변경]", 11
PRINTL 

;-------------------------------------------------
;경제 규모의 설정
;-------------------------------------------------
@EDIT_SET_ECONOMY(ARG:0)
PRINTFORML 경제 규모를 설정해 주세요(1～9999)  현재:{CITY_ECONOMY:(ARG:0) / 100}
INPUT
LOCAL:0 = LIMIT(RESULT, 1, 9999)
PRINTFORML 경제 규모를 {LOCAL:0}로 설정했습니다

PRINTFORML 경제 규모의 기본 한계를 설정해 주세요({LOCAL:0}～9999)  현재:{CITY_ECONOMY_LIMIT:(ARG:0) / 100}
INPUT
LOCAL:1 = LIMIT(RESULT, LOCAL:0, 9999)
PRINTFORMW 경제 규모의 기본 한계를 {LOCAL:1}로 설정했습니다

CITY_ECONOMY:(ARG:0) = LOCAL:0 * 100
CITY_ECONOMY_LIMIT:(ARG:0) = LOCAL:1 * 100

;-------------------------------------------------
;방위배율의 설정
;-------------------------------------------------
@EDIT_SET_GUARD_RATE(ARG:0)
PRINTFORML 방위배율을 설정해 주세요(실제의 100배의 값을 입력  10～999)  현재:{CITY_GUARD:(ARG:0)}
INPUT
CITY_GUARD:(ARG:0) = LIMIT(RESULT, 10, 999)
PRINTFORMW 방위배율을{CITY_GUARD:(ARG:0)}로 설정했습니다
