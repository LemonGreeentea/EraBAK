;-------------------------------------------------
;●국가 정책의 결정
;정책 메모
;AI는 전용의 국책을 취득할 수 있게 된다
;-------------------------------------------------
@SLG_AI_SETDOCTRINE(세력)
#DIM 세력
#DIM 세력타입
#DIM 인접세력
#DIM 적과인접
#DIM 경제
#DIM 경제한계
#DIM 경제비율
#DIM 병력
#DIM 병력한계
#DIM 인접병력
#DIM 인접병력한계
#DIM 인접경제
#DIM 인접경제한계
#DIM 인접병력비
#DIM 인접경제비
#DIM 스코아, 3

VARSET 스코아
VARSET 인접경제
VARSET 인접경제한계
VARSET 인접병력
VARSET 인접병력한계
VARSET 적과인접
세력타입 = COUNTRY_AI_TYPE:세력
경제 = TMP_SUM_ECONOMY:세력
경제한계 = TMP_SUM_ECONOMY_LIMIT:세력
병력 = TMP_SUM_SOLDIER:세력
병력한계 = 경제 * 10

;인접하는 적대 세력 가운데, 최대의 경제와 최대의 병력을 산출한다
FOR 인접세력, 1, MAX_COUNTRY
	IF IS_COUNTRY(인접세력) && TMP_COUNTRY_IS_NEIBORING:세력:인접세력 && TMP_COUNTRY_RELATION:세력:인접세력 == 0
		적과인접 = 1
		인접경제 = MAX(TMP_SUM_ECONOMY:인접세력, 인접경제)
		인접병력 = MAX(TMP_SUM_SOLDIER:인접세력, 인접병력)
	ENDIF
NEXT


;적과 인접하고 있다
IF 적과인접
	인접경제비 = 경제 * 100 / MAX(인접경제, 1)
	인접병력비 = 병력 * 100 / MAX(인접병력, 1)
	;인접 세력과의 경제의 비 없고 병력의 비 가운데, 작다（뒤떨어지고 있다）편으로 생각한다
	SELECTCASE MIN(인접경제비, 인접병력비)
		;자신보다 강력
		CASE 0 TO 74
			스코아:정책_경제 = 인접경제비 * 2 / 3
			스코아:정책_징병 = 인접병력비
			스코아:정책_방위 = 인접병력비 * 2
			IF TMP_OWN_CITY:세력 < 3
				스코아:정책_방위 *= 2
			ENDIF
		;동일한 정도
		CASE 75 TO 124
			스코아:정책_경제 = 인접경제비
			스코아:정책_징병 = 인접병력비
			스코아:정책_방위 = 인접병력비
		;자군에서 빈약
		;방위를 경시해, 경제와 병력에 중점을 둔다
		CASEELSE
			스코아:정책_경제 = 인접경제비
			스코아:정책_징병 = 인접병력비
	ENDSELECT
	SIF TMP_OWN_CITY:세력 < 3
		스코아:정책_방위 *= 2
;적과 인접하고 있지 않다
;방위은 생각하지 않는다
ELSE
	스코아:정책_경제 = 10000 - (경제 * 10000 / 경제한계)
	스코아:정책_징병 = 10000 - (병력 * 10000 / 병력한계)
ENDIF

스코아:정책_경제 = 스코아:정책_경제 * AI_POLICY_PRIORITY:세력타입:정책_경제 / 100
스코아:정책_징병 = 스코아:정책_징병 * AI_POLICY_PRIORITY:세력타입:정책_방위 / 100
스코아:정책_방위 = 스코아:정책_방위 * AI_POLICY_PRIORITY:세력타입:정책_징병 / 100

COUNTRY_POLICY:세력 = FINDELEMENT(스코아, MAXARRAY(스코아))
