;-------------------------------------------------
;「알현한다」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME79
RESULTS:0 '= "만난다"

;-------------------------------------------------
;「알현한다」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK79
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 != 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「알현한다」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY79
CALL SLG_AUDIENCE
RETURN 1

;-------------------------------------------------
;「알현한다」
;-------------------------------------------------
@SLG_AUDIENCE
#DIM 대상세력
CALL SINGLE_DRAWLINE
PRINTFORML 각 세력의 군주와 알현 해, 관계 개선을 노립니다
PRINTFORML 누구에게 알현 합니까?
CALL SINGLE_DRAWLINE
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || COUNTRY_IS_CLOSED:LOCAL
		CONTINUE
	LOCAL:1 = GET_COUNTRY_BOSS(LOCAL)
	PRINTFORM [{LOCAL, 2, RIGHT}] %ANAME(LOCAL:1), 16, LEFT%
	PRINTFORM 호:{REL_LIKE:(LOCAL:1):MASTER, 6, LEFT}
	PRINTFORML 혐:{REL_HATE:(LOCAL:1):MASTER, 6, LEFT}
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [99] 돌아온다

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN 0
ELSEIF RESULT < 0 || MAX_COUNTRY < RESULT
	GOTO INPUT_LOOP
ELSEIF !IS_COUNTRY(RESULT) || COUNTRY_IS_CLOSED:RESULT
	GOTO INPUT_LOOP
ELSE
	대상세력 = RESULT
	CALL COLOR_PRINTW(@"%ANAME(GET_COUNTRY_BOSS(대상세력))%에 알현 했습니다", 칼라_주의)
	CALL CHANGE_RELATION_C_TO_O(대상세력, MASTER, RAND(50, 100), RAND(50, 100) * -1)
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:소속 != 대상세력
			CONTINUE
		CFLAG:LOCAL:호감도 += 100
	NEXT
	RESULT = 1
	BEGIN TURNEND
ENDIF

