;-------------------------------------------------
;사관을 이적시킨다
;-------------------------------------------------
@DIPLOMACY_GIVE_CHARA(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;자국의 사관의 수를 센다(군주·주인공·차용물 제외함)
LOCAL:6 = 0
FOR LOCAL:0, 0, CHARANUM
	CALL SELECT_CHARA_LIST_SHOW_LOGIC_GIVE_CHARA(LOCAL)
	IF RESULT
		LOCAL:6 = 1
		BREAK
	ENDIF
NEXT

IF LOCAL:6 <= 0
	PRINTFORMW 이적 당하는 사관이 없습니다（의존도 100이 필요합니다）
	RETURN 0
ENDIF

FIRST_LINE = LINECOUNT

$CHARA_LIST_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 이적시키는 사관을 선택해 주세요
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("GIVE_CHARA", "NONE")


SIF RESULT == -1
	RETURN 0

LOCAL:15 = RESULT

;대상 캐릭터의 정보를 표시
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(LOCAL:15)
CALL SINGLE_DRAWLINE
PRINTFORML 이 캐릭터를 이적시킵니까?

;네／아니오 입력 처리
CALL ASK_YN()
IF RESULT == 1
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO CHARA_LIST_LOOP
ENDIF

PRINTFORML %ANAME(LOCAL:15)%는 %ANAME(LOCAL:5)% 세력의 사관이 되었습니다
CALL CHANGE_COUNTRY(LOCAL:15, ARG:0, 1)
;호감도 상승치·적대치 하강치의 계산
;주로 능력에 의해 결정. 상대 군주가 그 캐릭터에게 좋은 인상을 받고 있으면 보너스
LOCAL:25 = 0
LOCAL:25 += ABL_POWER(ABL:(LOCAL:15):무투, -1) + ABL_POWER(ABL:(LOCAL:15):방위, -1) + ABL_POWER(ABL:(LOCAL:15):지략, -1) + ABL_POWER(ABL:(LOCAL:15):정치, -1)
LOCAL:25 += ABL_POWER(ABL:(LOCAL:15):가창, -1) + ABL_POWER(ABL:(LOCAL:15):요리, -1)
LOCAL:25 /= 500

IF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 800
	LOCAL:25 += 50
ELSEIF REL_LIKE:(LOCAL:5):(LOCAL:15) >= 300
	LOCAL:25 += 20
ENDIF

LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

;정치·요리의 보정
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;서로의 나라끼리의 호감도를 상승시키고 적대치를 저하시킨다
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
RETURN 1

@SELECT_CHARA_LIST_SHOW_LOGIC_GIVE_CHARA(대상)
#DIM 대상
RETURN CFLAG:대상:소속 == CFLAG:MASTER:소속 && 대상 != MASTER && 대상 != GET_COUNTRY_BOSS(CFLAG:대상:소속) && !CFLAG:대상:포로처 && CFLAG:대상:의존도 >= 100 && !IS_SP_CHARA(대상)
