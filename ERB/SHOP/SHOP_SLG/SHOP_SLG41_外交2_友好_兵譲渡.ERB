;-------------------------------------------------
;병을 양도한다
;-------------------------------------------------
@DIPLOMACY_GIVE_SOLDIER(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = COUNTRY_SOLDIER:(CFLAG:MASTER:소속)

PRINTFORML 총병수:  {GET_SUM_SOLDIER(CFLAG:MASTER:소속)}
PRINTFORML 방위병수:{GET_SUM_ARMY_DF(CFLAG:MASTER:소속)}
PRINTFORML 잔존병수:{LOCAL:6}
CALL SINGLE_DRAWLINE

IF LOCAL:6 < 500
	PRINTFORMW 최악이어도 500이상의 잔존병수가 필요합니다
	RETURN 0
ENDIF

PRINTFORML 양도하는 병수를 입력해 주세요(0 이하로 캔슬)

CALL DRAW_SOLDIER_INPUT(LOCAL:6)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 > 0 && LOCAL:2 < 500
	CLEARLINE 1
	PRINTL 최악이어도 500이상의 군사를 양도할 필요가 있습니다
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6
	CLEARLINE 1
	PRINTL 병 수가 부족합니다
	GOTO INPUT_LOOP
ENDIF

COUNTRY_SOLDIER:(CFLAG:MASTER:소속) -= LOCAL:2
COUNTRY_SOLDIER:(ARG:0) += LOCAL:2

PRINTFORMW %조사처리(ANAME(MASTER),"는")% %ANAME(LOCAL:5)%에 군사{LOCAL:2}를 양도했다

;호감도 상승치·적대치 하강치의 계산
;같은 병수에서도, 자국의 규모가 작을 정도(수록), 또 상대 세력의 규모가 작을 정도 효과가 높다
LOCAL:10 = GET_SUM_ECONOMY(CFLAG:MASTER:소속)
LOCAL:11 = GET_SUM_ECONOMY(ARG:0)
LOCAL:12 = SQRT(LOCAL:2 * 26000 / LOCAL:10) + SQRT(LOCAL:2 * 26000 / LOCAL:11)
LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;정치·요리의 보정
CALL TMP_CREATE_IS_FREE_MAP

LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;서로의 나라끼리의 호감도를 상승시키고 적대치를 저하시킨다
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

