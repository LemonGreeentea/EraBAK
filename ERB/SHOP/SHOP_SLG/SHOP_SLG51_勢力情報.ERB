;-------------------------------------------------
;「세력정보」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME51
RESULTS:0 '= "세력정보"

;-------------------------------------------------
;「세력정보」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK51
RETURN 1

;-------------------------------------------------
;「세력정보」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY51
CALL SHOW_COUNTRY_DATA
RETURN 1


;군사 화면의 세력정보 표시
;※대부분의 함수로 고속화용의 변수를 사용하고 있기 때문에 안이한 유용은 피하는 것!
;-------------------------------------------------
;「세력정보」본체
;-------------------------------------------------
@SHOW_COUNTRY_DATA
#DIM LIST_COUNTRY, MAX_COUNTRY
#DIM LIST_ECONOMY, MAX_COUNTRY
#DIM LIST_STARS, MAX_COUNTRY
#DIM LIST_MAX
#DIM SELECTED
#DIM FIRST_LINE
VARSET SELECTED

LIST_MAX = 0
REDRAW 0

;필요한 고속화용 맵의 작성
CALL TMP_CREATE_RELATION_MAP
CALL TMP_CREATE_COUNTRY_BOSS_MAP
CALL TMP_CREATE_UNION_TARGET_MAP
CALL TMP_PREPARE_CHARA_STARS
CALL TMP_PREPARE_COUNTRY_STARS
;세력을 경제 규모순서에 줄서 바꾸어 리스트에 기록
FOR LOCAL:0, 1, MAX_COUNTRY
	;세력의 존재 판정 다만 도시가 없을 때의 도둑은 제외한다
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:5 = GET_SUM_ECONOMY(LOCAL:0)
		LIST_COUNTRY:(LIST_MAX) = LOCAL:0
		LIST_ECONOMY:(LIST_MAX) = LOCAL:5
		FOR LOCAL:1, 0, LIST_MAX
			IF LOCAL:5 > LIST_ECONOMY:(LOCAL:1)
				FOR LOCAL:2, 0, LIST_MAX - LOCAL:1
					SWAP LIST_COUNTRY:(LIST_MAX - LOCAL:2 - 1), LIST_COUNTRY:(LIST_MAX - LOCAL:2)
					SWAP LIST_ECONOMY:(LIST_MAX - LOCAL:2 - 1), LIST_ECONOMY:(LIST_MAX - LOCAL:2)
				NEXT
				BREAK
			ENDIF
		NEXT
		LIST_MAX ++
	ENDIF
NEXT

FOR LOCAL:0, 0, LIST_MAX
	LOCAL:2 = 0
	LOCAL:5 = LIST_COUNTRY:(LOCAL:0)
	FOR LOCAL:1, 0, CHARANUM
		SIF CFLAG:(LOCAL:1):소속 == LOCAL:5 && TMP_CHARA_STARS:(LOCAL:1) >= 7
			LOCAL:2 ++
	NEXT
	LIST_STARS:(LOCAL:0) = LOCAL:2
NEXT

;표시부분 개시
FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE

PRINTFORM %"군주",MAX_CHARANAME_LENGTH / 2 + 2,LEFT%
PRINTFORM %"경제", 6,RIGHT%
PRINTFORM %"국고", 6,RIGHT%
PRINTFORM %"병력", 7, RIGHT%
PRINTFORM %"경향", 6, RIGHT%
PRINTFORM %"도시", 6, RIGHT%
PRINTFORM %"사관", 6, RIGHT%
PRINTFORM %"★7↑", 8, RIGHT%
PRINTFORM %"관계", 8, RIGHT%
PRINTFORML %"평가", 13, RIGHT%

FOR LOCAL:0, 0, LIST_MAX
	LOCAL:5 = LIST_COUNTRY:(LOCAL:0)
	
	; IF CFLAG:MASTER:소속 == LOCAL:5
		; SETCOLOR 칼라_주의
	; ENDIF
	PRINT
	
	SETCOLOR COUNTRY_COLOR:(LOCAL:5)　
	
	PRINTBUTTON @"[%SNAME(TMP_COUNTRY_BOSS_NUMBER:(LOCAL:5)), MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL:5
	RESETCOLOR
	
	PRINTFORM {LIST_ECONOMY:(LOCAL:0) / 100, 6, RIGHT}
	PRINTFORM {MONEY:(LOCAL:5), 6, RIGHT}
	PRINTFORM {GET_SUM_SOLDIER(LOCAL:5), 7, RIGHT}
	PRINTFORM %TOSTR_AI_TYPE(COUNTRY_AI_TYPE:(LOCAL:5)), 6, RIGHT%
	PRINTFORM {GET_OWN_CITY(LOCAL:5), 6, RIGHT}
	PRINTFORM {GET_COMMANDER_NUM(LOCAL:5), 6, RIGHT}
	PRINTFORM {LIST_STARS:(LOCAL:0), 8, RIGHT}
	IF LOCAL:5 != CFLAG:MASTER:소속
		CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:소속, LOCAL:5)
		PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:소속, LOCAL:5, 1), 8, RIGHT%
	ELSE
		PRINTFORM %TOSTR_REPEAT(" ", 8)%
	ENDIF
	PRINT    
	CALL TMP_PRINT_COUNTRY_STARS(LOCAL:5)
	PRINTL 
	RESETCOLOR
NEXT
IF IS_COUNTRY(SELECTED)
	CALL SINGLE_DRAWLINE
	PRINTBUTTON @"[그림 \@ FLAG:화상설정 ? 표시 # 비표시 \@]", 98
	PRINTL
	SIF FLAG:화상설정
		CALL PRINT_STATE_IMAGE(GET_COUNTRY_BOSS(SELECTED))
	PRINTFORM %"★" + ANAME(GET_COUNTRY_BOSS(SELECTED))+ "★", 20, LEFT% 평가:
	CALL TMP_PRINT_COUNTRY_STARS(SELECTED)
	PRINTFORML  경제:{GET_SUM_ECONOMY(SELECTED)/100, 6, LEFT} 병력:{GET_SUM_SOLDIER(SELECTED), 7, LEFT} 도시:{GET_OWN_CITY(SELECTED), 3, LEFT}
	CALL SINGLE_DRAWLINE
	PRINTFORM 소속 무장:
	PRINTFORM %ANAME(GET_COUNTRY_BOSS(SELECTED))%  
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:(LOCAL):소속 == SELECTED && LOCAL != GET_COUNTRY_BOSS(SELECTED)
			PRINTFORM %ANAME(LOCAL)%  
	NEXT
	PRINTL
	PRINTFORM 포로:
	FOR LOCAL, 0, CHARANUM
		SIF CFLAG:LOCAL:포로처 == SELECTED
			PRINTFORM %ANAME(LOCAL)%  
	NEXT
	PRINTL
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON " 99[관계일람]", 99
PRINTL 
PRINTBUTTON " 0[ 돌아온다]", 0
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 99
	CALL SINGLE_DRAWLINE
	CALL TMP_SHOW_COUNTRY_RELATION1
	WAIT
	CALL SINGLE_DRAWLINE
	CALL TMP_SHOW_COUNTRY_RELATION2
	CALL SINGLE_DRAWLINE
	WAIT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 98
	FLAG:화상설정 = !FLAG:화상설정
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF IS_COUNTRY(RESULT)
	SELECTED = RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 0
	REDRAW 1
	RETURN
ELSE
	GOTO INPUT_LOOP
ENDIF

;-------------------------------------------------
;ARG:0 세력의 ARG:1 세력에 대한 관계를 문자열으로서 돌려주는 함수
;ARG:2가 0이라면 전각 2문자의 약식, 1이라면 전각 4문자의 문자열이 돌아간다
;-------------------------------------------------
@TMP_TOSTR_COUNTRY_RELATION(ARG:0, ARG:1, ARG:2)
#FUNCTIONS
;정식
IF ARG:2
	SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1)
		CASE 4
			RETURNF "영구 동맹"
		CASE 3
			RETURNF "동맹" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 2
			RETURNF "연합" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 1
			RETURNF "정전" + "(" + TOSTR(TMP_COUNTRY_RELATION_TERM:(ARG:0):(ARG:1)) + ")"
		CASE 0
			IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
				RETURNF "토벌대상"
			ENDIF
	ENDSELECT
	RETURNF "――"
;약식
;0-4  :2문자 표기
;5-9  :1문자 표기
;10-14:기호 표기

ELSE
	SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1) + CONFIG:319 * 5
		CASE 4
			RETURNF "영"
		CASE 9
			RETURNF "∞"
		CASE 3
			RETURNF "맹"
		CASE 8
			RETURNF "＋"
		CASE 2
			RETURNF "련"
		CASE 7
			RETURNF "‡"
		CASE 1
			RETURNF "정"
		CASE 6
			RETURNF "∴"
		CASE 0, 5, 10
			IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
				SELECTCASE CONFIG:319
				CASE 0
					RETURNF "토"
				CASE 2
					RETURNF "×"
				ENDSELECT
			ENDIF
	ENDSELECT
	RETURNF "・"
ENDIF

;-------------------------------------------------
;ARG:0 세력의 ARG:1 세력에 대한 관계를 조사해, 거기에 응한 문자 칼라를 설정하는 함수
;-------------------------------------------------
@TMP_SETCOLOR_COUNTRY_RELATION(ARG:0, ARG:1)
SELECTCASE TMP_COUNTRY_RELATION:(ARG:0):(ARG:1)
	CASE 4
		SETCOLOR 칼라_영구동맹
	CASE 3
		SETCOLOR 칼라_동맹
	CASE 2
		SETCOLOR 칼라_연합
	CASE 1
		SETCOLOR 칼라_정전
	CASE 0
		IF TMP_COUNTRY_UNION_TARGET:(ARG:0) == ARG:1
			SETCOLOR 칼라_주의
		ELSE
			SETCOLOR 칼라_적
		ENDIF
ENDSELECT

;-------------------------------------------------
;세력 관계의 표시
;※TMP_CREATE_BOSS_NAME_MAP 함수로 군주략명 맵을 사전에 작성해 둘 필요가 있다
;-------------------------------------------------
@TMP_SHOW_COUNTRY_RELATION1
PRINT     
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 3, LEFT%
		RESETCOLOR
	ENDIF
NEXT

;  
PRINTL 
PRINT    
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		PRINTFORM ---
	ENDIF
NEXT
PRINTL

FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && GET_OWN_CITY(LOCAL:0) > 0
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 2, LEFT%
		RESETCOLOR
		PRINT |
		FOR LOCAL:1, 1, MAX_COUNTRY
			IF TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1) >= 0 && GET_OWN_CITY(LOCAL:1) > 0
				PRINT  
				IF LOCAL:0 == LOCAL:1
					SETCOLOR 칼라_선택불가
					PRINT ―
				ELSE
					CALL TMP_SETCOLOR_COUNTRY_RELATION(LOCAL:0, LOCAL:1)
					PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(LOCAL:0, LOCAL:1, 0)%
				ENDIF
				RESETCOLOR
			ENDIF
		NEXT
		PRINTL 
	ENDIF
NEXT

IF CONFIG:319
	PRINT 범례　

	SETCOLOR 칼라_영구동맹
	PRINTFORM \@CONFIG:319 ? ∞ # 영 \@:영구 동맹
	RESETCOLOR
	PRINT ｜

	SETCOLOR 칼라_동맹
	;PRINT ∽:동맹
	PRINTFORM \@CONFIG:319 ? ＋ # 맹 \@:동맹
	RESETCOLOR
	PRINT ｜

	SETCOLOR 칼라_오렌지
	PRINTFORM \@CONFIG:319 ? ‡ # 련 \@:연합
	RESETCOLOR
	PRINT ｜

	SETCOLOR 칼라_정전
	PRINTFORM \@CONFIG:319 ? ∴ # 정 \@:정전
	RESETCOLOR
	PRINT ｜

	SETCOLOR 칼라_토벌
	PRINTFORM \@CONFIG:319 ? × # 토 \@:토벌
	RESETCOLOR
	PRINT ｜

	SETCOLOR 칼라_적
	PRINTL ・:통상
	RESETCOLOR
ENDIF
;-------------------------------------------------
;세력간의 공감치·평가치의 표시
;※TMP_CREATE_BOSS_NAME_MAP 함수로 군주략명 맵을 사전에 작성해 둘 필요가 있다
;-------------------------------------------------
@TMP_SHOW_COUNTRY_RELATION2
PRINTFORML 「행(세로축)에서 본 열(가로축)」에의 인상을 나타내고 있습니다.
PRINTFORML
PRINT     
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(특수세력_도둑) || GET_OWN_CITY(LOCAL:0) > 0)
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 3, LEFT%
		RESETCOLOR
	ENDIF
NEXT


;  
PRINTL 
PRINT    
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(특수세력_도둑) || GET_OWN_CITY(LOCAL:0) > 0)
		PRINTFORM ---
	ENDIF
NEXT
PRINTL

FOR LOCAL:0, 1, MAX_COUNTRY
	LOCAL:5 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:0)
	IF LOCAL:5 >= 0 && (COUNTRY_EVENT_ID:(LOCAL:0) != SP_COUNTRY_ID:(특수세력_도둑) || GET_OWN_CITY(LOCAL:0) > 0)
		SETCOLOR COUNTRY_COLOR:(LOCAL:0)　
		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 2, LEFT%
		RESETCOLOR
		PRINT |
		FOR LOCAL:1, 1, MAX_COUNTRY
			LOCAL:6 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1)
			IF LOCAL:6 >= 0 && GET_OWN_CITY(LOCAL:1) > 0
				CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:6)
			ENDIF
		NEXT
		PRINTL 
	ENDIF
NEXT

IF CONFIG:319
	PRINT 범례　

	SETCOLOR 0x12eca3
	PRINTFORM \@CONFIG:319 ? ◎ # 우 \@:우호
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x23b1b1
	PRINTFORM \@CONFIG:319 ? ○ # 호 \@:호의
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x9528cc
	PRINTFORM \@CONFIG:319 ? ◇ # 주 \@:주시
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x482889
	PRINTFORM \@CONFIG:319 ? ⇔ # 라 \@:호적수
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x808080
	PRINT ・:중립
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x8600c8
	PRINTFORM \@CONFIG:319 ? ≫ # 적 \@:천적
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0x842c2c
	PRINTFORM \@CONFIG:319 ? † # 악 \@:험악
	RESETCOLOR
	PRINT ｜

	SETCOLOR 0xff0000
	PRINTFORM \@CONFIG:319 ? ≠ # 원 \@:구적
	RESETCOLOR

	IF !RAND:500
		PRINT ｜

		SETCOLOR 0x333333
		PRINTFORM ⑨:바보
		RESETCOLOR
	ENDIF
	PRINTL
ENDIF

;●디버그용의 관계 상세 표시
;FOR LOCAL:0, 1, MAX_COUNTRY
;	LOCAL:5 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:0)
;	IF LOCAL:5 >= 0
;		PRINTFORM %TMP_COUNTRY_BOSS_NAME_SHORT:(LOCAL:0), 5, LEFT%
;		FOR LOCAL:1, 1, MAX_COUNTRY
;			LOCAL:6 = TMP_COUNTRY_BOSS_NUMBER:(LOCAL:1)
;			IF LOCAL:6 >= 0
;				LOCAL:10 = REL_LIKE:(LOCAL:5):(LOCAL:6)
;				LOCAL:11 = REL_HATE:(LOCAL:5):(LOCAL:6)
;				LOCAL:12 = (LOCAL:10 >= 800) + (LOCAL:10 >= 300)
;				LOCAL:13 = (LOCAL:11 >= 800) + (LOCAL:11 >= 300)
;				PRINTFORM {LOCAL:10/10, 3}{LOCAL:11/10, 3}
;			ENDIF
;		NEXT
;		PRINTL 
;	ENDIF
;NEXT

;-------------------------------------------------
;군주 ARG:0의 군주 ARG:1에 대한 인상을 표시하는 함수
;세력 번호는 아니고, 군주의 캐릭터 번호를 건네주는 점에 주의
;-------------------------------------------------
@TMP_PRINT_COUNTRY_RELATION2(ARG:0, ARG:1)
#DIMS REL_STR, 27
#DIM  REL_NUM
LOCAL:10 = REL_LIKE:(ARG:0):(ARG:1)
LOCAL:11 = REL_HATE:(ARG:0):(ARG:1)
LOCAL:12 = (LOCAL:10 >= 800) + (LOCAL:10 >= 300)
LOCAL:13 = (LOCAL:11 >= 800) + (LOCAL:11 >= 300)

REL_NUM = CONFIG:319 * 9 + LOCAL:12 * 3 + LOCAL:13

REL_STR:0  '= " ・", " 험악", " 원한", " 호감", " 주시", " 천적", " 우호", " 라", " 라"
REL_STR:9 '= " ・", " †", " ≠", " ○", " ◇", " ≫", " ◎", " ⇔", " ⇔"
IF ARG:0 == ARG:1
	SETCOLOR 칼라_선택불가
	PRINT  ―
ELSEIF CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속
	SETCOLOR 칼라_선택불가
	PRINT  ☆
ELSEIF LOCAL:12 == 2 && LOCAL:13 == 2
	SETCOLOR 0x482889
;	PRINT 호적수 
;	PRINT  ⇔  
	PRINTS REL_STR:REL_NUM

ELSEIF LOCAL:12 == 2 && LOCAL:13 == 1
	SETCOLOR 0x482889
;	PRINT 호적수 
;	PRINT  ⇔  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 2 && LOCAL:13 == 0
	SETCOLOR 0x12eca3
;	PRINT 우호 
;	PRINT  ◎  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 2
	SETCOLOR 0x8600c8
;	PRINT 천적 
;	PRINT  ≫  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 1
	SETCOLOR 0x9528cc
;	PRINT 주시 
;	PRINT  ◇  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 1 && LOCAL:13 == 0
	SETCOLOR 0x23b1b1
;	PRINT 호의 
;	PRINT  ○  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 0 && LOCAL:13 == 2
	SETCOLOR 0xff0000
;	PRINT 구적 
;	PRINT  ≠  
	PRINTS REL_STR:REL_NUM
ELSEIF LOCAL:12 == 0 && LOCAL:13 == 1
	SETCOLOR 0x842c2c
;	PRINT 험악 
;	PRINT  †  
	PRINTS REL_STR:REL_NUM
ELSE
	SETCOLOR 0x808080
;	PRINT 중립 
	PRINTS REL_STR:REL_NUM
ENDIF
RESULTS = %REL_STR:REL_NUM%
RESETCOLOR
