;-------------------------------------------------
;「징병 해고」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME62
RESULTS:0 '= "용병관리"

;-------------------------------------------------
;「징병 해고」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK62
SIF FLAG:클리어플래그
	RETURN 0
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「징병 해고」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY62
CALL SLG_EXTRA_DRAFT
RETURN 1

;-------------------------------------------------
;「징병한다」의 다음 행 설명 지정 유무
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD62

;-------------------------------------------------
;「징병한다」의 다음 행 설명 지정 내용
;-------------------------------------------------
@SHOP_SLG_NEXT_LINE_ADD_TEXT62
PRINTFORM %"레이트:" + NUM_FORMAT(SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:소속)), 12%

;------------------------------------------------
;「징병 해고」본체
;-------------------------------------------------
@SLG_EXTRA_DRAFT()
#DIM 고용레이트
#DIM 해고레이트
#DIM 고용병수한계
#DIM 입력치
#DIM 병수
고용레이트 = SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:소속)
해고레이트 = MAX(고용레이트 / 2, 1)
병수 = COUNTRY_SOLDIER:(CFLAG:MASTER:소속)
$SHOW_LOOP
CALL SINGLE_DRAWLINE
PRINTFORML 국고를 소비해 임시에 징병할 수가 있습니다
PRINTFORML 음수를 입력하면 해고해 자금을 얻을 수도 있습니다
PRINTFORML 몇 사람 고용/해고합니까? (0을 입력하면 돌아온다)

PRINTFORM 현재의 국고:
SETCOLOR 칼라_선택중
PRINTFORM {MONEY:(CFLAG:MASTER:소속)}
RESETCOLOR
PRINT    
PRINTFORM 고용 레이트:
SETCOLOR 칼라_선택중
PRINTFORM {고용레이트}
RESETCOLOR
PRINTFORM /군사
PRINT    
PRINTFORM 해고 레이트:
SETCOLOR 칼라_선택중
PRINTFORM {해고레이트}
RESETCOLOR
PRINTFORM /군사
PRINT    
PRINTFORM 현재의 병수（방위·부대 소속 제외하다）:
SETCOLOR 칼라_선택중
PRINTFORM {병수}
RESETCOLOR
PRINTL 
CALL SINGLE_DRAWLINE
IF MONEY:(CFLAG:MASTER:소속) - 100 * 고용레이트 > 0
	PRINTFORM [{100, 8}] {100, 8}징병
	PRINTFORML   병 수:{병수, 10} + {100, 10} = {병수 + 100, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {100 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 100 * 고용레이트, 10}
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [{100, 8}] {100, 8}징병
	PRINTFORML   병 수:{병수, 10} + {100, 10} = {병수 + 100, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {100 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 100 * 고용레이트, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:소속) - 1000 * 고용레이트 > 0
	PRINTFORM [{1000, 8}] {1000, 8}징병
	PRINTFORML   병 수:{병수, 10} + {1000, 10} = {병수 + 1000, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {1000 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 1000 * 고용레이트, 10}
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [{1000, 8}] {1000, 8}징병
	PRINTFORML   병 수:{병수, 10} + {1000, 10} = {병수 + 1000, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {1000 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 1000 * 고용레이트, 10}
	RESETCOLOR
ENDIF

IF MONEY:(CFLAG:MASTER:소속) - 10000 * 고용레이트 > 0
	PRINTFORM [{10000, 8}] {10000, 8}징병
	PRINTFORML   병 수:{병수, 10} + {10000, 10} = {병수 + 10000, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {10000 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 10000 * 고용레이트, 10}
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [{10000, 8}] {10000, 8}징병
	PRINTFORML   병 수:{병수, 10} + {10000, 10} = {병수 + 10000, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {10000 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 10000 * 고용레이트, 10}
	RESETCOLOR
ENDIF

고용병수한계 = MONEY:(CFLAG:MASTER:소속) / 고용레이트
LOCALS:0 = 한계까지
IF 고용병수한계 > 0
	PRINTFORM [{고용병수한계, 8}] %LOCALS:0, 8%징병
	PRINTFORML   병 수:{병수, 10} + {고용병수한계, 10} = {병수 + 고용병수한계, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {고용병수한계 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 고용병수한계 * 고용레이트, 10}
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [{고용병수한계, 8}] %LOCALS:0, 8%징병
	PRINTFORML   병 수:{병수, 10} + {고용병수한계, 10} = {병수 + 고용병수한계, 10}  자금:{MONEY:(CFLAG:MASTER:소속), 10} - {고용병수한계 * 고용레이트, 10} = {MONEY:(CFLAG:MASTER:소속) - 고용병수한계 * 고용레이트, 10}
	RESETCOLOR
ENDIF
CALL SINGLE_DRAWLINE
PRINTFORML [{0, 3}] 역시 그만둔다
CALL SINGLE_DRAWLINE
$INPUT_LOOP
INPUT 0
입력치 = RESULT
IF 입력치 == 0
	RETURN
ELSEIF 입력치 > 0 && MONEY:(CFLAG:MASTER:소속) >= 고용레이트 * 입력치
	CALL ICPRINT(@"국고로부터<{입력치 * 고용레이트}>를 거출해, {입력치}의 군사를 고용합니다.", "L", 칼라_주의)
	CALL ICPRINT(@"고용 후의 국고 자금은 {MONEY:(CFLAG:MASTER:소속) - 입력치 * 고용레이트}입니다. 좋습니까?", "L", 칼라_주의)
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 병을{입력치}인 고용했습니다!
		PRINTFORML 병 수:{병수} → {병수 + 입력치}
		PRINTFORMW 자금:{MONEY:(CFLAG:MASTER:소속)} → {MONEY:(CFLAG:MASTER:소속) - 입력치 * 고용레이트}
		COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += 입력치
		MONEY:(CFLAG:MASTER:소속) -= 입력치 * 고용레이트
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 입력치 < 0 && 입력치 * -1 <= 병수
	입력치 *= -1
	PRINTFORM 병
	CALL COLOR_PRINT(@"{입력치}", 칼라_주의)
	PRINTFORM 를 해고해,
	CALL COLOR_PRINT(@"{입력치 * 해고레이트}", 칼라_주의)
	PRINTFORML 의 자금을 얻습니다.
	PRINTFORM 해고 후의 자금은
	CALL COLOR_PRINT(@"{MONEY:(CFLAG:MASTER:소속) + 입력치 * 해고레이트}", 칼라_주의)
	PRINTFORML 입니다.
	PRINTFORML 좋습니까?
	CALL ASK_YN
	IF RESULT == 0
		PRINTFORML 병을{입력치}인 해고했습니다!
		PRINTFORML 병 수:{병수} → {병수 - 입력치}
		PRINTFORMW 자금:{MONEY:(CFLAG:MASTER:소속)} → {MONEY:(CFLAG:MASTER:소속) + 입력치 * 해고레이트}
		COUNTRY_SOLDIER: (CFLAG:MASTER:소속) -= 입력치
		MONEY:(CFLAG:MASTER:소속) += 입력치 * 해고레이트
	ELSE
		GOTO SHOW_LOOP
	ENDIF
	RETURN
ELSEIF 입력치 > 0 && MONEY:(CFLAG:MASTER:소속) < 고용레이트 * 입력치
	PRINTFORML 필요 자금은 {입력치 * 고용레이트}입니다만, 그렇게 자금이 없습니다
	GOTO SHOW_LOOP
ELSEIF 입력치 < 0 && 입력치 * -1 > 병수
	입력치 *= -1
	PRINTFORML {입력치}해고하려고 했습니다만, 그렇게 군사를 가지고 있지 않습니다
	GOTO SHOW_LOOP
ELSE
ENDIF

;-------------------------------------------------
;징병 해고의 고용 레이트 계산
;-------------------------------------------------
@SLG_EXTRA_DRAFT_CALC_RATE(세력)
#FUNCTION
#DIM 도시레이트기준
#DIM 고용레이트
#DIM 세력
#DIM COPY_SUM_ECONOMY, MAX_COUNTRY

SELECTCASE TMP_COUNTRY_STARS:세력
	CASE 5
		고용레이트 = 3
	CASE 3, 4
		고용레이트 = 2
	CASE 1, 2
		고용레이트 = 1
ENDSELECT

도시레이트기준 = MAX(GET_CITY_NUM() / 10, 1)
고용레이트 += MAX(TMP_OWN_CITY:세력 / 도시레이트기준, 0)
;고용 레이트 += DAY/10

RETURNF MAX(고용레이트, 1)
