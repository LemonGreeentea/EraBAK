;-------------------------------------------------
;「방위해제」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME32
RESULTS:0 '= "방어해제"

;-------------------------------------------------
;「방위해제」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK32
SIF FLAG:클리어플래그
	RETURN 0
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
SIF CONFIG:302 != 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「방위해제」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY32
CALL MINIMIZE_GUARD
RETURN 1

;-------------------------------------------------
;「방위해제」본체
;-------------------------------------------------
@MINIMIZE_GUARD
CALL SINGLE_DRAWLINE
SETCOLOR 0xFF6000
PRINT 적부대가 존재하는 도시를 제외한 
RESETCOLOR
PRINTL 모든 도시의 방위병수를 500으로 해, 수장을 해제합니다
PRINTL 좋습니까?

CALL ASK_YN("네", "캔슬")
IF RESULT == 0
	;부대 맵을 작성
	CALL TMP_CREATE_UNIT_MAP
	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:소속 && GET_CITYNAME(LOCAL:0) != "무명" && !TMP_IS_STAY_ENEMY_UNIT(LOCAL:0, CFLAG:MASTER:소속)
			IF CITY_SOLDIER:(LOCAL:0) > 500
				COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += CITY_SOLDIER:(LOCAL:0) - 500
				CITY_SOLDIER:(LOCAL:0) = 500
			ENDIF
			;이 턴에 할당했을 것은 아니면 쿨 타임 부여
			SIF GET_CITY_COMMANDER(LOCAL:0, 0) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(LOCAL:0, 0)):0
				COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 0):0 = 1
			SIF GET_CITY_COMMANDER(LOCAL:0, 1) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(LOCAL:0, 1)):0
				COOLTIME:GET_CITY_COMMANDER(LOCAL:0, 1):0 = 1
			CITY_COMMANDER:(LOCAL:0) = 0
		ENDIF
	NEXT
ENDIF

@SINGLE_MINIMIZE(ARG)
IF TMP_IS_STAY_ENEMY_UNIT(ARG, CFLAG:MASTER:소속)
	PRINTW 적부대가 존재하는 도시에는 실행할 수 없습니다
ELSEIF CITY_OWNER:(ARG) == CFLAG:MASTER:소속 && GET_CITYNAME(ARG) != "무명"
	IF CITY_SOLDIER:(ARG) > 500
		COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += CITY_SOLDIER:(ARG) - 500
		CITY_SOLDIER:(ARG) = 500
	ENDIF
	;이 턴에 할당했을 것은 아니면 쿨 타임 부여
	SIF GET_CITY_COMMANDER(ARG, 0) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(ARG:0, 0)):0
		COOLTIME:GET_CITY_COMMANDER(ARG, 0):0 = 1
	SIF GET_CITY_COMMANDER(ARG, 1) >= 0 && !ASSIGNED_THIS_TURN:(GET_CITY_COMMANDER(ARG:0, 1)):0
		COOLTIME:GET_CITY_COMMANDER(ARG, 1):0 = 1
	CITY_COMMANDER:(ARG) = 0
	PRINTW 이 도시의 방위를 해제했습니다
ENDIF

