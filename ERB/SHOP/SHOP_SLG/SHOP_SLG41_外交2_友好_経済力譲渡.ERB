;-------------------------------------------------
;경제력을 양도한다
;-------------------------------------------------
@DIPLOMACY_GIVE_ECONOMY(ARG:0)
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:소속)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)

LOCAL:20 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:21 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

PRINTFORML %ANAME(LOCAL:4), LOCAL:20%의 경제력:{LOCAL:6 / 100, LOCAL:21}
PRINTFORML %ANAME(LOCAL:5), LOCAL:20%의 경제력:{LOCAL:7 / 100, LOCAL:21}
CALL SINGLE_DRAWLINE

IF LOCAL:6 < 250
	PRINTFORMW 최악이어도 250이상의 경제력이 필요합니다
	RETURN 0
ENDIF

PRINTFORML 양도하는 경제력을 입력해 주세요（0 이하로 캔슬, 최대로 경제력의 20％까지）

CALL DRAW_ECONOMY_INPUT(LOCAL:6 / 500)

$INPUT_LOOP
INPUT

LOCAL:2 = RESULT

IF LOCAL:2 <= 0
	RETURN 0
ELSEIF LOCAL:2 < 50
	CLEARLINE 1
	PRINTL 최악이어도 50이상의 경제력을 양도할 필요가 있습니다
	GOTO INPUT_LOOP
ELSEIF RESULT > LOCAL:6 / 500
	CLEARLINE 1
	PRINTFORML 상한을 넘고 있습니다({LOCAL:6 / 500}까지)
	GOTO INPUT_LOOP
ENDIF

;경제력의 이동 처리
CALL SHIFT_ECONOMY(CFLAG:MASTER:소속, ARG:0, LOCAL:2 * 100)

PRINTFORMW %조사처리(ANAME(MASTER),"는")% %ANAME(LOCAL:5)%에 경제력{LOCAL:2}를 양도했다

;호감도 상승치·적대치 하강치의 계산
;같은 경제력에서도, 자국의 규모가 작을 정도(수록), 또 상대 세력의 규모가 작을 정도 효과가 높다
LOCAL:12 = SQRT(LOCAL:2 * 230000 / LOCAL:6) + SQRT(LOCAL:2 * 230000 / LOCAL:7)

LOCAL:12 = LIMIT(LOCAL:12, 25, 350)

;정치·요리의 보정
CALL TMP_CREATE_IS_FREE_MAP
LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:소속)) / 10
LOCAL:12 = LOCAL:12 * (LOCAL:0 + LOCAL:1 + 100) / 100

;서로의 나라끼리의 호감도를 상승시키고 적대치를 저하시킨다
CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:12)
RETURN 1

