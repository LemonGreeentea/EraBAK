;외교 관계의 기본 부분

;-------------------------------------------------
;「외교」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME41
RESULTS:0 '= "외교"

;-------------------------------------------------
;「외교」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK41
SIF FLAG:클리어플래그
	RETURN 0
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
SIF DAY < SLG_PP:0
	RETURN 0
SIF COUNTRY_IS_CLOSED:(CFLAG:MASTER:소속)
	RETURN 0
RETURN 1

;-------------------------------------------------
;「외교」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY41
CALL DIPLOMACY
RETURN 1

;-------------------------------------------------
;「외교」본체
;-------------------------------------------------
@DIPLOMACY
#DIM FIRST_LINE
FIRST_LINE = LINECOUNT
IF DAY < SLG_PP:0
	PRINTFORMW {SLG_PP:0}기까지는 외교를 실시할 수가 없습니다
	RETURN
ENDIF

IF COUNTRY_NOTDIPLOMACY_TERM:(CFLAG:MASTER:소속)
	PRINTFORMW 현재 우리 세력과 외교를 실시하려고 하는 세력이 없습니다（잔{COUNTRY_NOTDIPLOMACY_TERM:(CFLAG:MASTER:소속),2}턴）
	RETURN
ENDIF

;국가간의 인접 관계 맵의 작성(처리의 고속화)
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
;세력 관계 맵을 작성
CALL TMP_CREATE_RELATION_MAP
CALL TMP_CREATE_IS_FREE_MAP
CALL TMP_CREATE_POLITICS_POWER_MAP
CALL TMP_CREATE_SUM_SOLDIER_MAP

CALL SINGLE_DRAWLINE
PRINTL 외교를 실시하는 세력을 선택해 주세요
PRINTFORML 현재의 외교 경계치:{DIPLOMACY_HATE:(CFLAG:MASTER:소속)}
CALL SINGLE_DRAWLINE
PRINTFORM %"군주",10,LEFT%
PRINTFORM %TOSTR_SPACE(6)%
PRINTFORM %"관계",10, RIGHT%
PRINTFORM %"인접",6, RIGHT%
PRINTFORM %"인상",6, RIGHT%
PRINTFORM %"우호", 7, RIGHT%
PRINTFORM %"적대", 7, RIGHT%
PRINTFORM %"붙잡고 있는", 26, RIGHT%
PRINTFORML %"붙잡혀 있는", 26, RIGHT%

;외 세력 일람의 표시
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:소속
	
		LOCALS:0 = %SNAME(GET_COUNTRY_BOSS(LOCAL:0))%
		;외교 금지 플래그가 서 있다면 회색 표시
		IF COUNTRY_IS_CLOSED:(LOCAL:0)
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]
			PRINTBUTTON "[정보]", LOCAL:0 + 1000
			RESETCOLOR
		ELSE
			SETCOLOR COUNTRY_COLOR:(LOCAL:0)
			PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH / 2, LEFT%]", LOCAL:0
			PRINTBUTTON "[정보]", LOCAL:0 + 1000
			RESETCOLOR
		ENDIF

		CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:소속, LOCAL:0)
		PRINTFORM %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:소속, LOCAL:0, 1), 10, RIGHT%
		RESETCOLOR
		PRINTFORM %TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:소속):LOCAL == 1 ? "Ο" # "Χ", 6, RIGHT%
		LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
		LOCAL:5 = GET_COUNTRY_BOSS(LOCAL:0)
		PRINT    
		CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:4)

;		PRINTFORM 
		PRINTFORM {REL_LIKE:(LOCAL:5):(LOCAL:4), 7, RIGHT}
		PRINTFORM {REL_HATE:(LOCAL:5):(LOCAL:4), 7, RIGHT}

		;붙잡고 있는 포로의 표시
		LOCALS:1 = 
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):소속 == LOCAL:0 && CFLAG:(LOCAL:1):포로처 == CFLAG:MASTER:소속
				IF LOCALS:1 == ""
					LOCALS:1 = %ANAME(LOCAL:1)%
				ELSE
					IF STRLENS(@"%LOCALS:1%、%ANAME(LOCAL:1)%") >= 24
						LOCALS:1 = %LOCALS:1%, 외
						BREAK
					ELSE
						LOCALS:1 = %LOCALS:1%、%ANAME(LOCAL:1)%
					ENDIF
				ENDIF
			ENDIF
		NEXT
		SETCOLOR 칼라_오렌지
		PRINTFORM %LOCALS:1, 26, RIGHT%
		RESETCOLOR

		;붙잡혀 있는 포로의 표시
		LOCALS:1 = 
		FOR LOCAL:1, 0, CHARANUM
			IF CFLAG:(LOCAL:1):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:1):포로처 == LOCAL:0
				IF LOCALS:1 == ""
					LOCALS:1 = %ANAME(LOCAL:1)%
				ELSE
					IF STRLENS(@"%LOCALS:1%、%ANAME(LOCAL:1)%") >= 24
						LOCALS:1 = %LOCALS:1%, 외
						BREAK
					ELSE
						LOCALS:1 = %LOCALS:1%、%ANAME(LOCAL:1)%
					ENDIF
				ENDIF
			ENDIF
		NEXT
		SETCOLOR 칼라_시안
		PRINTFORM %LOCALS:1, 26, RIGHT%
		RESETCOLOR
		PRINTL 
	ENDIF
NEXT
CALL SINGLE_DRAWLINE
PRINTBUTTON "98[자동 송금 설정]",98
PRINTL
PRINTBUTTON "99[돌아온다]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT
IF INRANGE(RESULT, 1, MAX_COUNTRY - 1) && RESULT != CFLAG:MASTER:소속 && IS_COUNTRY(RESULT) && !COUNTRY_IS_CLOSED:RESULT
	REDRAW 1
	CALL DIPLOMACY_MAIN(RESULT)
	
	;외교 대상의 세력이 한 개도 없으면 돌아온다
	LOCAL:1 = 0
	FOR LOCAL:0, 1, MAX_COUNTRY
 		IF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:소속
			LOCAL:1 ++
		ENDIF
	NEXT
	IF LOCAL:1 >= 1
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
ELSEIF INRANGE(RESULT, 1001, 1000 + MAX_COUNTRY - 1) && IS_COUNTRY(RESULT - 1000)
	CALL SHOW_COUNTRY_INFO(RESULT - 1000)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT == 98
	CALL AUTO_SENDMONEY_SETTING
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ELSEIF RESULT != 99
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF

REDRAW 1

;-------------------------------------------------
;ARG:0 세력과의 외교 메뉴
;-------------------------------------------------
@DIPLOMACY_MAIN(ARG:0)
#DIMS 선택항목
#DIM REST_COUNTRY
#DIM COUNTRYNUMBER
#DIMS 관계도
#DIMS TEMP_COUNTRY
#DIMS sPrint_Text

VARSET 선택항목
;항복 권고로 나라가 없어졌을 때의 대책
IF !IS_COUNTRY(ARG:0)
	RETURN
ENDIF

IF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_도둑)
	TEMP_COUNTRY = 도둑
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_홉고블린)
	TEMP_COUNTRY = 홉고블린
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_외래인)
	TEMP_COUNTRY = 외래인
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_촉수)
	TEMP_COUNTRY = 촉수
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_자경단)
	TEMP_COUNTRY = 자경단
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_서큐버스)
	TEMP_COUNTRY = 서큐버스
ELSEIF CFLAG:GET_COUNTRY_BOSS(ARG:0):소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_광신도)
	TEMP_COUNTRY = 광신도
ELSE
	TEMP_COUNTRY = {GET_COUNTRY_BOSS(ARG:0)}
ENDIF

DIPLOMACY_DIPLOMAT = ARG:0


;나머지의 세력수를 계산
REST_COUNTRY = (MAX_COUNTRY - 1) - MATCH(COUNTRY_BOSS, 0)

;자국의 군주의 캐릭터 번호를 취득
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)

;상대 세력의 군주의 캐릭터 번호를 취득
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:6 = GET_SUM_ECONOMY(CFLAG:MASTER:소속)
LOCAL:7 = GET_SUM_ECONOMY(ARG:0)
LOCAL:8 = MAX(LOCAL:6, LOCAL:7) * 10 / MAX(MIN(LOCAL:6, LOCAL:7), 1)
CALL SINGLE_DRAWLINE
PRINTBUTTON @"[그림 \@ FLAG:화상설정 ? 표시 # 비표시 \@]", 98
PRINTL
;SIF FLAG:화상설정
;	CALL PRINT_STATE_IMAGE(LOCAL:5)
PRINTFORM 현재의 관계:
CALL TMP_SETCOLOR_COUNTRY_RELATION(CFLAG:MASTER:소속, ARG:0)
PRINTFORML %TMP_TOSTR_COUNTRY_RELATION(CFLAG:MASTER:소속, ARG:0, 1)%
RESETCOLOR

PRINTFORM 현재의 인상:
CALL TMP_PRINT_COUNTRY_RELATION2(LOCAL:5, LOCAL:4)

IF RESULTS == " 호감" || RESULTS == " 우호"
	관계도 == 미소
ELSEIF RESULTS == " 원한" || RESULTS == " 험악" || RESULTS == " 천적"
	관계도 == 분노
ELSE
	관계도 == 통상
ENDIF
PRINTL 

LOCAL:11 = MAX(STRLENS(NAME:(LOCAL:5)), STRLENS(NAME:(LOCAL:4)))
LOCAL:12 = MAX(GET_DIGIT(LOCAL:6 / 100), GET_DIGIT(LOCAL:7 / 100))

CALL SINGLE_DRAWLINE



PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML %ANAME(LOCAL:4), LOCAL:11%의 경제력:{LOCAL:6 / 100, LOCAL:12}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML %ANAME(LOCAL:5), LOCAL:11%의 경제력:{LOCAL:7 / 100, LOCAL:12}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:6, LOCAL:7)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:8, 1)%배    

IF LOCAL:8 < 15
	SETCOLOR 0xFF40FF
	PRINTL 대등
ELSEIF LOCAL:6 > LOCAL:7
	IF LOCAL:8 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 압도
	ELSEIF LOCAL:8 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 유리
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 약간 유리
	ENDIF
ELSE
	IF LOCAL:8 >= 60
		SETCOLOR 0xA02020
		PRINTL 압도적 불리
	ELSEIF LOCAL:8 >= 30
		SETCOLOR 0xFF4040
		PRINTL 불리
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 약간 불리
	ENDIF
ENDIF

RESETCOLOR

LOCAL:13 = TMP_SUM_POLITICS_POWER:(CFLAG:MASTER:소속)
LOCAL:14 = TMP_SUM_POLITICS_POWER:(ARG:0)
LOCAL:15 = MAX(LOCAL:13, LOCAL:14) * 10 / MAX(MIN(LOCAL:13, LOCAL:14), 1)
LOCAL:16 = MAX(GET_DIGIT(LOCAL:13), GET_DIGIT(LOCAL:14))

PRINTFORM %TOSTR_SPACE(20)%
;CALL SINGLE_DRAWLINE
PRINTFORML %TOSTR_REPEAT("-", 50)%
;화상출력
IF FLAG:화상설정
	CALL PRINT_IMAGE_CHARA(LOCAL:5, "스탠딩", 관계도)
	sPrint_Text = <p align='left'>
	sPrint_Text += @"<shape type='space' param='0'><img src='%RESULTS%' height='1200' ypos='-400'>"
	HTML_PRINT sPrint_Text
	
ENDIF


PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML %ANAME(LOCAL:4), LOCAL:11%의 정치력:{LOCAL:13, LOCAL:16}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML %ANAME(LOCAL:5), LOCAL:11%의 정치력:{LOCAL:14, LOCAL:16}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:13, LOCAL:14)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:15, 1)%배    

IF LOCAL:15 < 15
	SETCOLOR 0xFF40FF
	PRINTL 대등
ELSEIF LOCAL:13 > LOCAL:14
	IF LOCAL:15 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 압도
	ELSEIF LOCAL:15 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 유리
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 약간 유리
	ENDIF
ELSE
	IF LOCAL:15 >= 60
		SETCOLOR 0xA02020
		PRINTL 압도적 불리
	ELSEIF LOCAL:15 >= 30
		SETCOLOR 0xFF4040
		PRINTL 불리
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 약간 불리
	ENDIF
ENDIF

RESETCOLOR


LOCAL:17 = TMP_SUM_SOLDIER:(CFLAG:MASTER:소속)
LOCAL:18 = TMP_SUM_SOLDIER:(ARG:0)
LOCAL:19 = MAX(LOCAL:17, LOCAL:18) * 10 / MAX(MIN(LOCAL:17, LOCAL:18), 1)
LOCAL:20 = MAX(GET_DIGIT(LOCAL:17), GET_DIGIT(LOCAL:18))
PRINTFORM %TOSTR_SPACE(20)%
;CALL SINGLE_DRAWLINE
PRINTFORML %TOSTR_REPEAT("-", 50)%
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML   %ANAME(LOCAL:4), LOCAL:11%의 병력:{LOCAL:17, LOCAL:16}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORML   %ANAME(LOCAL:5), LOCAL:11%의 병력:{LOCAL:18, LOCAL:16}
PRINTFORM %TOSTR_SPACE(20)%
PRINTFORM %ANAME(LOCAL:4)% %TOSTR_COMPARE(LOCAL:17, LOCAL:18)% %ANAME(LOCAL:5)%  %DECIMAL_STRING(LOCAL:19, 1)%배    

IF LOCAL:19 < 15
	SETCOLOR 0xFF40FF
	PRINTL 대등
ELSEIF LOCAL:17 > LOCAL:18
	IF LOCAL:19 >= 60
		SETCOLOR 0x40FFFF
		PRINTL 압도
	ELSEIF LOCAL:19 >= 30
		SETCOLOR 0x40A0FF
		PRINTL 유리
	ELSE
		SETCOLOR 0x4040FF
		PRINTL 약간 유리
	ENDIF
ELSE
	IF LOCAL:19 >= 60
		SETCOLOR 0xA02020
		PRINTL 압도적 불리
	ELSEIF LOCAL:19 >= 30
		SETCOLOR 0xFF4040
		PRINTL 불리
	ELSE
		SETCOLOR 0xFF4080
		PRINTL 약간 불리
	ENDIF
ENDIF

RESETCOLOR

CALL SINGLE_DRAWLINE

PRINT 조약 체결 가능수 
PRINTFORM  정전:{COUNTRY_TREATY_NO:(CFLAG:MASTER:소속):1, 2}/{MAX(REST_COUNTRY / 3, 1), 2}, 
PRINTFORML 동맹:{COUNTRY_TREATY_NO:(CFLAG:MASTER:소속):0, 2}/{MAX(REST_COUNTRY / 3, 1), 2}

CALL SINGLE_DRAWLINE

LOCALS:0 = 정전을 신청한다
LOCALS:1 = 동맹을 신청한다
LOCALS:2 = 영구 동맹을 신청한다
LOCALS:3 = 외교 병합을 신청한다
LOCALS:4 = 동맹(정전)을 파기한다
LOCALS:5 = 추문을 퍼뜨린다
LOCALS:6 = 내부 공작
LOCALS:7 = 토벌령을 낸다
LOCALS:10 = 회담한다
LOCALS:11 = 병을 양도한다
LOCALS:12 = 경제력을 양도한다
LOCALS:13 = 사관을 이적시킨다
LOCALS:14 = 영토를 할양 한다
LOCALS:20 = 항복 권고를 실시한다
LOCALS:21 = 병을 요구한다
LOCALS:22 = 경제력을 요구한다
LOCALS:23 = 인질을 요구한다
LOCALS:24 = 인질 해방을 요구한다
LOCALS:25 = 조교한다
LOCALS:26 = 부하를 조교한다
LOCALS:27 = 영토 할양을 요구한다
LOCALS:30 = 포로 반환 교섭


VARSET LOCAL, 0, 100, 130

IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) >= 1 || COUNTRY_TREATY_NO:(CFLAG:MASTER:소속):1 >= MAX(REST_COUNTRY / 3, 1)
	LOCAL:100 = 1
ENDIF
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) >= 3 || COUNTRY_TREATY_NO:(CFLAG:MASTER:소속):0 >= MAX(REST_COUNTRY / 3, 1)
	LOCAL:101 = 1
ENDIF
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) >= 4
	LOCAL:102 = 1
ENDIF
IF TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) >= 1 || MATCH(TREATY_U_TARGET, 0) != MAX_TREATY_U
	LOCAL:107 = 1
ENDIF

LOCAL:124 = 1
LOCAL:130 = 1
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:0):포로처 == ARG:0
		LOCAL:124 = 0
		LOCAL:130 = 0
		BREAK
	ENDIF
NEXT
IF LOCAL:130
	FOR LOCAL:0, 0, CHARANUM
		IF CFLAG:(LOCAL:0):소속 == ARG:0 && CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속
			LOCAL:130 = 0
		ENDIF
	NEXT
ENDIF

IF CFLAG:(LOCAL:5):외교조교중
	LOCAL:125 = 1
ENDIF

IF DONE_DIPLOMACY_SCANDAL:(ARG:0)
	LOCAL:105  = 1
ENDIF

LOCAL:1 = 1
FOR LOCAL, 0, CHARANUM
	IF CFLAG:LOCAL:소속 == ARG:0 && LOCAL != GET_COUNTRY_BOSS(ARG:0) && (IS_SLAVE(LOCAL) || GETBIT(TALENT:LOCAL:음란계, 소질_음란_꼭두각시) || GETBIT(TALENT:LOCAL:음란계, 소질_음란_약물중독)) && CFLAG:LOCAL:포로처 == 0
		LOCAL:1 = 0
		BREAK
	ENDIF
NEXT

LOCAL:106 = LOCAL:1

IF DONE_DIPLOMACY_SABOTAGE:(ARG:0)
	LOCAL:106 = 1
ENDIF

IF DONE_DIPLOMACY_FRIENDSHIP:(ARG:0)
	VARSET LOCAL, 1, 110, 120
ENDIF

IF DONE_DIPLOMACY_REQUEST:(ARG:0)
	VARSET LOCAL, 1, 120, 130
ENDIF

PRINT ◇관계를 묶는다◇
LOCAL:1 = 0
FOR LOCAL:0, 0, 10
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++
		PRINT     
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 
PRINTL 

PRINT ◇우호가 깊어진다◇
LOCAL:1 = 0
FOR LOCAL:0, 10, 20
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++
		PRINT     
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 
PRINTL 

PRINT ◇요구를 실시한다◇
LOCAL:1 = 0
FOR LOCAL:0, 20, 30
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++
		PRINT     
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 
PRINTL 

PRINT ◇교섭한다◇
LOCAL:1 = 0
FOR LOCAL:0, 30, 40
	IF LOCALS:(LOCAL:0) != ""
		IF LOCAL:1 % 3 == 0
			PRINTL 
		ENDIF
		LOCAL:1 ++
		PRINT     
		IF LOCAL:(LOCAL:0 + 100)
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM {LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]
			RESETCOLOR
		ELSE
			PRINTBUTTON @"{LOCAL:0, 2}[%LOCALS:(LOCAL:0), 22%]", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 
CALL SINGLE_DRAWLINE
PRINT     
PRINTBUTTON "99[              돌아온다]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN 0

ELSEIF RESULT == 98
	FLAG:화상설정 = !FLAG:화상설정
	
ELSEIF RESULT >= 0 && RESULT < 40 && !LOCAL:(RESULT + 100)
	IF RESULT >= 0 && RESULT < 10
		선택항목 = 관계
	ELSEIF RESULT >= 10 && RESULT < 20
		선택항목 = 우호
	ELSEIF RESULT >= 20 && RESULT < 30
		선택항목 = 요구
	ELSEIF RESULT >= 30 && RESULT < 40
		선택항목 = 교섭
	ENDIF
	;정전을 신청한다
	IF RESULT == 0
		CALL DIPLOMACY_CEASEFIRE(ARG:0)
	;동맹을 신청한다
	ELSEIF RESULT == 1
		CALL DIPLOMACY_ALLIANCE(ARG:0)
	;영구 동맹을 신청한다
	ELSEIF RESULT == 2
		CALL DIPLOMACY_ALLIANCE_INDIFINITE(ARG:0)
	;외교 병합을 신청한다
	ELSEIF RESULT == 3
		CALL DIPLOMACY_PERSONAL_UNION(ARG:0)
	;동맹을 파기한다
	ELSEIF RESULT == 4
		CALL DIPLOMACY_ALLIANCE_RENOUNCE(ARG:0)
	;추문을 퍼뜨린다
	ELSEIF RESULT == 5
		CALL DIPLOMACY_CREATE_SCANDAL(ARG:0)
		DONE_DIPLOMACY_SCANDAL:(ARG:0) = RESULT
	;내부 공작
	ELSEIF RESULT == 6
		CALL DIPLOMACY_SABOTAGE(ARG:0)
		DONE_DIPLOMACY_SABOTAGE:(ARG:0) = RESULT
	;토벌령을 낸다
	ELSEIF RESULT == 7
		CALL SLG_DIPLOMACY_CREATE_UNION(CFLAG:MASTER:소속, ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;회담한다
	ELSEIF RESULT == 10
		CALL DIPLOMACY_TALK(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT
	;병을 양도한다
	ELSEIF RESULT == 11
		CALL DIPLOMACY_GIVE_SOLDIER(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT
	;경제력을 양도한다
	ELSEIF RESULT == 12
		CALL DIPLOMACY_GIVE_ECONOMY(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT
	;사관을 이적시킨다
	ELSEIF RESULT == 13
		CALL DIPLOMACY_GIVE_CHARA(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT
	;영토를 할양 한다
	ELSEIF RESULT == 14
		CALL DIPLOMACY_GIVE_CITY(ARG:0)
		DONE_DIPLOMACY_FRIENDSHIP:(ARG:0) = RESULT

	;항복 권고를 실시한다
	ELSEIF RESULT == 20
		CALL DIPLOMACY_SURRENDER(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;병을 요구한다
	ELSEIF RESULT == 21
		CALL DIPLOMACY_REQUEST_SOLDIER(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;경제력을 요구한다
	ELSEIF RESULT == 22
		CALL DIPLOMACY_REQUEST_ECONOMY(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;인질을 요구한다
	ELSEIF RESULT == 23
		CALL DIPLOMACY_REQUEST_HOSTAGE(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;인질 해방을 요구한다
	ELSEIF RESULT == 24
		CALL DIPLOMACY_REQUEST_RELEASE(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;조교한다
	ELSEIF RESULT == 25
		CALL DIPLOMACY_REQUEST_TRAIN(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;부하를 조교
	ELSEIF RESULT == 26
		CALL DIPLOMACY_REQUEST_TRAIN_BUKA(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT
	;영토 할양을 요구
	ELSEIF RESULT == 27
		CALL DIPLOMACY_WANT_CITY(ARG:0)
		DONE_DIPLOMACY_REQUEST:(ARG:0) = RESULT

	;인질 교섭
	ELSEIF RESULT == 30
		CALL DIPLOMACY_PRISONER_TRADE(ARG:0)
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
[SKIPSTART]
;DIPLOMACY_REQUEST_SUCCESS_COUNT를 DIPLOMACY_HATE에 통합하기 위해(때문에) 스킵 처리
	IF 선택 항목 == "요구" && RESULT == 1 && DIPLOMACY_REQUEST_SUCCESS_COUNT % 3 == 0 && DIPLOMACY_REQUEST_SUCCESS_COUNT
		SETCOLOR 칼라_경고
		PRINTFORMW 거듭되는 것 외 세력에의 요구에 의해, %ANAME(LOCAL:4)%를 경계하는 움직임이 각국에서 나와 있는 것 같습니다!
		PRINTFORMW %조사처리(ANAME(LOCAL:4),"과")% 타국과의 관계가 악화되었습니다
		FOR LOCAL:0, 0, MAX_COUNTRY

			SIF IS_COUNTRY(LOCAL:0) && LOCAL:0 != CFLAG:MASTER:소속
				CALL CHANGE_RELATION_C_TO_C(LOCAL, CFLAG:MASTER:소속, -1 * RAND(DIPLOMACY_REQUEST_SUCCESS_COUNT * 10, DIPLOMACY_REQUEST_SUCCESS_COUNT * 15),   RAND(DIPLOMACY_REQUEST_SUCCESS_COUNT * 10, DIPLOMACY_REQUEST_SUCCESS_COUNT * 15))
		NEXT
		RESETCOLOR
	ENDIF
[SKIPEND]
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
RESTART

;-------------------------------------------------
;ARG:0 세력이, 모든 세력 중(안)에서 최대의 경제 규모를 가지고 있을지 어떨지를 판정
;-------------------------------------------------
@CHECK_IS_BEST_COUNTRY(ARG:0)
LOCAL:1 = -1
LOCAL:2 = 0
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0)
		LOCAL:3 = GET_SUM_ECONOMY(LOCAL:0)
		IF LOCAL:3 > LOCAL:1
			LOCAL:1 = LOCAL:3
			LOCAL:2 = LOCAL:0
		ENDIF
	ENDIF
NEXT
IF LOCAL:2 == ARG:0
	RETURN 1
ENDIF
RETURN 0

;-------------------------------------------------
;ARG:0 세력이, 다른 적대적이고 대등(=경제 규모가 ARG:0 세력의 2/3이상)인 세력과 인접하고 있는지를 판정한다
;-------------------------------------------------
@CHECK_IS_STRONG_NEIBOR(ARG:0)
;세력 관계 맵을 작성
CALL TMP_CREATE_RELATION_MAP

;세력간의 인접 관계 맵을 작성
CALL TMP_CREATE_COUNTRY_NEIBORING_MAP

LOCAL:1 = GET_SUM_ECONOMY(ARG:0) * 2 / 3
FOR LOCAL:0, 1, MAX_COUNTRY
	IF IS_COUNTRY(LOCAL:0) && TMP_COUNTRY_IS_NEIBORING:(ARG:0):(LOCAL:0) && TMP_COUNTRY_RELATION:(ARG:0):(LOCAL:0) <= 1
		IF GET_SUM_ECONOMY(LOCAL:0) >= LOCAL:1
			RETURN 1
		ENDIF
	ENDIF
NEXT
RETURN 0

;-------------------------------------------------
;ARG:0 세력으로부터 ARG:1 세력에, ARG:2만 경제력을 이동시키는 처리
;ARG:2는 경제력의 내부치(표시치의 100배)인 것으로 주의
;-------------------------------------------------
@SHIFT_ECONOMY(ARG:0, ARG:1, ARG:2)
LOCAL:2 = GET_SUM_ECONOMY(ARG:0)
LOCAL:3 = GET_SUM_ECONOMY(ARG:1)

;ARG:0 세력의 경제 규모를 ARG:2분만 줄인다(상한치도 감소치의 것 1/5만 감소)
FOR LOCAL:0, 1, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == ARG:0
		LOCAL:5 = CITY_ECONOMY:(LOCAL:0) * ARG:2 / LOCAL:2
		CITY_ECONOMY:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0) - LOCAL:5, 5000)
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY_LIMIT:(LOCAL:0) - LOCAL:5 / 5, 5000)
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MIN(CITY_ECONOMY_LIMIT:(LOCAL:0), 999900)
		CITY_ECONOMY:(LOCAL:0) = MIN(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
NEXT

LOCAL:5 = GET_SUM_ECONOMY(ARG:0) + ARG:2 - LOCAL:2
CALL FISHER_YATES_SHAFFLE(MAX_CITY)
FOR LOCAL:1, 1, MAX_CITY
	LOCAL:0 = SHAFFLE_ARRAY:(LOCAL:1)
	IF CITY_OWNER:(LOCAL:0) == ARG:0
		CITY_ECONOMY:(LOCAL:0) -= LOCAL:5
		IF CITY_ECONOMY:(LOCAL:0) >= 5000
			BREAK
		ENDIF
		LOCAL:5 = 5000 - CITY_ECONOMY:(LOCAL:0)
		CITY_ECONOMY:(LOCAL:0) = 5000
	ENDIF
NEXT

;ARG:1 세력의 경제 규모를 ARG:2분만 늘린다(상한치를 웃돌면, 그 만큼 상한치도 상승)
LOCAL:0 = 0
WHILE 1
	LOCAL:0 = FINDELEMENT(CITY_OWNER, ARG:1, LOCAL:0)
	SIF LOCAL:0 < 0
		BREAK
	CITY_ECONOMY:(LOCAL:0) += CITY_ECONOMY:(LOCAL:0) * ARG:2 / LOCAL:3
	;상한을 넘은 만큼의 1%가 상한을 끌어올린다
	IF CITY_ECONOMY:(LOCAL:0) > CITY_ECONOMY_LIMIT:(LOCAL:0)
		CITY_ECONOMY_LIMIT:(LOCAL:0) += (CITY_ECONOMY:(LOCAL:0) - CITY_ECONOMY_LIMIT:(LOCAL:0)) / 100
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MIN(CITY_ECONOMY_LIMIT:(LOCAL:0), 999900)
		CITY_ECONOMY:(LOCAL:0) = MIN(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
	LOCAL:0 ++
WEND

LOCAL:5 = LOCAL:3 + ARG:2 - GET_SUM_ECONOMY(ARG:1)

CALL FISHER_YATES_SHAFFLE(MAX_CITY)
FOR LOCAL:1, 1, MAX_CITY
	LOCAL:0 = SHAFFLE_ARRAY:(LOCAL:1)
	IF CITY_OWNER:(LOCAL:0) == ARG:1
		CITY_ECONOMY:(LOCAL:0) += LOCAL:5
		IF CITY_ECONOMY:(LOCAL:0) <= 999900
			CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
			BREAK
		ENDIF
		LOCAL:5 = CITY_ECONOMY:(LOCAL:0) - 999900
		CITY_ECONOMY:(LOCAL:0) = 999900
		CITY_ECONOMY_LIMIT:(LOCAL:0) = MAX(CITY_ECONOMY:(LOCAL:0), CITY_ECONOMY_LIMIT:(LOCAL:0))
	ENDIF
NEXT

;-------------------------------------------------
;ARG:1의 치 분만큼 세력 ARG:0과의 호감도를 상승시키고 적대치를 저하시키는 처리
;-------------------------------------------------
@DIPLOMACY_IMPROVE_RELATION(ARG:0, ARG:1)
#DIM 군주
#DIM 자세력군주
#DIM 호감도
#DIM 적대도
군주  = GET_COUNTRY_BOSS(ARG:0)
자세력군주 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
호감도 = REL_LIKE:군주:자세력군주
적대도 = REL_HATE:군주:자세력군주

CALL CHANGE_RELATION_C_TO_C(CFLAG:MASTER:소속, ARG:0, ARG:1, -ARG:1)
CALL CHANGE_RELATION_C_TO_C(ARG:0, CFLAG:MASTER:소속, ARG:1, -ARG:1)

SETCOLOR 칼라_주의
PRINTFORML 호감도:{호감도, 4, RIGHT} → {REL_LIKE:군주:자세력군주, 4, RIGHT}
PRINTFORML 적대도:{적대도, 4, RIGHT} → {REL_HATE:군주:자세력군주, 4, RIGHT}
RESETCOLOR

