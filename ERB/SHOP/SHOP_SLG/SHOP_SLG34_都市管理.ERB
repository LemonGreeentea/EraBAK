;-------------------------------------------------
;「도시관리」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME34
RESULTS:0 '= "도시관리"

;-------------------------------------------------
;「도시관리」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK34
SIF FLAG:클리어플래그
	RETURN 0
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「도시관리」의 오른쪽 컬럼 온 판정
;-------------------------------------------------
;@SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK34

;-------------------------------------------------
;「도시관리」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY34
CALL SHOW_CITY_ARMY
RETURN 1

;-------------------------------------------------
;「도시관리」의 표시 처리 본체
;-------------------------------------------------
@SHOW_CITY_ARMY
#DIM LINE
#DIM WIDTH_CITYNAME = 30
#DIM CITY_TABLE, MAX_CITY
#DIM PTR
#DIM CONST CITY_PER_PAGE = 30
#DIM MAX_PAGE
#DIM NOW_PAGE
#DIM NOW_SORT
#DIM PREV_SORT
#DIM DESC
#DIM SORT_VALUE, MAX_CITY
#DIM FIRST_LINE

VARSET CITY_TABLE, -1
NOW_PAGE = 1
PTR = 0
DESC = 0
NOW_SORT = 0

FOR LOCAL, 0, CITY_NUM+1
	IF CITY_OWNER:(LOCAL) == CFLAG:MASTER:소속
		CITY_TABLE:PTR = LOCAL
		PTR++
	ENDIF
NEXT

MAX_PAGE = (PTR/CITY_PER_PAGE)

;잉여산으로 변경
SIF PTR%CITY_PER_PAGE != 0
	MAX_PAGE ++

FIRST_LINE = LINECOUNT
$SHOW_LOOP

PRINTFORM %TOSTR_SPACE( (WIDTH_CITYNAME-6)/2 )%
PRINTBUTTON "도시명", 0
PRINTFORM %TOSTR_SPACE( (WIDTH_CITYNAME-6+1)/2 )%┃  
PRINTBUTTON "경제", 1
PRINTFORM   ┃
PRINTBUTTON "성장여력", 2
PRINTFORM ┃
PRINTBUTTON "방위병수", 3
PRINTFORM ┃  
PRINTBUTTON "수장", 4
PRINTFORM   ┃
PRINTBUTTON "병력손실", 5
PRINTFORM ┃ 
PRINTBUTTON "위험도", 6
PRINTFORM  ┃
PRINTL
PRINTL ━━━━━━━━━━━━━━━╋━━━━╋━━━━╋━━━━╋━━━━╋━━━━╋━━━━┫


FOR LINE, 0, CITY_PER_PAGE
	LOCAL = CITY_TABLE:(LINE + CITY_PER_PAGE * (NOW_PAGE - 1))
	IF LOCAL >= 0
		LOCALS =  %CITY_NAME:LOCAL%%TOSTR_SPACE(WIDTH_CITYNAME-STRLENS(CITY_NAME:LOCAL))%
		PRINTBUTTON LOCALS, 1000+LOCAL
		PRINT ┃
		PRINTPLAINFORM  {(CITY_ECONOMY:LOCAL)/100, 7, RIGHT}┃
		PRINTPLAINFORM  {(CITY_ECONOMY_LIMIT:LOCAL-CITY_ECONOMY:LOCAL)/100, 7, RIGHT}┃
		LOCALS = {CITY_SOLDIER:LOCAL, 8, RIGHT}
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR 칼라_경고
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 200+LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		LOCALS = \@ GET_CITY_COMMANDER(LOCAL, 0) != -1 ? %"유", 8, RIGHT% # %"무", 8, RIGHT% \@
		IF IS_STAY_ENEMY_UNIT(LOCAL) == 1
			SETCOLOR 칼라_경고
			PRINTFORM %LOCALS%
			RESETCOLOR
		ELSE
			PRINTBUTTON LOCALS, 400 + LOCAL
		ENDIF
		PRINTPLAINFORM ┃
		PRINTPLAINFORM  {CITY_SOLDIER_PREV:LOCAL, 7, RIGHT}┃
		SELECTCASE GET_CITY_RISK(LOCAL)
			CASE 0
				LOCALS '= "  안전  "
			CASE 1
				LOCALS '= "  주의  "
			CASE 2
				LOCALS '= "  주의  "
			CASE 3
				LOCALS '= "  주의  "
			CASE 4
				LOCALS '= "  전선  "
				SETCOLOR 칼라_오렌지
			CASE 5
				LOCALS '= " 적발견 "
				SETCOLOR 칼라_경고
			CASE 6
				LOCALS '= " 교전중 "
				SETCOLOR 칼라_경고
		ENDSELECT
		
		PRINTPLAINFORM %LOCALS%
		RESETCOLOR
		PRINT ┃
	ELSE
	ENDIF
	PRINTL
NEXT

PRINTL
PRINTFORM                         

IF NOW_PAGE != 1
	PRINTBUTTON " <<< ", 100
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM  <<< 
	RESETCOLOR
ENDIF

PRINT   
IF MAX_PAGE > 1
	PRINTBUTTON " << ", 101
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM  << 
	RESETCOLOR
ENDIF

PRINT    
PRINTFORM {NOW_PAGE, 2, RIGHT} / {MAX_PAGE, 2, LEFT}

PRINT    
IF MAX_PAGE > 1
	PRINTBUTTON " >> ", 102
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM  >> 
	RESETCOLOR
ENDIF

PRINT   
IF NOW_PAGE != MAX_PAGE
	PRINTBUTTON " >>> ", 103
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM  >>> 
	RESETCOLOR
ENDIF
PRINTL 
CALL SINGLE_DRAWLINE
CALL SHOW_CITY_INFO(SHOWN_CITY, 1)
CALL SINGLE_DRAWLINE
PRINTBUTTON "[99] 돌아온다", 99
PRINTL

INPUT

SELECTCASE RESULT
	CASE 100
		NOW_PAGE = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 101
		NOW_PAGE --
		SIF NOW_PAGE <= 0
			NOW_PAGE = MAX_PAGE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 102
		NOW_PAGE ++
		SIF NOW_PAGE > MAX_PAGE
			NOW_PAGE = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 103
		NOW_PAGE = MAX_PAGE
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	CASE 99
		RETURN 0
ENDSELECT

LOCAL = RESULT

;도시명 클릭 했을 때 SHOP_SLG측에서 쓸데없게 CLEARLINE 되는 것에의 대책
LINES_SHOP = LINECOUNT
CALL USERSHOP_SLG(LOCAL)
IF RESULT
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

PREV_SORT = NOW_SORT
NOW_SORT = LOCAL

IF NOW_SORT == PREV_SORT
	DESC = !DESC
ELSE
	DESC = 0
ENDIF

SELECTCASE NOW_SORT
	CASE 0
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_TABLE:LOCAL
		NEXT
	CASE 1
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_ECONOMY:(CITY_TABLE:LOCAL)
		NEXT
	CASE 2
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_ECONOMY_LIMIT:(CITY_TABLE:LOCAL)-CITY_ECONOMY:(CITY_TABLE:LOCAL)
		NEXT
	CASE 3
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_SOLDIER:(CITY_TABLE:LOCAL)
		NEXT
	CASE 4
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = GET_CITY_COMMANDER_NUM(CITY_TABLE:LOCAL)
		NEXT
	CASE 5
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = CITY_SOLDIER_PREV:(CITY_TABLE:LOCAL)
		NEXT
	CASE 6
		FOR LOCAL, 0, PTR
			SORT_VALUE:LOCAL = GET_CITY_RISK(CITY_TABLE:LOCAL)
		NEXT
ENDSELECT

FOR LOCAL,0, PTR
	SIF CITY_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = LOCAL:11-1
		LOCAL:13 = LOCAL:11
		IF (DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
			SWAP CITY_TABLE:(LOCAL:12),CITY_TABLE:(LOCAL:13)
			SWAP SORT_VALUE:(LOCAL:12),SORT_VALUE:(LOCAL:13)
		ELSE
			BREAK
		ENDIF
	NEXT
NEXT

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP


;-------------------------------------------------
;「도시관리」방위병수의 설정
;-------------------------------------------------
@SET_CITY_SOLDIER(ARG)
#DIM FIRST_LINE
#DIM PLUS = 100, 500, 1000, 5000, 10000, 50000, 100000
#DIM ERRMSG = 0

ERRMSG = 0

SIF !INRANGE(ARG, 1, CITY_NUM) || CITY_OWNER:ARG != CFLAG:MASTER:소속
	RETURN 0
SIF IS_STAY_ENEMY_UNIT(ARG)
	RETURN 0


REDRAW 0
;CLEARLINE LINES_SHOP_SLG-27-3-1

LOCAL:11 = CITY_SOLDIER:ARG
FIRST_LINE = LINECOUNT 
$INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:소속) - (LOCAL:11 - CITY_SOLDIER:ARG)
CALL SINGLE_DRAWLINE
PRINTFORML %CITY_NAME:ARG%의 방위병수를 결정해 주세요
PRINTFORML 직접 입력도 가능합니다
PRINTFORML 방위병수   {CITY_SOLDIER:ARG,8,RIGHT} → {LOCAL:11,8,RIGHT}
PRINTFORML 잔존병수   {COUNTRY_SOLDIER:(CFLAG:MASTER:소속),8,RIGHT} → {LOCAL:21,8,RIGHT}
SETCOLOR 칼라_파파랑
FOR LOCAL , 0, 7
	LOCALS = [+{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -1000-LOCAL
NEXT
PRINTL 
SETCOLOR 칼라_파빨강
FOR LOCAL , 0, 7
	LOCALS = [-{PLUS:LOCAL}]
	PRINTBUTTON LOCALS, -10000-LOCAL
NEXT
RESETCOLOR
PRINTL 
PRINTL 
PRINTBUTTON "[확정]", -1
PRINTBUTTON "[캔슬]", -2
PRINTL
CALL SINGLE_DRAWLINE
PRINTL 

SELECTCASE ERRMSG
	CASE 1
		PRINTFORM 최악이어도 500의 병수가 필요합니다
	CASE 2
		PRINT 병 수가 충분하지 않습니다
ENDSELECT
PRINTL 
REDRAW 1
INPUT

ERRMSG = 0
IF RESULT >= 0
	IF RESULT >= 500
		IF COUNTRY_SOLDIER:(CFLAG:MASTER:소속) + CITY_SOLDIER:ARG < RESULT
			ERRMSG = 2
		ELSE
			GOTO OUT_OF_INPUT_LOOP
		ENDIF
	ELSE
		ERRMSG = 1
	ENDIF
ELSE
	LOCAL:0 = -1
	
	SIF RESULT == -1
		GOTO OUT_OF_INPUT_LOOP
		
	SIF RESULT == -2
		RETURN
	
	IF ABS(RESULT) >= 1000 && ABS(RESULT) <= 1001 + CITY_NUM
		LOCAL:0 = ABS(RESULT)-1000
		LOCAL:1 = 1
	ELSEIF ABS(RESULT) >= 10000 && ABS(RESULT) < 10100
		LOCAL:0 = ABS(RESULT)-10000
		LOCAL:1 = -1
	ENDIF
	
	IF (LOCAL:0 >= 0 && LOCAL:0 < 7)
		LOCAL:11 = LOCAL:11 + (PLUS:(LOCAL:0))*LOCAL:1
		LOCAL:11 = MIN(LOCAL:11, COUNTRY_SOLDIER:(CFLAG:MASTER:소속) + CITY_SOLDIER:ARG)
		LOCAL:11 = MAX(LOCAL:11, 500)
	ENDIF
ENDIF

REDRAW 0

CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

REDRAW 1

$OUT_OF_INPUT_LOOP
LOCAL:21 = COUNTRY_SOLDIER:(CFLAG:MASTER:소속) - (LOCAL:11 - CITY_SOLDIER:ARG)
CITY_SOLDIER:ARG = LOCAL:11
COUNTRY_SOLDIER:(CFLAG:MASTER:소속) = LOCAL:21

;-------------------------------------------------
;어디에서도 불려 가지 않았다
;-------------------------------------------------
;@MEMORY_PREVIOUS_ARMY
;
;FOR LOCAL:0, 0, MAX_COUNTRY
;	FOR LOCAL:1, 0, MAX_UNIT
;		UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1) = UNIT_SOLDIER_PREV:(LOCAL:0):(LOCAL:1)
;	NEXT
;NEXT
;
;FOR LOCAL, 0, MAX_CITY
;	CITY_SOLDIER_PREV = CITY_SOLDIER
;NEXT
