;-------------------------------------------------
;「독립한다」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME73
RESULTS:0 '= "독립한다"

;-------------------------------------------------
;「독립한다」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK73
SIF CFLAG:MASTER:소속 == 0
	RETURN 0
SIF GET_NEW_COUNTRY() == -1
	RETURN 0
SIF GET_COUNTRY_BOSS(CFLAG:MASTER:소속) == MASTER
	RETURN 0
SIF CFLAG:MASTER:행동불능상태 != 0
	RETURN 0
SIF GET_OWN_CITY(CFLAG:MASTER:소속) < 2
	RETURN 0
SIF IS_SP_COUNTRY(CFLAG:MASTER:소속)
	RETURN 0
RETURN 1

;-------------------------------------------------
;「독립한다」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY73
CALL PLAYER_INDEPENDENCE
RETURN 1

;-------------------------------------------------
;「독립한다」본체
;-------------------------------------------------
@PLAYER_INDEPENDENCE
;선택한 캐릭터의 리스트
#DIM SELECT_LIST, 3000
#DIM FIRST_LINE
#DIM 구세력
#DIM 신세력
#DIM 군주
#DIM 도시
VARSET LOCAL, 0
VARSET SELECT_LIST, 0

구세력 = CFLAG:MASTER:소속
군주 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)

CALL SINGLE_DRAWLINE
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE
PRINTL 신세력시작 도시를 선택해 주세요
PRINTL 현재 소속해 있는 세력의 도시만 지정 가능합니다
CALL SINGLE_DRAWLINE
PRINTBUTTON " 0[캔슬]", 0
PRINTL 

REDRAW 0

$INPUT_LOOP_CITY
INPUT

IF RESULT == 0
	REDRAW 1
	RETURN
ELSEIF RESULT >= 1001 && RESULT <= 1001 + CITY_NUM
	도시 = RESULT - 1000
	;지정된 도시가 존재해, 주인공 세력의 지배하에 있는 경우
	IF GET_RELAYNAME(도시) != "무명" && CITY_TYPE:(도시) == 0 && CITY_OWNER:(도시) == 구세력
		REDRAW 1
		PRINTFORMW %GET_RELAYNAME(도시)%로 개양 합니다
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP_CITY
	ENDIF
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP_CITY
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML 관계성의 깊은 캐릭터를 새로운 세력으로 이끌 수가 있습니다
PRINTFORML 새로운 세력의 사관으로서 가세하는 캐릭터를 선택해 주세요
CALL SINGLE_DRAWLINE
CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(-1, "PLAYER_INDEPENDENCE", "NONE")
신세력 = GET_NEW_COUNTRY()

;부대의 해산
CALL CLEAR_ALL_UNIT(구세력, 1)

;초기설정
CITY_OWNER:도시 = 신세력
COUNTRY_BOSS:신세력 = GET_ID(MASTER)
COUNTRY_COLOR:신세력 = 칼라_주의

;병력의 이동
COUNTRY_SOLDIER:신세력 += COUNTRY_SOLDIER:구세력 / GET_OWN_CITY(구세력) + 1
COUNTRY_SOLDIER:구세력 -= COUNTRY_SOLDIER:신세력
COUNTRY_SOLDIER:신세력 += CITY_SOLDIER:도시
CITY_SOLDIER:도시 = 0

CITY_SOLDIER:도시 = MIN(COUNTRY_SOLDIER:신세력, 500)
COUNTRY_SOLDIER:신세력 -= CITY_SOLDIER:도시

CALL CHANGE_COUNTRY(MASTER, 신세력, 1)

;사관을 이동
FOR LOCAL, 0, SELECTED_CHARA_NUM
	CALL CHANGE_COUNTRY(SELECTED_CHARA:LOCAL, 신세력, 1)
NEXT


CALL SINGLE_DRAWLINE
PRINTL 새로운 세력의 색을 선택해 주세요
CALL SINGLE_DRAWLINE
CALL CHANGE_COUNTRY_COLOR(신세력, 1)

SETCOLOR 칼라_주의
PRINTL 
PRINTFORMW %조사처리(ANAME(MASTER),"는")% %GET_RELAYNAME(도시)%를 점거해 독립했습니다!
PRINTL 
RESETCOLOR

;전의 군주와의 관계를 큰폭으로 악화시킨다
CALL CHANGE_RELATION_O_TO_C(구세력, 신세력, -200, 500)

@SELECT_CHARA_LIST_SHOW_LOGIC_PLAYER_INDEPENDENCE(대상)
#DIM 대상
RETURN GET_COUNTRY_BOSS(CFLAG:대상:소속) != 대상 && CFLAG:대상:행동불능상태 != 행동불능_아이 && CFLAG:대상:소속 == CFLAG:MASTER:소속 && !IS_ANIMAL(대상) && (IS_LOVER(대상) || IS_SLAVE(대상) || IS_SLAVED_BY(대상))

