;-------------------------------------------------
;인질 교섭
;-------------------------------------------------
@DIPLOMACY_PRISONER_TRADE(ARG:0)
#DIM FIRST_LINE
VARSET LOCAL, 0
CVARSET CFLAG, 58, 0

LOCAL:12 = 1
LOCAL:22 = 1

LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

;사관의 부대 소속 맵을 작성
CALL TMP_CREATE_IS_FREE_MAP

;정치 능력의 차이에 의한 보정 점수의 계산
LOCAL:32 = TMP_GET_POLITICS_POWER(CFLAG:MASTER:소속)
LOCAL:33 = TMP_GET_POLITICS_POWER(ARG:0)
LOCAL:34 = SQRT(ABS(LOCAL:32 - LOCAL:33) * 4)
IF LOCAL:32 >= LOCAL:33
	LOCAL:35 = 1000 + LOCAL:34
	LOCAL:36 = 1000000 / (1000 + LOCAL:34)
ELSE
	LOCAL:36 = 1000 + LOCAL:34
	LOCAL:35 = 1000000 / (1000 + LOCAL:34)
ENDIF

;양국의 포로의 수를 각각 카운트
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):소속 == ARG:0 && CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속
		LOCAL:10 ++
	ELSEIF CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:0):포로처 == ARG:0
		LOCAL:20 ++
	ENDIF
NEXT

;각 페이지수를 계산
LOCAL:11 = MAX(LOCAL:10 - 1, 0) / 20 + 1
LOCAL:21 = MAX(LOCAL:20 - 1, 0) / 20 + 1

$SHOW_LOOP_INIT

CALL SINGLE_DRAWLINE
PRINTL ★★서로의 해방하는 포로를 선택해 주세요★★
CALL SINGLE_DRAWLINE
PRINTFORML 【붙잡고 있는 포로】%TOSTR_SPACE(11)%가치   【붙잡혀 있는 포로】%TOSTR_SPACE(7)%가치

FIRST_LINE = LINECOUNT
$SHOW_LOOP

;서로의 포로를 리스트에 표시
LOCAL:13 = 0
LOCAL:14 = (LOCAL:12 - 1) * 20
LOCAL:15 = LOCAL:12 * 20
LOCAL:16 = 0
LOCAL:23 = 0
LOCAL:24 = (LOCAL:22 - 1) * 20
LOCAL:25 = LOCAL:22 * 20
LOCAL:26 = 0
FOR LOCAL:1, 0, 20
	LOCAL:17 = 1
	FOR LOCAL:0, LOCAL:13, CHARANUM
		IF CFLAG:(LOCAL:0):소속 == ARG:0 && CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속
			IF LOCAL:16 >= LOCAL:14 && LOCAL:16 < LOCAL:15
				PRINTPLAIN   
				IF CFLAG:(LOCAL:0):외교선택플래그
					SETCOLOR 칼라_선택중
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				PRINT   ┃
				LOCAL:13 = LOCAL:0 + 1
				LOCAL:16 ++
				LOCAL:17 = 0
				BREAK
			ELSE
				LOCAL:16 ++
			ENDIF
		ENDIF
	NEXT
	IF LOCAL:17
		PRINTFORM %TOSTR_SPACE(35)%┃
	ENDIF
	LOCAL:27 = 1
	FOR LOCAL:0, LOCAL:23, CHARANUM
		IF CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:0):포로처 == ARG:0
			IF LOCAL:26 >= LOCAL:24 && LOCAL:26 < LOCAL:25
				PRINTPLAIN  
				IF CFLAG:(LOCAL:0):외교선택플래그
					SETCOLOR 칼라_선택중
				ENDIF
				PRINTBUTTON @"{NO:(LOCAL:0) + 99, 3}\{%ANAME(LOCAL:0), 18, LEFT%\}{GET_PRISONER_VALUE(LOCAL:0), 8, RIGHT}", NO:(LOCAL:0) + 99
				RESETCOLOR
				LOCAL:23 = LOCAL:0 + 1
				LOCAL:26 ++
				LOCAL:27 = 0
				BREAK
			ELSE
				LOCAL:26 ++
			ENDIF
		ENDIF
	NEXT
	PRINTL 
NEXT

PRINTPLAIN    
IF LOCAL:12 <= 1
	SETCOLOR 칼라_선택불가
	PRINTPLAIN 10[←전]
	RESETCOLOR
ELSE
	PRINTBUTTON "10[←전]", 10
ENDIF
PRINTPLAINFORM     {LOCAL:12, 2, RIGHT}/{LOCAL:11, 2, LEFT}    
IF LOCAL:12 >= LOCAL:11
	SETCOLOR 칼라_선택불가
	PRINTPLAIN 11[차→]
	RESETCOLOR
ELSE
	PRINTBUTTON "11[차→]", 11
ENDIF
PRINTPLAIN    ┃  
IF LOCAL:22 <= 1
	SETCOLOR 칼라_선택불가
	PRINTPLAIN 20[←전]
	RESETCOLOR
ELSE
	PRINTBUTTON "20[←전]", 20
ENDIF
PRINTPLAINFORM     {LOCAL:22, 2, RIGHT}/{LOCAL:21, 2, LEFT}    
IF LOCAL:22 >= LOCAL:21
	SETCOLOR 칼라_선택불가
	PRINTPLAIN 21[차→]
	RESETCOLOR
ELSE
	PRINTBUTTON "21[차→]", 21
ENDIF
PRINTL 

;●교섭 점수의 계산
;선택한 사관의 점수를 합계
LOCAL:30 = 0
LOCAL:31 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):외교선택플래그
		IF CFLAG:(LOCAL:0):소속 == ARG:0 && CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속
			LOCAL:30 += GET_PRISONER_VALUE(LOCAL:0)
		ELSEIF CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:0):포로처 == ARG:0
			LOCAL:31 += GET_PRISONER_VALUE(LOCAL:0)
		ENDIF
	ENDIF
NEXT

LOCAL:37 = LOCAL:30 * LOCAL:35 / 1000
LOCAL:38 = LOCAL:31 * LOCAL:36 / 1000
LOCAL:39 = LOCAL:37 - LOCAL:38
LOCAL:45 = MAX(GET_DIGIT(LOCAL:30), GET_DIGIT(LOCAL:31), 4)

CALL SINGLE_DRAWLINE
PRINTFORM   해방 점수: {ABS(LOCAL:30), LOCAL:45}(가치 합계) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR 칼라_시안
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR 칼라_오렌지
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:35, 3)%
RESETCOLOR
PRINTFORML (정치 능력차이) = {LOCAL:37}

PRINTFORM   요구 점수: {ABS(LOCAL:31), LOCAL:45}(가치 합계) ×
IF LOCAL:32 > LOCAL:33
	SETCOLOR 칼라_시안
ELSEIF LOCAL:32 < LOCAL:33
	SETCOLOR 칼라_오렌지
ENDIF
PRINTFORM %DECIMAL_STRING(LOCAL:36, 3)%
RESETCOLOR
PRINTFORML (정치 능력차이) = {LOCAL:38}

PRINTFORM       합계:
IF LOCAL:39 == 0
	LOCALS:0 =  {ABS(LOCAL:39)}
ELSEIF LOCAL:39 > 0
	LOCALS:0 = +{ABS(LOCAL:39)}
	SETCOLOR 칼라_시안
ELSE
	LOCALS:0 = -{ABS(LOCAL:39)}
	SETCOLOR 칼라_오렌지
ENDIF
PRINTFORM %LOCALS:0, LOCAL:45 + 1%
RESETCOLOR
PRINTL 

LOCAL:40 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):외교선택플래그
		IF CFLAG:(LOCAL:0):소속 == ARG:0 && CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속
			LOCAL:40 = 1
			BREAK
		ELSEIF CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:0):포로처 == ARG:0
			LOCAL:40 = 1
			BREAK
		ENDIF
	ENDIF
NEXT

IF LOCAL:40
	PRINTBUTTON "  0[차분을 군사로 보충해 정산]", 0
	PRINT     
	PRINTBUTTON "  1[차분을 경제력으로 보충해 정산]", 1
	PRINT     
	IF LOCAL:39 >= 0
	PRINTBUTTON "  2[상관하지 않고 반환]", 2
	ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAIN   2[상관하지 않고 반환]
	RESETCOLOR
	ENDIF
	
ELSE
	SETCOLOR 칼라_선택불가
	PRINTPLAIN   0[차분을 군사로 보충해 정산]      1[차분을 경제력으로 보충해 정산]      2[상관하지 않고 반환]
	RESETCOLOR

ENDIF
PRINTL 
CALL SINGLE_DRAWLINE
PRINTBUTTON " 99[돌아온다]", 99
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT

IF RESULT == 99
	REDRAW 1
	RETURN
ELSEIF RESULT == 0 && LOCAL:40
	;요구병수는, 양국의 규모의 평균치에 비례
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:소속) + GET_SUM_ECONOMY(ARG:0)
	LOCAL:52 = GET_SUM_SOLDIER(CFLAG:MASTER:소속)
	LOCAL:53 = GET_SUM_ARMY_DF(CFLAG:MASTER:소속)
	LOCAL:54 = MAX(GET_SUM_SOLDIER(ARG:0) - GET_OWN_CITY(ARG:0) * 500, 0)
	LOCAL:55 = MAX(GET_SUM_SOLDIER(CFLAG:MASTER:소속) - GET_OWN_CITY(CFLAG:MASTER:소속) * 500, 0)

	LOCAL:51 = ABS(LOCAL:39) * LOCAL:50 / 360000
	
	;다만, 자국이 보충해 받는 경우（해방 점수가 요구 점수보다 큰 경우）, 최대에서도 보충국의 40%가 한계
	;그리고, 자국이 보충하는 경우（요구 점수가 해방 점수 이상의 경우）, 산출한 요구병수와 합계 점수 가운데, 큰 (분)편을 채용
	IF LOCAL:39 > 0
		LOCAL:51 = MIN(LOCAL:51, LOCAL:54 * 40 / 100)
	ELSE
		LOCAL:51 = MAX(LOCAL:51, ABS(LOCAL:39))
	ENDIF
	IF LOCAL:39 == 0
		PRINTFORML 선택한 포로를 해방합니다
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			;해방 처리＆해방 메세지의 표시
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%에게 군사{LOCAL:51}를 요구할 수가 있습니다
		PRINTFORML 현재의 총병수:{LOCAL:52}(우리집 미할당병수:{COUNTRY_SOLDIER:(CFLAG:MASTER:소속)})
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:51
				PRINTFORML %조사처리(ANAME(LOCAL:5),"는")% 요구를 채울 뿐(만큼)의 군사를 준비 할 수 없는 것 같다…(준비 가능:{LOCAL:54})
				CALL ASK_YN("있을 뿐(만큼) 보내라!", "그렇다면 교섭은 결렬이다")
				IF RESULT == 0
					LOCAL:51 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;해방 처리＆해방 메세지의 표시
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;상대 세력으로부터 자국에 군사를 이동
				CALL SHIFT_SOLDIER(ARG:0, CFLAG:MASTER:소속, LOCAL:51)
				PRINTFORMW %ANAME(LOCAL:5)% 세력으로부터{LOCAL:51}의 군사를 받았습니다
			ENDIF
		ENDIF

	ELSE
		PRINTFORML %ANAME(LOCAL:5)%에 군사{LOCAL:51}를 건네줄 필요가 있습니다
		PRINTFORML 현재의 총병수:{LOCAL:52}(우리집 미할당병수:{COUNTRY_SOLDIER:(CFLAG:MASTER:소속)})
		IF LOCAL:51 > LOCAL:52
			CALL ASK_MULTI_JUDGE("결정", 0, "캔슬", 1)
		ELSE
			IF LOCAL:51 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속)
				IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속) + LOCAL:13
					PRINTFORML ※요구병수가 현재의 미할당병수보다 많기 때문에, 전부대를 해산해, 방위에 도착해 있는 군사도 건네줄 필요가 있습니다
				ELSE
					PRINTFORML ※요구병수가 현재의 미할당병수보다 많기 때문에, 방위에 도착해 있는 군사도 건네줄 필요가 있습니다
				ENDIF
			ENDIF
			CALL ASK_YN("결정", "캔슬")
		ENDIF
		IF RESULT == 0
			;해방 처리＆해방 메세지의 표시
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;자국으로부터 상대 세력에 군사를 이동
			CALL SHIFT_SOLDIER(CFLAG:MASTER:소속, ARG:0, LOCAL:51)
			PRINTFORMW %ANAME(LOCAL:5)% 세력에{LOCAL:51}의 군사를 인도했습니다
		ENDIF
	ENDIF

	REDRAW 1
	RESTART

ELSEIF RESULT == 1 && LOCAL:40
	;요구 경제력은, 양국의 규모의 평균치에 비례
	LOCAL:50 = GET_SUM_ECONOMY(CFLAG:MASTER:소속) / 100
	LOCAL:51 = GET_SUM_ECONOMY(ARG:0) / 100
	LOCAL:52 = ABS(LOCAL:39) * (LOCAL:50 + LOCAL:51) / 70000
	LOCAL:53 = MAX(LOCAL:50 - GET_OWN_CITY(CFLAG:MASTER:소속) * 50, 0)
	LOCAL:54 = MAX(LOCAL:51 - GET_OWN_CITY(ARG:0) * 50, 0)


	;다만, 자국이 보충해 받는 경우（해방 점수가 요구 점수보다 큰 경우）, 최대에서도 보충국의 20%가 한계
	;그리고, 자국이 보충하는 경우（요구 점수가 해방 점수 이상의 경우）, 산출한 요구 경제력과 합계 점수의 것 1/3 가운데, 큰 (분)편을 채용
	IF LOCAL:39 > 0
		LOCAL:52 = MIN(LOCAL:52, LOCAL:54 * 20 / 100)
	ELSE
		LOCAL:52 = MAX(LOCAL:52, ABS(LOCAL:39) / 3)
	ENDIF

	IF LOCAL:39 == 0
		PRINTFORML 선택한 포로를 해방합니다
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			;해방 처리＆해방 메세지의 표시
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%에게 경제력{LOCAL:52}를 요구할 수가 있습니다
		PRINTFORML 현재의 경제력……%ANAME(LOCAL:4)%:{LOCAL:50}    %ANAME(LOCAL:5)%:{LOCAL:51}
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			LOCAL:60 = 1
			IF LOCAL:54 < LOCAL:52
				PRINTFORML %조사처리(ANAME(LOCAL:5),"는")% 요구를 채울 뿐(만큼)의 경제력을 가지고 있지 않은 것 같다…(준비 가능:{LOCAL:54})
				CALL ASK_YN("있을 뿐(만큼) 보내라!", "그렇다면 교섭은 결렬이다")
				IF RESULT == 0
					LOCAL:52 = LOCAL:54
				ELSE
					LOCAL:60 = 0
				ENDIF
			ENDIF
			IF LOCAL:60
				;해방 처리＆해방 메세지의 표시
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;상대 세력으로부터 자국에 경제력을 이동
				CALL SHIFT_ECONOMY(ARG:0, CFLAG:MASTER:소속, LOCAL:52 * 100)
				PRINTFORMW %ANAME(LOCAL:5)% 세력으로부터 경제력{LOCAL:52}를 받아 소유 도시에 배분했습니다
			ENDIF
		ENDIF
	ELSE
		PRINTFORML %ANAME(LOCAL:5)%에 경제력{LOCAL:52}를 건네줄 필요가 있습니다
		PRINTFORML 현재의 경제력……%ANAME(LOCAL:4)%:{LOCAL:50}    %ANAME(LOCAL:5)%:{LOCAL:51}
		IF LOCAL:52 > LOCAL:53
			PRINTFORML ※주고 받을 수 있는 경제력의 한계는 {LOCAL:53}까지
			CALL ASK_MULTI_JUDGE("결정", 0, "캔슬", 1)
		ELSE
			CALL ASK_YN("결정", "캔슬")
		ENDIF
		IF RESULT == 0
			;해방 처리＆해방 메세지의 표시
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			;자국으로부터 상대 세력에 군사를 이동
			CALL SHIFT_ECONOMY(CFLAG:MASTER:소속, ARG:0, LOCAL:52 * 100)
			PRINTFORMW %ANAME(LOCAL:5)% 세력에{LOCAL:52}의 경제력을 주고 받았습니다
		ENDIF
	ENDIF

	REDRAW 1
	RESTART
	
;;;추가 여기로부터  인질 교섭  2 반환
ELSEIF RESULT == 2 && LOCAL:40
	IF LOCAL:39 == 0
		PRINTFORML 선택한 포로를 해방합니다
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			;해방 처리＆해방 메세지의 표시
			CALL PRISONER_TRADE_RELEASE(ARG:0)
			WAIT
		ENDIF
	ELSEIF LOCAL:39 > 0
		PRINTFORML %ANAME(LOCAL:5)%에 포로를 반환합니다
		CALL ASK_YN("결정", "캔슬")
		IF RESULT == 0
			LOCAL:60 = 1  ;해방의 실시
			LOCAL:25 = LOCAL:39 / 30		;호감도·적대도의 조작치는 요점 조정

			IF LOCAL:60	;해방을 실시==1
				;해방 처리＆해방 메세지의 표시
				CALL PRISONER_TRADE_RELEASE(ARG:0)
				;서로의 나라끼리의 호감도를 상승시키고 적대치를 저하시킨다
				CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
			ENDIF
		ENDIF
	ELSE
		PRINTFORML 자군이 우위의 경우만 선택할 수 있습니다	;나올 리가 없는 메세지
	ENDIF
	REDRAW 1
	RESTART
;;추가 여기까지  인질 교섭  2 반환

ELSEIF RESULT == 10
	LOCAL:12 = MAX(LOCAL:12 - 1, 1)
ELSEIF RESULT == 11
	LOCAL:12 = MIN(LOCAL:12 + 1, LOCAL:11)
ELSEIF RESULT == 20
	LOCAL:22 = MAX(LOCAL:22 - 1, 1)
ELSEIF RESULT == 21
	LOCAL:22 = MIN(LOCAL:12 - 1, LOCAL:11)
ELSE
	LOCAL:2 = NO_TO_CHARA(RESULT - 99)
	IF LOCAL:2 < 0
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	IF (CFLAG:(LOCAL:2):소속 == ARG:0 && CFLAG:(LOCAL:2):포로처 == CFLAG:MASTER:소속) || (CFLAG:(LOCAL:2):소속 == CFLAG:MASTER:소속 && CFLAG:(LOCAL:2):포로처 == ARG:0)
		CFLAG:(LOCAL:2):외교선택플래그 = !CFLAG:(LOCAL:2):외교선택플래그
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
ENDIF
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------------------------------
;선택한 해방해, 해방 메세지를 표시한다
;-------------------------------------------------
@PRISONER_TRADE_RELEASE(ARG:0)
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):외교선택플래그 && CFLAG:(LOCAL:0):소속 == ARG:0
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %조사처리(ANAME(LOCAL:0),"를")% 해방했습니다
		IF TALENT:(LOCAL:0):임신
			SELECTCASE RAND:4
				CASE 0
					PRINTFORML 포로가 되어 있었던 %ANAME(LOCAL:0)%의 배에는 선물이 갖게하고 있다
				CASE 1
					PRINTFORML %ANAME(LOCAL:0)%의 배의 아이에 대해 여러가지 억측이 흐르고 있다
				CASE 2
					PRINTFORML 욕보일 수 있었던 위, 이런 모습으로 아군의 바탕으로 돌려주어지는 것을 %조사처리(ANAME(LOCAL:0),"는")% 울고 있다
				CASE 3
					PRINTFORML %조사처리(ANAME(LOCAL:0),"가")% 부풀어 오른 배는 반환식에 따라 온 병사들의 호기의 시선에 노출되고 있다
			ENDSELECT
		ELSEIF IS_LOVER(LOCAL:0)
			PRINTFORML %조사처리(ANAME(LOCAL:0),"는")% 해방을 기뻐하면서도 %조사처리(ANAME(MASTER),"를")% 생각해, 몇 번이나 되돌아 보고 있다
		ELSEIF TALENT:(LOCAL:0):인기
			PRINTFORML 인기가 있는 %ANAME(LOCAL:0)%의 귀환을 모두가 기뻐하고 있다
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):외교선택플래그 && CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속
		CALL CAPTURE(LOCAL:0, 0)
		PRINTFORML %조사처리(ANAME(LOCAL:0),"는")% 해방되었습니다
		IF IS_LOVER(LOCAL:0) || IS_SLAVED_BY(LOCAL:0)
			PRINTFORML %조사처리(ANAME(LOCAL:0),"는")% %조사처리(ANAME(MASTER),"가")% 해방에 진력해 준 것을 기뻐하고 있다
		ELSEIF IS_SLAVE(LOCAL:0)
			PRINTFORML %조사처리(ANAME(LOCAL:0),"는")% %ANAME(MASTER)%에 포로가 되어 버린 것을 사과하고 있다
		ELSEIF TALENT:(LOCAL:0):인기
			PRINTFORML 인기가 있는 %ANAME(LOCAL:0)%의 귀환을 모두가 기뻐하고 있다
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------
;세력 ARG:0으로부터 세력 ARG:1에 ARG:2만의 병력을 이동시키는 처리
;-------------------------------------------------
@SHIFT_SOLDIER(ARG:0, ARG:1, ARG:2)
LOCAL:12 = GET_SUM_SOLDIER(ARG:0)
LOCAL:13 = GET_SUM_ARMY_DF(ARG:0)

IF ARG:2 > COUNTRY_SOLDIER:(ARG:0)
	IF ARG:2 > COUNTRY_SOLDIER:(ARG:0) + LOCAL:13
		CALL CLEAR_ALL_UNIT(ARG:0)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			COUNTRY_SOLDIER:(ARG:0) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == ARG:0
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(ARG:0), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(ARG:0) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(ARG:0) = MAX(COUNTRY_SOLDIER:(ARG:0) - ARG:2, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:1) += ARG:2

;-------------------------------------------------
;인질 교섭에 있어서의 캐릭터 ARG:0의 가치  능력이 높을 정도 가치가 높아진다
;-------------------------------------------------
@GET_PRISONER_VALUE(ARG:0)
#FUNCTION
LOCAL:0 = 0
LOCAL:0 += (ABL:(ARG:0):무투 + ABL:(ARG:0):방위 + ABL:(ARG:0):지략 + ABL:(ARG:0):정치) / 4
LOCAL:0 += (ABL:(ARG:0):가창 + ABL:(ARG:0):요리) / 2
;대상이 특수한 세력에 잡히고 있어 그것이 자국이 잡고 있는 포로가 아닌 경우, 가치는 5배에
SIF CFLAG:MASTER:소속 != CFLAG:(ARG:0):포로처 && IS_SP_COUNTRY(CFLAG:(ARG:0):포로처)
	LOCAL:0 *= 5
RETURNF POWER(MAX(LOCAL:0 - 40, 10), 2)

