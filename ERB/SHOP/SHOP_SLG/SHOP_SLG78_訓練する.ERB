;-------------------------------------------------
;「훈련한다」의 명칭
;-------------------------------------------------
@SHOP_SLG_NAME78
RESULTS:0 '= "훈련한다"

;-------------------------------------------------
;「훈련한다」의 선택 가부
;-------------------------------------------------
@SHOP_SLG_CHECK78
SIF FLAG:관전모드
	RETURN 0
SIF CFLAG:MASTER:소속 != 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;「훈련한다」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_SLG_EVENTBUY78
CALL SLG_TRAIN
RETURN 1

;-------------------------------------------------
;「훈련한다」본체
;-------------------------------------------------
@SLG_TRAIN
#DIM 대상항목
#DIM 취득경험치
CALL SINGLE_DRAWLINE
PRINTFORML 여가를 이용해, 능력을 단련합니다
PRINTFORML 무엇을 늘립니까?
CALL SINGLE_DRAWLINE
PRINTFORM [{GETNUM(ABL, "무투")}] 무투 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:무투)
PRINTFORML {ABL:MASTER:무투, 3, RIGHT}
PRINTFORM [{GETNUM(ABL, "방위")}] 방위 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:방위)
PRINTFORML {ABL:MASTER:방위, 3, RIGHT}
PRINTFORM [{GETNUM(ABL, "지략")}] 지략 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:지략)
PRINTFORML {ABL:MASTER:지략, 3, RIGHT}
PRINTFORM [{GETNUM(ABL, "정치")}] 정치 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:정치)
PRINTFORML {ABL:MASTER:정치, 3, RIGHT}
IF TALENT:MASTER:신비지식
	PRINTFORM [{GETNUM(ABL, "신비")}] 신비 
	CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:신비)
	PRINTFORML {ABL:MASTER:신비, 3, RIGHT}
ENDIF
PRINTFORM [{GETNUM(ABL, "가창")}] 가창 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:가창)
PRINTFORML {ABL:MASTER:가창, 3, RIGHT}
PRINTFORM [{GETNUM(ABL, "요리")}] 요리 
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:MASTER:요리)
PRINTFORML {ABL:MASTER:요리, 3, RIGHT}
PRINTL 
CALL SINGLE_DRAWLINE
PRINTFORML [0] 돌아온다
PRINTL 

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF !GROUPMATCH(RESULT, GETNUM(ABL, "무투"), GETNUM(ABL, "방위"), GETNUM(ABL, "지략"), GETNUM(ABL, "정치"), GETNUM(ABL, "요리"), GETNUM(ABL, "가창"), GETNUM(ABL, "신비"))
	GOTO INPUT_LOOP
ELSEIF RESULT == GETNUM(ABL, "신비") && !TALENT:MASTER:신비지식
	GOTO INPUT_LOOP
ENDIF

대상항목 = RESULT
취득경험치 = SPECIAL_TRAIN_CALC_EFFECT(MASTER, 대상항목) / 2

PRINTFORML 훈련을 실시했다!

CALL PRINT_ADD_EXP(MASTER, EXPNAME:GET_EXP(대상항목), 취득경험치, 1)
CALL TRAIN_AUTO_ABLUP(MASTER)
PRINTFORMW 좋은 땀을 흘렸다……
RESULT = 1
BEGIN TURNEND

