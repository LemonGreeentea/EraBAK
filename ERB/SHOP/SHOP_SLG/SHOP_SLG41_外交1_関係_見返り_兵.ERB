;-------------------------------------------------
;병의 요구의 처리
;ARG:0은 상대의 세력 번호, ARG:1은 요구의 무게. 요구를 마시면 1, 거절하면 0을 돌려준다
;-------------------------------------------------
@DIPLOMACTY_REQUESTED_SOLDIER(ARG:0, ARG:1)
;상대 세력의 군주의 캐릭터 번호를 취득
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)

LOCAL:12 = GET_SUM_SOLDIER(CFLAG:MASTER:소속)
LOCAL:13 = GET_SUM_ARMY_DF(CFLAG:MASTER:소속)
IF ARG:1 == 0
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:소속) * 3 / 1000, LOCAL:12 / 2)
ELSEIF ARG:1 == 1
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:소속) * 8 / 1000, LOCAL:12 * 2 / 3)
ELSE
	LOCAL:11 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:소속) * 20 / 1000, LOCAL:12)
ENDIF
PRINTFORML %조사처리(ANAME(LOCAL:5),"는")%, 이쪽의 제안을 마시는 대신에 군사{LOCAL:11}를 요구했다
PRINTFORML 현재의 총병수:{LOCAL:12}(우리집 미할당병수:{COUNTRY_SOLDIER:(CFLAG:MASTER:소속)})
IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속)
	IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속) + LOCAL:13
		PRINTFORML ※요구병수가 현재의 미할당병수보다 많기 때문에, 전부대를 해산해, 방위에 도착해 있는 군사도 건네줄 필요가 있습니다
	ELSE
		PRINTFORML ※요구병수가 현재의 미할당병수보다 많기 때문에, 방위에 도착해 있는 군사도 건네줄 필요가 있습니다
	ENDIF
ENDIF
WAIT
CALL ASK_YN("받아들인다", "거절한다")
IF RESULT != 0
	RETURN 0
ENDIF

IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속)
	IF LOCAL:11 > COUNTRY_SOLDIER:(CFLAG:MASTER:소속) + LOCAL:13
		CALL CLEAR_ALL_UNIT(CFLAG:MASTER:소속)
	ENDIF

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:소속
			COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += CITY_SOLDIER:(LOCAL:0)
			CITY_SOLDIER:(LOCAL:0) = 0
		ENDIF
	NEXT

	COUNTRY_SOLDIER:(CFLAG:MASTER:소속) = MAX(COUNTRY_SOLDIER:(CFLAG:MASTER:소속) - LOCAL:11, 0)

	FOR LOCAL:0, 0, MAX_CITY
		IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:소속
			LOCAL:15 = MIN(COUNTRY_SOLDIER:(CFLAG:MASTER:소속), 500)
			CITY_SOLDIER:(LOCAL:0) += LOCAL:15
			COUNTRY_SOLDIER:(CFLAG:MASTER:소속) -= LOCAL:15
		ENDIF
	NEXT

ELSE
	COUNTRY_SOLDIER:(CFLAG:MASTER:소속) = MAX(COUNTRY_SOLDIER:(CFLAG:MASTER:소속) - LOCAL:11, 0)
ENDIF

COUNTRY_SOLDIER:(ARG:0) += LOCAL:11

RETURN 1

