;-------------------------------------------------
;영토를 할양 한다
;-------------------------------------------------
@DIPLOMACY_GIVE_CITY(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
LOCAL:5 = GET_COUNTRY_BOSS(ARG:0)
CALL TMP_CREATE_UNIT_MAP
CALL TMP_CREATE_RELATION_MAP
;자국의 도시의 수를 센다
LOCAL:6 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == CFLAG:MASTER:소속 && GET_CITYNAME(LOCAL:0) != "무명" && !IS_STAY_ENEMY_UNIT(LOCAL:0)
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 1
	PRINTFORMW 할양 할 수 있는 영토가 없습니다
	RETURN 0
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML 할양 하는 도시를 선택해 주세요
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE
PRINTBUTTON "[돌아온다]", 0


$INPUT_LOOP
INPUT

SIF RESULT == 0
	RETURN 0


LOCAL:15 = (RESULT - 1000)

IF LOCAL:15 >= 0 && CITY_OWNER:(LOCAL:15) == CFLAG:MASTER:소속
	
	;대상 캐릭터의 정보를 표시
	CALL SINGLE_DRAWLINE
	CALL SHOW_CITY_INFO(LOCAL:15)
	CALL SINGLE_DRAWLINE
	PRINTFORML 이 영토를 할양 합니까?

	;네／아니오 입력 처리
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF

	PRINTFORMW %GET_CITYNAME(LOCAL:15)%는 %ANAME(LOCAL:5)% 세력의 영토가 되었습니다
	CALL SINGLE_MINIMIZE(LOCAL:15)
	CITY_OWNER:(LOCAL:15) = ARG:0

	;호감도 상승치·적대치 하강치의 계산
	LOCAL:25 = CITY_ECONOMY:(LOCAL:15) / 100
	LOCAL:25 = LIMIT(LOCAL:25, 25, 400)

	;정치·요리의 보정
	CALL TMP_CREATE_IS_FREE_MAP
	LOCAL:0 = SQRT(TMP_GET_POLITICS_POWER(CFLAG:MASTER:소속)) / 10
	LOCAL:1 = SQRT(TMP_GET_COOKING_POWER(CFLAG:MASTER:소속)) / 10
	LOCAL:25 = LOCAL:25 * (LOCAL:0 / 10 + LOCAL:1 / 10) / 10

	;서로의 나라끼리의 호감도를 상승시키고 적대치를 저하시킨다
	CALL DIPLOMACY_IMPROVE_RELATION(ARG:0, LOCAL:25)
	RETURN 1
ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

;-------------------------------------------------
;영토 할양을 요구한다
;-------------------------------------------------
@DIPLOMACY_WANT_CITY(ARG:0)
#DIM FIRST_LINE
LOCAL:4 = GET_COUNTRY_BOSS(ARG:0)
LOCAL:5 = GET_COUNTRY_BOSS(CFLAG:MASTER:소속)
CALL TMP_CREATE_UNIT_MAP
CALL TMP_CREATE_RELATION_MAP

;일시적으로 해당 세력을 아군취급한다
LOCAL:10 = TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0)
LOCAL:11 = TMP_COUNTRY_RELATION:(ARG:0):(CFLAG:MASTER:소속)
TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) = 1
TMP_COUNTRY_RELATION:(ARG:0):(CFLAG:MASTER:소속) = 1

;해당 세력의 도시의 수를 센다
LOCAL:6 = 0
FOR LOCAL:0, 0, MAX_CITY
	IF CITY_OWNER:(LOCAL:0) == ARG:0 && GET_CITYNAME(LOCAL:0) != "무명" && IS_STAY_ENEMY_UNIT(LOCAL:0) != 1
		LOCAL:6 ++
	ENDIF
NEXT

IF LOCAL:6 <= 1
	PRINTFORMW 할양을 요구 할 수 있는 영토가 없습니다
	RETURN 0
ENDIF

CALL SINGLE_DRAWLINE
PRINTFORML 할양을 요구할 도시를 선택해 주세요
CALL SINGLE_DRAWLINE
FIRST_LINE = LINECOUNT
$SHOW_LOOP
CALL SET_CITY_COLOR_COUNTRY
CALL SET_CITY_COLOR_UNIT
CALL DRAWMAP
CALL RESET_CITY_COLOR
CALL SINGLE_DRAWLINE
PRINTBUTTON "[돌아온다]", 0


$INPUT_LOOP
INPUT

IF RESULT == 0
	;아군취급했던 해당 세력을 원상태로 되돌린다
	TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) = LOCAL:10
	TMP_COUNTRY_RELATION:(ARG:0):(CFLAG:MASTER:소속) = LOCAL:11
	RETURN 0
ENDIF

LOCAL:15 = (RESULT - 1000)

IF LOCAL:15 >= 0 && CITY_OWNER:(LOCAL:15) == ARG:0
	
	;대상 캐릭터의 정보를 표시
	CALL SINGLE_DRAWLINE
	CALL SHOW_CITY_INFO(LOCAL:15)
	CALL SINGLE_DRAWLINE

	IF IS_STAY_ENEMY_UNIT(LOCAL:15) == 1
		PRINTFORMW 해당 영토에는 적국 부대가 주둔하고 있어 할양을 요구할 수 없습니다
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	PRINTFORML 이 영토를 요구 합니까?

	;네／아니오 입력 처리
	CALL ASK_YN()
	IF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF

	CALL DIPLOMACY_REQUEST_COMMON(ARG:0, 300)
	LOCAL:2 = RESULT
	SIF LOCAL:2 <= 0
		RETURN 0

	PRINTFORMW %조사처리(GET_CITYNAME(LOCAL:15),"는")% %ANAME(LOCAL:5)% 세력의 영토가 되었습니다
	;해당 도시 접수 처리
	CALL TAKEOVER_CITY_FROM_NO(CFLAG:MASTER:소속, LOCAL:15)

	;아군취급했던 해당 세력을 원상태로 되돌린다
	TMP_COUNTRY_RELATION:(CFLAG:MASTER:소속):(ARG:0) = LOCAL:10
	TMP_COUNTRY_RELATION:(ARG:0):(CFLAG:MASTER:소속) = LOCAL:11

	;쾌락으로 말하는 것을 들려주었을 경우, 포상의 처리
	SIF LOCAL:2 == 2
		CALL DIPLOMACY_REQUEST_PRIZE(ARG:0)
	;약물로 말하는 것을 들려주었을 경우, 드라그(약품, 마약)의 처리
	SIF LOCAL:2 == 3
		CALL DIPLOMACY_REQUEST_DRUG(ARG:0)
	;최면으로 말하는 것을 들려주었을 경우, 최면의 처리
	SIF LOCAL:2 == 4
		CALL DIPLOMACY_REQUEST_HYPNOSIS(ARG:0)
	RETURN 1

ENDIF
CLEARLINE 1
GOTO INPUT_LOOP

