;-------------------------------------------------
;캐릭터 표시순서의 리스트를 작성
;RESORT 소속 세력마다의 재소트를 실시할까의 플래그
;-------------------------------------------------
@CREATE_SELECTOR_SORT_LIST(RESORT = 1)
#DIM RESORT
#DIM SORT_VALUE,  3000
#DIMS SORT_STRING, 3000
#DIM CHARA_TABLE, 3000
#DIM SORT_BY_STRING
VARSET SHOP_CHARA_LIST, -1

VARSET CHARA_TABLE, -1
VARSET SORT_VALUE, -1
VARSET SORT_STRING
SORT_BY_STRING = 0

FOR LOCAL, 0, CHARANUM
	SELECTCASE 소트키
		CASE 소트_소속 
			SORT_VALUE:LOCAL = CFLAG:LOCAL:소속
		CASE 소트_NO 
			SORT_VALUE:LOCAL = NO:LOCAL
		CASE 소트_쇼우
			SORT_VALUE:LOCAL = TMP_CHARA_STARS:LOCAL
		CASE 소트_이름 
			SORT_STRING:LOCAL = %ANAME(LOCAL)%
			SORT_BY_STRING = 1
		CASE 소트_호감도
			SORT_VALUE:LOCAL = CFLAG:LOCAL:호감도
		CASE 소트_종속도
			SORT_VALUE:LOCAL = CFLAG:LOCAL:종속도
		CASE 소트_지배도
			SORT_VALUE:LOCAL = CFLAG:LOCAL:지배도
		CASE 소트_임신 
			IF CFLAG:LOCAL:행동불능상태 == 행동불능_임월
				SORT_VALUE:LOCAL = 2
			ELSEIF TALENT:LOCAL:임신
				SORT_VALUE:LOCAL = 1
			ELSEIF CFLAG:LOCAL:행동불능상태 == 행동불능_육아
				SORT_VALUE:LOCAL = 3
			ELSEIF CFLAG:LOCAL:행동불능상태 == 행동불능_아이
				SORT_VALUE:LOCAL = 4
			ELSE
				SORT_VALUE:LOCAL = __INT_MAX__
			ENDIF
		CASE 소트_성별 
			SORT_VALUE:LOCAL = TALENT:LOCAL:성별
		CASE 소트_안면
			SORT_VALUE:LOCAL = CFLAG:LOCAL:안면
		CASE 소트_무투
			SORT_VALUE:LOCAL = ABL:LOCAL:무투
		CASE 소트_방위
			SORT_VALUE:LOCAL = ABL:LOCAL:방위
		CASE 소트_지략
			SORT_VALUE:LOCAL = ABL:LOCAL:지략
		CASE 소트_정치
			SORT_VALUE:LOCAL = ABL:LOCAL:정치
		CASE 소트_가창
			SORT_VALUE:LOCAL = ABL:LOCAL:가창
		CASE 소트_요리
			SORT_VALUE:LOCAL = ABL:LOCAL:요리
		CASE 소트_신비
			SORT_VALUE:LOCAL = ABL:LOCAL:신비
		CASE 소트_능력4종
			SORT_VALUE:LOCAL = ABL:LOCAL:무투 + ABL:LOCAL:방위 + ABL:LOCAL:지략 + ABL:LOCAL:정치
		CASE 소트_능력7종
			SORT_VALUE:LOCAL = ABL:LOCAL:무투 + ABL:LOCAL:방위 + ABL:LOCAL:지략 + ABL:LOCAL:정치 + ABL:LOCAL:요리 + ABL:LOCAL:가창 + ABL:LOCAL:신비
		CASE 소트_요바이조수
			SORT_VALUE:LOCAL = YOBAI_ZYOSYU_CALC_POINT(LOCAL)
		CASE 소트_안내조수
			SORT_VALUE:LOCAL = TEBIKI_ZYOSYU_CALC_POINT(LOCAL)
		CASE 소트_오락담당
			SORT_VALUE:LOCAL = ABL:LOCAL:가창 + ABL:LOCAL:요리
	ENDSELECT

	CHARA_TABLE:LOCAL = LOCAL

NEXT

FOR LOCAL,1,CHARANUM
	SIF CHARA_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
		LOCAL:13 = CHARA_TABLE:(LOCAL:11)
		IF !SORT_BY_STRING
			IF (내림차순소트 && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!내림차순소트 && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
				SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
			ELSE
				BREAK
			ENDIF
		ELSE
			IF (내림차순소트 && SORT_STRING:(LOCAL:12) > SORT_STRING:(LOCAL:13)) || (!내림차순소트 && SORT_STRING:(LOCAL:12) < SORT_STRING:(LOCAL:13))
				SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
			ELSE
				BREAK
			ENDIF
		ENDIF
	NEXT
NEXT

IF RESORT == 0
	FOR LOCAL:0, 0, CHARANUM
		SHOP_CHARA_LIST:LOCAL = CHARA_TABLE:LOCAL
	NEXT
ELSE

	LOCAL:5 = 0

	;SHOP_CHARA_LIST:(LOCAL:5) = MASTER
	;LOCAL:5 ++

	;주인공과 동일 세력의 사관이 제 1 우선
	FOR LOCAL:0, 0, CHARANUM
		LOCAL:1 = CHARA_TABLE:LOCAL
		;주인공이 아닌 「당신」는 날린다
		SIF LOCAL:1 == NAME_TO_CHARA("당신") && LOCAL:1 != MASTER
			CONTINUE
		IF CFLAG:(LOCAL:1):소속 == CFLAG:MASTER:소속
			SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:1
			LOCAL:5 ++
		ENDIF
	NEXT

	;외 세력·포로가 아닌 사관이 제 2 우선
	FOR LOCAL:0, 0, CHARANUM
		LOCAL:1 = CHARA_TABLE:LOCAL
		;주인공이 아닌 「당신」는 날린다
		SIF LOCAL:1 == NAME_TO_CHARA("당신") && LOCAL:1 != MASTER
			CONTINUE
		IF CFLAG:(LOCAL:1):소속 != CFLAG:MASTER:소속 && !CFLAG:(LOCAL:1):포로처
			SIF LOCAL:1 == NAME_TO_CHARA("당신") && LOCAL:1 != MASTER
				CONTINUE
			SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:1
			LOCAL:5 ++
		ENDIF
	NEXT

	;외 세력·포로가 라스트
	FOR LOCAL:0, 0, CHARANUM
		LOCAL:1 = CHARA_TABLE:LOCAL
		;주인공이 아닌 「당신」는 날린다
		SIF LOCAL:1 == NAME_TO_CHARA("당신") && LOCAL:1 != MASTER
			CONTINUE
		IF CFLAG:(LOCAL:1):소속 != CFLAG:MASTER:소속 && CFLAG:(LOCAL:1):포로처
			SHOP_CHARA_LIST:(LOCAL:5) = LOCAL:1
			LOCAL:5 ++
		ENDIF
	NEXT
ENDIF

;-------------------------------------------------
;오른쪽 컬럼의 표준적인 캐릭터 리스트
;인수）
;  LINE_NUM_BUTTON			일행에 버튼을 몇개 늘어놓을까（초기치 2）
;  BUTTON_NUM_LINE			1 버튼 지정으로 몇행 취할까（초기치 1）
;							포로의취급등, 1 캐릭터에게 대량의 버튼이 있는 경우등에 사용하는 상정
;외）
;  MAX_BUTTON				1 페이지에 표시 가능한 버튼의 최대치
;  NOW_BUTTON				현재 몇개눈의 버튼을 표시했는지
;  NOW_BUTTON_PAGE			현재의 페이지로 몇개눈의 버튼을 표시했는지
;  MENU_CHARANUM			이 메뉴로 선택 가능한 캐릭터의 인원수
;							회색 표시로 선택 할 수 없지만 표시하는 캐릭터도 포함한다
;  NOW_CHECK_CHARA			현재 체크중의 캐릭터.
;							캐릭터 리스트순서에 버튼을 늘어놓고 싶기 때문에 루프 카운터를
;							그대로 캐릭터 번호로서 취급하지 않는다
;  NOW_PAGE_BUTTON_START	현재의 페이지의 최초로 두는 버튼이 몇 번인가
;  NOW_PAGE_BUTTON_END		현재의 페이지의 마지막에 두는 버튼이 몇 번인가
;  PRINTOPTION				이 캐릭터는 「선택 불가능비표시」인가 「선택 가능」인가
;							「선택 불가능 회색 표시」나 판정의 반환값
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST(LINE_NUM_BUTTON = 2, BUTTON_LINE = 1)
#DIM MAX_BUTTON
#DIM NOW_BUTTON
#DIM NOW_BUTTON_PAGE
#DIM MENU_CHARANUM
#DIM NOW_CHECK_CHARA
#DIM NOW_PAGE_BUTTON_START
#DIM NOW_PAGE_BUTTON_END
#DIM LINE_NUM_BUTTON
#DIM BUTTON_LINE
#DIM PRINTOPTION

;하나의 페이지에 표시 가능한 버튼수의 최대치를 계산((거점 페이즈 화면 최대행수 - 2 - 현재 표시행수) * LINE_NUM_BUTTON)/BUTTON_LINE
MAX_BUTTON = ((LINES_SHOP_DRAW - 2 - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * LINE_NUM_BUTTON) / BUTTON_LINE

;선택 가능 캐릭터 인원수
MENU_CHARANUM = 0

FOR LOCAL:0, 0, CHARANUM
	;선택 가능한 조건을 채우고 있는 캐릭터를 판정
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:거점페이즈선택커맨드}(LOCAL:0)
	IF RESULT
		;선택 가능 캐릭터 인원수 가산
		MENU_CHARANUM ++
	ENDIF
NEXT

;최대 페이지수의 계산（캐릭터수-1÷최대 버튼수로 계산해＋1해, 최악이어도 1이상으로 한다）
SHOP_LIFE_LIST1_PAGE_MAX = MAX((MENU_CHARANUM - 1) / MAX_BUTTON + 1, 1)

;현재의 페이지수를 범위에 거둔다
FLAG:거점페이즈페이지 = (SHOP_LIFE_LIST1_PAGE_MAX - 1 + FLAG:거점페이즈페이지 % SHOP_LIFE_LIST1_PAGE_MAX) % SHOP_LIFE_LIST1_PAGE_MAX + 1

;리셋트 현재 버튼수
NOW_BUTTON = 0
;리셋트 현재 페이지의 표시 버튼수
NOW_BUTTON_PAGE = 0
;리셋트 현재 페이지의 개시치（버튼수）
NOW_PAGE_BUTTON_START = (FLAG:거점페이즈페이지 - 1) * MAX_BUTTON
;리셋트 현재 페이지의 종료치（버튼수）
NOW_PAGE_BUTTON_END = FLAG:거점페이즈페이지 * MAX_BUTTON

;캐릭터 리스트 개시
FOR LOCAL:0, 0, CHARANUM
	;이번 체크하는 캐릭터（캐릭터 번호순서는 아니고, 리스트용으로 줄서 바꿀 수 있었던 순서로 체크）
	NOW_CHECK_CHARA = SHOP_CHARA_LIST:(LOCAL:0)
	;리스트로부터 제외되고 있는 캐릭터라면 다음의 캐릭터에게
	SIF NOW_CHECK_CHARA == -1
		CONTINUE
	;캐릭터가 선택 가능한 조건을 채우고 있는지를 판정（PRINTOPTION=2조건은 채우고 있지만 행동 끝난 경우 회색 표시）
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA)
	PRINTOPTION = RESULT
	IF RESULT
		IF NOW_BUTTON >= NOW_PAGE_BUTTON_START && NOW_BUTTON < NOW_PAGE_BUTTON_END
			;버튼을 1행에 늘어놓는 수
			IF NOW_BUTTON_PAGE % LINE_NUM_BUTTON != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			;버튼의 독자 지정이 있으면 불러, 없으면 통상의 버튼을 표시
			TRYCCALLFORM SHOP_LIFE_LIST1_BUTTON{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA, PRINTOPTION)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA, 0, SHOP_LIFE_LIST1_ADD_INPUT, PRINTOPTION)
			ENDCATCH
			;현재 페이지의 표시 버튼수를 추가
			NOW_BUTTON_PAGE ++
		ENDIF
		;현재 버튼수를 추가
		NOW_BUTTON ++
	ENDIF
NEXT

;페이지 이동 표시
IF LINECOUNT - FIRST_COLUMN_RIGHT_LINE < LINES_SHOP_DRAW
	FOR LOCAL:0, LINECOUNT - FIRST_COLUMN_RIGHT_LINE, LINES_SHOP_DRAW - 1
		CALL COLUMN_RIGHT_PRINTL
		IF LOCAL:0 == LINES_SHOP_DRAW - 3
			CALL COLUMN_RIGHT_LINE
		ELSEIF LOCAL:0 == LINES_SHOP_DRAW - 2
			IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
				PRINTBUTTON @"[%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_BACK
			ELSE
				SETCOLOR 칼라_선택불가
				PRINTPLAINFORM [%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]
				RESETCOLOR
			ENDIF
			PRINTPLAINFORM %TOSTR_SPACE(10)%
			LOCALS:0 = page{FLAG:거점페이즈페이지}/{SHOP_LIFE_LIST1_PAGE_MAX}
			PRINTPLAINFORM %LOCALS:0, 16, LEFT%
			IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
				PRINTBUTTON @"[%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_NEXT
			ELSE
				SETCOLOR 칼라_선택불가
				PRINTPLAINFORM [%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]
				RESETCOLOR
			ENDIF
		ENDIF
	NEXT
ENDIF

PRINTL 

RETURN 0

;-------------------------------------------------
;오른쪽 컬럼의 상하 2단의 캐릭터 리스트상
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_TOP(LINE_NUM_BUTTON = 2, BUTTON_LINE = 1)
#DIM MAX_BUTTON
#DIM NOW_BUTTON
#DIM NOW_BUTTON_PAGE
#DIM MENU_CHARANUM
#DIM NOW_CHECK_CHARA
#DIM NOW_PAGE_BUTTON_START
#DIM NOW_PAGE_BUTTON_END
#DIM LINE_NUM_BUTTON
#DIM BUTTON_LINE
#DIM PRINTOPTION

MAX_BUTTON = ((LINES_SHOP_DRAW / 2 - 2  - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * LINE_NUM_BUTTON) / BUTTON_LINE

MENU_CHARANUM = 0

FOR LOCAL:0, 0, CHARANUM
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:거점페이즈선택커맨드}(LOCAL:0)
	IF RESULT
		MENU_CHARANUM ++
	ENDIF
NEXT

SHOP_LIFE_LIST1_PAGE_MAX = MAX((MENU_CHARANUM - 1) / MAX_BUTTON + 1, 1)

FLAG:거점페이즈페이지 = (SHOP_LIFE_LIST1_PAGE_MAX - 1 + FLAG:거점페이즈페이지 % SHOP_LIFE_LIST1_PAGE_MAX) % SHOP_LIFE_LIST1_PAGE_MAX + 1

NOW_BUTTON = 0
NOW_BUTTON_PAGE = 0
NOW_PAGE_BUTTON_START = (FLAG:거점페이즈페이지 - 1) * MAX_BUTTON
NOW_PAGE_BUTTON_END = FLAG:거점페이즈페이지 * MAX_BUTTON

FOR LOCAL:0, 0, CHARANUM
	NOW_CHECK_CHARA = SHOP_CHARA_LIST:(LOCAL:0)
	SIF NOW_CHECK_CHARA == -1
		CONTINUE
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA)
	PRINTOPTION = RESULT
	IF RESULT
		IF NOW_BUTTON >= NOW_PAGE_BUTTON_START && NOW_BUTTON < NOW_PAGE_BUTTON_END
			IF NOW_BUTTON_PAGE % LINE_NUM_BUTTON != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			TRYCCALLFORM SHOP_LIFE_LIST1_BUTTON{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA, PRINTOPTION)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA, 0, SHOP_LIFE_LIST1_ADD_INPUT, PRINTOPTION)
			ENDCATCH
			NOW_BUTTON_PAGE ++
		ENDIF
		NOW_BUTTON ++
	ENDIF
NEXT

FOR LOCAL:0, LINECOUNT - FIRST_COLUMN_RIGHT_LINE, LINES_SHOP_DRAW / 2 - 1
	CALL COLUMN_RIGHT_PRINTL
	IF LOCAL:0 == LINES_SHOP_DRAW / 2 - 3
		CALL COLUMN_RIGHT_LINE
	ELSEIF LOCAL:0 == LINES_SHOP_DRAW / 2 - 2
		IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
			PRINTBUTTON @"[%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_BACK
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTPLAINFORM %TOSTR_SPACE(10)%
		LOCALS:0 = page{FLAG:거점페이즈페이지}/{SHOP_LIFE_LIST1_PAGE_MAX}
		PRINTPLAINFORM %LOCALS:0, 16, LEFT%
		IF SHOP_LIFE_LIST1_PAGE_MAX >= 2
			PRINTBUTTON @"[%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST1_PAGE_NEXT
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
	ENDIF
NEXT
RETURN 0

;-------------------------------------------------
;오른쪽 컬럼의 상하 2단의 캐릭터 리스트하
;-------------------------------------------------
@COLUMN_RIGHT_CHARALIST_BOTTOM(LINE_NUM_BUTTON_BOTTOM = 2, BUTTON_LINE_BOTTOM = 1)
#DIM MAX_BUTTON_BOTTOM
#DIM NOW_BUTTON_BOTTOM
#DIM NOW_BUTTON_PAGE_BOTTOM
#DIM MENU_CHARANUM_BOTTOM
#DIM NOW_CHECK_CHARA_BOTTOM
#DIM NOW_PAGE_BUTTON_START_BOTTOM
#DIM NOW_PAGE_BUTTON_END_BOTTOM
#DIM LINE_NUM_BUTTON_BOTTOM
#DIM BUTTON_LINE_BOTTOM
#DIM PRINTOPTION_BOTTOM

MAX_BUTTON_BOTTOM = ((LINES_SHOP_DRAW- 2 - (LINECOUNT - FIRST_COLUMN_RIGHT_LINE)) * LINE_NUM_BUTTON_BOTTOM) / BUTTON_LINE_BOTTOM
SHOP_LIFE_LIST2_PAGE_MAX = 0
FOR LOCAL:0, 0, CHARANUM
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:거점페이즈선택커맨드}(LOCAL:0)
	IF RESULT
		SHOP_LIFE_LIST2_PAGE_MAX ++
	ENDIF
NEXT
SHOP_LIFE_LIST2_PAGE_MAX = MAX((SHOP_LIFE_LIST2_PAGE_MAX - 1) / MAX_BUTTON_BOTTOM + 1, 1)

FLAG:거점페이즈페이지2 = (SHOP_LIFE_LIST2_PAGE_MAX - 1 + FLAG:거점페이즈페이지2 % SHOP_LIFE_LIST2_PAGE_MAX) % SHOP_LIFE_LIST2_PAGE_MAX + 1

;캐릭터 리스트의 표시
NOW_BUTTON_BOTTOM = 0
NOW_BUTTON_PAGE_BOTTOM = 0
NOW_PAGE_BUTTON_START_BOTTOM = (FLAG:거점페이즈페이지2 - 1) * MAX_BUTTON_BOTTOM
NOW_PAGE_BUTTON_END_BOTTOM = FLAG:거점페이즈페이지2 * MAX_BUTTON_BOTTOM
FOR LOCAL:0, 0, CHARANUM
	NOW_CHECK_CHARA_BOTTOM = SHOP_CHARA_LIST:(LOCAL:0)
	SIF NOW_CHECK_CHARA_BOTTOM == -1
			CONTINUE
	;선택 가능한 조건을 채우고 있는지 어떤지를 판정
	RESULT = 0
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA_BOTTOM)
	PRINTOPTION_BOTTOM = RESULT
	IF RESULT
		IF NOW_BUTTON_BOTTOM >= NOW_PAGE_BUTTON_START_BOTTOM && NOW_BUTTON_BOTTOM < NOW_PAGE_BUTTON_END_BOTTOM
			IF NOW_BUTTON_PAGE_BOTTOM % LINE_NUM_BUTTON_BOTTOM != 0
				PRINTPLAIN 　
			ELSE
				IF NOW_BUTTON_PAGE_BOTTOM >= 1
					CALL COLUMN_RIGHT_PRINTL
				ENDIF
			ENDIF
			TRYCCALLFORM SHOP_LIFE_LIST2_BUTTON{FLAG:거점페이즈선택커맨드}(NOW_CHECK_CHARA_BOTTOM, PRINTOPTION_BOTTOM)
			CATCH
				CALL COLUMN_RIGHT_CHARALIST_BUTTON(NOW_CHECK_CHARA_BOTTOM, 0, SHOP_LIFE_LIST2_ADD_INPUT, PRINTOPTION_BOTTOM)
			ENDCATCH
			NOW_BUTTON_PAGE_BOTTOM ++
		ENDIF
		NOW_BUTTON_BOTTOM ++
	ENDIF
NEXT

FOR LOCAL:0, (LINECOUNT - FIRST_COLUMN_RIGHT_LINE), LINES_SHOP_DRAW - 1
	CALL COLUMN_RIGHT_PRINTL
	IF LOCAL:0 == LINES_SHOP_DRAW - 3
		CALL COLUMN_RIGHT_LINE
	ELSEIF LOCAL:0 == LINES_SHOP_DRAW - 2
		IF SHOP_LIFE_LIST2_PAGE_MAX >= 2
			PRINTBUTTON @"[%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST2_PAGE_BACK
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"이전페이지", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTPLAINFORM %TOSTR_SPACE(10)%
		LOCALS:0 = page{FLAG:거점페이즈페이지2}/{SHOP_LIFE_LIST2_PAGE_MAX}
		PRINTPLAINFORM %LOCALS:0, 16, LEFT%
		IF SHOP_LIFE_LIST2_PAGE_MAX >= 2
			PRINTBUTTON @"[%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]", SHOP_LIFE_LIST2_PAGE_NEXT
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"다음페이지", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
	ENDIF
NEXT

PRINTL 

RETURN 0

