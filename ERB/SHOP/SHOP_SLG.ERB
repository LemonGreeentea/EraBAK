;전략 페이즈의 숍 화면
;-------------------------------------------------
;숍 화면의 표시 처리(전략 페이즈)
;-------------------------------------------------
@SHOW_SHOP_SLG
;커맨드의 선택 가부 판정
CALL CHECK_SAVE_SLG

;카테고리 번호와 메뉴 번호를 보존하는 배열의 리셋트
CALL COLUMN_LEFT_MENULINE_SAVE_SLG

MAP_LINES = MENU_HEIGHT

;행 번호의 리셋트
FIRST_COLUMN_RIGHT_LINE_SLG = LINECOUNT

;로드 데이터용의 에러 회피
IF FLAG:전략페이즈페이지 == 0
	FLAG:전략페이즈페이지 = 1
ENDIF

;오른쪽 컬럼의 표시 처리 호출이 있는 컨텐츠
TRYCCALLFORM SHOP_SLG_EVENTBUY_SHOW{FLAG:전략페이즈선택커맨드}
CATCH
	;맵과 커맨드 버튼을 표시
	CALL SET_CITY_COLOR_COUNTRY
	CALL SET_CITY_COLOR_UNIT
	CALL DRAWMAP(1, 1)
	CALL RESET_CITY_COLOR
	;도시의 정보를 표시
	CALL SHOW_CITY_INFO(SHOWN_CITY)
ENDCATCH

;-------------------------------------------------
;숍 화면의 입력 처리(전략 페이즈 0～99)
;ARG:0에 입력한 값이 들어온다
;처리를 실시했을 경우는 1을, 실시하지 않았던 경우는 0을 돌려주는 것
;-------------------------------------------------
@EVENTBUY_SLG(ARG:0)
;일단 에러 회피
;컨텐츠내의 최대 페이지수가 잡히지 않겠지만
;페이지 이동을 클릭 되었을 경우용
SIF SHOP_SLG_LIST1_MAXPAGE == 0
	SHOP_SLG_LIST1_MAXPAGE = 1

;입력 처리
IF SHOP_AVAIL:(ARG:0) && ARG:0 >= COLUMN_LEFT_MENU_START_VALUE && ARG:0 <= COLUMN_LEFT_MENU_END_VALUE
	;2회 같은 녀석을 클릭 하는 것은 아마 타글
	IF FLAG:전략페이즈선택커맨드 == ARG:0
		TRYCCALLFORM SHOP_SLG_NAME{FLAG:전략페이즈선택커맨드}
		CATCH
		ENDCATCH
	ELSE
		FLAG:전략페이즈선택커맨드 = ARG:0
	ENDIF
	TRYCCALLFORM SHOP_SLG_EVENTBUY{FLAG:전략페이즈선택커맨드}
		RETURN RESULT
	CATCH
		RETURN 0
	ENDCATCH
ELSE
	SELECTCASE ARG:0
		;이전페이지
		CASE SHOP_SLG_LIST1_PAGE_BACK
			FLAG:전략페이즈페이지 --
			IF FLAG:전략페이즈페이지 < 1
				FLAG:전략페이즈페이지 = SHOP_SLG_LIST1_MAXPAGE
			ENDIF
			RETURN 0
		;다음페이지
		CASE SHOP_SLG_LIST1_PAGE_NEXT
			FLAG:전략페이즈페이지 ++
			IF FLAG:전략페이즈페이지 > SHOP_SLG_LIST1_MAXPAGE
				FLAG:전략페이즈페이지 = 1
			ENDIF
			RETURN 0
		;최초의 페이지
		CASE SHOP_SLG_LIST1_PAGE_RESET
			FLAG:전략페이즈페이지 = 1
			RETURN 0
		;마지막 페이지
		CASE SHOP_SLG_LIST1_PAGE_MAX
			FLAG:전략페이즈페이지 = SHOP_SLG_LIST1_MAXPAGE
			RETURN 0
	ENDSELECT
ENDIF
RETURN 0

;-------------------------------------------------
;숍 화면의 입력 처리(전략 페이즈 100～)
;ARG:0에 입력한 값이 들어온다
;처리를 실시했을 경우는 1을, 실시하지 않았던 경우는 0을 돌려주는 것
;-------------------------------------------------
@USERSHOP_SLG(ARG:0)

;도시 정보
IF ARG:0 >= 1000 && ARG:0 < 1000 + MAX_CITY
	SHOWN_CITY = ARG:0 - 1000
	REDRAW 0
	CLEARLINE LINECOUNT - LINES_SHOP
;방위병수의 설정
ELSEIF ARG:0 >= 200 && ARG:0 < 200 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = ARG:0 - 200
	;표시하는 도시를 그 도시에（도시관리 화면이나 직 쳐 했을 때 용）
	SHOWN_CITY = LOCAL:5
	IF CFLAG:MASTER:소속 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:소속
		;적부대가 있는 도시는 변경 불가
		IF IS_STAY_ENEMY_UNIT(LOCAL:5) == 1
			PRINTFORMW 적부대가 존재하는 도시의 병수는 변경할 수 없습니다
		ELSE
			CALL SHOP_CITY_SOLDIER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;수장의 설정
ELSEIF ARG:0 >= 400 && ARG:0 < 400 + MAX_CITY && CONFIG:302 == 0
	LOCAL:5 = RESULT - 400
	;표시하는 도시를 그 도시에（도시관리 화면이나 직 쳐 했을 때 용）
	SHOWN_CITY = LOCAL:5
	IF CFLAG:MASTER:소속 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:소속
		;적부대가 있는 도시는 변경 불가
		IF IS_STAY_ENEMY_UNIT(LOCAL:5) == 1
			PRINTFORMW 적부대가 존재하는 도시의 수장은 변경할 수 없습니다
		ELSE
			CALL SHOP_CITY_COMMANDER(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
	RETURN 0
;세력색변경
ELSEIF ARG:0 >= 600 && ARG:0 < 600 + MAX_COUNTRY
	LOCAL:5 = ARG:0 - 600
	IF LOCAL:5 == 0
		CALL CHANGE_COUNTRY_COLOR(LOCAL:5)
		RETURN 1
	ENDIF
	IF IS_COUNTRY(LOCAL:5)
		CALL CHANGE_COUNTRY_COLOR(LOCAL:5)
		RETURN 1
	ENDIF
	RETURN 0
ELSEIF ARG:0 >= 650 && ARG:0 < 650 + MAX_COUNTRY
	LOCAL:5 = ARG:0 - 650
	SIF IS_COUNTRY(LOCAL:5)
		CALL SHOW_COUNTRY_INFO(LOCAL:5)
ELSEIF ARG:0 >= 700 && ARG:0 < 700 + MAX_COUNTRY
	LOCAL:5 = ARG:0 - 700
	IF IS_COUNTRY(LOCAL:5) && !COUNTRY_IS_CLOSED:(LOCAL:5) && IS_COUNTRY(CFLAG:MASTER:소속) && !COUNTRY_IS_CLOSED:(CFLAG:MASTER:소속) && SLG_PP:0 <= DAY && LOCAL:5 != CFLAG:MASTER:소속
		;국가간의 인접 관계 맵의 작성(처리의 고속화)
		CALL TMP_CREATE_COUNTRY_NEIBORING_MAP
		;세력 관계 맵을 작성
		CALL TMP_CREATE_RELATION_MAP
		CALL TMP_CREATE_IS_FREE_MAP
		CALL TMP_CREATE_POLITICS_POWER_MAP
		CALL TMP_CREATE_SUM_SOLDIER_MAP
		CALL DIPLOMACY_MAIN(LOCAL:5)
	ENDIF

;한곳만 방위해제
ELSEIF ARG:0 > 800 && ARG:0 < 800 + MAX_CITY
	CALL SINGLE_MINIMIZE(ARG:0 - 800)
ELSEIF ARG:0 > 1500 && ARG:0 < 1500 + MAX_CITY
	;표시하는 도시를 그 도시에（도시관리 화면이나 직 쳐 했을 때 용）
	LOCAL:5 = ARG:0 - 1500
	SHOWN_CITY = LOCAL:5
	IF CFLAG:MASTER:소속 >= 1 && CITY_OWNER:(LOCAL:5) == CFLAG:MASTER:소속
		;적부대가 있는 도시는 변경 불가
		CALL GET_STAY_ENEMY_UNIT(LOCAL:5)
		IF RESULT:0 >= 0
			PRINTFORMW 적부대가 존재하는 도시에는 투자할 수 없습니다
		ELSEIF CITY_INVESTED:(LOCAL:5)
			PRINTFORMW 이미 투자가 끝난 상태입니다
		ELSEIF NOW_INVEST == CALC_MAX_INVEST()
			PRINTFORMW 금기는 이제 투자할 수 없습니다
		ELSE
			CALL CITY_INVEST(LOCAL:5)
		ENDIF
		RETURN 1
	ENDIF
;침공·이동 버튼
ELSEIF ARG:0 > 2000 && ARG:0 < 2000 + MAX_CITY
	IF CHECK_COUNTRY_RELATION_F(CITY_OWNER:(ARG:0 - 2000), CFLAG:MASTER:소속) >= 2
		CALL MOVETO(ARG:0 - 2000)
	ELSE
		CALL INVADE(ARG:0 - 2000)
	ENDIF
	RETURN 1
;부대편성 버튼
ELSEIF ARG:0 >= 2500 && ARG:0 < 2500 + MAX_UNIT && IS_COUNTRY(CFLAG:MASTER:소속) && UNIT_SOLDIER:(CFLAG:MASTER:소속):(ARG:0 - 2500) > 0
	CALL UNIT_SETTING(CFLAG:MASTER:소속, ARG:0 - 2500)
ELSEIF ARG:0 == 3990
	맵_폰트 '= "MS Gothic"
	RETURN 1
ELSEIF ARG:0 == 3991
	맵_폰트 '= "굴림체"
	RETURN 1
ELSEIF ARG:0 == 3992
	맵_폰트 '= "바탕체"
	RETURN 1
ELSEIF ARG:0 == 3993
	맵_폰트 '= "돋움체"
	RETURN 1
ELSEIF ARG:0 == 3994
	맵_폰트 '= "궁서체"
	RETURN 1
ELSE
	RETURN 0
ENDIF
RETURN 1

