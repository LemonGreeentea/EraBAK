;-------------------------------------------------
;왼쪽 컬럼 카테고리 명칭 설정
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME
COLUMN_LEFT_CATEGORY_NAME:1 '= "친교"
COLUMN_LEFT_CATEGORY_NAME:2 '= "모략"
COLUMN_LEFT_CATEGORY_NAME:3 '= "포로"
COLUMN_LEFT_CATEGORY_NAME:4 '= "정무"
COLUMN_LEFT_CATEGORY_NAME:5 '= "정보"
COLUMN_LEFT_CATEGORY_NAME:6 '= "특수"
COLUMN_LEFT_CATEGORY_NAME:7 '= "매매"
COLUMN_LEFT_CATEGORY_NAME:8 '= "진행"
COLUMN_LEFT_CATEGORY_NAME:9 '= "설정"

;-------------------------------------------------
;왼쪽 컬럼 카테고리 표시
;ARGS:0=표시하는 텍스트（전각 2문자까지）
; 　　　빈지정（""）라면 동폭의 스페이스 표시
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME_SHOW(ARGS:0)
;빈지정이라면 동폭의 스페이스
IF ARGS:0 == ""
	LOCALS:0 '= TOSTR_SPACE(6)
	PRINTFORM %LOCALS:0%
;지정이 있으면 물이 들 수 있는 해 표시
ELSE
	SETCOLOR 칼라_주석
	PRINTFORM %TOSTR_SPACE(1)%
	PRINTFORM %ARGS:0, 4, LEFT%
	PRINTFORM %TOSTR_SPACE(1)%
	RESETCOLOR
ENDIF
RETURN

;-------------------------------------------------
;왼쪽 컬럼 메뉴 버튼
;ARG:0=SHOP_AVAIL 번호（메뉴 번호）
;ARGS:0=표시 텍스트  빈지정이라면 동폭의 스페이스 표시
;ARG:1=1으로 선택중 칼라에 착색（ARG:1=FLAG:거점페이즈선택커맨드 번호를 대입하는 상정）
;-------------------------------------------------
@COLUMN_LEFT_PRINTBUTTON(ARG:0, ARGS:0, ARG:1)
#DIM EXTERNAL_LINK

SIF ARG:0 < 0 || 99 < ARG:0
	THROW COLUMN_LEFT_PRINTBUTTON의 항번이 0～99의 범위에 없습니다

;빈지정이라면 동폭의 스페이스
IF ARGS:0 == ""
	PRINTPLAINFORM %TOSTR_SPACE(12)%
;선택 불가（표시가 제한되어 있지 않은 경우）
ELSEIF !SHOP_AVAIL:(ARG:0)
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [%ARGS:0, 10%]
	RESETCOLOR
;현재 선택중（오른쪽 컬럼이 없는 메뉴도 착색하도록(듯이) 변경했기 때문에 버튼에）
ELSEIF ARG:1
	SETCOLOR 칼라_선택중
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
	RESETCOLOR
;통상의 버튼
ELSE
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
ENDIF

;-------------------------------------------------
;왼쪽 컬럼의 배열:행 번호에 카테고리와 메뉴를 보존
;※SHOP_AVAIL를 참조해 선택 불가의 메뉴를 비표시로 하고 있다
;  참조를 그만두면 이것까지 대로 선택 불가의 메뉴는
;  버튼측에서 표시 제한되고 있으므로 회색 표시된다
;-------------------------------------------------
@COLUMN_LEFT_MENULINE_SAVE
#DIM COLUMN_LEFT_CATEGORY_SAVEFLAG, 10

;리셋트 각 행의 카테고리 번호와 메뉴 번호를 보존하는 배열（배열 번호=행 번호）
;세이브할 정도는 아니지만 일시적으로 보존해@COLUMN_LEFT_MENU_SHOW로 사용하므로
;SHOP_LIFE_VARIABLE.ERH로 선언하고 있다
VARSET COLUMN_LEFT_CATEGORY_NUM, 0
VARSET COLUMN_LEFT_MENU_NUM, 0

;리셋트 카테고리의 보존이 끝난 플래그
VARSET COLUMN_LEFT_CATEGORY_SAVEFLAG, 0

;카테고리 명칭의 read
CALL COLUMN_LEFT_CATEGORY_NAME

;행 번호
LOCAL:0 = 1
;어떠한 카테고리를 보존한 플래그
LOCAL:4 = 0
;카테고리 번호 1～7
FOR LOCAL:1, 1, 8
	;LOCAL:1번의 카테고리가 존재
	IF COLUMN_LEFT_CATEGORY_NAME:(LOCAL:1) != ""
		;메뉴 번호 1～9
		FOR LOCAL:2, 1, 10
			;카테고리 번호를 10의 위로서 계산
			LOCAL:3 = LOCAL:1 * 10 + LOCAL:2
			;메뉴 명칭으로 존재 체크
			RESULTS:0 '= ""
			TRYCALLFORM SHOP_LIFE_NAME{LOCAL:3}
			;선택 불가의 메뉴도 회색 표시하고 싶은 경우는 && SHOP_AVAIL:(LOCAL:3)를 깎는다
			IF RESULTS:0 != "" && SHOP_AVAIL:(LOCAL:3)
				;카테고리 보존이 끝난 상태가 아닌 경우
				IF COLUMN_LEFT_CATEGORY_SAVEFLAG:(LOCAL:1) != 1
					;둘째 이후의 카테고리라면 빈행을 사이에 둔다
					IF LOCAL:4 == 1
						LOCAL:0 ++
					ENDIF
					LOCAL:4 = 1
					;배열 번호=행 번호에 카테고리 번호를 보존
					COLUMN_LEFT_CATEGORY_NUM:(LOCAL:0) = LOCAL:1
					;현재의 카테고리 보존이 끝난 플래그를 세운다
					COLUMN_LEFT_CATEGORY_SAVEFLAG:(LOCAL:1) = 1
					;어떠한 카테고리 보존이 끝난 플래그를 세운다
				ENDIF
				;배열 번호=행 번호에 메뉴 번호를 보존
				COLUMN_LEFT_MENU_NUM:(LOCAL:0) = LOCAL:3
				LOCAL:0 ++
			ENDIF
		NEXT
	ENDIF
NEXT

;-------------------------------------------------
;왼쪽 컬럼 메뉴 표시 버튼 호출 ARG:0은 행수
;
;  0～9번）오른쪽 컬럼의 버튼에 자유롭게 사용할 수 있도록(듯이) 사용하지 않는다
;11～79번）메뉴 번호
;　　　　　10의 위를 카테고리 번호에, 1～9를 메뉴 번호로 하면
;　　　　　COMF같이 자동적으로 읽히지만
;　　　　　행 수가 넘쳤을 경우의 처리는 하고 있지 않기 때문에 요점 확인
;　　81번）고정행·휴식  80 접수대를 독점하고 있어 한 번 더 없다
;　　　　　우선 페이지 번호는 알기 쉬운 90번대로 했지만
;　　　　　90 접수대가 부족한다면 96～99는 80 접수대로 변경하면내림
;  91～차례）고정행·설정계
;　　　　　91 설정·92 세이브·93 로드·94 QSAVE·95 QLOAD
;　　　　　96 오른쪽 컬럼 리스트 2 전페이지·97 오른쪽 컬럼 리스트 2차 페이지
;　　　　　98 오른쪽 컬럼 리스트 1 전페이지·99 오른쪽 컬럼 리스트 1차 페이지
;-------------------------------------------------
@COLUMN_LEFT_MENU_SHOW(ARG:0)
;배열의 최대치를 넘는 행 번호가 지정되었을 경우
IF ARG:0 >= 40
	RETURN
ENDIF

SELECTCASE ARG:0
	;고정행·나머지 행동횟수
	CASE 0
		;포로시는 표시하지 않는다
		IF CFLAG:MASTER:포로처 == 0
			PRINT  남은 행동:
			CALL COLOR_PRINT(@"{CALC_SHOP_TIME() - SHOP_TIME, 6}", 0xC0CAA0)
		ELSE
			CALL COLOR_PRINT("            투옥중", 0xB0BA90)
		ENDIF
	;고정행·휴식
	CASE 32
		;포로가 아니다
		IF CFLAG:MASTER:포로처 == 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME:8)
			TRYCALLFORM SHOP_LIFE_NAME81
			CALL COLUMN_LEFT_PRINTBUTTON( 81, RESULTS:0, FLAG:거점페이즈선택커맨드 == 81)
		;포로
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
			CALL COLUMN_LEFT_PRINTBUTTON( 0, "", 0)
		ENDIF
	;고정행·1회 기다린다
	CASE 33
		;포로가 아니다
		IF CFLAG:MASTER:포로처 == 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
			TRYCALLFORM SHOP_LIFE_NAME82
			CALL COLUMN_LEFT_PRINTBUTTON( 82, RESULTS:0, FLAG:거점페이즈선택커맨드 == 82)
		;포로
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
			CALL COLUMN_LEFT_PRINTBUTTON( 0, "", 0)
		ENDIF
	;고정행·설정
	CASE 35
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME:9)
		CALL COLUMN_LEFT_PRINTBUTTON( 91, "설정", FLAG:거점페이즈선택커맨드 == 91)
	;고정행·세이브
	CASE 36
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON( 92, "세이브", FLAG:거점페이즈선택커맨드 == 92)
	;고정행·로드
	CASE 37
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON( 93, "로드", FLAG:거점페이즈선택커맨드 == 93)
	;가변행·타카테고리명과 메뉴 버튼
	CASEELSE
		;카테고리
		IF COLUMN_LEFT_CATEGORY_NUM:(ARG:0) != 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME:(COLUMN_LEFT_CATEGORY_NUM:(ARG:0)))
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		ENDIF
		;메뉴 버튼
		IF COLUMN_LEFT_MENU_NUM:(ARG:0) != 0
			TRYCALLFORM SHOP_LIFE_NAME{COLUMN_LEFT_MENU_NUM:(ARG:0)}
			CALL COLUMN_LEFT_PRINTBUTTON( COLUMN_LEFT_MENU_NUM:(ARG:0), RESULTS:0, FLAG:거점페이즈선택커맨드 == COLUMN_LEFT_MENU_NUM:(ARG:0))
		ELSE
			CALL COLUMN_LEFT_PRINTBUTTON( 0, "", 0)
		ENDIF
ENDSELECT

RESETCOLOR
PRINT   

