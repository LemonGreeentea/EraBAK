;-------------------------------------------------
;왼쪽 컬럼 카테고리 명칭 설정
;-------------------------------------------------
@COLUMN_LEFT_CATEGORY_NAME_SLG
COLUMN_LEFT_CATEGORY_NAME_SLG:1 '= "자동"
COLUMN_LEFT_CATEGORY_NAME_SLG:2 '= "정책"
COLUMN_LEFT_CATEGORY_NAME_SLG:3 '= "전략"
COLUMN_LEFT_CATEGORY_NAME_SLG:4 '= "교섭"
COLUMN_LEFT_CATEGORY_NAME_SLG:5 '= "정보"
COLUMN_LEFT_CATEGORY_NAME_SLG:6 '= "인재"
COLUMN_LEFT_CATEGORY_NAME_SLG:7 '= "특수"
COLUMN_LEFT_CATEGORY_NAME_SLG:8 '= "진행"
COLUMN_LEFT_CATEGORY_NAME_SLG:9 '= "설정"

;-------------------------------------------------
;왼쪽 컬럼 메뉴 버튼
;-------------------------------------------------
@COLUMN_LEFT_PRINTBUTTON_SLG(ARG:0, ARGS:0, ARG:1 = 0)
#DIM EXTERNAL_LINK

SIF ARG:0 < 0 || 99 < ARG:0
	THROW COLUMN_LEFT_PRINTBUTTON의 항번이 0～99의 범위에 없습니다

;빈지정이라면 동폭의 스페이스
IF ARGS:0 == ""
	PRINTPLAINFORM %TOSTR_SPACE(12)%
;선택 불가（표시가 제한되어 있지 않은 경우）
ELSEIF !SHOP_AVAIL:(ARG:0)
	SETCOLOR 칼라_선택불가
	PRINTPLAINFORM [%ARGS:0, 10%]
	RESETCOLOR
;현재 선택중
ELSEIF ARG:1
	SETCOLOR 칼라_선택중
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
	RESETCOLOR
;통상의 버튼
ELSE
	PRINTBUTTON @"[%ARGS:0, 10%]", ARG:0
ENDIF

;-------------------------------------------------
;왼쪽 컬럼의 배열:행 번호에 카테고리와 메뉴를 보존
;-------------------------------------------------
@COLUMN_LEFT_MENULINE_SAVE_SLG
#DIM COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 10

VARSET COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG, 0
VARSET COLUMN_LEFT_CATEGORY_NUM_SLG, 0
VARSET COLUMN_LEFT_MENU_NUM_SLG, 0

CALL COLUMN_LEFT_CATEGORY_NAME_SLG

LOCAL:0 = 1
LOCAL:4 = 0
FOR LOCAL:1, 1, 8
	IF COLUMN_LEFT_CATEGORY_NAME_SLG:(LOCAL:1) != ""
		FOR LOCAL:2, 1, 10
			LOCAL:3 = LOCAL:1 * 10 + LOCAL:2
			RESULTS:0 '= ""
			TRYCALLFORM SHOP_SLG_NAME{LOCAL:3}
			IF RESULTS:0 != "" && SHOP_AVAIL:(LOCAL:3)
				IF COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) != 1
					IF LOCAL:4 == 1
						LOCAL:0 ++
					ENDIF
					LOCAL:4 = 1
					COLUMN_LEFT_CATEGORY_NUM_SLG:(LOCAL:0) = LOCAL:1
					COLUMN_LEFT_CATEGORY_SAVEFLAG_SLG:(LOCAL:1) = 1
				ENDIF
				COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0) = LOCAL:3
				;이 메뉴에 추가 설명문이 있으면 빈행을 추가한다
				TRYCCALLFORM SHOP_SLG_NEXT_LINE_ADD{COLUMN_LEFT_MENU_NUM_SLG:(LOCAL:0)}
					LOCAL:0 ++
				CATCH
				ENDCATCH
				LOCAL:0 ++
			ENDIF
		NEXT
	ENDIF
NEXT

;-------------------------------------------------
;왼쪽 컬럼 메뉴 표시 버튼 호출 ARG:0은 행수
;-------------------------------------------------
@COLUMN_LEFT_MENU_SHOW_SLG(ARG:0)
#DIM SHOP_SLG_NEXT_LINE_ADD_FLAG = 0
#DIM SHOP_SLG_NAME_COLOR = 0
;고정행
SELECTCASE ARG:0
	;고정·안내
	CASE 0
		SETCOLOR 칼라_주석
		IF FLAG:관전모드
			CALL COLOR_PRINT(" 관전중           ", 0xB0BA90)
		ELSEIF CFLAG:MASTER:소속 == 0
			CALL COLOR_PRINT(" 방랑중           ", 0xB0BA90)
		ELSE
			CALL COLOR_PRINT(" 위임시는자동     ", 0xB0BA90)
		ENDIF
		RESETCOLOR
	;고정·개전
	CASE 33
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:8)
		TRYCALLFORM SHOP_SLG_NAME81
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 81, RESULTS:0)
	;고정·설정
	CASE 35
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:9)
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 91, "설정")
	;고정·세이브
	CASE 36
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 92, "세이브")
	;고정·로드
	CASE 37
		CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		CALL COLUMN_LEFT_PRINTBUTTON_SLG( 93, "로드")
	;가변·타카테고리명과 메뉴 버튼
	CASEELSE
		;카테고리
		IF COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0) != 0
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW(COLUMN_LEFT_CATEGORY_NAME_SLG:(COLUMN_LEFT_CATEGORY_NUM_SLG:(ARG:0)))
		ELSE
			CALL COLUMN_LEFT_CATEGORY_NAME_SHOW("")
		ENDIF
		;하나전의 행의 메뉴에 추가 설명문이 있을까 체크
		TRYCCALLFORM SHOP_SLG_NEXT_LINE_ADD{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1)}
			SHOP_SLG_NEXT_LINE_ADD_FLAG = 1
		CATCH
			SHOP_SLG_NEXT_LINE_ADD_FLAG = 0
		ENDCATCH
		;오른쪽 컬럼에 표시가 있는 메뉴의 경우
		TRYCCALLFORM SHOP_SLG_EVENTBUY_COLUMN_RIGHT_ON_CHECK{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			SHOP_SLG_NAME_COLOR = 1
		CATCH
			SHOP_SLG_NAME_COLOR = 0
		ENDCATCH
		;버튼 칼라 지정이 있으면 읽어들여, 오른쪽 컬럼에 표시가 있어도 버튼을 온 칼라로 하지 않는다
		TRYCCALLFORM SHOP_SLG_NAME_COLOR{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			SHOP_SLG_NAME_COLOR = 0
		CATCH
		ENDCATCH
		;메뉴 버튼이 있으면 표시
		IF COLUMN_LEFT_MENU_NUM_SLG:(ARG:0) != 0
			TRYCALLFORM SHOP_SLG_NAME{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0)}
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( COLUMN_LEFT_MENU_NUM_SLG:(ARG:0), RESULTS:0, SHOP_SLG_NAME_COLOR && FLAG:전략페이즈선택커맨드 == COLUMN_LEFT_MENU_NUM_SLG:(ARG:0))
		;메뉴 버튼이 없고, 하나전의 행의 메뉴에 추가 설명문이 있는 경우는 표시
		ELSEIF SHOP_SLG_NEXT_LINE_ADD_FLAG == 1
			TRYCALLFORM SHOP_SLG_NEXT_LINE_ADD_TEXT{COLUMN_LEFT_MENU_NUM_SLG:(ARG:0 - 1)}
		;메뉴 버튼이 없고, 하나전의 행의 메뉴에 추가 설명문이 없으면 빈행
		ELSE
			CALL COLUMN_LEFT_PRINTBUTTON_SLG( 0, "")
		ENDIF
ENDSELECT

RESETCOLOR
PRINT   

