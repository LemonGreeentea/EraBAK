;-------------------------------------------------
;능력의 일람표시
;LOCAL:1=표시 페이지 2=페이지 최대 3=표시 채널 4=표시행수 0, 10, 11=카운터
;-------------------------------------------------
@SHOP_LIFE_CHARA_TABLE
#DIM CHARA_TABLE,3000
#DIM SORT_VALUE,3000
{
#DIM HEAD_SLG =
GETNUM(ABL, "무투"), GETNUM(ABL, "방위"), GETNUM(ABL, "지략"), GETNUM(ABL, "정치"),
GETNUM(ABL, "신비"), GETNUM(ABL, "가창"), GETNUM(ABL, "요리"), GETNUM(ABL, "야심")
}

{
#DIM HEAD_ABL =
GETNUM(ABL, "Ｃ감"), GETNUM(ABL, "Ｖ감"), GETNUM(ABL, "Ａ감"), GETNUM(ABL, "Ｂ감"), GETNUM(ABL, "Ｍ감"),
GETNUM(ABL, "욕망"), GETNUM(ABL, "성기술"), GETNUM(ABL, "성지식")
}

#DIM HEAD_BASE = GETNUM(BASE, "체력"), GETNUM(BASE, "기력"), GETNUM(BASE, "정신력")

{
#DIM HEAD_ABL_2 =
GETNUM(ABL, "봉사"), GETNUM(ABL, "성교"), GETNUM(ABL, "레즈"), GETNUM(ABL, "ＢＬ"), GETNUM(ABL, "노출"),
GETNUM(ABL, "자위"), GETNUM(ABL, "정애"), GETNUM(ABL, "사정"), GETNUM(ABL, "분유"), GETNUM(ABL, "배설"),
GETNUM(ABL, "촉수"), GETNUM(ABL, "출산")
}

{
#DIM HEAD_CFLAG = 
GETNUM(CFLAG, "호감도"), GETNUM(CFLAG, "의존도"), GETNUM(CFLAG, "종속도"), GETNUM(CFLAG, "지배도")
}

#DIM FIRST_LINE
#DIM DESC
#DIM NOW_SORT
#DIM PREV_SORT
#DIM 페이지
#DIM 최대페이지
#DIM 항목
#DIM 캐릭터수
#DIM 최대캐릭터수

VARSET CHARA_TABLE, -1
페이지 = 1
항목 = 1
최대캐릭터수 = 23
캐릭터수 = 0
FOR LOCAL, 0, CHARANUM
	SIF SHOP_CHARA_LIST:LOCAL == -1
		CONTINUE
	CHARA_TABLE:LOCAL = SHOP_CHARA_LIST:LOCAL
NEXT
FOR LOCAL:0, 0, CHARANUM
	SIF CHARA_TABLE:(LOCAL:0) == -1
		BREAK
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_NOURYOKU(CHARA_TABLE:(LOCAL:0))
	SIF RESULT
		캐릭터수 ++
NEXT

최대페이지 = 캐릭터수 / 최대캐릭터수 + 1
FIRST_LINE = LINECOUNT
$INPUT_LOOP

;헤더
CALL SINGLE_DRAWLINE
PRINTFORM ★능력을 표시하는 캐릭터를 선택해 주세요★항목명 클릭으로 소트 할 수 있습니다★   
PRINTBUTTON @"[%CHARALIST_MODE(), 14, LEFT%]", 95
PRINTL
PRINTBUTTON "|  NO", 50
PRINTFORM |
PRINTFORM %TOSTR_REPEAT("-",MAX_CHARANAME_LENGTH/2)%
SELECTCASE 항목
	CASE 1
		PRINTFORM |
		PRINTBUTTON "★  ", 30
		PRINTFORM |
		PRINTBUTTON "4합계", 31
		PRINTFORM |
		PRINTBUTTON "7합계", 32
		FOR LOCAL, 0, VARSIZE("HEAD_SLG")
			PRINTFORM |
			PRINTBUTTON " " + ABLNAME:(HEAD_SLG:LOCAL), LOCAL
		NEXT
	CASE 2
		FOR LOCAL, 0, VARSIZE("HEAD_ABL")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_ABL:LOCAL),0,4), LOCAL
		NEXT
		FOR LOCAL, 0, VARSIZE("HEAD_BASE")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(BASENAME:(HEAD_BASE:LOCAL), 0, 4), LOCAL + 70
		NEXT
	CASE 3
		FOR LOCAL, 0, VARSIZE("HEAD_ABL_2")
			PRINTFORM |
			PRINTBUTTON " " + SUBSTRING(ABLNAME:(HEAD_ABL_2:LOCAL),0,4), LOCAL
		NEXT
	CASE 4
		FOR LOCAL, 0, VARSIZE("HEAD_CFLAG")
			PRINTFORM |
			PRINTBUTTON "  " + CFLAGNAME:(HEAD_CFLAG:LOCAL), LOCAL
		NEXT
	CASE 5
		FOR LOCAL, 1, MAX_SP_COUNTRY
			PRINTFORM |
			PRINTBUTTON SUBSTRINGU(SP_COUNTRY_NAME:LOCAL, 0, 2), LOCAL + 10
		NEXT
	CASEELSE
		PRINTFORML %TOSTR_REPEAT("-",76)%
ENDSELECT
PRINTFORML |

;메인
캐릭터수 = 0
FOR LOCAL:0, 0, CHARANUM
	LOCAL:5 = CHARA_TABLE:(LOCAL:0)
	SIF LOCAL:5 == -1
		BREAK
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_NOURYOKU(LOCAL:5)
	IF RESULT
		캐릭터수 ++
		IF (페이지-1)*최대캐릭터수 <= 캐릭터수 -1 && 페이지*최대캐릭터수 > 캐릭터수 -1
			CALL PRINT_PARTNER_DATA_TABLE(LOCAL:5,항목)
			PRINTL
		ENDIF
	ENDIF
NEXT


;footer
CALL SINGLE_DRAWLINE
PRINTFORM %TOSTR_SPACE(4)%
PRINTBUTTON "[표시전환  "+TOSTR(항목)+"/ 5]", 94
PRINTFORM %TOSTR_SPACE(4)%
IF 최대페이지 >= 2
	PRINTBUTTON "[이전페이지    ]", SHOP_LIFE_LIST1_PAGE_BACK
	PRINTFORM %TOSTR_SPACE(3)%page{페이지,2}/{최대페이지,2}%TOSTR_SPACE(3)%
	PRINTBUTTON "[다음페이지    ]", SHOP_LIFE_LIST1_PAGE_NEXT
	PRINTFORM %TOSTR_SPACE(4)%
ELSE
	PRINTFORM %TOSTR_SPACE(51)%
ENDIF
PRINTBUTTON "[돌아온다      ]", 93
PRINTL

INPUT 0
CLEARLINE 1

SELECTCASE RESULT
	CASE 1
		FOR LOCAL, 0, CHARANUM
			SORT_VALUE:LOCAL = NO:LOCAL
		NEXT
	CASE 95
		;캐릭터수의 재계산이 필요하게 되므로 리스타트
		FLAG:능력표시필터 = ROUND_INCREMENT(FLAG:능력표시필터, 0, 3)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	CASE 93
		RETURN
	CASE SHOP_LIFE_LIST1_PAGE_BACK
		페이지 --
			SIF 페이지 < 1
				페이지 = 최대페이지
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
	CASE SHOP_LIFE_LIST1_PAGE_NEXT
		페이지 ++
			SIF 페이지 > 최대페이지
				페이지 = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP

	CASE 94
		항목 ++
		SIF 항목 > 5
			항목 = 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
	CASE IS >= 100
		SIF NO_TO_CHARA(RESULT - 100) != -1
			CALL SHOW_INFO_WITH_UI(NO_TO_CHARA(RESULT - 100), 1)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO INPUT_LOOP
ENDSELECT

NOW_SORT = RESULT

IF NOW_SORT == PREV_SORT
	DESC = !DESC
ELSE
	DESC = 0
ENDIF

PREV_SORT = RESULT

;NO소트
IF RESULT == 1

ENDIF

;배열 인도가 좀 더 착실한 대용품이라면 이런 쓰레기 같은 실장하지 않아도 괜찮은데
SELECTCASE 항목
	CASE 1
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_SLG")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_SLG:NOW_SORT)
				NEXT
			CASE 30
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = TMP_CHARA_STARS:LOCAL
				NEXT
			CASE 31
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):무투 + ABL:(LOCAL:0):방위 + ABL:(LOCAL:0):정치 + ABL:(LOCAL:0):지략
				NEXT
			CASE 32
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:(LOCAL:0) = ABL:(LOCAL:0):무투 + ABL:(LOCAL:0):방위 + ABL:(LOCAL:0):정치 + ABL:(LOCAL:0):지략 + ABL:(LOCAL):신비 + ABL:LOCAL:가창 + ABL:LOCAL:요리
				NEXT
			CASE 50
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = NO:LOCAL
				NEXT
		ENDSELECT
	CASE 2
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_ABL")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_ABL:NOW_SORT)
				NEXT
			CASE 70 TO 70 + VARSIZE("HEAD_BASE")
				NOW_SORT -= 70
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = BASE:LOCAL:(HEAD_BASE:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 3
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_ABL_2")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = ABL:LOCAL:(HEAD_ABL_2:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 4
		SELECTCASE NOW_SORT
			CASE 0 TO VARSIZE("HEAD_CFLAG")
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = CFLAG:LOCAL:(HEAD_CFLAG:NOW_SORT)
				NEXT
		ENDSELECT
	CASE 5
		NOW_SORT -= 10
		SELECTCASE NOW_SORT
			CASE 1 TO MAX_SP_COUNTRY - 1
				FOR LOCAL, 0, CHARANUM
					SORT_VALUE:LOCAL = GETBIT(TALENT:LOCAL:특수세력함락계, NOW_SORT)
				NEXT
		ENDSELECT
ENDSELECT

FOR LOCAL,1,CHARANUM
	SIF CHARA_TABLE:LOCAL == -1
		BREAK
	FOR LOCAL:11,LOCAL,0,-1
		LOCAL:12 = CHARA_TABLE:(LOCAL:11-1)
		LOCAL:13 = CHARA_TABLE:(LOCAL:11)
		IF (DESC && SORT_VALUE:(LOCAL:12) < SORT_VALUE:(LOCAL:13)) || (!DESC && SORT_VALUE:(LOCAL:12) > SORT_VALUE:(LOCAL:13))
			SWAP CHARA_TABLE:(LOCAL:11),CHARA_TABLE:(LOCAL:11 -1)
		ELSE
			BREAK
		ENDIF
	NEXT
NEXT

;소트순서에 따라 SHOP_CHARA_LIST를 갱신
VARSET SHOP_CHARA_LIST, -1
FOR LOCAL, 0, VARSIZE("SHOP_CHARA_LIST")
	SHOP_CHARA_LIST:LOCAL = CHARA_TABLE:LOCAL
NEXT

CLEARLINE LINECOUNT - FIRST_LINE
GOTO INPUT_LOOP

;-------------------------------------------------
;ARG:0번의 캐릭터의 정보를 표시하는 함수
;ARG:1은 표시 채널
;버튼의 선택 번호는 NO + ARG:2가 된다
;-------------------------------------------------
@PRINT_PARTNER_DATA_TABLE(ARG:0, ARG:1 = 0, ARG:2 = 100)
{
#DIM HEAD_SLG =
GETNUM(ABL, "무투"), GETNUM(ABL, "방위"), GETNUM(ABL, "지략"), GETNUM(ABL, "정치"),
GETNUM(ABL, "신비"), GETNUM(ABL, "가창"), GETNUM(ABL, "요리"), GETNUM(ABL, "야심")
}

{
#DIM HEAD_ABL =
GETNUM(ABL, "Ｃ감"), GETNUM(ABL, "Ｖ감"), GETNUM(ABL, "Ａ감"), GETNUM(ABL, "Ｂ감"), GETNUM(ABL, "Ｍ감"),
GETNUM(ABL, "욕망"), GETNUM(ABL, "성기술"), GETNUM(ABL, "성지식")
}

#DIM HEAD_BASE = GETNUM(BASE, "체력"), GETNUM(BASE, "기력"), GETNUM(BASE, "정신력")

{
#DIM HEAD_ABL_2 =
GETNUM(ABL, "봉사"), GETNUM(ABL, "성교"), GETNUM(ABL, "레즈"), GETNUM(ABL, "ＢＬ"), GETNUM(ABL, "노출"),
GETNUM(ABL, "자위"), GETNUM(ABL, "정애"), GETNUM(ABL, "사정"), GETNUM(ABL, "분유"), GETNUM(ABL, "배설"),
GETNUM(ABL, "촉수"), GETNUM(ABL, "출산")
}

{
#DIM HEAD_CFLAG = 
GETNUM(CFLAG, "호감도"), GETNUM(CFLAG, "의존도"), GETNUM(CFLAG, "종속도"), GETNUM(CFLAG, "지배도")
}

PRINTFORM [{NO:(ARG:0) + ARG:2, 4}] %SNAME(ARG:0), MAX_CHARANAME_LENGTH/2, LEFT%
SELECTCASE ARG:1
	CASE 1
		CALL TMP_PRINT_CHARA_STARS_NUM(ARG:0)
		PRINT |
		PRINTFORM {ABL:(ARG:0):무투 + ABL:(ARG:0):방위 + ABL:(ARG:0):지략 + ABL:(ARG:0):정치,5}|
		PRINTFORM {ABL:(ARG:0):무투 + ABL:(ARG:0):방위 + ABL:(ARG:0):지략 + ABL:(ARG:0):정치 + ABL:(ARG:0):신비 + ABL:(ARG:0):가창+ABL:(ARG:0):요리,5}
		FOR LOCAL, 0, VARSIZE("HEAD_SLG")
			PRINT |
			CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:(ARG:0):(HEAD_SLG:LOCAL))
			PRINTFORM {ABL:(ARG:0):(HEAD_SLG:LOCAL), 4}
		NEXT
		PRINT |
	CASE 2
		FOR LOCAL, 0, VARSIZE("HEAD_ABL")
			IF HEAD_ABL:LOCAL == GETNUM(ABL, "성지식")
				CALL PRINT_ALPHABET_RANK(랭크_성지식, ABL:(ARG:0):(HEAD_ABL:LOCAL))
			ELSE
				CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:(ARG:0):(HEAD_ABL:LOCAL))
			ENDIF
			PRINTFORM %TOSTR_R(ABL:(ARG:0):(HEAD_ABL:LOCAL), 4)%|
		NEXT
		FOR LOCAL, 0, VARSIZE("HEAD_BASE")
			PRINTFORM %TOSTR_R(BASE:(ARG:0):(HEAD_BASE:LOCAL), 5)%|
		NEXT
	CASE 3
		FOR LOCAL, 0, VARSIZE("HEAD_ABL_2")
			CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:(ARG:0):(HEAD_ABL_2:LOCAL))
			PRINTFORM %TOSTR_R(ABL:(ARG:0):(HEAD_ABL_2:LOCAL), 4)%|
		NEXT
	CASE 4
		FOR LOCAL, 0, VARSIZE("HEAD_CFLAG")
			PRINTFORM {CFLAG:(ARG:0):(HEAD_CFLAG:LOCAL), 8}|
		NEXT
	CASE 5
		FOR LOCAL, 1, MAX_SP_COUNTRY
			PRINTFORM  \@ GETBIT(TALENT:(ARG:0):특수세력함락계, LOCAL) ? 〇 # -- \@ |
		NEXT
ENDSELECT
RETURN
;-------------------------------------------------
; ARG:0을 문자열에 변환 ARG:1의 폭으로 오른쪽 공격
;-------------------------------------------------
@TOSTR_R(ARG:0,ARG:1)
#FUNCTIONS
VARSET LOCALS
LOCALS '= TOSTR(ARG:0)
LOCAL = STRLENS(LOCALS)
IF LOCAL < ARG:1
	FOR LOCAL:1,0,ARG:1-LOCAL
		LOCALS:1 += " "
	NEXT
ENDIF
LOCALS:1 += LOCALS
RETURNF LOCALS:1
