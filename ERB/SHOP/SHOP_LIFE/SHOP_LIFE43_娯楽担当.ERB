;-------------------------------------------------
;「오락담당」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME43
RESULTS:0 '= "오락담당"

;-------------------------------------------------
;「오락담당」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK43
SIF CFLAG:MASTER:포로처
	RETURN 0
SIF !CFLAG:MASTER:소속
	RETURN 0
RETURN 1

;-------------------------------------------------
;「오락담당」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA43(ARG:0)
;비주인공, 포로가 아닌, 이베캐라가 아닌, 육아 되고 있는 아이가 아니다
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):포로처 == 0 && !IS_SP_CHARA(ARG:0) && CFLAG:(ARG:0):행동불능상태 != 행동불능_아이　&& (IS_FREE(ARG:0) || ID_TO_CHARA(FLAG:오락담당) == ARG:0)

;-------------------------------------------------
;「오락담당의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY43
FLAG:거점페이즈페이지 = 1
FLAG:요바이 = 0
RETURN 0

;-------------------------------------------------
;「오락담당」의 오른쪽 컬럼 캐릭터 리스트 버튼 표시에 추적 시키는 정보
;  Default의 정보를 표시하지 않는 경우는 RETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD43(ARG:0)
PRINTPLAIN  
PRINTFORM 료:
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:(ARG:0):요리)
PRINTFORM  {ABL:(ARG:0):요리, 3, RIGHT} 
PRINTFORM 노래:
CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:(ARG:0):가창)
PRINTFORM  {ABL:(ARG:0):가창, 3, RIGHT} 
PRINTFORM 계:{ABL:(ARG:0):가창 + ABL:(ARG:0):요리, 3, RIGHT} 
RETURN 1

;-------------------------------------------------
;「오락담당」의 오른쪽 컬럼 캐릭터 리스트 1의 버튼（함수미지정이라면 선택중색없음 표시）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON43(ARG:0, ARG:1)
;캐릭터, 선택중 칼라 표시 플래그, 버튼 번호에 추가하는 수치, CHECKCHARA의 반환값, 행동이 끝난 마크를 오프로 하는 플래그
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, ARG:0 == ID_TO_CHARA(FLAG:오락담당), SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 1)
RETURN 0

;-------------------------------------------------
;「오락담당」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW43
#DIM 선택인원수
#DIM 최대페이지수
#DIM PRINTOPTION

IF GET_COOK()
	선택인원수 = 1
ELSE
	선택인원수 = 0
ENDIF

;타이틀
CALL COLUMN_RIGHT_TITLE("오락담당 선택", TOSTR(선택인원수), "1", "0", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 오락담당은 거점 페이즈의 행동 후에 체력·기력을 회복해 줍니다", 칼라_주석)
CALL COLUMN_RIGHT_PRINTL
CALL COLOR_PRINT(" 회복량은 오락담당의 요리·가창에 응합니다  ※오락담당은 부대에의 참가를 할 수 없습니다", 칼라_주석)
CALL COLUMN_RIGHT_PRINTL
CALL COLOR_PRINT(" 현재의 오락담당은", 칼라_주석)
IF GET_COOK() != -1
	FONTSTYLE 1
	SETCOLOR 칼라_오렌지
	PRINTFORM  %ANAME(GET_COOK())% 
	FONTSTYLE 0
	RESETCOLOR
	CALL COLOR_PRINT("입니다", 칼라_주석)
ELSE
	FONTSTYLE 1
	CALL COLOR_PRINT(" 없습니다 ", 칼라_주석)
	FONTSTYLE 0
ENDIF
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT(" 현재 오락담당이 되어 있는 캐릭터를 선택하는 것으로 해제할 수 있습니다", 칼라_주석)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;표준적인 캐릭터 리스트와 페이지 이동
;ARG:0=3 리스트 1행에 늘어놓는 버튼수를 3으로 바꾼다
소트키보존 = 소트키
소트순서보존 = 내림차순소트
소트키 = 소트_오락담당
내림차순소트 = 1
CALL CREATE_SELECTOR_SORT_LIST
CALL COLUMN_RIGHT_CHARALIST(2)
소트키 = 소트키보존
내림차순소트 = 소트순서보존
CALL CREATE_SELECTOR_SORT_LIST
RETURN 0

;-------------------------------------------------
;「오락담당」의 리스트 1 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP43(ARG:0)
CALL SINGLE_DRAWLINE
IF ARG:0 == GET_COOK()
	PRINTFORMW %조사처리(ANAME(ARG:0),"를")% 오락담당으로부터 해임했습니다
	CALL SET_COOK(-1)
ELSE
	LINES_SHOP = LINECOUNT
	PRINTFORMW %조사처리(ANAME(ARG:0),"를")% 오락담당에 임명했습니다
	PRINTFORMW %ANAME(ARG:0)%의 요리·가창의 값에 응해 거점 페이즈의 행동 후, %ANAME(MASTER)%의 체력이 회복합니다
	PRINTFORMW 다만, %조사처리(ANAME(ARG:0),"가")% 포로가 되면 효과가 없어집니다
	CALL SET_COOK(ARG:0)
ENDIF
RETURN 1

