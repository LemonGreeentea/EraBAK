;-------------------------------------------------
;「아이템」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME71
RESULTS:0 '= "엔젤 24"

;-------------------------------------------------
;「아이템」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK71
SIF CFLAG:MASTER:포로처
	RETURN 0
RETURN 1

;-------------------------------------------------
;「아이템의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY71
CALL SHOP_ITEM
RETURN 1

;-------------------------------------------------
;「아이템」의 오른쪽 컬럼 오프 판정
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_COLUMN_RIGHT_OFF_CHECK71

;-------------------------------------------------
;「아이템」의 처리
;-------------------------------------------------
@SHOP_ITEM
#DIM FIRST_LINE_ITEM_MAIN
#DIM IS_GISHI
#DIM ITEM_NUM_DISPFLAG

;171~200번 아이템 구매 불가
VARSET ITEMSALES, 0, 171, 200

REDRAW 0
FIRST_LINE_ITEM_MAIN = LINECOUNT

;[기사]소유가 주인공 세력에 존재할지 어떨지를 판정
SIF TALENT:MASTER:기사
	IS_GISHI = 1

;타이틀
CALL SINGLE_DRAWLINE
PRINTPLAIN  
CALL COLUMN_RIGHT_TITLE("구입/개발/매각하는 아이템을 선택", "", "", "0", "200～전")
PRINTL 
CALL SINGLE_DRAWLINE

;소지 아이템 표시
CALL COLOR_PRINT(@" 소지 ", 칼라_주석)
CALL COLOR_PRINT(@"금:", 칼라_선택불가)
CALL COLOR_PRINT(@"%NUM_FORMAT(MONEY)%", 칼라_선택중)

LOCAL:1 = 999
FOR LOCAL:0, 100, 300
	IF ITEM:(LOCAL:0) >= 1
		IF LOCAL:1 > 80
			LOCAL:1 = 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ENDIF
		LOCALS:0 = %ITEMNAME:(LOCAL:0)%
		IF ITEM:(LOCAL:0) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:0)}
		ENDIF
		LOCALS:0 = %LOCALS:0, 18, LEFT%
		LOCAL:1 += STRLENS(LOCALS:0) + 2
		PRINTFORM %LOCALS:0%　
	ENDIF
NEXT
IF LOCAL:1 == 999
	PRINTL 
	PRINTFORM %TOSTR_SPACE(6)%
	CALL COLOR_PRINT(@"소지 아이템 없음", 칼라_선택불가)
ENDIF
PRINTL 

SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR

CALL COLOR_PRINT(@" 구입 ", 칼라_주석)
CALL COLOR_PRINT(@"선택 후에 설명 표시", 칼라_선택불가)
PRINTL 
PRINTFORM %TOSTR_SPACE(6)%
LOCAL:1 = 0
FOR LOCAL:0, 100, 300
	IF ITEMNAME:(LOCAL:0) != "" && ITEMSALES:(LOCAL:0)
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ELSEIF LOCAL:1 > 0
			PRINTFORM %TOSTR_SPACE(9)%
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:0)
		;소지금 부족 or 소지수 오버로 구입 할 수 없을 때는 색을 바꾼다（를 클릭은 할 수 있다）
		IF MONEY < RESULT || ITEM:(LOCAL:0) >= 99
			SETCOLOR 칼라_선택불가
			PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]%NUM_FORMAT(RESULT), 9%", LOCAL:0
			RESETCOLOR
		ELSE
			PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]%NUM_FORMAT(RESULT), 9%", LOCAL:0
		ENDIF
	ENDIF
NEXT
PRINTL 

SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
CALL COLOR_PRINT(@" 개발 ", 칼라_주석)

IF IS_GISHI
	CALL COLOR_PRINT(@"소질", 칼라_선택불가)
	CALL COLOR_PRINT(@"<기사>할인", 칼라_초록)
	CALL COLOR_PRINT(@"있음", 칼라_선택불가)
	CALL COLOR_PRINT(@"%"개발비", 21%%TOSTR_SPACE(2)%%"기간", 6%%TOSTR_SPACE(7)%%"개발비", MAX_CHARANAME_LENGTH + 27%%TOSTR_SPACE(2)%%"기간", 6%", 칼라_주석)
ELSE
	CALL COLOR_PRINT(@"소질<기사>할인없음", 칼라_선택불가)
	CALL COLOR_PRINT(@"%"개발비", 11%%TOSTR_SPACE(2)%%"기간", 4%%TOSTR_SPACE(9)%%"개발비", MAX_CHARANAME_LENGTH + 17%%TOSTR_SPACE(2)%%"기간", 4%", 칼라_주석)
ENDIF
PRINTL 

LOCAL:1 = 0
FOR LOCAL:0, 200, 300
	IF ITEMNAME:(LOCAL:0) != "" && !ITEMSALES:(LOCAL:0) && ITEMTIME:(LOCAL:0) == 0 && (!INRANGE(LOCAL:0, 200, 249) || ITEM:LOCAL == 0)
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:0}
		IF RESULT
			IF LOCAL:1 == 0
				PRINTFORM %TOSTR_SPACE(6)%
			;2열로 개행
			ELSEIF LOCAL:1 > 0 && LOCAL:1 % 2 == 0
				PRINTL 
				PRINTFORM %TOSTR_SPACE(6)%
			;기사라면 가로폭을 넓게 취하므로 사이의 스페이스는 좁은에 있다
			ELSEIF LOCAL:1 > 0 && IS_GISHI
				PRINTFORM %TOSTR_SPACE(7)%
			;기사 없음이라면 가로폭이 좁기 때문에 사이의 스페이스는 넓은에 있다
			ELSEIF LOCAL:1 > 0
				PRINTFORM %TOSTR_SPACE(9)%
			ENDIF
			LOCAL:1 ++
			IF IS_GISHI
				IF MONEY < ITEMPRICE:(LOCAL:0) * 75 / 100
					SETCOLOR 칼라_선택불가
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTPLAINFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM →%NUM_FORMAT((ITEMPRICE:(LOCAL:0) * 75 / 100)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTPLAINFORM {ITEMTERM:(LOCAL:0), 2}
					PRINTPLAINFORM →{ITEMTERM:(LOCAL:0) / 2 + 1, 2}
					RESETCOLOR
				ELSE
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					SETCOLOR 칼라_초록
					PRINTFORM →%NUM_FORMAT((ITEMPRICE:(LOCAL:0) * 75 / 100)), 8%
					RESETCOLOR
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 2}
					SETCOLOR 칼라_초록
					PRINTFORM →{ITEMTERM:(LOCAL:0) / 2 + 1, 2}
					RESETCOLOR
				ENDIF
			ELSE
				IF MONEY < ITEMPRICE:(LOCAL:0)
					SETCOLOR 칼라_선택불가
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 4}
					RESETCOLOR
				ELSE
					PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%]", LOCAL:0
					PRINTPLAIN  
					PRINTFORM %NUM_FORMAT(ITEMPRICE:(LOCAL:0)), 8%
					PRINTPLAINFORM %TOSTR_SPACE(2)%
					PRINTFORM {ITEMTERM:(LOCAL:0), 4}
				ENDIF
			ENDIF
		ENDIF
	ENDIF
NEXT
IF LOCAL:1 != 0
	PRINTL 
ENDIF

SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
CALL COLOR_PRINTL(@" 개발중인 아이템 ", 칼라_주석)
SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR

LOCAL:1 = 0
FOR LOCAL, 0, VARSIZE("ITEMTIME")
	SIF ITEMTIME:LOCAL == 0
		CONTINUE
	IF LOCAL:1 == 0
		PRINTFORM %TOSTR_SPACE(6)%
	;3열로 개행
	ELSEIF LOCAL:1 > 0 && LOCAL:1 % 4 == 0
		PRINTL 
		PRINTFORM %TOSTR_SPACE(6)%
	ENDIF
	PRINTFORM %ITEMNAME:(LOCAL:0), MAX_CHARANAME_LENGTH + 6, LEFT%
	PRINTPLAINFORM %TOSTR_SPACE(2)%
	SETCOLOR 칼라_초록
	PRINTPLAINFORM {ITEMTERM:(LOCAL:0) + 1 - ITEMTIME:(LOCAL:0), 2}
	PRINTFORM %TOSTR_SPACE(3)%
	RESETCOLOR
	LOCAL:1 ++
NEXT
IF LOCAL:1 != 0
	PRINTL 
ENDIF




SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
CALL COLOR_PRINT(@" 파기 ", 칼라_주석)
CALL COLOR_PRINT(@"구입치로 매각가능", 칼라_선택불가)
PRINTL 
PRINTFORM %TOSTR_SPACE(6)%
LOCAL:1 = 0
FOR LOCAL:0, 500, 900
	LOCAL:2 = LOCAL:0 - 500
	IF ITEMNAME:(LOCAL:2) != "" && ITEM:(LOCAL:2) > 0
		LOCALS:0 = %ITEMNAME:(LOCAL:2)%
		IF ITEM:(LOCAL:2) >= 2
			LOCALS:0 = %LOCALS:0%×{ITEM:(LOCAL:2)}
		ENDIF
		IF LOCAL:1 > 0 && LOCAL:1 % 3 == 0
			PRINTL 
			PRINTFORM %TOSTR_SPACE(6)%
		ELSEIF LOCAL:1 > 0
			PRINTFORM %TOSTR_SPACE(9)%
		ENDIF
		LOCAL:1 ++
		CALL GET_PRICE(LOCAL:2)
		PRINTBUTTON @"[%LOCALS:0, MAX_CHARANAME_LENGTH + 6, LEFT%]%NUM_FORMAT(RESULT), 9%", LOCAL:0
	ENDIF
NEXT
PRINTL 

FOR LOCAL:0, LINECOUNT - FIRST_LINE_ITEM_MAIN, LINES_SHOP_DRAW + 1
	PRINTL 
NEXT

SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN  
PRINTBUTTON @"[%"돌아온다", MAX_CHARANAME_LENGTH, LEFT%]", 999
PRINTL 

REDRAW 0

$INPUT_LOOP
INPUT 999

IF RESULT == 999
	REDRAW 1
	RETURN

;구입 혹은 개발
ELSEIF RESULT >= 100 && RESULT < 300 && ITEMNAME:RESULT != ""
	LOCAL:2 = RESULT

	;구입 아이템
	IF ITEMSALES:(LOCAL:2)
		CALL GET_PRICE(LOCAL:2)
		LOCAL:3 = RESULT

		; 아이템의 종별을 판정
		IF (LOCAL:2 >= 150 && LOCAL:2 < 200) || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			LOCAL:5 = 1
		ELSE
			LOCAL:5 = 0
		ENDIF

		; 아이템의 설명을 표시해, 구입의 입력 처리를 실시한다
		CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, LOCAL:5)
		LOCAL:4 = RESULT

		;구입 개수가 0이라면 돌아온다
		IF LOCAL:4 == 0
			CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
			RESTART
		ENDIF

		CLEARLINE 2
		;구입 아이템
		IF LOCAL:2 < 150
			IF ITEMNAME:(LOCAL:2) == "태평신비서"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSEIF ITEMNAME:(LOCAL:2) == "음기술대전"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSEIF ITEMNAME:(LOCAL:2) == "약속의목걸이"
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF !RESULT
					RESTART
				ENDIF
			ELSE
				PRINTFORMW %TOSTR_SPACE(6)%%ITEMNAME:(LOCAL:2)%를 구입했습니다
				ITEM:(LOCAL:2) ++
				ITEMSALES:(LOCAL:2) = 0
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		;구입 아이템(소모품)
		ELSEIF LOCAL:2 < 200 || (LOCAL:2 >= 250 && LOCAL:2 < 280)
			PRINTFORMW %TOSTR_SPACE(6)%%ITEMNAME:(LOCAL:2)%를 {LOCAL:4}개 구입했습니다
			MONEY -= LOCAL:3 * LOCAL:4
			ITEM:(LOCAL:2) += LOCAL:4
			RESTART
		;구입 아이템(그 자리 사용)
		ELSEIF LOCAL:2 >= 280 && LOCAL:2 < 300
			IF GROUPMATCH(LOCAL:2, GETNUM(ITEM, "개"), GETNUM(ITEM, "돼지"), GETNUM(ITEM, "말"), GETNUM(ITEM, "원숭이"))
				PRINTFORMW %TOSTR_SPACE(6)%%ITEMNAME:(LOCAL:2)%를 구입했습니다
				MONEY -= LOCAL:3
				SELECTCASE LOCAL:2
					CASE GETNUM(ITEM, "개")
						CALL ADD_ANIMAL(CFLAG:MASTER:소속, 동물_개)
					CASE GETNUM(ITEM, "돼지")
						CALL ADD_ANIMAL(CFLAG:MASTER:소속, 동물_돼지)
					CASE GETNUM(ITEM, "말")
						CALL ADD_ANIMAL(CFLAG:MASTER:소속, 동물_말)
					CASE GETNUM(ITEM, "원숭이")
						CALL ADD_ANIMAL(CFLAG:MASTER:소속, 동물_원숭이)
				ENDSELECT
				RESTART
			ELSE
				CALL SELECT_ITEM_USER(LOCAL:2)
				IF RESULT
					MONEY -= LOCAL:3
				ENDIF
				RESTART
			ENDIF
		ENDIF

	;개발 아이템
	ELSEIF ITEMTIME:(LOCAL:2) == 0 && !ITEMSALES:(LOCAL:2)
		RESULT = 1
		TRYCALLFORM ITEM_ABLE{LOCAL:2}
		IF RESULT
			LOCAL:3 = ITEMPRICE:(LOCAL:2)
			IF IS_GISHI
				TIMES LOCAL:3, 0.75
			ENDIF

			; 아이템의 설명을 표시해, 구입의 입력 처리를 실시한다
			CALL ITEM_EXPLANATION(LOCAL:2, LOCAL:3, 2)
			LOCAL:4 = RESULT

			;구입 개수가 0이라면 돌아온다
			IF LOCAL:4 == 0
				CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
				RESTART
			ENDIF

			CLEARLINE 2
			PRINTFORMW %TOSTR_SPACE(6)%%ITEMNAME:(LOCAL:2)%의 개발을 개시했습니다
			IF IS_GISHI
				ITEMTIME:(LOCAL:2) = ITEMTERM:(LOCAL:2) / 2 + 1
			ELSE
				ITEMTIME:(LOCAL:2) = 1
			ENDIF
			MONEY -= LOCAL:3
			RESTART
		ENDIF
	ENDIF

;파기
ELSEIF 500 <= RESULT && RESULT <= 900 && ITEMNAME:(RESULT - 500) != ""
	CLEARLINE 1
	LOCAL:1 = RESULT - 500
	IF ITEM:(LOCAL:1) > 0
		CALL GET_PRICE(LOCAL:1)
		LOCAL:2 = RESULT
		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTFORM  ★ %ITEMNAME:(LOCAL:1)%
		CALL COLOR_PRINT(@"  가격:", 칼라_주석)
		CALL COLOR_PRINT(@"%NUM_FORMAT(LOCAL:2)%", 칼라_선택중)
		PRINTL 

		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		CALL COLOR_PRINT(@"현재{ITEM:(LOCAL:1)}개 가지고 있는 %ITEMNAME:(LOCAL:1)%를 매각합니다", 칼라_주석)
		PRINTL 

		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		PRINTFORM 매각하는 수를 선택해 주세요
		CALL COLOR_PRINT(@"돌아오는 것으로 캔슬", 칼라_주석)
		PRINTL 

		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		CALL PROMPTLY_BUTTONS(0, ITEM:(LOCAL:1), 0)
		LOCAL:3 = RESULT
		;구입 개수가 0이라면 돌아온다
		IF LOCAL:3 == 0 || LOCAL:3 > ITEM:(LOCAL:1)
			RESTART
		ENDIF
		CLEARLINE 2
		MONEY += RESULT * LOCAL:2
		PRINTFORMW %TOSTR_SPACE(6)%%ITEMNAME:(LOCAL:1)%를 {RESULT}개 매각해, 돈 %NUM_FORMAT(RESULT * LOCAL:2)%를 얻었습니다
		ITEM:(LOCAL:1) -= RESULT
		IF LOCAL:1 < 150
			ITEMSALES:(LOCAL:1) = 1
		ELSEIF ITEMTERM:(LOCAL:1) > 0
			ITEMTIME:(LOCAL:1) = 0
		ENDIF
		RESTART
	ENDIF
ENDIF

CLEARLINE 1
REUSELASTLINE 
REDRAW 2
GOTO INPUT_LOOP

;-------------------------------------------------
; 아이템의 설명을 표시해, 구입의 입력 처리를 실시한다
;ARG:0= 아이템 번호
;ARG:1= 아이템의 가격
;ARG:2= 아이템의 타입(0=단품 1=소모품 2=개발)
;반환값으로서 구입 개수를 돌려준다
;-------------------------------------------------
@ITEM_EXPLANATION(ARG:0, ARG:1, ARG:2)
#DIM RETURN_NUM
#DIM MAXITEM_NUM

RETURN_NUM = 0
MAXITEM_NUM = 0

CLEARLINE 1

SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTFORM  ★ %ITEMNAME:(ARG:0)%  
IF ARG:2 == 2
	CALL COLOR_PRINT(@"개발비:", 칼라_주석)
	CALL COLOR_PRINT(@"%NUM_FORMAT(ARG:1)%", 칼라_선택중)
ELSE
	CALL COLOR_PRINT(@"가격:", 칼라_주석)
	CALL COLOR_PRINT(@"%NUM_FORMAT(ARG:1)%", 칼라_선택중)
ENDIF
PRINTL 
SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
SETCOLOR 칼라_주석
TRYCALLFORM ITEM_EXPLANATION{ARG:0}
RESETCOLOR

SELECTCASE ARG:2
	CASE 0
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		IF MONEY >= ARG:1
			PRINTBUTTON @"[%"이것을 구입한다", MAX_CHARANAME_LENGTH, LEFT%]", 0
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"이것을 구입한다", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTL 
		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAIN  
		PRINTBUTTON @"[%"돌아온다", MAX_CHARANAME_LENGTH, LEFT%]", 1
		PRINTL 
		INPUT 1
		RETURN_NUM = (RESULT == 0)

	CASE 1
		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		IF ITEM:(ARG:0) >= 99
			SETCOLOR 칼라_선택불가
			PRINTW  소지수한계에 의해 구입 불가
			RESETCOLOR
			RETURN_NUM = 0
		ELSEIF MONEY < ARG:1
			SETCOLOR 칼라_선택불가
			PRINTW  소지금 부족으로 구입 불가
			RESETCOLOR
			RETURN_NUM = 0
		ELSE
			PRINTPLAINFORM %TOSTR_SPACE(6)%구입하는 수를 선택해 주세요  
			CALL COLOR_PRINT(@"돌아오는 것으로 캔슬", 칼라_주석)
			PRINTL 
			SETCOLOR 칼라_회색
			CALL SINGLE_DRAWLINE
			RESETCOLOR
			$INPUT_LOOP_NUM
			MAXITEM_NUM = MONEY / ARG:1
			IF ITEM:(ARG:0) + MAXITEM_NUM > 99
				MAXITEM_NUM = MAXITEM_NUM - ( MAXITEM_NUM + ITEM:(ARG:0) - 99 )
			ENDIF
			SETCOLOR 칼라_회색
			CALL SINGLE_DRAWLINE
			RESETCOLOR
			;ARG:0=최소치 ARG:1=최대치 ARG:2=돌아오는 버튼의 번호
			CALL PROMPTLY_BUTTONS(0, MAXITEM_NUM, 0)
			IF ITEM:(ARG:0) + RESULT > 99
				CLEARLINE 1
				PRINTW  아이템의 수가 99개를 넘고 있습니다
				GOTO INPUT_LOOP_NUM
			ELSEIF RESULT * ARG:1 > MONEY
				CLEARLINE 1
				PRINTW 소지금이 부족합니다
				GOTO INPUT_LOOP_NUM
			ENDIF
			RETURN_NUM = MAX(RESULT, 0)
		ENDIF

	CASE 2
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(6)%
		IF MONEY >= ARG:1
			PRINTBUTTON @"[%"이것을 개발한다", MAX_CHARANAME_LENGTH, LEFT%]", 0
		ELSE
			SETCOLOR 칼라_선택불가
			PRINTPLAINFORM [%"이것을 개발한다", MAX_CHARANAME_LENGTH, LEFT%]
			RESETCOLOR
		ENDIF
		PRINTL 
		SETCOLOR 칼라_회색
		CALL SINGLE_DRAWLINE
		RESETCOLOR
		PRINTPLAIN  
		PRINTBUTTON @"[%"돌아온다", MAX_CHARANAME_LENGTH, LEFT%]", 1
		PRINTL 
		INPUT 1
		RETURN_NUM = (RESULT == 0)
ENDSELECT

RETURN RETURN_NUM

;-------------------------------------------------
;즉결 버튼
;
;PROMPTLY_MIN		최소 입력치
;PROMPTLY_MAX		최대 입력치
;PROMPTLY_BACK		돌아오는 버튼에 할당하는 번호
;
;DUM_DISPFLAG		버튼 표시 플래그
;PROMPTLY_NUM		버튼에 표시하는 수치
;PROMPTLY_POWER_NUM	버튼에 누승하는 수치
;
;CALL PROMPTLY_BUTTONS(0, 100, 0)
;-------------------------------------------------
@PROMPTLY_BUTTONS(PROMPTLY_MIN, PROMPTLY_MAX, PROMPTLY_BACK)
#DIM PROMPTLY_MIN
#DIM PROMPTLY_MAX
#DIM PROMPTLY_BACK
#DIM PROMPTLY_DISP
#DIM PROMPTLY_NUM
#DIM PROMPTLY_POWER_NUM
#DIM FIRST_LINE
#DIM SAVE_REDRAW

CURRENTREDRAW
SAVE_REDRAW = RESULT:0
REDRAW 0

FIRST_LINE = LINECOUNT
$SHOP_LOOP

PROMPTLY_NUM = 0
PROMPTLY_POWER_NUM = 0
PRINTPLAINFORM %TOSTR_SPACE(5)%
FOR LOCAL:1, 0, 2
	POWER PROMPTLY_POWER_NUM, 10, LOCAL:1
	PROMPTLY_DISP = 0
	FOR LOCAL:0, 1, 10
		PROMPTLY_NUM = LOCAL:0 * PROMPTLY_POWER_NUM
		IF PROMPTLY_NUM <= PROMPTLY_MAX
			PRINTPLAIN  
			PRINTBUTTON @"[%NUM_FORMAT(PROMPTLY_NUM), 2%]", PROMPTLY_NUM
			PROMPTLY_DISP ++
		ENDIF
	NEXT
	IF PROMPTLY_DISP
		PRINTL 
		PRINTPLAINFORM %TOSTR_SPACE(5)%
	ENDIF
NEXT
PRINTL 
PRINTPLAINFORM %TOSTR_SPACE(6)%
PRINTBUTTON @"[%"최대수로 결정", MAX_CHARANAME_LENGTH - 2, LEFT%{PROMPTLY_MAX, 2}]", PROMPTLY_MAX
PRINTL 
SETCOLOR 칼라_회색
CALL SINGLE_DRAWLINE
RESETCOLOR
PRINTPLAIN  
PRINTBUTTON @"[%"돌아온다", MAX_CHARANAME_LENGTH, LEFT%]", PROMPTLY_BACK
PRINTL 

INPUT 0
IF RESULT == PROMPTLY_BACK
	CLEARLINE 1
	RETURN PROMPTLY_BACK
ELSE
	CLEARLINE 1
	IF PROMPTLY_MIN > RESULT:0 || PROMPTLY_MAX < RESULT:0
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOP_LOOP
	ENDIF
	PRINTL 
	REDRAW SAVE_REDRAW
	RETURN RESULT:0
ENDIF


;-------------------------------------------------
;동물을 추가한다
;-------------------------------------------------
@ADD_ANIMAL(ARG:0, ARG:1)
CALL ADD_VOID_CHARA()
LOCAL = RESULT
SELECTCASE ARG:1
	CASE 동물_개
		NAME:LOCAL     = 개
		CALLNAME:LOCAL = 개
	CASE 동물_돼지
		NAME:LOCAL     = 돼지
		CALLNAME:LOCAL = 돼지
	CASE 동물_말
		NAME:LOCAL     = 말
		CALLNAME:LOCAL = 말
	CASE 동물_원숭이
		NAME:LOCAL     = 원숭이
		CALLNAME:LOCAL = 원숭이
ENDSELECT
MAXBASE:LOCAL:체력 = 3000
MAXBASE:LOCAL:기력 = 3000
MAXBASE:LOCAL:정신력 = 1500

BASE:LOCAL:체력 = MAXBASE:LOCAL:체력
BASE:LOCAL:기력 = MAXBASE:LOCAL:기력
BASE:LOCAL:정신력 = MAXBASE:LOCAL:정신력

ABL:LOCAL:Ｃ감 = 5
ABL:LOCAL:욕망 = 5
ABL:LOCAL:성기술 = 5
ABL:LOCAL:성지식 = 5
ABL:LOCAL:봉사 = 3
ABL:LOCAL:성교 = 10
ABL:LOCAL:사정 = 5

EXP:LOCAL:절정경험 = 300

ABL:LOCAL:무투 = 5
ABL:LOCAL:방위 = 5
ABL:LOCAL:지략 = 5
ABL:LOCAL:정치 = 1
ABL:LOCAL:가창 = 1
ABL:LOCAL:요리 = 1
TALENT:LOCAL:정조관념없음 = 1
TALENT:LOCAL:동물귀 = 1
TALENT:LOCAL:꼬리 = 1
TALENT:LOCAL:복종 = 1
TALENT:LOCAL:예속 = 1
TALENT:LOCAL:절륜 = 1
TALENT:(LOCAL:0):합의 = 1
TALENT:LOCAL:음모현재치 = 음모_표준
TALENT:LOCAL:음모목표치 = 음모_표준
TALENT:LOCAL:Ｖ조임 = GET_DEFAULT_TIGHTNESS("보통")
TALENT:LOCAL:Ａ조임 = GET_DEFAULT_TIGHTNESS("보통")
CFLAG:(LOCAL):호감도 = 3000
CFLAG:(LOCAL):종속도 = 10000
CFLAG:(LOCAL):의존도 = 5000
CFLAG:(LOCAL):지배도 = 3000

CFLAG:(LOCAL:0):안면 = 1
CALL CHANGE_COUNTRY(LOCAL, ARG:0)

TALENT:LOCAL:동물 = ARG:1

