;-------------------------------------------------
;「요바이한다」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME21
RESULTS:0 '= "요바이한다"

;-------------------------------------------------
;「요바이한다」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK21
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || LOCAL == CFLAG:MASTER:소속
		CONTINUE
	SIF !CFLAG:MASTER:소속 || TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:소속):LOCAL
		GOTO FOUND
NEXT

RETURN 0

$FOUND

SIF CFLAG:MASTER:포로처
	RETURN 0
SIF CFLAG:MASTER:행동불능상태 == 행동불능_임월 || COOLTIME:MASTER:0 > 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;「요바이한다」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA21(ARG:0)
;인접하고 있는 도시가 있다
IF !CFLAG:MASTER:소속 || TMP_COUNTRY_IS_NEIBORING:(CFLAG:MASTER:소속):(CFLAG:(ARG:0):소속)
	;비주인공, 포로는 아닌, 별세력, 어딘가의 세력에 소속해 있다（방랑, 미등장, 사망이 끝난 상태는 아니다）
	IF (ARG:0 != MASTER && !CFLAG:(ARG:0):포로처 && CFLAG:MASTER:소속 != CFLAG:(ARG:0):소속 && CFLAG:(ARG:0):소속 && !IS_SP_CHARA(ARG:0) && CFLAG:(ARG:0):행동불능상태 != 행동불능_아이)
		SIF YOBAI_ABLE(ARG:0)
			RETURN CHECK91(ARG:0, 2, 1)
		RETURN 2
	ENDIF
	RETURN 0
ENDIF
RETURN 0

;-------------------------------------------------
;「요바이한다」의 추가 체크
;-------------------------------------------------
@SHOP_LIFE_CHECK_ADDITIONAL_CHECK21()
RETURN !FLAG:클리어플래그

;-------------------------------------------------
;「요바이한다」의 조수의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA_SUB21(ARG:0)
;포로로서 잡히고 있는 경우
IF CFLAG:(ARG:0):포로처
	;비주인공, 통상 상태 또는 육아중, 합의, 주인공 세력의 포로 또한 연금중
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):행동불능상태, 0, 2) && (TALENT:(ARG:0):합의 || TALENT:(ARG:0):친구) && CFLAG:(ARG:0):포로처 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):연금중 == 1 && SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0) && CFLAG:(ARG:0):행동불능상태 != 행동불능_아이)
ELSE
	;포로가 아닌, 비주인공, 통상 상태 또는 육아중, 합의나 친구내지 소속이 특수 세력이라면 대응하는 특수 세력 함락소질 가져, 동일 세력
	RETURN CHECK91(ARG:0, 2, ARG:0 != MASTER && GROUPMATCH(CFLAG:(ARG:0):행동불능상태, 0, 2) && (TALENT:(ARG:0):합의 || TALENT:(ARG:0):친구 || (IS_SP_COUNTRY(CFLAG:(ARG:0):소속) && GETBIT(TALENT:(ARG:0):특수세력함락계, SP_COUNTRY_TO_CONST(CFLAG:(ARG:0):소속)))) && CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):소속 && SHOP_LIFE_CHECKCHARA_ZYOSYU(ARG:0) && CFLAG:(ARG:0):행동불능상태 != 행동불능_아이)
ENDIF

;-------------------------------------------------
;「요바이한다」의 함락/능력 판정
;-------------------------------------------------
@YOBAI_ABLE(ARG)
#FUNCTION
LOCAL:1 = YOBAI_ZYOSYU_CALC_POINT(MASTER)
FOR LOCAL,1, CHARANUM
	SIF CFLAG:LOCAL:덮치는건조수
		LOCAL:1 += YOBAI_ZYOSYU_CALC_POINT(LOCAL)
NEXT
RETURNF LOCAL:1 > YOBAI_TARGET_CALC_POINT(ARG)

;-------------------------------------------------
;「요바이한다」의 능력 판정용 함수
;-------------------------------------------------
@YOBAI_ZYOSYU_CALC_POINT(ARG)
#FUNCTION
RETURNF ABL:ARG:무투 + ABL:ARG:지략

;-------------------------------------------------
;「요바이한다」의 능력 판정용 함수
;-------------------------------------------------
@YOBAI_TARGET_CALC_POINT(ARG)
#FUNCTION
SIF GROUPMATCH(1, TALENT:ARG:친애, TALENT:ARG:연모, TALENT:ARG:예속, TALENT:ARG:복종, TALENT:ARG:주인, TALENT:ARG:소유자)
	RETURNF 0
LOCAL = (ABL:ARG:무투 + ABL:ARG:지략)

SIF TALENT:(ARG:0):낙인
	RETURNF LOCAL * 2
SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):소속) == ARG:0
	RETURNF LOCAL * 7 / 2
RETURNF LOCAL * 3


;-------------------------------------------------
;「요바이한다의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY21
FLAG:거점페이즈페이지 = 1
FLAG:요바이 = 1
RETURN 0

;-------------------------------------------------
;「요바이한다」의 오른쪽 컬럼 캐릭터 리스트 1의 버튼（함수미지정이라면 선택중색없음 표시）
;-------------------------------------------------
@SHOP_LIFE_LIST1_BUTTON21(ARG:0, ARG:1)
;캐릭터, 선택중 칼라 표시 플래그, 버튼 번호에 추가하는 수치, CHECKCHARA의 반환값
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, 0, SHOP_LIFE_LIST1_ADD_INPUT, ARG:1, 0, "TOP")
RETURN 0

;-------------------------------------------------
;「요바이한다」의 오른쪽 컬럼 캐릭터 리스트 2의 버튼（함수미지정이라면 선택중색없음 표시）
;-------------------------------------------------
@SHOP_LIFE_LIST2_BUTTON21(ARG:0, ARG:1)
;캐릭터, 선택중 칼라 표시 플래그, 버튼 번호에 추가하는 수치, CHECKCHARA의 반환값
CALL COLUMN_RIGHT_CHARALIST_BUTTON(ARG:0, CFLAG:(ARG:0):덮치는건조수, SHOP_LIFE_LIST2_ADD_INPUT, ARG:1, 0, "BOTTOM")
RETURN 0

;-------------------------------------------------
;「요바이」의 오른쪽 컬럼 캐릭터 리스트 버튼（상）표시에 추적 시키는 정보
;  Default의 정보를 표시하지 않는 경우는 RETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_TOP21(ARG:0)
PRINTFORM P:{YOBAI_TARGET_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_TOP_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「요바이」의 오른쪽 컬럼 캐릭터 리스트 버튼（상）표시에 추적 시키는 정보
;  Default의 정보를 표시하지 않는 경우는 RETURN 1
;-------------------------------------------------
@SHOP_LIFE_BUTTON_ADD_BOTTOM21(ARG:0)
PRINTFORM P:{YOBAI_ZYOSYU_CALC_POINT(ARG:0), 3, RIGHT}
CALL SHOP_LIFE_BUTTON_ADD_BOTTOM_PRISONER(ARG:0)
RETURN RESULT

;-------------------------------------------------
;「요바이한다」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW21
CALL COLUMN_RIGHT_TITLE("대상자 선택", "0", "1", "1", "0", "조수 선택가능, P=무투+지략")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;2단조캐릭터 리스트상과 페이지 이동
;여기서 판정전에 추가 조건으로서 지정되고 있던 TMP_COUNTRY_IS_NEIBORING는
;SHOP_LIFE_CHECKCHARA내에 이동
CALL COLUMN_RIGHT_CHARALIST_TOP(1)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;조수를 센다
LOCAL:1 = 0
LOCAL:2 = YOBAI_ZYOSYU_CALC_POINT(MASTER)
FOR LOCAL:0, 0, CHARANUM
	;선택 가능한 조건을 채우고 있는지 어떤지를 판정
	TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:거점페이즈선택커맨드}(LOCAL:0)
	IF RESULT == 1
		;선택중의 인원수를 센다
		IF CFLAG:(LOCAL:0):덮치는건조수
			LOCAL:1 ++
			LOCAL:2 += YOBAI_ZYOSYU_CALC_POINT(LOCAL)
		ENDIF
	ELSE
		;조건을 채우지 않으면 강제적으로ＯＦＦ
		CFLAG:(LOCAL:0):덮치는건조수 = 0
	ENDIF
NEXT

PRINTFORM 조수를 최대 2명까지 데리고 가는 것이 가능합니다
SIF LOCAL:1 >= 2
	SETCOLOR 칼라_오렌지
PRINTFORM (현재{LOCAL:1}/2인)
RESETCOLOR

PRINTFORM  포인트:{LOCAL:2}
PRINTFORM  
PRINTBUTTON "[조수 자동 선택]", 1
PRINTFORM  
PRINTBUTTON @"[%ZYOSYU_MODE()%]", 2
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;2단조캐릭터 리스트하와 페이지 이동
소트키보존 = 소트키
소트순서보존 = 내림차순소트
소트키 = 소트_요바이조수
내림차순소트 = 1
CALL CREATE_SELECTOR_SORT_LIST
CALL COLUMN_RIGHT_CHARALIST_BOTTOM(1)
소트키 = 소트키보존
내림차순소트 = 소트순서보존
CALL CREATE_SELECTOR_SORT_LIST
RETURN 0

;-------------------------------------------------
;「요바이한다」의 오른쪽 컬럼 버튼의 입력 처리 보좌
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB21(ARG:0)
#DIM 후보치, 3000
#DIM 후보, 3000
#DIM 후보수
#DIM 조수수
VARSET 후보치, __INT_MIN__
VARSET 후보, -1
VARSET 후보수
VARSET 조수수
CVARSET CFLAG, GETNUM(CFLAG, "덮치는건조수"), 0

;최적 멤버의 결정
IF ARG:0 == 1
	CVARSET CFLAG, GETNUM(CFLAG, "덮치는건조수"), 0
	FOR LOCAL:0, 1, CHARANUM
		;선택 가능한 조건을 채우고 있는지 어떤지를 판정
		TRYCALLFORM SHOP_LIFE_CHECKCHARA_SUB{FLAG:거점페이즈선택커맨드}(LOCAL:0)
		IF RESULT == 1
			후보:후보수 = LOCAL
			후보치:후보수 = YOBAI_ZYOSYU_CALC_POINT(LOCAL)
			후보수 ++
		ENDIF
	NEXT

	SIF 후보수 == 0
		RETURN 0

	WHILE 1
		LOCAL = FINDELEMENT(후보치, MAXARRAY(후보치), 0, 후보수)
		SIF 후보치:LOCAL == __INT_MIN__ || 후보:LOCAL == -1
			BREAK
		CFLAG:(후보:LOCAL):덮치는건조수 = 1

		후보치:LOCAL = __INT_MIN__
		후보:LOCAL = -1
		조수수 ++
		SIF 조수수 >= 2
			BREAK
	WEND
ELSEIF ARG:0 == 2
	FLAG:조수필터 = ROUND_INCREMENT(FLAG:조수필터, 0, 3)
ENDIF
RETURN 0


;-------------------------------------------------
;「요바이한다」의 리스트 1 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP21(ARG:0)
#DIM 호감도외감소

;SHOW_INFO의 초기 화면을 기본 정보로 한다
FLAG:능력표시모드 = 0

$SHOW_LOOP_INFO_Y

;대상 캐릭터의 정보를 표시
CALL SINGLE_DRAWLINE
CALL SHOW_INFO(ARG:0)
CALL SINGLE_DRAWLINE
	PRINTFORML %조사처리(ANAME(ARG:0),"를")% 덮치러 갑니다
	PRINTFORML 좋습니까?
	CALL ASK_YN
	IF RESULT == 0
		CVARSET CFLAG, 6, 0
		FOR LOCAL:0, 0, CHARANUM
			IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):덮치는건조수
				CFLAG:(LOCAL:0):조교참가플래그 = 1
			ENDIF
		NEXT
		;요바이경험
		EXP:ARG:59 ++
		;조교 초기설정
		CALL TRAIN_TARGET_SETTING(ARG:0)
		;메인 타겟을 조교 대상으로 설정
		VARSET PRISONER_TARGET, -1
		PRISONER_TARGET:0 = ARG:0
		;초기의 우후후플래그를ＯＮ로 한다
		FLAG:우후후플래그 = 1
		;포로 조교 모드
		FLAG:조교모드 = 2
		;요바이 플래그
		FLAG:요바이 = 1
		;행동 개시시의 공통 설정
		CALL START_TRAIN_COMMON
		IF !GROUPMATCH(1, TALENT:ARG:친애, TALENT:ARG:연모, TALENT:ARG:예속, TALENT:ARG:복종, TALENT:ARG:주인, TALENT:ARG:소유자)
			호감도외감소 = LIMIT(RAND(100, 200) - ABL:ARG:욕망 * 20 - ABL:ARG:봉사 * 20 ,0, 100)
			SIF TALENT:ARG:낙인
				호감도외감소 /= 2
			SIF CFLAG:(ARG:0):호감도 > 0
				CFLAG:(ARG:0):호감도 -= 호감도외감소
			SIF CFLAG:(ARG:0):종속도 > 0
				CFLAG:(ARG:0):종속도 -= 호감도외감소
			SIF CFLAG:(ARG:0):의존도 > 0
				CFLAG:(ARG:0):의존도 -= 호감도외감소
			SIF CFLAG:(ARG:0):지배도 > 0
				CFLAG:(ARG:0):지배도 -= 호감도외감소 / 2
			;상대국과의 관계 악화（적당）
			CALL CHANGE_RELATION_C_TO_C(CFLAG:ARG:소속, CFLAG:MASTER:소속, RAND(50, 100) * -1, RAND(50, 100))
			;대상이 군주라면 한 그릇 더
			SIF GET_COUNTRY_BOSS(CFLAG:(ARG:0):소속) == ARG:0
				CALL CHANGE_RELATION_C_TO_C(CFLAG:ARG:소속, CFLAG:MASTER:소속, RAND(100, 150) * -1, RAND(100, 150))
		ENDIF
		REDRAW 1
		RETURN 1
ENDIF
REDRAW 1

;-------------------------------------------------
;「요바이한다」의 리스트 2 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP_SUB21(ARG:0)
;참가 캐릭터의 수를 센다
LOCAL:5 = 0
FOR LOCAL:0, 0, CHARANUM
	IF CFLAG:(LOCAL:0):덮치는건조수
		LOCAL:5 ++
	ENDIF
NEXT
IF CFLAG:(ARG:0):덮치는건조수 || LOCAL:5 < 2
	;요바이 참가 플래그를 반전
	CFLAG:(ARG:0):덮치는건조수 = !CFLAG:(ARG:0):덮치는건조수
ENDIF
RETURN 0

