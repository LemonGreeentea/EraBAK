;-------------------------------------------------
;「거리탐색」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME47
RESULTS:0 '= "거리탐색"

;-------------------------------------------------
;「거리탐색」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK47
SIF CFLAG:MASTER:포로처
	RETURN 0
RETURN 1

;-------------------------------------------------
;「거리탐색」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA47(ARG:0)
;포로가 아닌, 동일 세력, 아이가 아니다
RETURN CHECK91(ARG:0, 2, CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && (ARG:0 == MASTER || CFLAG:(ARG:0):소속) && !CFLAG:(ARG:0):포로처 && !IS_ANIMAL(ARG:0) && CFLAG:(ARG:0):행동불능상태 != 행동불능_아이)

;-------------------------------------------------
;「거리탐색의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY47
FLAG:거점페이즈페이지 = 1
FLAG:요바이 = 0
RETURN 0

;-------------------------------------------------
;「거리탐색」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW47
CALL COLUMN_RIGHT_TITLE("탐색자 선택", "0", "1", "1", "0", "")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
PRINTBUTTON @"[위험한 탐색 \@ TERRITORY_EXPLORING_DANGER_EVENT ? 온 # 오프 \@]", 1
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;표준적인 캐릭터 리스트와 페이지 이동
CALL COLUMN_RIGHT_CHARALIST
RETURN


;-------------------------------------------------
;「거리탐색」의 오른쪽 컬럼 버튼의 입력 처리 보좌
;  입력치 0～9를 상정하고 있다
;  RETURN 0:CLEARLINE LINECOUNT - LINES_SHOP 한다
;  RETURN 1:CLEARLINE LINECOUNT - LINES_SHOP 하지 않는다
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SUB47(ARG:0)
;표시 필터 변환
IF ARG:0 == 1
	TERRITORY_EXPLORING_DANGER_EVENT = !TERRITORY_EXPLORING_DANGER_EVENT
	RETURN 0
ENDIF

;-------------------------------------------------
;「거리탐색」의 리스트 1 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP47(대상)
#DIM 대상
#DIM 총이벤트수
#DIM 번호
#DIM 배열

TERRITORY_EXPLORING_TARGET = 대상

PRINTFORMW %조사처리(ANAME(대상),"는")% 거리를 탐색하러 다녔다……
CFLAG:대상:행동완료 = 1

;발생하는 이벤트 배열과 번호
IF TERRITORY_EXPLORING_DANGER_EVENT
	총이벤트수 = SUMARRAY(TERRITORY_EVENT_NUM)
ELSE
	총이벤트수 = TERRITORY_EVENT_NUM:거리탐색_통상
ENDIF

SIF 총이벤트수 == 0
	GOTO NOTHING_HAPPEN

번호 = RAND:총이벤트수

IF 번호 >= TERRITORY_EVENT_NUM:거리탐색_통상
	번호 -= TERRITORY_EVENT_NUM:거리탐색_통상
	배열 = 거리탐색_위험
ELSE
	배열 = 거리탐색_통상
ENDIF

CALLFORM TERRITORY_EVENT_%TERRITORY_EVENT_NAME_ENG:배열:번호%_DECISION(대상)

IF RESULT == 1
	CALL SINGLE_DRAWLINE
	PRINTFORML --이벤트 『%TERRITORY_EVENT_NAME:배열:번호%』발생--
	CALL SINGLE_DRAWLINE
	WAIT
	CALLFORM TERRITORY_EVENT_%TERRITORY_EVENT_NAME_ENG:배열:번호%(대상)
ELSE
	$NOTHING_HAPPEN
	PRINTFORML ……하지만 아무런 일도 없었다……
ENDIF

WAIT

CALL TRAIN_AUTO_ABLUP(대상)
RESULT = 1
SHOP_TIME ++
BEGIN TURNEND

@SELECT_CHARA_RANDOM_LOGIC_FOR_TERRITORY_EXPLORING_BASE(대상)
#FUNCTION
#DIM 대상
RETURNF 대상 != TERRITORY_EXPLORING_TARGET && (CFLAG:대상:소속 || CFLAG:대상:특수상태 == 특수상태_방랑) && !CFLAG:대상:포로처
