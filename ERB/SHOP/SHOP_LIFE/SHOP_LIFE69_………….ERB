;-------------------------------------------------
;「…………」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME69
RESULTS:0 '= "…………"

;-------------------------------------------------
;「…………」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK69
;주인공이 포로는 아니면 돌아온다
SIF !CFLAG:(MASTER):포로처
	RETURN 0
RETURN 1

;-------------------------------------------------
;「…………의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY69
CALL TRAIN_IN_PRISON
IF RESULT == 0
	RESULT = 1
	BEGIN TURNEND
ENDIF
RETURN 0

;-------------------------------------------------
;「…………」의 오른쪽 컬럼 오프 판정
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_COLUMN_RIGHT_OFF_CHECK69

;-------------------------------------------------
;주인공이 투옥중 때의 역조교이벤트
;0를 돌려주면 조교 개시하지 않고 턴 엔드
;-------------------------------------------------
@TRAIN_IN_PRISON
#DIM CHARA_LIST, 3000

IF IS_SP_COUNTRY(CFLAG:MASTER:포로처)
	CALL SP_COUNTRY_REV_TRAIN()
	IF RESULT
		CALL START_TRAIN_COMMON()
		RETURN 1
	ELSE
		RETURN 0
	ENDIF
ENDIF

;특수 세력 이외의 포로의 경우
;전원의 조교참가플래그를 해제
CVARSET CFLAG, 6, 0

LOCAL:10 = -1
LOCAL:11 = 0
CALL SELECT_REV_TRAINER()
LOCAL:10 = RESULT:0
LOCAL:11 = RESULT:1

;FLAG:역조교메인 조교자에게 기록된 ID로부터 메인 조교자의 캐릭터 번호를 취득
LOCAL:12 = ID_TO_CHARA(FLAG:역조교메인조교자)
IF LOCAL:12 >= 0 && CFLAG:(LOCAL:12):소속 == CFLAG:MASTER:포로처 && CFLAG:(LOCAL:12):포로처 == 0 && GROUPMATCH(CFLAG:(LOCAL:12):행동불능상태, 0, 2)
	;메인 조교자를 조교에 참가시킨다
	CFLAG:(LOCAL:12):조교참가플래그 = 1
	CFLAG:(LOCAL:12):강제우호화 = 1
	LOCAL:10 = LOCAL:12
	LOCAL:11 ++
;메인 조교자가 없을 것 같으면 여기서 설정 해 둘 수 있는 해 두어라～～～
ELSEIF LOCAL:12 < 0
	FLAG:역조교메인조교자 = GET_ID(LOCAL:10)
	LOCAL:12 = LOCAL:10
ENDIF

IF CFLAG:MASTER:행동불능상태 == 행동불능_임월
	IF LOCAL:10 >= 0
		LOCALS:0 = 
		IF LOCAL:11 >= 2
			LOCALS:0 = 들
		ENDIF
		CALL SINGLE_DRAWLINE
		PRINTFORML %ANAME(LOCAL:10)% %조사처리(LOCALS:0,"는")% 완전히 커진 %ANAME(MASTER)%의 배를 봐,
		PRINTFORMW 아이가 출생할 때까지 조교를 보류하는 일로 결정한 것 같다
		PRINTFORMW %ANAME(LOCAL:10)% %조사처리(LOCALS:0,"는")% %ANAME(MASTER)%에, 건강한 아이를 낳도록(듯이) 타일러 돌아갔다…

	ELSE
		CALL SINGLE_DRAWLINE
		PRINTFORMW %조사처리(ANAME(MASTER),"는")% 감옥의 안에서 나날을 보냈다…
	ENDIF
	;해방카운터를 리셋트
	CFLAG:MASTER:해방카운터 = 0

;참가자가 한사람 이상 있으면 역조교를 개시
ELSEIF LOCAL:11 > 0
	IF FLAG:역조교회수 == 0
		LOCALS:0 = 
		IF LOCAL:11 >= 2
			LOCALS:0 = 들
		ENDIF
		CALL SINGLE_DRAWLINE
		PRINTFORMW %ANAME(MASTER)%의 감옥의 앞에, %ANAME(LOCAL:10)% %조사처리(LOCALS:0,"가")% 왔다
		PRINTFORMW %ANAME(LOCAL:10)% %조사처리(LOCALS:0,"는")% %ANAME(MASTER)%에 대해, %조사처리(ANAME(MASTER),"가")% 임관한다 신경이 쓰인다 때까지 조교한다고 선언했다…
		;설득하고 나서의 기간을 0으로 한다
		FLAG:권유경과기간 = 0
	ENDIF
	;만약을 위해
	FLAG:요바이 = 0
	;포로 역조교모드
	FLAG:조교모드 = 5
	;우후후모드
	FLAG:우후후플래그 = 1
	;회수를 카운트
	FLAG:역조교회수 ++
	;설득하고 나서의 기간을 증가
	FLAG:권유경과기간 ++
	;해방카운터를 리셋트
	CFLAG:MASTER:해방카운터 = 0
	; 아이템을 대충 추가
	ITEM:A_로터 = 1
	ITEM:A_바이브 = 1
	ITEM:A_애널바이브 = 1
	ITEM:A_페니스밴드 = 1
	ITEM:A_오나홀 = 1
	ITEM:A_클리캡 = 1
	ITEM:A_유두캡 = 1
	ITEM:A_채찍 = 1
	ITEM:A_로션 = 99
	;행동 개시시의 공통 처리
	CALL START_TRAIN_COMMON
	RETURN 1

ELSE
	IF RAND:3 == 0
		;연모 또는 복종의 캐릭터가 있으면 가끔 해 온다
		LOCAL:11 = 0
		FOR LOCAL:0, 0, CHARANUM
			IF LOCAL:0 != MASTER && CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:포로처 && CFLAG:(LOCAL:0):포로처 == 0 && GROUPMATCH(CFLAG:(LOCAL:0):행동불능상태, 0, 2) && (TALENT:(LOCAL:0):연모 || TALENT:(LOCAL:0):복종)
				CHARA_LIST:(LOCAL:11) = LOCAL:0
				LOCAL:11 ++
			ENDIF
		NEXT
		IF LOCAL:11 >= 1
			LOCAL:12 = CHARA_LIST:(RAND:(LOCAL:11))
			CFLAG:(LOCAL:12):조교참가플래그 = 1
			CFLAG:(LOCAL:12):강제우호화 = 1
			LOCAL:10 = 1

			CALL SINGLE_DRAWLINE
			PRINTFORMW %ANAME(MASTER)%의 감옥의 앞에, %ANAME(LOCAL:12)%가 왔다
			PRINTFORMW %ANAME(LOCAL:12)%는 %ANAME(MASTER)%의 감옥을 공중에 들어가면, 조금 이야기를 하고 싶다고 말해 왔다…

			;포로 회화 모드
			FLAG:조교모드 = 6
			;비우후후모드
			FLAG:우후후플래그 = 0
			;행동 개시시의 공통 처리
			CALL START_TRAIN_COMMON
			RETURN 1
		ENDIF
	ENDIF
	CALL SINGLE_DRAWLINE
	PRINTFORMW %조사처리(ANAME(MASTER),"는")% 감옥의 안에서 나날을 보냈다…
ENDIF
SHOP_TIME = CALC_SHOP_TIME()
RETURN 0
