;-------------------------------------------------
;「능력상승」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME53
RESULTS:0 '= "능력상승"

;-------------------------------------------------
;「능력상승」의 선택 가부
;-------------------------------------------------
@SHOP_LIFE_CHECK53
SIF CFLAG:MASTER:포로처
	RETURN 0
FOR COUNT, 171, 200
	SIF ITEM:COUNT
		RETURN 1
NEXT
RETURN 0

;-------------------------------------------------
;「능력상승」의 선택 가능 캐릭터 존재 판정
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA53(ARG:0)
;동일세력, 포로가 아님, 동물이 아님
RETURN CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):포로처 == 0 && !IS_ANIMAL(ARG:0)

;-------------------------------------------------
;「능력상승」의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY53
FLAG:거점페이즈페이지 = 1
FLAG:요바이 = 0
RETURN 0

;-------------------------------------------------
;「능력상승」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW53
CALL COLUMN_RIGHT_TITLE("대상자 선택", "0", "1", "0", "0")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

CALL COLOR_PRINT("가챠", 칼라_선택중)
CALL COLOR_PRINT("로 얻은 아이템을 사용할 수 있습니다", 칼라_주석)
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL

;표준적인 캐릭터 리스트와 페이지 이동
CALL COLUMN_RIGHT_CHARALIST
RETURN 0

;-------------------------------------------------
;「능력상승」의 리스트 1 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP53(대상)
#DIM FIRST_LINE_ITEM_MAIN
#DIM IS_GISHI
#DIM FIRST_LINE
#DIM 대상
#DIM 아이템번호
#DIM 아이템종류
#DIM 아이템랭크
#DIM 변환아이템
#DIM 교환비
#DIM 교환결과
#DIM 버튼체크,9

VARSET 버튼체크,0

REDRAW 0
FIRST_LINE_ITEM_MAIN = LINECOUNT

;타이틀
CALL SINGLE_DRAWLINE
PRINTPLAIN  
CALL COLUMN_RIGHT_TITLE("아이템을 선택", "", "", "0", "")
PRINTL 
CALL SINGLE_DRAWLINE
CALL COLOR_PRINTL(" 가챠로 획득한 아이템을 사용하거나 아이템을 교환/변환할 수 있습니다", 칼라_주석)
CALL COLOR_PRINT(" 아이템 교환은 ",칼라_주석,"하위 아이템",칼라_선택중)
CALL COLOR_PRINT("을 소모하여 동일계열 ",칼라_주석,"상위 아이템",칼라_선택중)
CALL COLOR_PRINTL("으로 교환하는 기능입니다",칼라_주석)
CALL COLOR_PRINTL(" ex) 무투UP 5개 -> 무투UP+ 1개 / 무투UP+ 2개 -> 무투UP++ 1개",칼라_주석)
CALL COLOR_PRINT(" 아이템 변환은 ",칼라_주석,"동일계열 아이템",칼라_선택중)
CALL COLOR_PRINT("을 소모하여 ",칼라_주석,"다른계열 아이템",칼라_선택중)
CALL COLOR_PRINTL("으로 변환하는 기능입니다",칼라_주석)
CALL COLOR_PRINTL(" 단, 이때 교환비는 ",칼라_주석,"10:1",칼라_주의,"이므로 주의해 주세요",칼라_주석)
CALL COLOR_PRINTL(" ex) 무투UP 10개 -> 방위UP 1개 / 가창UP++ 10개 -> 무투UP++ 1개",칼라_주석)
CALL SINGLE_DRAWLINE
CALL ICPRINT(@"<%ANAME(대상)%>%조사만처리(ANAME(대상),"를")% 선택중", "L", 칼라_주의)
CALL SINGLE_DRAWLINE

;소지 아이템 표시
CALL COLOR_PRINT(@" 소지 ", 칼라_주석)

LOCAL:1 = 999
FOR LOCAL:0, 171, 200
	SIF ITEMNAME:(LOCAL:0) == ""
		CONTINUE
	IF LOCAL:1 > 80
		LOCAL:1 = 0
		PRINTL 
		PRINTFORM %TOSTR_SPACE(6)%
	ENDIF
	LOCAL:1 += 30
	IF ITEM:(LOCAL:0) == 0
		SETCOLOR 칼라_선택불가
		PRINTPLAINFORM [%ITEMNAME:(LOCAL:0), 14, LEFT%]×{ITEM:(LOCAL:0), 2}
		RESETCOLOR
		PRINTPLAIN       
	ELSE
		PRINTBUTTON @"[%ITEMNAME:(LOCAL:0), 14, LEFT%]×{ITEM:(LOCAL:0), 2}", LOCAL:0
		PRINTPLAIN       
	ENDIF
	IF LOCAL:0 == 171
		PRINTL
		PRINTFORM %TOSTR_SPACE(6)%
		LOCAL:1 = 0
	ENDIF
NEXT
PRINTL 
CALL SINGLE_DRAWLINE
PRINTFORML [999] 돌아온다
$INPUT_LOOP
INPUT 999
IF RESULT == 999
	REDRAW 1
	RETURN
;구입 혹은 개발
ELSEIF RESULT > 170 && RESULT < 200 && ITEM:RESULT
	아이템번호 = RESULT
	SETCOLOR 칼라_회색
	CALL SINGLE_DRAWLINE
	RESETCOLOR
	PRINTFORML  ★ %ITEMNAME:아이템번호%  
	SETCOLOR 칼라_회색
	CALL SINGLE_DRAWLINE
	RESETCOLOR
	SETCOLOR 칼라_주석
	TRYCALLFORM ITEM_EXPLANATION{아이템번호}
	RESETCOLOR
	PRINTL
	PRINTPLAINFORM %TOSTR_SPACE(6)%
	PRINTBUTTON @"[%"아이템을 사용한다", 30, LEFT%]", 0
	PRINTL
	IF 아이템번호 != 171 && 아이템번호 != 174 && 아이템번호 != 177 && 아이템번호 != 180 && 아이템번호 != 183 && 아이템번호 != 186 && 아이템번호 != 189 && 아이템번호 != 192 && 아이템번호 != 195
		버튼체크:0 = 1
		PRINTFORM %TOSTR_SPACE(6)%
		PRINTBUTTON @"[%"상위 아이템으로 교환한다", 30, LEFT%]", 1
		PRINTL
	ENDIF
	IF 아이템번호 != 171 && 아이템번호 != 193 && 아이템번호 != 194 && 아이템번호 != 195
		버튼체크:1 = 1
		PRINTFORM %TOSTR_SPACE(6)%
		PRINTBUTTON @"[%"다른계열 아이템으로 변환한다", 30, LEFT%]", 2
		PRINTL
	ENDIF
	PRINTFORM %TOSTR_SPACE(6)%
	PRINTBUTTON @"[%"돌아간다", 30, LEFT%]", 99
	PRINTL

	INPUT
	IF RESULT == 99
		CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
		RESTART
	ELSEIF RESULT == 0
		IF 아이템번호 == 171
			CALL STATUS_REDISTRIBUTION(대상)
			SIF RESULT
				ITEM:171 --
		ELSE
			CALLFORM ITEM_EFFECT{아이템번호}(대상)
			ITEM:아이템번호 --
			WAIT
		ENDIF
		CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
		RESTART
	ELSEIF RESULT == 1 && 버튼체크:0
		교환비 = 2
		SIF 아이템번호 == 172 || 아이템번호 == 175 || 아이템번호 == 178 || 아이템번호 == 181 || 아이템번호 == 184 || 아이템번호 == 187 || 아이템번호 == 190 || 아이템번호 == 193
			교환비 = 5
		IF ITEM:(아이템번호 + 1) == 99
			PRINTFORMW  %ITEMNAME:(아이템번호 + 1)%의 개수는 이미 최대입니다.
		ELSEIF ITEM:아이템번호 >= 교환비 * 2
			PRINTFORML  교환할 아이템 개수를 입력해 주세요 (MAX {MIN(99 / 교환비, ITEM:아이템번호 / 교환비)})
			INPUT
			교환결과 = RESULT
			SIF 교환결과 > 99 / 교환비
				교환결과 = 99 / 교환비
			IF 교환결과 < 1
				GOTO INPUT_LOOP_2
			ELSEIF 교환결과 * 교환비 > ITEM:아이템번호
				교환결과 = ITEM:아이템번호 / 교환비
			ENDIF
			IF ITEM:(아이템번호 + 1) + 교환결과 > 99
				교환결과 -= ITEM:(아이템번호 + 1) + 교환결과 - 99
			ENDIF
			PRINTFORMW  %ITEMNAME:아이템번호% {교환결과 * 교환비}개를 %ITEMNAME:(아이템번호 + 1)% {교환결과}개로 교환했다.
			ITEM:아이템번호 -= 교환결과 * 교환비
			ITEM:(아이템번호 + 1) += 교환결과
		ELSEIF ITEM:아이템번호 >= 교환비
			PRINTFORMW  %ITEMNAME:아이템번호% {교환비}개를 %ITEMNAME:(아이템번호 + 1)% 1개로 교환했다.
			ITEM:아이템번호 -= 교환비
			ITEM:(아이템번호 + 1) ++
		ELSE
			PRINTFORMW  %ITEMNAME:아이템번호%의 개수가 부족합니다.
		ENDIF
		$INPUT_LOOP_2
		CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
		RESTART
	ELSEIF RESULT == 2 && 버튼체크:1
		IF ITEM:아이템번호 < 10
			PRINTFORMW  %ITEMNAME:아이템번호%의 개수가 부족합니다.
			GOTO INPUT_LOOP_3
		ELSE
			FIRST_LINE = LINECOUNT
			아이템종류 = (아이템번호 - 172) / 3
			아이템랭크 = (아이템번호 - 172) % 3
			$CHANGE_LOOP
			PRINTFORML  어떤 아이템으로 변환하겠습니까?
			FOR COUNT:0, 0, 8
				SIF COUNT:0 == 아이템종류
					CONTINUE
				SELECTCASE COUNT:0
					CASE 0
						PRINTPLAIN  
						PRINTBUTTON "[무투]", 1
						PRINTPLAIN  
						버튼체크:2 = 1
					CASE 1
						PRINTPLAIN  
						PRINTBUTTON "[방위]", 2
						PRINTPLAIN  
						버튼체크:3 = 1
					CASE 2
						PRINTPLAIN  
						PRINTBUTTON "[지략]", 3
						PRINTPLAIN  
						버튼체크:4 = 1
					CASE 3
						PRINTPLAIN  
						PRINTBUTTON "[정치]", 4
						PRINTPLAIN  
						버튼체크:5 = 1
					CASE 4
						PRINTPLAIN  
						PRINTBUTTON "[신비]", 5
						PRINTPLAIN  
						버튼체크:6 = 1
					CASE 5
						PRINTPLAIN  
						PRINTBUTTON "[가창]", 6
						PRINTPLAIN  
						버튼체크:7 = 1
					CASE 6
						PRINTPLAIN  
						PRINTBUTTON "[요리]", 7
						PRINTPLAIN  
						버튼체크:8 = 1
					CASE 7
						PRINTL 
						PRINTBUTTON " [돌아간다    ]", 0
				ENDSELECT
			NEXT
			PRINTL
			INPUT
			IF RESULT == 0
				GOTO INPUT_LOOP_3
			ELSEIF INRANGE(RESULT, 1, 7) && 버튼체크:(RESULT + 1)
				변환아이템 = RESULT * 3 + 169 + 아이템랭크
				교환비 = 10
				IF ITEM:변환아이템 == 99
					PRINTFORMW  %ITEMNAME:변환아이템%의 개수는 이미 최대입니다.
				ELSEIF ITEM:아이템번호 >= 교환비 * 2
					PRINTFORML  변환할 아이템 개수를 입력해 주세요 (MAX {MIN(99 / 교환비, ITEM:아이템번호 / 교환비)})
					INPUT
					교환결과 = RESULT
					SIF 교환결과 > 99 / 교환비
						교환결과 = 99 / 교환비
					IF 교환결과 < 1
						GOTO CHANGE_LOOP
					ELSEIF 교환결과 * 교환비 > ITEM:아이템번호
						교환결과 = ITEM:아이템번호 / 교환비
					ENDIF
					IF ITEM:변환아이템 + 교환결과 > 99
						교환결과 -= ITEM:변환아이템 + 교환결과 - 99
					ENDIF
					PRINTFORMW  %ITEMNAME:아이템번호% {교환결과 * 교환비}개를 %ITEMNAME:변환아이템% {교환결과}개로 교환했다.
					ITEM:아이템번호 -= 교환결과 * 교환비
					ITEM:변환아이템 += 교환결과
				ELSE
					PRINTFORMW  %ITEMNAME:아이템번호% {교환비}개를 %ITEMNAME:변환아이템% 1개로 변환했다.
					ITEM:아이템번호 -= 교환비
					ITEM:변환아이템 ++
				ENDIF
			ENDIF
		ENDIF
		$INPUT_LOOP_3
		CLEARLINE LINECOUNT - FIRST_LINE_ITEM_MAIN
		RESTART
	ENDIF
ENDIF

CLEARLINE 1
REUSELASTLINE 
REDRAW 2
GOTO INPUT_LOOP

;-------------------------------------------------
;「능력상승」의 리스트 2 입력 처리
;-------------------------------------------------
@STATUS_REDISTRIBUTION(대상)
#DIM FIRST_LINE
#DIM 대상
#DIM 원의능력, 7
#DIM 선택항목,1
#DIM 선택상세,1
#DIM 변화량,1
#DIM 포인트, 1
#DIM 배분후, 7
#DIM LOOPCOUNT, 1
#DIM LOOPMAX, 1
VARSET 포인트
VARSET 배분후
원의능력:0 = ABL:대상:무투
원의능력:1 = ABL:대상:방위
원의능력:2 = ABL:대상:지략
원의능력:3 = ABL:대상:정치
원의능력:4 = ABL:대상:신비
원의능력:5 = ABL:대상:가창
원의능력:6 = ABL:대상:요리
포인트 = SUMARRAY(원의능력)

FLAG:능력표시모드 = 0

FIRST_LINE = LINECOUNT
REDRAW 0
$SHOW_LOOP
CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(대상)%의 원래의 능력치의 범위내에서, 능력값을 다시 정할 수 있습니다
PRINTFORML 나머지 포인트:{포인트}
CALL SINGLE_DRAWLINE
CALL ICPRINT(@"<%ANAME(대상)%>%조사만처리(ANAME(대상),"를")% 설정중", "L", 칼라_주의)
CALL SINGLE_DRAWLINE
PRINTFORM 무투:{배분후:0, 3} 원:{원의능력:0, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 200, 배분후:0, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 201, 배분후:0, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 202, 배분후:0, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 203, 배분후:0, 포인트, -10)
PRINTL
PRINTFORM 방위:{배분후:1, 3} 원:{원의능력:1, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 210, 배분후:1, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 211, 배분후:1, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 212, 배분후:1, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 213, 배분후:1, 포인트, -10)
PRINTL
PRINTFORM 지략:{배분후:2, 3} 원:{원의능력:2, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 220, 배분후:2, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 221, 배분후:2, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 222, 배분후:2, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 223, 배분후:2, 포인트, -10)
PRINTL
PRINTFORM 정치:{배분후:3, 3} 원:{원의능력:3, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 230, 배분후:3, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 231, 배분후:3, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 232, 배분후:3, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 233, 배분후:3, 포인트, -10)
PRINTL
IF TALENT:대상:신비지식
	PRINTFORM 신비:{배분후:4, 3} 원:{원의능력:4, 3} -- 
	CALL PRINTBUTTON_FAVORITE("[ +1]", 240, 배분후:4, 포인트,   1)
	CALL PRINTBUTTON_FAVORITE("[ -1]", 241, 배분후:4, 포인트,  -1)
	CALL PRINTBUTTON_FAVORITE("[+10]", 242, 배분후:4, 포인트,  10)
	CALL PRINTBUTTON_FAVORITE("[-10]", 243, 배분후:4, 포인트, -10)
	PRINTL
ENDIF
PRINTFORM 가창:{배분후:5, 3} 원:{원의능력:5, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 250, 배분후:5, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 251, 배분후:5, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 252, 배분후:5, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 253, 배분후:5, 포인트, -10)
PRINTL
PRINTFORM 요리:{배분후:6, 3} 원:{원의능력:6, 3} -- 
CALL PRINTBUTTON_FAVORITE("[ +1]", 260, 배분후:6, 포인트,   1)
CALL PRINTBUTTON_FAVORITE("[ -1]", 261, 배분후:6, 포인트,  -1)
CALL PRINTBUTTON_FAVORITE("[+10]", 262, 배분후:6, 포인트,  10)
CALL PRINTBUTTON_FAVORITE("[-10]", 263, 배분후:6, 포인트, -10)
PRINTL
CALL SINGLE_DRAWLINE
PRINTFORML [0] 결정
PRINTFORML [999] 돌아온다

INPUT 999
IF RESULT == 999
	REDRAW 1
	RETURN 0
ELSEIF RESULT == 0
	REDRAW 1
	PRINTFORML 이상으로 능력치 재분배를 끝냅니까?
	SIF 포인트 > 0
		CALL COLOR_PRINTL("포인트가 남아 있어요?   남겨도 진짜로 아무것도 좋은 일 없어요?", 칼라_주의)
	CALL ASK_YN("끝낸다", "좀 더 만진다")
	IF RESULT == 0
		ABL:대상:무투 = 배분후:0
		ABL:대상:방위 = 배분후:1
		ABL:대상:지략 = 배분후:2
		ABL:대상:정치 = 배분후:3
		ABL:대상:신비 = 배분후:4
		ABL:대상:가창 = 배분후:5
		ABL:대상:요리 = 배분후:6
		PRINTFORMW %ANAME(대상)%의 능력치를 재분배 했습니다!
		RETURN 1
	ELSEIF RESULT == 1
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
ELSE
	선택항목 = (RESULT - 200) / 10
	선택상세 = (RESULT - 200) % 10
	SELECTCASE 선택상세
		CASE 0
			선택상세 = 1
		CASE 1
			선택상세 = -1
		CASE 2
			선택상세 = 10
		CASE 3
			선택상세 = -10
		CASEELSE
			CLEARLINE LINECOUNT - FIRST_LINE
			GOTO SHOW_LOOP
	ENDSELECT
	IF !INRANGE(선택항목, 0, 6) || 선택항목 == 4 && !TALENT:대상:신비지식 || !FAVORITE_CAN_CHOOSE(배분후:선택항목, 포인트, 선택상세)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	배분후:선택항목 += 선택상세
	포인트 -= 선택상세
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP
