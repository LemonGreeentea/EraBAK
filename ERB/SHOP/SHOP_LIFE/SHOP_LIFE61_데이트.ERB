;-------------------------------------------------
;「데이트」의 명칭
;-------------------------------------------------
@SHOP_LIFE_NAME61
RESULTS:0 '= "데이트"

;-------------------------------------------------
;「데이트」의 선택 가부 판정
;-------------------------------------------------
@SHOP_LIFE_CHECK61
;데이트대 부족
SIF MONEY < 3000
	RETURN 0
SIF CFLAG:MASTER:포로처
	RETURN 0
SIF !CFLAG:MASTER:소속
	RETURN 0
RETURN 1

;-------------------------------------------------
;「데이트」의 선택 가능 캐릭터 존재 판정（권할 수 있는 캐릭터）
;-------------------------------------------------
@SHOP_LIFE_CHECKCHARA61(ARG:0)
;비주인공, 동일 세력, 포로가 아닌, 동물이 아닌, 통상 상태 또는 육아중
RETURN ARG:0 != MASTER && CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속 && CFLAG:(ARG:0):포로처 == 0 && !IS_ANIMAL(ARG:0) && GROUPMATCH(CFLAG:(ARG:0):행동불능상태, 0, 2)

;-------------------------------------------------
;「데이트의 왼쪽 컬럼 메뉴의 입력 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY61
FLAG:거점페이즈페이지 = 1
FLAG:요바이 = 0
RETURN 0

;-------------------------------------------------
;「데이트」의 오른쪽 컬럼 표시 처리
;-------------------------------------------------
@SHOP_LIFE_EVENTBUY_SHOW61
;타이틀
CALL COLUMN_RIGHT_TITLE("대상자 선택", "0", "1", "1～전", "3, 000～", "자세력만 가능")
CALL COLUMN_RIGHT_PRINTL
CALL COLUMN_RIGHT_LINE
CALL COLUMN_RIGHT_PRINTL
;오른쪽 컬럼의 표준적인 캐릭터 리스트와 페이지 이동 호출
CALL COLUMN_RIGHT_CHARALIST
RETURN

;-------------------------------------------------
;「데이트」의 리스트 1 입력 처리
;-------------------------------------------------
@SHOP_LIFE_USERSHOP61(대상)
#DIM 대상, 1
#DIM 결과, 1
#DIM 전소비, 1
#DIM 내용, 1
#DIM 능력, 1
#DIM 이벤트발생, 1
;대상자와 데이트 한다
;돈소비, 1회 10000
;호감도+ 200+RAND:100
;행선지는 호, 숲, 마을 etc

FLAG:능력표시모드 = 0

이벤트발생 = 0
LOCAL:5 = 0
CALL SINGLE_DRAWLINE
PRINTFORML 행동횟수와 돈을 소비해, %조사처리(ANAME(대상),"와")% 데이트 합니다.
PRINTFORML 1회 데이트 할 때마다 금 3, 000을 소비합니다.
PRINTFORML 어디로 이끕니까?
PRINTFORML 
PRINTFORML 잔행동:{CALC_SHOP_TIME() - SHOP_TIME}
PRINTFORML 소지금:%NUM_FORMAT(MONEY)%
CALL SINGLE_DRAWLINE
PRINTBUTTON @"%"[호수]", 12, LEFT%", 1
PRINTBUTTON "[행동전소비]", 11
PRINTL   호수로 이끕니다. 무투치가 높을 정도 상대에게 좋은 인상이 주어집니다.
PRINTBUTTON @"%"[산]", 12, LEFT%", 2
PRINTBUTTON "[행동전소비]", 12
PRINTL   산으로 이끕니다. 방위치가 높을 정도 상대에게 좋은 인상이 주어집니다.
PRINTBUTTON @"%"[숲]", 12, LEFT%", 3
PRINTBUTTON "[행동전소비]", 13
PRINTL   숲으로 이끕니다. 지략치가 높을 정도 상대에게 좋은 인상이 주어집니다.
PRINTBUTTON @"%"[마을]", 12, LEFT%", 4
PRINTBUTTON "[행동전소비]", 14
PRINTL   마을로 이끕니다. 정치치가 높을 정도 상대에게 좋은 인상이 주어집니다.
PRINTBUTTON @"%"[꽃밭]", 12, LEFT%", 5
PRINTBUTTON "[행동전소비]", 15
PRINTL   꽃밭으로 이끕니다. 가창치가 높을 정도 상대에게 좋은 인상이 주어집니다.
PRINTBUTTON @"%"[자택]", 12, LEFT%", 6
PRINTBUTTON "[행동전소비]", 16
PRINTL   자택으로 이끕니다. 요리치가 높을 정도 상대에게 좋은 인상이 주어집니다.
CALL SINGLE_DRAWLINE
PRINTBUTTON "[0] - 돌아온다", 0
$INPUT_LOOP
INPUT 0

내용 = RESULT
;0라면 되돌린다
IF 내용 == 0
	RETURN 0
ENDIF

IF GROUPMATCH(내용, 11, 12, 13, 14, 15, 16)
	IF MONEY < 3000 * (CALC_SHOP_TIME() - SHOP_TIME)
		PRINTFORMW 돈이 충분하지 않습니다.
		GOTO INPUT_LOOP
	ELSE
		전소비 = 1
		내용 -= 10
		LOCAL:5 = 1
	ENDIF
ELSEIF GROUPMATCH(내용, 1, 2, 3, 4 ,5)
	IF MONEY < 3000
		PRINTFORMW 돈이 충분하지 않습니다.
		GOTO INPUT_LOOP
	ELSE
		전소비 = 0
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

;전소비의 경우 여기로 돌아온다
$DATE_LOOP

SHOP_TIME += 1
SHOP_WORK_COUNT += 1


;결과의 계산
SELECTCASE RAND:100
	CASE 0 TO 19
		결과 = 0
	CASE 20 TO 79
		결과 = 1
	CASEELSE
		결과 = 2
ENDSELECT

SELECTCASE 내용
	CASE 1
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 호수로 이끌었다.
		ELSE
			PRINTFORML 계속호수로 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:무투
	CASE 2
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 산으로 이끌었다.
		ELSE
			PRINTFORML 계속산에서 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:방위
	CASE 3
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 숲으로 이끌었다.
		ELSE
			PRINTFORML 계속숲에서 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:지략
	CASE 4
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 마을로 이끌었다.
		ELSE
			PRINTFORML 계속 마을에서 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:정치
	CASE 5
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 꽃밭으로 이끌었다.
		ELSE
			PRINTFORML 계속 꽃밭으로 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:가창
	CASE 6
		IF LOCAL:5 == 0
			PRINTFORML %조사처리(ANAME(대상),"를")% 자택으로 이끌었다.
		ELSE
			PRINTFORML 계속 자택에서 %조사처리(ANAME(대상),"와")% 즐겼다.
		ENDIF
		능력 = ABL:MASTER:요리
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

;구상으로 캐릭터마다별의 대사를 말하게 할 수 있도록(듯이)하고 싶다
SELECTCASE RAND:5
	CASE 0
		PRINTFORMW %PRONOUN(대상)%는 즐겨 주고 있는 것 같다.
	CASE 1
		PRINTFORMW 둘이서 즐거운 시간을 보냈다.
	CASE 2
		PRINTFORMW %PRONOUN(대상)%와의 회화가 활기를 띠었다.
	CASE 3
		PRINTFORMW 까불며 떠드는 %PRONOUN(대상)%에 좌지우지되었다.
	CASE 4
		PRINTFORMW 너무 즐거워 눈 깜짝할 순간에 시간이 지나 버렸다.
ENDSELECT

PRINTFORML
결과 = 100 + RAND:100 + 능력
CFLAG:대상:호감도 += 결과
MONEY -= 3000
SIF CFLAG:대상:안면 == 0
	CFLAG:대상:안면 = 1
PRINTFORML %ANAME(대상)%의 호감도가 {결과} 상승했다
PRINTFORML 금 3, 000을 소비했다

IF !이벤트발생
	CALL KOJO_DATE_EVENT(대상, 내용)
	SIF RESULT
		이벤트발생 = 1
ENDIF

PRINTFORMW 
IF SHOP_TIME < CALC_SHOP_TIME()
	CALL COOK_HEAL(MASTER)
	SIF 전소비
		GOTO DATE_LOOP
	RESTART
ENDIF

;데이트 이벤트가 발생하고 있지 않으면, 키스와 고백을 받을지도 모른다
IF !이벤트발생
	;데이트 후, 일정 조건으로 키스 된다
	IF CFLAG:대상:호감도 >= 1000 && RAND:3 == 0
		PRINTFORML 
		PRINTFORML 데이트의 돌아가는 길, 돌연 %ANAME(대상)%에 키스를 되었다
		PRINTFORMW 놀라는 %조사처리(ANAME(MASTER),"를")% 거들떠보지도 않고, %PRONOUN(대상)%는 못된 장난 같게 웃으면 빠른 걸음에 앞에 가 버렸다
		CALL KISS_COMMON(대상, @"%ANAME(MASTER)%의 입술", "데이트 돌아오는 길에서의 화간", 1)
		LOCAL:6 = 1
	ENDIF

	;데이트 후, 일정 조건으로 고백된다
	IF CFLAG:대상:호감도 >= 1500 && !IS_SLAVE(대상) && !IS_SLAVED_BY(대상) && !TALENT:대상:연인 && LOCAL:6 != 1 && RAND:2 == 0
		PRINTFORML 
		PRINTFORML 데이트의 돌아가는 길, 돌연 %ANAME(대상)%에 고백되었다
		PRINTFORML %PRONOUN(대상)%는 희미하게 눈동자를 물기를 띠게 하면서 곧바로 %조사처리(ANAME(MASTER),"를")% 응시해 온다
		PRINTFORML 어떻게 하지?
		CALL ASK_YN("받는다", "거절한다")
		IF RESULT == 1
			PRINTFORML 미안하지만 거절했다
			PRINTFORMW %PRONOUN(대상)%는 일순간 외로운 것 같은 얼굴을 보였지만, 아무 일도 없었는지같이 웃으면 빠른 걸음에 먼저 가 버렸다
		ELSE
			PRINTFORML %조사처리(ANAME(MASTER),"는")% %ANAME(대상)%의 손을 잡으면, 곧바로 응시하고 돌려주어 대답을 했다
			PRINTFORML 하면 %PRONOUN(대상)%는 팟 웃는 얼굴이 되어, 힘차게 %ANAME(MASTER)%에 껴안아 키스했다
			PRINTFORMW 잠깐 달콤한 입맞춤을 주고 받은 뒤로 입술을 떼어 놓으면 어쩐지 쑥스러운 분위기가 흘러, 서로 뺨을 느슨해지게 하면서 손을 잡아 함께 돌아갔다
			CALL KISS_COMMON(대상, @"%ANAME(MASTER)%의 입술", "데이트 돌아오는 길에서의 화간", 1)
			CALL GET_LOVERS(대상)
			TALENT:대상:합의 = 1
			PRINTFORMW %ANAME(대상)%의[합의]를 얻었습니다
		ENDIF
	ENDIF
ENDIF

RESULT = 1

LINES_SHOP = LINECOUNT
BEGIN TURNEND

RETURN 1

