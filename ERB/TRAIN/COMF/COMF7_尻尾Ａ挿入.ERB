;꼬리Ａ삽입

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME7
LOCALS:0 = 꼬리Ａ삽입

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE7
;공통 부분
CALL COM_ABLE_COMMON(7)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;플레이어가 안면승마 되고 있다면 불가
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;플레이어가 바닥에 엎드리며 있다면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;플레이어에 꼬리가 필요
SIF !TALENT:(MPLY:0):꼬리
	RETURN 0
;플레이어가 풋잡·전기안마중이라면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
;플레이어가 꼬리를 사용중이라면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 6, 17)
	RETURN 0
;타겟의Ａ가 플레이어의 꼬리 이외로 메워지고 있다면 불가
SIF IS_A_HOLD(MTAR:0) && SEARCH_EQUIP(7, MPLY:0, MTAR:0) == -1
	RETURN 0
;타겟이 안면승마중이라면 불가
SIF IS_RIDE(MTAR:0)
	RETURN 0
;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위·배면기승위라면 불가
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4, 6)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM7
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
EXP:(MPLY:0):성기술경험치 += 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(LOCAL:2, 200)
SOURCE:(MPLY:0):접촉 = 60
SOURCE:(MPLY:0):성행동 = 60

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 120, 50)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

;●타겟으로 붙어 처리
SOURCE:(MTAR:0):노출 = 50
SOURCE:(MTAR:0):접촉 = 60
SOURCE:(MTAR:0):성행동 = 150
SOURCE:(MTAR:0):쾌Ａ += SENSE_HOUSHI(MPLY:0, MTAR:0, 1400)

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 60, 100)

CALL VIRGIN_COMMON_A(MTAR:0, @"%ANAME(MPLY:0)%의 꼬리", GET_SITUATION_BY_TRAIN_MODE())


;사정 개소와 대상을 세트
CALL STACK_SPERM(MPLY:0, MTAR:0, 0)
CALL STACK_SPERM(MTAR:0, MPLY:0, 0)

;대상의 질이 느슨해진다
CALL TIGHTNESS_DECREASE_A(MTAR:0, RAND(1, 4))

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 3

;레즈·ＢＬ경험기준치
TFLAG:51 = 5

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP7
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP7(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●플레이어에 대해 처리
EXP:(LOCAL:2):성기술경험치 += 1

SOURCE:(LOCAL:2):봉사 += SERVE_HOUSHI(LOCAL:2, 60)
SOURCE:(LOCAL:2):접촉 += 30
SOURCE:(LOCAL:2):성행동 += 20

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(LOCAL:2):4 = 1

;●타겟으로 붙어 처리
SOURCE:(LOCAL:3):쾌Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 500)
SOURCE:(LOCAL:3):노출 += 20
SOURCE:(LOCAL:3):접촉 += 30
SOURCE:(LOCAL:3):성행동 += 50

CALL VIRGIN_COMMON_A(LOCAL:3, @"%ANAME(LOCAL:2)%의 꼬리", GET_SITUATION_BY_TRAIN_MODE())

;대상의 질이 느슨해진다
CALL TIGHTNESS_DECREASE_A(LOCAL:3, RAND(1, 4))

;사정 개소와 대상을 세트
CALL STACK_SPERM(LOCAL:2, LOCAL:3, 0, 0)
CALL STACK_SPERM(LOCAL:3, LOCAL:2, 0, 0)

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE7(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%의 애널에 꼬리를 삽입중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP7(ARG:0)
PRINTFORML %EQUIP_PLAYER_ANAME(ARG:0)%의 꼬리가 %EQUIP_TARGET_ANAME(ARG:0)%의 애널을 후벼파고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP7(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER7(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 65

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "미약", 6)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE7
IF ABL:(MTAR:0):Ａ감 >= 3
	LOCALS:0 = 충분히 개발 된 애널
ELSEIF ABL:(MTAR:0):Ａ감 >= 2
	LOCALS:0 = 개발 도상의 애널
ELSE
	LOCALS:0 = 힘들게 닫은 애널
ENDIF

IF TALENT:(MTAR:0):예쁜엉덩이
	LOCALS:1 = %STR_BODY("엉덩이:엉덩이살:장:접촉", MTAR:0)%를 나누어 열어,
ELSEIF GET_HIPSIZE(MTAR:0) >= 1
	LOCALS:1 = %STR_BODY("엉덩이살:장:접촉", MTAR:0)%를 써 (뜻)이유,
ELSE
	LOCALS:1 = 
ENDIF

;이미 꼬리를 삽입중의 경우
IF SEARCH_EQUIP(7, MPLY:0, MTAR:0) >= 0
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 %LOCALS:1%애널에 삽입된 꼬리를 몇 번이나 뽑아 꽂아, %조사처리(ANAME(MTAR:0),"를")% 자극%TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에 촉구받는 대로, %ANAME(MTAR:0)%의 %LOCALS:1%애널에 삽입된 꼬리를 움직여 봉사하고 있다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에 %ANAME(MTAR:0)%의 %LOCALS:1%엉덩이 구멍에 삽입된 꼬리를 한층 더 움직이도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 꼬리를 몇 번이나 뽑아 꽂아, %ANAME(MTAR:0)%의 애널을 자극했다…
	ENDSELECT

;그 이외
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 %LOCALS:1% %LOCALS:0%에 꼬리를 삽입해, 전후에 움직여 자극%TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MTAR:0),"는")% %LOCALS:1%자신의 애널에 %ANAME(MPLY:0)%의 꼬리를 삽입시켜, 봉사하도록(듯이) 명령했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, 꼬리로 %ANAME(MTAR:0)%의 엉덩이 구멍을 꾸짖도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 %LOCALS:1% %LOCALS:0%에 꼬리를 삽입해, 전후에 움직였다…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST7
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):쾌Ａ

		IF TCVAR:(LOCAL:1):52
			;기절중
			PRINTFORML 애널에의 자극에, %조사처리(ANAME(LOCAL:1),"는")% 의식을 잃은 채로 작게 신음했다
		ELSEIF LOCAL:2 < 300 
			PRINTFORML %ANAME(LOCAL:1)%의 개발되어 있지 않은 거기는 아직 이물을 받아들이는 장소는 아니고, %조사처리(ANAME(LOCAL:1),"는")% 아픈 듯이 얼굴을 비뚤어지게 했다
		ELSEIF LOCAL:2 < 1000
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 괴로운 듯이 하면서도 그 표정에는 얼마 안 되는 쾌락과 당황스러움이 보인다
		ELSEIF LOCAL:2 < 3000
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 참을 수 없는 쾌락에 부끄러운 것 같은 소리를 흘리고 있다
		ELSEIF LOCAL:2 < 6000
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 대단히 소화되어 온 항문의 감각에, 때때로 스스로 쾌락을 추구하도록(듯이) 작게 허리를 흔들고 있다
		ELSEIF LOCAL:2 < 10000
			PRINTFORM %조사처리(ANAME(LOCAL:1),"는")%
			IF TALENT:(LOCAL:1):억압 || TALENT:(LOCAL:1):고고함 || TALENT:(LOCAL:1):쾌감을부정
				PRINTFORML 저항할 수 없는 쾌락에, 눈물고인 눈이 되면서 신음소리를 올리고 있다
			ELSE
				PRINTFORML 희롱하는 손가락의 움직임에 맞추어 등을 뒤로 젖혀, 직장의 자극에 쾌락의 신음소리를 올렸다
			ENDIF
		ELSE
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 추잡하게 허리를 흔들면서, 완전히 엉덩이구멍의 쾌락에 빠져 교성을 올리고 있다
		ENDIF
		IF TALENT:(LOCAL:1):예쁜엉덩이
			PRINTFORML %STR_BODY("엉덩이살:장:외관", MTAR:0)%가 흔들흔들 떨고 있다……
		ELSEIF GET_HIPSIZE(LOCAL:1) >= 1
			PRINTFORML %STR_BODY("엉덩이살:장:외관", MTAR:0)%가 흔들흔들 떨고 있다……
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN7
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_7()
RETURN POWER(2, 성적기호_애무하고싶다)

@COM_PREFERENCE_TARGET_7()
RETURN POWER(2, 성적기호_애무되고싶다)

