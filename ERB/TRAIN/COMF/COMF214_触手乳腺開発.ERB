;촉수착유

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME214
IF TALENT:(MTAR:(LOCAL:0)):모유체질
	LOCALS:0 = 촉수 유선 고문
ELSE
	LOCALS:0 = 촉수유선개발
ENDIF
RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE214
;공통 부분
CALL COM_ABLE_COMMON(214)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;플레이어가 촉수소환중이 아니면 불가
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;타겟이 가슴애무·유두빨기 되고 있는, 유두캡 사용중이라면 불가
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 1, 9, 64, 67)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM214
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;촉수가슴애무를 해제
FOR LOCAL:0, 0, MTAR_NUM
	CALL RELEASE_EQUIP_EX(204, MPLY:0, MTAR:(LOCAL:0))
NEXT

;●플레이어에 대해 처리
EXP:(MPLY:0):성기술경험치 += 1
EXP:(MPLY:0):신비경험치 += 1
EXP:(MPLY:0):촉수경험치 += 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):기학 = 50
SOURCE:(MPLY:0):일탈 = 100
SOURCE:(MPLY:0):촉수 = 30
SOURCE:(MPLY:0):성행동 = 90

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 140, 30)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

;●모든 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):촉수경험치 += 1

	LOCAL:3 = 1000 + ABL:(MPLY:0):신비 * 8
	SOURCE:(LOCAL:2):쾌Ｂ = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 500)
	SIF TALENT:(LOCAL:2):모유체질
		SOURCE:(LOCAL:2):착유 = SENSE_HOUSHI(MPLY:0, LOCAL:2, LOCAL:3 * 3 / 5) + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 500)
	SOURCE:(LOCAL:2):고통 = 1000
	SOURCE:(LOCAL:2):일탈 = 500 - (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 250)
	SOURCE:(LOCAL:2):촉수 = 400 + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 200)
	SOURCE:(LOCAL:2):성행동 = 180 + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 90)

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 80)
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 4

;레즈·ＢＬ경험기준치
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP214
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP214(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):성행동 += 30

;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;소스를 퇴피
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):촉수경험치 += 1

	LOCAL:4 = 800 + ABL:(LOCAL:2):신비 * 6
	SOURCE:(LOCAL:3):쾌Ｂ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 400)
	SIF TALENT:(LOCAL:3):모유체질
		SOURCE:(LOCAL:3):착유 += SENSE_HOUSHI(LOCAL:2, LOCAL:3, LOCAL:4 * 3 / 5) + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 400)
	SOURCE:(LOCAL:3):고통 += 500
	SOURCE:(LOCAL:3):일탈 += 150 - (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 75)
	SOURCE:(LOCAL:3):촉수 += 200 + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 100)
	SOURCE:(LOCAL:3):성행동 += 60 + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 30)

	;도착도 변화
	TCVAR:(LOCAL:3):50 += 2

	;퇴피한 소스를 가산
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE214(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%에게 %EQUIP_PLAYER_ANAME(ARG:0)%의 촉수가 유선 개발 안

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP214(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% 조종하는 촉수가 %EQUIP_TARGET_ANAME(ARG:0)%의 유두에 기어들어 유선을 괴롭히고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP214(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER214(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 120

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"촉수 Lv{ABL:(ARG:0):촉수}", ABL:(ARG:0):촉수 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):호기심
	CALL COM_ORDER_ELEMENT(ARG:0, "호기심", 2)
ENDIF
IF TALENT:(ARG:0):보수적
	CALL COM_ORDER_ELEMENT(ARG:0, "보수적", -7)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 4)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -2)
ENDIF

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드) || GETBIT(TALENT:(ARG:0):음란계, 소질_음란_마조) || ABL:(ARG:0):도착도 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 32)
ELSEIF ABL:(ARG:0):도착도 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 24)
ELSEIF ABL:(ARG:0):도착도 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 16)
ELSEIF ABL:(ARG:0):도착도 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 0)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE214
;타겟이 2명 이상
IF MTAR_NUM >= 2
	IF MTAR_NUM >= 3
		LOCALS:1 = %ANAME(MTAR:0)%들
	ELSEIF MTAR_NUM == 2
		LOCALS:1 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락보다 가는 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 %TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORML %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에, 촉수로 유선을\@ TALENT:(MTAR:0):모유체질 ? 개발 한다 # 꾸짖는다 \@명했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락보다 가는 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, 촉수로 %LOCALS:1%의 유선을\@ TALENT:(MTAR:0):모유체질 ? 개발 한다 # 꾸짖는다 \@명했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락보다 가는 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 했다…
	ENDSELECT

;타겟이 1명
ELSE
	IF GET_BUSTSIZE(MTAR:0) < 0
		LOCALS:0 = 평평한 가슴
	ELSEIF GET_BUSTSIZE(MTAR:0) > 0
		LOCALS:0 = 풍만한 가슴
	ELSE
		LOCALS:0 = 가슴
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 한층 더 굵은 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 %TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORML %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에, 촉수로 유선을\@ TALENT:(MTAR:0):모유체질 ? 개발 한다 # 꾸짖는다 \@명했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락보다 가는 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, 촉수로 %LOCALS:1%의 모유를 짜도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락보다 가는 촉수를 %LOCALS:1%의 유두에 꽂아, 유선으로 비집고 들어가게 했다…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST214
FOR LOCAL, 0, MTAR_NUM
	LOCAL:1 = MTAR:LOCAL
	SIF ABL:(LOCAL:1):Ｂ감 < 랭크역치:랭크_그외:랭크_C || ABL:(LOCAL:1):촉수 < 랭크역치:랭크_그외:랭크_D
		CONTINUE
	SETCOLOR 칼라_핑크
	IF CFLAG:(LOCAL:1):일시적모유체질
		PRINTFORML %ANAME(LOCAL:1)%의 유방은 촉수의 개조에 의해 다시 만들 수 있던 것 같다……
		PRINTFORML 일시적인 것이었던 %ANAME(LOCAL:1)%의 모유체질이 정착했다
		CFLAG:(LOCAL:1):일시적모유체질 = 0
	ELSEIF !TALENT:(LOCAL:1):모유체질
		PRINTFORML %ANAME(LOCAL:1)%의 유방은 촉수의 개조에 의해 다시 만들 수 있던 것 같다……
		PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 임신하지 않았는데＜모유체질＞이 되었다
		TALENT:(LOCAL:1):모유체질 = 1
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN214
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_214()
RETURN POWER(2, 성적기호_촉수로범하고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_214()
RETURN POWER(2, 성적기호_촉수로범해지고싶다) + POWER(2, 성적기호_시달리고싶다)

