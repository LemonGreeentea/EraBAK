;충충성을맹세하게한다

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME299
RESULTS:0 = 충성을맹세하게한다
RESULTS:1 = 충성을맹세하게한다
RESULTS:2 = 충성을맹세하게한다
RESULTS:3 = 충성을맹세하게한다
RESULTS:4 = 충성을맹세하게한다
RESULTS:5 = 충성을맹세하게한다

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE299
;공통 부분
CALL COM_ABLE_COMMON(299)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어와 타겟의 쳐 어느쪽이든가 행동불능이라면 불가(타겟은 체력 조각이라면 가능)
SIF !IS_PLAYABLE(MPLY:0) || TCVAR:(MTAR:0):52 || TCVAR:(MTAR:0):53
	RETURN 0
;주인공이 플레이어 때 한정
SIF MPLY:0 != MASTER
	RETURN 0
;주인공에게 주도권이 있을 때 한정
SIF FLAG:주도권소유자 != MASTER && FLAG:주도권소유자 != -1
	RETURN 0
;이미 낙인 orIS_LOVER의 경우, 「자세력 소속의 캐릭터」와 「자신의 세력 이외의 포로」는 불가
SIF (TALENT:(MTAR:0):낙인 || IS_LOVER(MTAR:0) || IS_SLAVED_BY(MTAR:0)) && (CFLAG:MASTER:소속 == CFLAG:(MTAR:0):소속 || CFLAG:(MTAR:0):포로처 != CFLAG:MASTER:소속)
	RETURN 0
;플레이어의 입이 막히고 있다면 불가
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;타겟의 입이 막히고 있다면 불가
SIF IS_M_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM299
;●실행치의 계산
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(MTAR:0):25 = 90

CALL COM_ORDER(MTAR:0)

CALL COM_ORDER_ELEMENT(MTAR:0, "의존도", CFLAG:(MTAR:0):3 / 50)

IF TALENT:(MTAR:0):복종
	CALL COM_ORDER_ELEMENT(MTAR:0, "복종", 50)
ENDIF

IF TALENT:(MTAR:0):예속
	CALL COM_ORDER_ELEMENT(MTAR:0, "예속", 50)
ENDIF

IF TALENT:(MTAR:0):선을넘지않음
	CALL COM_ORDER_ELEMENT(MTAR:0, "선을넘지않음", -15)
ENDIF

IF FLAG:요바이
	CALL COM_ORDER_ELEMENT(MTAR:0, "요바이/안내", -50)
ENDIF

LOCAL = LIMIT(REL_LIKE:(MTAR:0):(MPLY:0) / 75 - REL_HATE:(MTAR:0):(MPLY:0) / 50,  -20, 20)

IF LOCAL != 0
	CALL COM_ORDER_ELEMENT(MTAR:0, "우호도/혐오도", LOCAL)
ENDIF

;판정 결과를 표시
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 실행치{TCVAR:(MTAR:0):25}

;판정 성공
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	;구상 표시
	CALL KOJO_EVENT(MTAR:0, 50)

	LOCALS:0 = 
	IF !IS_LOVER(MTAR:0)
		TALENT:(MTAR:0):낙인 = 1
		LOCALS:0 += "<낙인>"
	ENDIF
	IF !TALENT:(MTAR:0):합의
		TALENT:(MTAR:0):합의 = 1
		LOCALS:0 += "<합의>"
	ENDIF

	PRINTL 
	IF LOCALS:0 != ""
		SETCOLOR 칼라_주의
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %조사처리(LOCALS:0,"를")% 얻었다
		RESETCOLOR
	ENDIF

	IF !TALENT:(MTAR:0):특수세력소질 && IS_SP_COUNTRY(CFLAG:MASTER:소속) && !TALENT:(MTAR:0):특수세력함락계 && !GETBIT(TALENT:(MTAR:0):특수세력함락계, SP_COUNTRY_TO_CONST(CFLAG:MASTER:소속))
		CALL COLOR_PRINTW(@"%조사처리(ANAME(MTAR:0),"는")%<%SP_FALLEN_TALENT_NAME:(SP_COUNTRY_TO_CONST(CFLAG:MASTER:소속))%>가 되었다", 칼라_주의)
		SETBIT TALENT:(MTAR:0):특수세력함락계, SP_COUNTRY_TO_CONST(CFLAG:MASTER:소속)
	ENDIF

	IF TALENT:(MTAR:0):프라이드높음 && CFLAG:(MTAR:0):호감도 < -500 && !TALENT:(MTAR:0):복종 && STRFIND(LOCALS:0, "낙인")
		PRINTL 
		SETCOLOR 칼라_주의
		PRINTFORML 혐오 하고 있는 %ANAME(MASTER)%의 노예가 된다고 인정한 것으로, %ANAME(MTAR:0)%의 프라이드는 완전하게 접혀 버렸다
		CALL TALENT_LOSE_PRIDE(MTAR:0, 1, 1)
		RESETCOLOR
	ENDIF

	IF IS_COUNTRY(CFLAG:MASTER:소속) && CFLAG:(MTAR:0):포로처 == CFLAG:MASTER:소속
		PRINTFORMW %조사처리(ANAME(MTAR:0),"가")% 우리 나라의 사관이 되었습니다
		PRINTFORML %조사처리(ANAME(MTAR:0),"를")% 감옥에서 내 줍니까?
		CALL ASK_YN("낸다", "감옥에서의 조교를 계속한다(나중에 해방가능)")
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:소속, !RESULT)
	ENDIF

	IF IS_COUNTRY(CFLAG:(MTAR:0):소속) && CFLAG:(MTAR:0):소속 != CFLAG:MASTER:소속
		IF GET_COUNTRY_BOSS(CFLAG:(MTAR:0):소속) == MTAR:0
			CALL CHANGE_RELATION_C_TO_C(CFLAG:(MTAR:0):소속, CFLAG:MASTER:소속, 500, -500)
		ELSE
			CALL CHANGE_RELATION_C_TO_C(CFLAG:(MTAR:0):소속, CFLAG:MASTER:소속, -200, 200)
		ENDIF
	ENDIF

;판정 실패
ELSE
	;구상 표시
	CALL KOJO_EVENT(MTAR:0, 51)

	IF CFLAG:(MTAR:0):포로처 == CFLAG:MASTER:소속
		SOURCE:(MTAR:0):반감 = 300
	ELSE
		SOURCE:(MTAR:0):반감 = 3000
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN299
RETURN 커맨드_우후후
