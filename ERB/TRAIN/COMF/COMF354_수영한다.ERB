;수영
; 2023-04-09 번역체 교정 - 레몬녹차
;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME354
RESULTS:0 = 수영한다
RESULTS:1 = 수영한다

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE354
;공통 부분
CALL COM_ABLE_COMMON(354)
SIF RESULT == 0
	RETURN 0
;주인공이 플레이어 때 한정
SIF MPLY:0 != MASTER
	RETURN 0
;강변에서의 데이트 중한정
SIF GET_PLACENAME(TFLAG:54) != "강변"
	RETURN 0
;상처라면 불가
SIF CFLAG:(MTAR:0):행동불능상태 == 3
	RETURN 0
;키스중은 불가
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM354
;실행 판정
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;커맨드의 성공 여부를 TFLAG:18에 세트
	CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)
	LOCAL:1 = MTAR:0
ELSE
	;커맨드의 성공 여부를 TFLAG:18에 세트
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
	LOCAL:1 = MPLY:0
ENDIF

;●플레이어측의 처리
;고정으로 획득하는 소스
SOURCE:(MPLY:0):환락 = 20
SOURCE:(MPLY:0):접촉 = 10

;친밀하게 응한 환락의 소스 추가
CALL ADD_SOURCE_KANRAKU(MPLY:0, 190)
SOURCE:(MPLY:0):환락 = MAX(SOURCE:(MPLY:0):환락, 0)

IF TALENT:(MPLY:0):겁쟁이
	TIMES SOURCE:(MPLY:0):환락, 0.85
ENDIF
IF TALENT:(MPLY:0):얌전함
	TIMES SOURCE:(MPLY:0):환락, 0.85
ENDIF
IF TALENT:(MPLY:0):수줍음
	TIMES SOURCE:(MPLY:0):환락, 0.85
ENDIF

;주도권에 응한 소스의 추가
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 100, 100)

;●타겟측의 처리
;고정으로 획득하는 소스
SOURCE:(MTAR:0):환락 = 20
SOURCE:(MTAR:0):접촉 = 10

;친밀하게 응한 환락의 소스 추가
CALL ADD_SOURCE_KANRAKU(MTAR:0, 190)
SOURCE:(MTAR:0):환락 = MAX(SOURCE:(MTAR:0):환락, 0)

IF TALENT:(MTAR:0):겁쟁이
	TIMES SOURCE:(MTAR:0):환락, 0.85
ENDIF
IF TALENT:(MTAR:0):얌전함
	TIMES SOURCE:(MTAR:0):환락, 0.85
ENDIF
IF TALENT:(MTAR:0):수줍음
	TIMES SOURCE:(MTAR:0):환락, 0.85
ENDIF

;동성
IF IS_SAMESEX(MPLY:0, MTAR:0)
	SOURCE:(MPLY:0):노출 = 5
	SOURCE:(MTAR:0):노출 = 5
;이성
ELSE
	SOURCE:(MPLY:0):노출 = 20
	SOURCE:(MTAR:0):노출 = 20
ENDIF

;주도권에 응한 소스의 추가
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 100, 100)

;실패
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):환락, 0.10
	TIMES SOURCE:(MTAR:0):환락, 0.10
	SOURCE:(MPLY:0):불만 += 500
	SOURCE:(MTAR:0):불만 += 500
	TFLAG:37 -= 5
;성공
ELSEIF TFLAG:18 == 0

;대성공
ELSE
	TIMES SOURCE:(MPLY:0):환락, 2.00
	TIMES SOURCE:(MTAR:0):환락, 2.00
	TFLAG:37 += 5
ENDIF

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER354(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 30

;공통 부분
CALL COM_ORDER(ARG:0)

;호감도
IF CFLAG:(ARG:0):2 < 300
	IF CFLAG:(ARG:0):2 < 50
		LOCAL:0 = -30
	ELSEIF CFLAG:(ARG:0):2 < 150
		LOCAL:0 = -15
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"호감도 부족", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"호감도", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):겁쟁이
	CALL COM_ORDER_ELEMENT(ARG:0, "겁쟁이", -3)
ENDIF
IF TALENT:(ARG:0):얌전함
	CALL COM_ORDER_ELEMENT(ARG:0, "얌전함", -2)
ENDIF
IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -3)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE354
;주도권이 주인공측
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %조사처리(ANAME(MTAR:0),"와")% 함께 강가에서 수영을 했다
;주도권이 상대측
ELSE
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에게 이끌려 함께 강가에서 수영을 했다
ENDIF

IF TFLAG:18 == 1
	PRINTFORML %조사처리(ANAME(MTAR:0),"는")% 진심으로 즐거운 듯이 웃고 있다…
ELSEIF TFLAG:18 == -1
	PRINTFORML %조사처리(ANAME(MTAR:0),"는")% 그다지 즐겁지 않는 것 같다…
ENDIF

PRINTFORM %ANAME(MTAR:0)%의 옷이 흠뻑 젖어 피부에 들러붙어서, 
	SIF TALENT:(MTAR:0):처녀 == 1 && IS_FEMALE(MTAR:0)
		PRINT 남자를 모르는 
	SIF TALENT:(MTAR:0):체격 == 체격_아이
		PRINT 미숙한 
	SIF GET_HIPSIZE(MTAR:0) >= 1 || IS_BOIN(MTAR:0)
		PRINT 풍만한 
	PRINTFORML 신체의 선이 부각되고 있다.


;-------------------------------------------------
;지문(파라미터·각인 변동 후)
;-------------------------------------------------
@COM_TEXT_LAST354
;일상 커맨드에 공통의 지문
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN354
RETURN 커맨드_일상
