;오오줌을뿌린다

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME131
LOCAL:1 = 1
FOR LOCAL:0, 0, MTAR_NUM
	IF TCVAR:(MTAR:(LOCAL:0)):52 == 0
		LOCAL:1 = 0
	ENDIF
NEXT

IF LOCAL:1
	LOCALS:0 = 깨달을 수 있는 방뇨

	RESULTS:0 = %LOCALS:0%한다
	RESULTS:4 = %LOCALS:0%시킨다
	RESULTS:5 = %LOCALS:0%과시하고

ELSEIF IS_RIDE(MPLY:0)
	LOCALS:0 = 안기방뇨

	RESULTS:0 = %LOCALS:0%한다
	RESULTS:1 = %LOCALS:0%당한다
	RESULTS:2 = %LOCALS:0%시킨다
	RESULTS:3 = %LOCALS:0%된다
	RESULTS:4 = %LOCALS:0%시킨다
	RESULTS:5 = %LOCALS:0%과시하고

ELSE
	LOCALS:0 = 오줌을 하는 도중

	RESULTS:0 = %LOCALS:0%
	RESULTS:1 = %LOCALS:0%당한다
	RESULTS:2 = %LOCALS:0%시킨다
	RESULTS:3 = %LOCALS:0%
	RESULTS:4 = %LOCALS:0%시킨다
	RESULTS:5 = 뇨 걸고 과시하고
ENDIF

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE131
;공통 부분
CALL COM_ABLE_COMMON(131)
SIF RESULT == 0
	RETURN 0
;플레이어는 1명 이상
SIF MPLY_NUM <= 0
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

LOCAL:1 = 0
;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;뇨잔량이 충분하지 않으면 불가
	SIF TCVAR:(MPLY:(LOCAL:0)):54 < 1000
		RETURN 0
	;안면승마중은 1-1또한 플레이어가 타겟으로 걸치고 있을 필요가 있다
	IF IS_RIDE(MPLY:(LOCAL:0))
		SIF MPLY_NUM >= 2 || MTAR_NUM >= 2
			RETURN 0
		SIF !IS_RIDE(MPLY:(LOCAL:0), MTAR:0)
			RETURN 0
	ENDIF
	;플레이어가 삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "전")
		RETURN 0
	;페라·귀두 페라되고 있다면 불가
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 11, 107)
		RETURN 0
		;플레이어가 바닥에 엎드리며 있다면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM131
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;전플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):배설경험치 += 1
	EXP:(LOCAL:2):기학경험치 += 1

	LOCAL:3 = 800
	LOCAL:4 = 300

	SELECTCASE ABL:(LOCAL:2):배설
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:2):배설 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):노출 = 1200
	SOURCE:(LOCAL:2):일탈 = 600
	SOURCE:(LOCAL:2):성행동 = 150
	SOURCE:(LOCAL:2):중독충족 = LOCAL:3
	SOURCE:(LOCAL:2):반감 = LOCAL:4
	

	;플레이어에 주도권이 있는 경우
	IF IS_INITIATIVE(LOCAL:2)
		SOURCE:(LOCAL:2):기학 = 200
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 120, 150)

	;뇨잔량을 0으로 한다
	TCVAR:(LOCAL:2):54 = 0
NEXT

;주도권의 소재를 취득
LOCAL:11 = GET_COM_INITIATIVE()

;전 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	SOURCE:(LOCAL:1):성행동 = 90
	EXP:(LOCAL:2):피학경험치 += 1
	;타겟으로 주도권
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):기학 = 150
		SOURCE:(LOCAL:1):일탈 = 500
	;타겟으로 주도권이 없다
	ELSE
		SOURCE:(LOCAL:1):일탈 = 700
		SOURCE:(LOCAL:1):공포 = MAX(200 - 50 * ABL:(LOCAL:1):욕망, 0)
	ENDIF

	;타겟이 기절하고 있는 경우
	IF TCVAR:(LOCAL:1):52
		SOURCE:(LOCAL:1):기절회복 = 500
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 110, 130)
NEXT

;주도도 변화 기준치
TFLAG:49 = 4

;도착도 변화 기준치
TFLAG:50 = 5

;레즈·ＢＬ경험기준치
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP131
RETURN 0

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER131(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 100

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"노출 Lv{ABL:(ARG:0):노출}", ABL:(ARG:0):노출 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"배설 Lv{ABL:(ARG:0):배설}", ABL:(ARG:0):배설 * 7)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -12)
ENDIF
IF TALENT:(ARG:0):수치심옅음
	CALL COM_ORDER_ELEMENT(ARG:0, "수치심옅음", 8)
ENDIF
IF TALENT:(ARG:0):자제심
	CALL COM_ORDER_ELEMENT(ARG:0, "자제심", -6)
ENDIF
IF TALENT:(ARG:0):주목받기원함
	CALL COM_ORDER_ELEMENT(ARG:0, "주목받기원함", 6)
ENDIF
IF TALENT:(ARG:0):도착적
	CALL COM_ORDER_ELEMENT(ARG:0, "도착적", 8)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE131
IF MPLY_NUM == 1
	LOCALS:1 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:1 = %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%
ELSE
	LOCALS:1 = %ANAME(MPLY:0)%들
ENDIF

IF MTAR_NUM == 1
	LOCALS:2 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:2 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
ELSE
	LOCALS:2 = %ANAME(MTAR:0)%들
ENDIF

;타겟이 전원 기절하고 있다면 LOCAL:5를 1에, 그 이외는 0으로 한다
LOCAL:5 = 1
FOR LOCAL:0, 0, MTAR_NUM
	IF TCVAR:(MTAR:(LOCAL:0)):52 == 0
		LOCAL:5 = 0
	ENDIF
NEXT

;타겟이 전원 기절(깨달을 수 있는 방뇨)
IF LOCAL:5
	PRINTFORMW %조사처리(LOCALS:1,"는")% 기절해 녹초가 되어 있는 %LOCALS:2%의 안면에 오줌을 마구 퍼부었다…

;플레이어가 안면승마중
ELSEIF IS_RIDE(MPLY:0)
	;장대가 있는 경우
	IF HAS_PENIS(MPLY:0)
		SELECTCASE GET_COM_INITIATIVE()
			;플레이어에 주도권
			CASE 0
				PRINTFORMW %조사처리(LOCALS:1,"는")% 허리를 올리면, %LOCALS:2%의 안면에 페니스의 첨단을 향해, 얼굴 위에 방뇨%TARGET_STR("했다", "해 왔다")%…
			;타겟으로 주도권
			CASE 1
				PRINTFORML %조사처리(LOCALS:2,"는")% 얼굴 위에 걸치고 있는 %LOCALS:1%의 페니스를 잡아 자신의 (분)편에 향하면, %LOCALS:1%에 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, %LOCALS:2%의 얼굴에 향하여 방뇨했다…
			;제삼자에게 주도권
			CASEELSE
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:1%에게, %LOCALS:2%의 얼굴에 향하여 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% 허리를 올리면, %LOCALS:2%의 안면에 페니스의 첨단을 향해, (들)물은 대로얼굴 위에 방뇨했다…
		ENDSELECT

	;장대가 없는 경우
	ELSE
		SELECTCASE GET_COM_INITIATIVE()
			;플레이어에 주도권
			CASE 0
				PRINTFORMW %조사처리(LOCALS:1,"는")% %LOCALS:2%의 얼굴에 동글동글 비순을 강압하면서 방뇨%TARGET_STR("했다", "해 왔다")%…
			;타겟으로 주도권
			CASE 1
				PRINTFORML %조사처리(LOCALS:2,"는")%, 얼굴 위에 걸치고 있는 %LOCALS:1%에 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, %LOCALS:2%의 얼굴에 비순을 강압한 채로 방뇨했다…
			;제삼자에게 주도권
			CASEELSE
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:1%에게, %LOCALS:2%의 얼굴에 걸친 채로 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, %LOCALS:2%의 얼굴에 비순을 강압한 채로 방뇨했다…
		ENDSELECT
	ENDIF

;그 이외
ELSE
	;플레이어 전원에게 장대가 있다면 LOCAL:6을 1에, 전원에 없으면-1에, 혼합이라면 0으로 한다
	LOCAL:6 = 0
	FOR LOCAL:0, 0, MPLY_NUM
		IF HAS_PENIS(MPLY:(LOCAL:0))
			LOCAL:6 ++
		ELSE
			LOCAL:6 --
		ENDIF
	NEXT
	LOCAL:6 = ABS(LOCAL:6) / MPLY_NUM * SIGN(LOCAL:6)

	IF MPLY_NUM >= 2
		LOCALS:3 = 일제히
	ENDIF

	;플레이어 전원에게 장대가 있는 경우 or 유무 혼합의 경우
	IF LOCAL:6 == 1 || LOCAL:6 == 0
		SELECTCASE GET_COM_INITIATIVE()
			;플레이어에 주도권
			CASE 0
				PRINTFORMW %조사처리(LOCALS:1,"는")% %LOCALS:2%의 전신에 %LOCALS:3%오줌을 마구 퍼부%TARGET_STR("었다", "어 왔다")%…
			;타겟으로 주도권
			CASE 1
				PRINTFORML %조사처리(LOCALS:2,"는")% 자신의 몸에 향하여 방뇨하도록(듯이)%LOCALS:1%에 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, %LOCALS:3% %LOCALS:2%의 몸에 오줌을 마구 퍼부었다…
			;제삼자에게 주도권
			CASEELSE
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:1%에게, %LOCALS:2%에 향하여 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, %LOCALS:3% %LOCALS:2%의 몸에 오줌을 마구 퍼부었다…
		ENDSELECT

	;플레이어 전원에게 장대가 없는 경우
	ELSEIF LOCAL:6 == -1
		SELECTCASE GET_COM_INITIATIVE()
			;플레이어에 주도권
			CASE 0
				PRINTFORMW %조사처리(LOCALS:1,"는")% %조사처리(LOCALS:2,"를")% 앉게 하면, %LOCALS:3%두로부터 오줌을 마구 퍼부%TARGET_STR("었다", "어 왔다")%…
			;타겟으로 주도권
			CASE 1
				PRINTFORML %조사처리(LOCALS:2,"는")% %조사처리(LOCALS:1,"를")% 앞에 서게 하면, 그대로 자신의 몸에 향하여 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% (들)물은 대로, 가랑이를 열어 %LOCALS:3% %LOCALS:2%의 몸에 오줌을 마구 퍼부었다…
			;제삼자에게 주도권
			CASEELSE
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:1%에게, %LOCALS:2%의 몸에 방뇨하도록(듯이) 명령했다
				PRINTFORMW %조사처리(LOCALS:1,"는")% %조사처리(LOCALS:2,"를")% 앉게 하면, %LOCALS:3%두로부터 오줌을 마구 퍼부었다…
		ENDSELECT
	ENDIF
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST131
IF MPLY_NUM == 1
	LOCALS:2 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:2 = %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%
ELSE
	LOCALS:2 = %ANAME(MPLY:0)%들
ENDIF

FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):배설 >= 2
			IF IS_INITIATIVE(LOCAL:1) && ABL:(LOCAL:1):새드 >= 3
				PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 배뇨의 해방감에 잠기면서, 만족한 것 같게 미소를 띄웠다
			ELSE
				PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 보여지면서 방뇨하는 해방감에 황홀로 한 표정을 띄우고 있다
			ENDIF
		ELSEIF ABL:(LOCAL:1):배설 >= 1
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 수치에 얼굴을 붉히면서도, 배뇨의 해방감에 브르르 떨렸다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 방뇨하면서 얼굴을 붉히고 부끄러운 듯이 하고 있다
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):로주 + CUP:(LOCAL:1):액주

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):욕정 >= 500
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 오줌을 걸려지면서 흥분한 모습으로 숨을 거칠게 하고 있다
		ELSEIF LOCAL:2 < 0
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 오줌으로 흠뻑 젖음이 되면서, 곤혹의 표정을 띄우고 있다
		ELSEIF IS_INITIATIVE(LOCAL:1)
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 불쾌감인 나머지 %조사처리(LOCALS:2,"를")% 노려보도록(듯이) 응시했다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 오줌으로 흠뻑 젖음이 되면서, 굴욕인 나머지 %조사처리(LOCALS:2,"를")% 노려봤다
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN131
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_131()
RETURN POWER(2, 성적기호_욕보이고싶다)

@COM_PREFERENCE_TARGET_131()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_노출하고싶다)

