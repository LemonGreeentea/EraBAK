;조르기

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME111
LOCALS:0 = 조르기
RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE111
;공통 부분
CALL COM_ABLE_COMMON(111)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 5명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 5
	RETURN 0
;타겟은 최대로 5명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 5
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;안면승마 하고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;키스중, 패합중이라면 불가
	SIF IS_EQUIP(MPLY:(LOCAL:0), 20, 21, 22)
		RETURN 0
	;대딸, 다리 코키, 페라, 스마타, 파이즈리, 오나홀대딸중이라면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 10, 11, 12, 14, 13, 17)
		RETURN 0
	;Ｖ삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ｖ")
		RETURN 0
	;Ａ삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "Ａ")
		RETURN 0
	;구속되고 있으면 안 돼
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MTAR:(LOCAL:0))
		RETURN 0
	;삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "전")
		RETURN 0
	;키스중이라면 불가
	SIF IS_EQUIP(MTAR:(LOCAL:0), 20)
		RETURN 0
	;구속되고 있으면 안 돼
	SIF IS_BIND(MTAR:(LOCAL:0))
		RETURN 0
NEXT

RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM111
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0


;●전플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 40

	SOURCE:(LOCAL:2):노출 = 50
	SOURCE:(LOCAL:2):일탈 = 40
	SOURCE:(LOCAL:2):반감 = 20
	SOURCE:(LOCAL:2):노출 = 1200

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(MPLY:0):4 = 1

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 200)
NEXT

;●전 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 40

	SOURCE:(LOCAL:2):기학 = 50
	SOURCE:(LOCAL:2):일탈 = 20
	SOURCE:(LOCAL:2):접촉 = 30

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 160, 80)
NEXT

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP111
RETURN 0

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER111(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 100

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"마조 Lv{ABL:(ARG:0):마조}", ABL:(ARG:0):마조 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "미약", 6)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -4)
ENDIF


RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE111

;플레이어가 2명 이상
IF MPLY_NUM >= 2
	LOCALS:0 = %ANAME(MPLY:0)%들
;플레이어가 1명
ELSE
	LOCALS:0 = %ANAME(MPLY:0)%
ENDIF
;타겟이 2명 이상
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%들
;타겟이 1명
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	CASE 0
	CASE 1
		IF MTAR_NUM == 1 && ABL:(MTAR:0):새드 >= 3
			PRINTFORMW %조사처리(LOCALS:1,"는")% 기학적인 미소를 띄우고 %LOCALS:0%에 명령해, 억지로 졸라대게 %PLAYER_STR("했다", "해 왔다")%…
		ELSEIF MTAR_NUM == 1 && ABL:(MTAR:0):주도도Ｕ < 100
			PRINTFORMW %조사처리(LOCALS:1,"는")% 조금 긴장한 모습으로 %LOCALS:0%에 명령해, 졸라대게 했다…
		ELSE
			PRINTFORMW %조사처리(LOCALS:1,"는")% %LOCALS:0%에 명령해, 억지로 졸라대게 했다…
		ENDIF
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:0%에, %LOCALS:1%에 졸라대도록(듯이) 명령했다
ENDSELECT

FOR LOCAL:0, 0, MPLY_NUM
	PRINTFORM %조사처리(ANAME(MPLY:(LOCAL:0)),"는")%,
	SELECTCASE RAND:7
		CASE 0
			PRINTFORM 위로돌리고의 자세로 양다리를 벌려,
		CASE 1
			PRINTFORM 엎드려 있는 자세로 엉덩이를 내밀어,
		CASE 2
			PRINTFORM 스스로\@ IS_FEMALE(MPLY:(LOCAL)) ? 굴곡위# ちんぐり返し\@의 자세를 취해,
		CASE 3
			PRINTFORM 지에 머리를 문지르고 도게자를 하면서,
		CASE 4
			PRINTFORM 쭈구린 자세로 가랑이를 넓혀,
		CASE 5
			PRINTFORM 가슴을 강조하는 자세를 취하면서,
		CASE 6
			PRINTFORM %LOCALS:1%에 껴안으면서,
	ENDSELECT
	SELECTCASE RAND:10
		CASE 0
			PRINTFORM \@ IS_FEMALE(MPLY:(LOCAL)) ? 비순을 손가락으로 나누어 넓혀,  # 발기한 육봉을 가볍게 흔들어,  \@
		CASE 1
			PRINTFORM 엉덩이 구멍을 손가락으로 나누어 넓혀,
		CASE 2
			PRINTFORM \@ IS_FEMALE(MPLY:(LOCAL)) ? 비밀의 틈에 손가락을 가볍게 출납해,  # 육봉을 가볍게 잡아당겨 \@
		CASE 3
			PRINTFORM \@ HAS_PENIS(MPLY:(LOCAL)) ? 육봉 # 클리토리스 \@를 손가락끝으로 만지작거려,
		CASE 4
			PRINTFORM 국좌를 손가락끝으로 희롱하면서,
		CASE 5
			PRINTFORM \@ IS_FEMALE(MPLY:(LOCAL)) ? 유방을 팔로 대도록(듯이) 해,  # 가슴뼈를 강조해,  \@
		CASE 6
			PRINTFORM 유두를 손가락끝으로 가볍게 만지작거려,
		CASE 7
			PRINTFORM 추잡하게 허리를 흔들어,
		CASE 8
			PRINTFORM 육봉을 빠는 것 같은 제스추어를 해,
		CASE 9
			PRINTFORM 육봉을 잡아당기는 것 같은 제스추어를 해,
	ENDSELECT
	SELECTCASE RAND:9
		CASE 0
			PRINTFORM 범해 주세요
		CASE 1
			PRINTFORM 자지를 주세요
		CASE 2
			PRINTFORM 기분 좋게 해 주세요
		CASE 3
			PRINTFORM \@ IS_FEMALE(MPLY:(LOCAL)) ? 질내에 흘러 들어가 주세요 # 사정시켜 주세요 \@
		CASE 4
			PRINTFORM \@ IS_FEMALE(MPLY:(LOCAL)) ? 씨뿌리기 해 주세요 # 페니스를 괴롭혀 주세요 \@
		CASE 5
			PRINTFORM 욕보여 주세요
		CASE 6
			PRINTFORM 나를 노예로 해 주세요
		CASE 7
			PRINTFORM 나는 당신의 성노예입니다
		CASE 8
			PRINTFORM 섹스\@ IS_FEMALE(MPLY:(LOCAL)) ? 해 주세요 # 시켜 주세요 \@
	ENDSELECT
	PRINTFORMW 라고 말했다……
NEXT


@COM_TEXT_LAST111
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)
	SIF LOCAL:1 == MASTER
		CONTINUE
	IF GETBIT(TALENT:(LOCAL:1):음란계, 소질_음란_정액변소)
		PRINTFORML 정액변소에 되어버린 %조사처리(ANAME(LOCAL:1),"는")%, 졸라대는 것을 쾌락을 발견하고 있다…
	ELSEIF IS_SLAVE(LOCAL:1)
		PRINTFORML 굴복 나누었던 %조사처리(ANAME(LOCAL:1),"는")%, 졸라대는 것을 기쁨을 발견하고 있다…
	ELSEIF GETBIT(TALENT:(LOCAL:1):음란계, 소질_음란_마조)
		PRINTFORML 완전히 피학이 몸에 붙었던 %조사처리(ANAME(LOCAL:1),"는")%, 졸라대는 것을 쾌락을 발견하고 있다…
	ELSEIF ABL:(LOCAL:1):주도도Ｕ <= -100 && ABL:(LOCAL:1):도착도 >= 300
		PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 조르기에 대해 굴욕과 동시에 쾌락을 느끼고 있는 것 같다…
	ELSE
		PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 졸라대는 것의 굴욕에 이를 갈고 있다…
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN111
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_111()
RETURN POWER(2, 성적기호_욕보이고싶다)

@COM_PREFERENCE_TARGET_111()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_노출하고싶다)

