;도원향

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME76
LOCALS:0 = 도원향

RESULTS:0 = %조사처리(LOCALS:0,"를")% 피운다
RESULTS:1 = %조사처리(LOCALS:0,"를")% 피워진다
RESULTS:2 = %조사처리(LOCALS:0,"를")% 피우게 한다
RESULTS:3 = %조사처리(LOCALS:0,"를")% 피워진다
RESULTS:4 = %조사처리(LOCALS:0,"를")% 피우게 한다
RESULTS:5 = %LOCALS:0%사용 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE76
;공통 부분
CALL COM_ABLE_COMMON(76)
SIF RESULT == 0
	RETURN 0
	;플레이어는 최대로 1명까지
	SIF MPLY_NUM <= 0 || MPLY_NUM > 1
		RETURN 0
	;타겟은 최대로 1명까지
	SIF MTAR_NUM > 1
		RETURN 0
	;플레이어가 행동불능이라면 불가
	SIF !IS_PLAYABLE(MPLY:0)
		RETURN 0
	;도원향이 필요
	SIF ITEM:도원향 <= 0 && ITEM:A_도원향 <= 0
		RETURN 0
	;이미 타겟이 도원향을 사용되고 있다면 불가
	SIF TFLAG:69
		RETURN 0
	;플레이어가 구속중이라면 불가
	SIF IS_BIND(MPLY:0)
		RETURN 0
	;플레이어가 안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:0)
		RETURN 0
	;플레이어가 쿤닐링구스·페라·파이즈리중이라면 불가
	SIF IS_EQUIP_PLAYER(MPLY:0, 2, 11, 12)
		RETURN 0
	RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM76
#DIM 대상

;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;미약을 1개 감소시킨다(추가 아이템 사용시는 감소시키지 않다)
IF ITEM:A_도원향 <= 0
	ITEM:도원향 = MAX(ITEM:도원향 - 1, 0)
ENDIF


;도원향 사용 플래그를 세운다
TFLAG:69 = 1

;●플레이어측의 처리

SOURCE:(MPLY:0):일탈 = 50


IF MTAR_NUM > 0
	;●타겟측의 처리

	SOURCE:(MTAR:0):일탈 = 100

	SELECTCASE ABL:(MTAR:0):욕망
		CASE 0
			SOURCE:(MTAR:0):반감 = 200
			SOURCE:(MTAR:0):불안 = 400
		CASE 1
			SOURCE:(MTAR:0):반감 = 150
			SOURCE:(MTAR:0):불안 = 300
		CASE 2
			SOURCE:(MTAR:0):반감 = 80
			SOURCE:(MTAR:0):불안 = 150
		CASE 3
			SOURCE:(MTAR:0):반감 = 50
			SOURCE:(MTAR:0):불안 = 100
		CASEELSE
			SOURCE:(MTAR:0):반감 = MAX(30 - (ABL:(MTAR:0):욕망 - 5) * 20, 0)
			SOURCE:(MTAR:0):불안 = MAX(50 - (ABL:(MTAR:0):욕망 - 5) * 20, 0)
	ENDSELECT

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 30, 40)

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MPLY:0, MTAR:0, 0)
ENDIF

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 4

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

;경과시간을 무리하게 1줄인다
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER76(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 65

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE76
IF MTAR_NUM <= 0
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 도원향의 봉을 풀%TARGET_STR("었다", "어 왔다")%…
	RETURN
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 도원향의 봉을 풀%TARGET_STR("었다", "어 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 도원향을 사용하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 향로의 봉을 풀었다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, 도원향을 사용하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 향로의 봉을 풀었다…
ENDSELECT

;-------------------------------------------------
;지문(실행 후)
;-------------------------------------------------
@COM_TEXT_AFTER76
IF MTAR_NUM <= 0
	PRINTFORML 근처에 충만하는 요염한 향기가, 그 자리에 있는 사람의 육체와 사고를 분홍색에 염색해 내 간다…
	RETURN
ENDIF

IF MTAR:0 != MASTER
	IF (TCVAR:(MTAR:0):52) || (TCVAR:(MTAR:0):53 == 1)
		PRINTFORML 의식을 잃고 있는지 아닌지에 관련되지 않고, 충만하는 분홍색의 향기가 %ANAME(MTAR:0)%들의 몸을 뜨겁게 달아오르게 해 간다…
	ELSE
		SIF TCVAR:(MTAR:0):51
			PRINT 지쳐 좋든 싫든 들이마셔 버린다
		IF TALENT:(MTAR:0):신비지식 || TALENT:(MTAR:0):기사
			PRINTFORML 주위를 감도는 달콤한 향기에 %조사처리(ANAME(MTAR:0),"가")% 깨달았을 때에는, 이미 신체의 욱신거림을 제지당하는 사람은 누구하나 없었다…
		ELSE
			PRINTFORML 근처에 충만하는 요염한 향기가, 그 자리에 있는 사람의 육체와 사고를 분홍색에 염색해 내 간다…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN76
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_76()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_76()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

