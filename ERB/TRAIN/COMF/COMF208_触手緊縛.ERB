;촉촉수긴박

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME208
LOCALS:0 = 촉수긴박

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE208
;공통 부분
CALL COM_ABLE_COMMON(208)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;플레이어가 촉수소환중이 아니면 불가
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;이미 구속중이라면 불가
	SIF IS_BIND(MTAR:(LOCAL:0))
		RETURN 0
	;봉사중이라면 불가(꼬리계 제외함)
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 103, 104, 106, 107)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM208
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
EXP:(MPLY:0):신비경험치 += 1
EXP:(MPLY:0):촉수경험치 += 1
EXP:(LOCAL:2):기학경험치 += 1

SOURCE:(MPLY:0):기학 = 400
SOURCE:(MPLY:0):일탈 = 200
SOURCE:(MPLY:0):촉수 = 30

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 180, 50)

;●모든 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):긴박경험 += 1
	EXP:(LOCAL:2):촉수경험치 += 1
	EXP:(LOCAL:2):피학경험치 += 1

	SOURCE:(LOCAL:2):고통 = 120 + MIN(ABL:(MPLY:0):신비, 90) / 2
	SOURCE:(LOCAL:2):노출 = 50 + GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 25
	SOURCE:(LOCAL:2):촉수 = 400 + GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 200
	SOURCE:(LOCAL:2):일탈 = 500 - GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 250

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 0, 180)

	IF GETBIT(TALENT:(MPLY:0):음란계, 소질_음란_새드)
		TIMES SOURCE:(LOCAL:2):고통, 1.50
	ENDIF
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 7

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP208
;계속 커맨드 한편 필터링 불가
RETURN 2

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP208(ARG:0)
;모든 타겟으로 붙어 처리
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:3):긴박경험 += 1
	EXP:(LOCAL:3):촉수경험치 += 1

	SOURCE:(LOCAL:3):고통 += 60
	SOURCE:(LOCAL:3):촉수 += 200 + GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 100
	SOURCE:(LOCAL:3):일탈 += 150 - GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 75

	;도착도 변화
	TCVAR:(LOCAL:3):50 += 4
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE208(ARG:0)
RESULTS = %조사처리(EQUIP_TARGET_ANAME(ARG:0),"를")% 촉수로 긴박중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP208(ARG:0)
PRINTFORML %조사처리(EQUIP_TARGET_ANAME(ARG:0),"는")% 촉수에 전신을 얽어매져 동작할 수 없다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP208(ARG:0)
PRINTFORMW %조사처리(EQUIP_TARGET_ANAME(ARG:0),"를")% 구속하고라고 있던 촉수를 되돌렸다

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER208(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 125

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"촉수 Lv{ABL:(ARG:0):촉수}", ABL:(ARG:0):촉수 * 5)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):얌전함
	CALL COM_ORDER_ELEMENT(ARG:0, "얌전함", -3)
ENDIF
IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ기질
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ기질", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드)
	CALL COM_ORDER_ELEMENT(ARG:0, "새드", 40)
ELSE
	IF ABL:(ARG:0):주도도Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 15)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 10)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 5)
	ELSEIF ABL:(ARG:0):주도도Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", -5)
	ENDIF

	IF ABL:(ARG:0):도착도 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 20)
	ELSEIF ABL:(ARG:0):도착도 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 15)
	ELSEIF ABL:(ARG:0):도착도 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 10)
	ELSEIF ABL:(ARG:0):도착도 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE208
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 전신을 촉수로 얽어매 동작을 봉%TARGET_STR("했다", "해 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 자신의 몸을 촉수로 구속하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (들)물은 대로, %ANAME(MTAR:0)%의 전신을 촉수로 얽어매 동작을 봉했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, 촉수로 %ANAME(MTAR:0)%의 몸을 구속하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 전신을 촉수로 얽어매 동작을 봉했다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN208
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_208()
RETURN POWER(2, 성적기호_촉수로범하고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_208()
RETURN POWER(2, 성적기호_촉수로범해지고싶다) + POWER(2, 성적기호_시달리고싶다) + POWER(2, 성적기호_속박되고싶다)

