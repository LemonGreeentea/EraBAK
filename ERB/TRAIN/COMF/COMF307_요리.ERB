;요리

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME307
RESULTS:0 = 요리한다
RESULTS:1 = 요리한다

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE307
;공통 부분
CALL COM_ABLE_COMMON(307)
SIF RESULT == 0
	RETURN 0
;포로 회화에서는 불가
SIF FLAG:조교모드 == 6
	RETURN 0
;주인공이 플레이어 때 한정
SIF MPLY:0 != MASTER
	RETURN 0
;데이트중은 무리
SIF TFLAG:54
	RETURN 0
;임월 or상처라면 불가
SIF CFLAG:(MTAR:0):행동불능상태 == 행동불능_임월 || CFLAG:(MTAR:0):행동불능상태 == 3
	RETURN 0
;키스중은 불가
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM307
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "요리"), 0)

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE307
;주도권이 주인공측
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %조사처리(ANAME(MTAR:0),"와")% 함께 요리를 만들었다
ELSE
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %조사처리(ANAME(MTAR:0),"와")% 함께 요리를 만들었다
ENDIF

IF ABL:(MPLY:0):요리 < 0 || ABL:(MTAR:0):요리 < 0
	IF TFLAG:18 == 1
		PRINTFORML 사람이 먹을 순 있는 물건이 만들어졌다…
	ELSEIF TFLAG:18 == -1
		IF RAND:2 == 0
			PRINTFORML ……엄청 수상해보이는 보랏빛 요리가 완성했다
		ELSE
			PRINTFORML ……접시안에 뭔가 이상하게 꿈틀거리고 있는 것이 보인다
		ENDIF
	ELSE
		PRINTFORML 고문 대비라고 생각하면 먹을 수 없는 것도 아니다……라고 생각한다
	ENDIF
ELSE
	IF TFLAG:18 == 1
		PRINTFORML 생각한 이상으로 맛있는 음식이 완성되었다…
	ELSEIF TFLAG:18 == -1
		PRINTFORML ……간 조절에 실패했다
	ENDIF
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST307
;일상 커맨드에 공통의 지문
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN307
RETURN 커맨드_일상
