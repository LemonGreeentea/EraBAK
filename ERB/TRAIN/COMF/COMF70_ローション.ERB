;로션

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME70
LOCALS:0 = 로션

RESULTS:0 = %조사처리(LOCALS:0,"를")% 바른다
RESULTS:1 = %조사처리(LOCALS:0,"를")% 적셔진다
RESULTS:2 = %조사처리(LOCALS:0,"를")% 바르게 한다
RESULTS:3 = %조사처리(LOCALS:0,"를")% 칠해진다
RESULTS:4 = %조사처리(LOCALS:0,"를")% 바르게 한다
RESULTS:5 = %LOCALS:0%발라 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE70
;공통 부분
CALL COM_ABLE_COMMON(70)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟이 있는 일
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	;타겟이ＶＡ삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "ＶＡ")
		RETURN 0
	;타겟이 엉덩이코키중이라면 불가
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 15)
		RETURN 0
	;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
	SIF GROUPMATCH(GET_SEX_POSITION(MTAR:(LOCAL:0), MPLY:0), 2, 4)
		RETURN 0
NEXT
;로션이 인원수분 필요
SIF ITEM:로션 < MTAR_NUM && ITEM:A_로션 < MTAR_NUM
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;플레이어가 풋잡·전기안마중이라면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM70
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;로션을 1개 감소시킨다(추가 아이템 사용시는 감소시키지 않다)
IF ITEM:A_로션 <= 0
	ITEM:로션 = MAX(ITEM:로션 - MTAR_NUM, 0)
ENDIF

;●플레이어측의 처리
SOURCE:(MPLY:0):봉사 = 50
SOURCE:(MPLY:0):성행동 = 60

FOR LOCAL, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)
	IF LOCAL:2 == MASTER
		CALL ADD_SOURCE_AIZYOU(MPLY:0, 50)
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 40, 40)

	;●타겟측의 처리
	SOURCE:(LOCAL:2):노출 = 100
	SOURCE:(LOCAL:2):성행동 = 150
	SOURCE:(LOCAL:2):액체추가 = 10000

	IF MPLY:0 == MASTER
		CALL ADD_SOURCE_AIZYOU(LOCAL:2, 50)
	ELSEIF LOCAL:2 != MASTER
		SOURCE:(LOCAL:2):노출 += 50
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 40, 40)

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MPLY:0, LOCAL:2, 0)
NEXT

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER70(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 65

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE70
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%들
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF
IF !HAS_VAGINA(MTAR:0)
	LOCALS:0 = 애널
ELSE
	LOCALS:0 = 사타구니
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 %LOCALS:0%에 로션을 발%TARGET_STR("랐다", "라 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(LOCALS:1,"는")% 자신의 %조사처리(LOCALS:0,"를")% 손가락으로 벌려%ANAME(MPLY:0)%에 과시하면, 로션을 바르도록(듯이) 요구했다…
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %LOCALS:1%의 %LOCALS:0%에 로션을 발랐다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %LOCALS:1%의 %LOCALS:0%에 로션을 바르도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %LOCALS:1%의 %LOCALS:0%에 로션을 발랐다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN70
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_70()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_70()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

