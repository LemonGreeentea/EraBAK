;바이브

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME68
LOCALS:0 = 페니스밴드재갈

RESULTS:0 = %LOCALS:0%로 꾸짖는다
RESULTS:1 = %LOCALS:0%로 봉사한다
RESULTS:2 = %LOCALS:0%로 봉사시킨다
RESULTS:3 = %LOCALS:0%로 꾸짖을 수 있다
RESULTS:4 = %LOCALS:0%로 꾸짖게 한다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE68
;공통 부분
CALL COM_ABLE_COMMON(68)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;페니스밴드가 필요
SIF (!ITEM:페니스밴드 && !ITEM:A_페니스밴드) || (!ITEM:재갈 && !ITEM:A_재갈)
	RETURN 0
FOR LOCAL:0, 0, MTAR_NUM
	;타겟의 입이 막히고 있다면 불가
	SIF IS_M_HOLD(MTAR:(LOCAL:0))
		RETURN 0
NEXT

FOR LOCAL:0, 0, MPLY_NUM
	;플레이어가 안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;플레이어가 구속중이라면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM68
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
DOWNBASE:(MPLY:0):체력 += 80

EXP:(MPLY:0):성기술경험치 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):접촉 = 10
SOURCE:(MPLY:0):성행동 = 150

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 20)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)
	;●타겟으로 붙어 처리
	DOWNBASE:(LOCAL:2):체력 += 80
	EXP:(LOCAL:2):성기술경험치 += 1
	SOURCE:(LOCAL:2):노출 = 50
	SOURCE:(LOCAL:2):일탈 = 120
	SOURCE:(LOCAL:2):접촉 = 10
	SOURCE:(LOCAL:2):성행동 = 300
	IF TALENT:(MPLY:0):기사
		SOURCE:(LOCAL:2):쾌Ｍ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 900)
	ELSE
		SOURCE:(LOCAL:2):쾌Ｍ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 600)
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 20)

	;바이브의 공통 처리
	CALL COM_COMMON68(LOCAL:2)
NEXT
;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 1

;레즈·ＢＬ경험기준치
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP68
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP68(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
;소스를 퇴피
CALL PUTOUT_SOURCE(LOCAL:2)
SOURCE:(LOCAL:2):성행동 += 50
;퇴피한 소스를 가산
CALL SUM_SOURCE(LOCAL:2)

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)
	CALL PUTOUT_SOURCE(LOCAL:3)
	EXP:(LOCAL:2):성기술경험치 += 1
	IF TALENT:(LOCAL:2):기사
		SOURCE:(LOCAL:3):쾌Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
	ELSE
		SOURCE:(LOCAL:3):쾌Ｍ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 300)
	ENDIF
	SOURCE:(LOCAL:3):노출 += 20
	SOURCE:(LOCAL:3):일탈 += 50
	SOURCE:(LOCAL:3):성행동 += 100

	;도착도 변화
	TCVAR:(LOCAL:3):50 += 1

	;공통 처리
	CALL COM_COMMON68(LOCAL:3)

	;퇴피한 소스를 가산
	CALL SUM_SOURCE(LOCAL:3)
NEXT
;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE68(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%
RESULTS += "의 입을 페니스밴드 고문중"

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP68(ARG:0)
PRINTFORM %EQUIP_TARGET_ANAME(ARG:0)%
PRINTFORML 의 입안을 페니스밴드가 꾸짖고 있다

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP68(ARG:0)
PRINTFORM %EQUIP_TARGET_ANAME(ARG:0)%
PRINTFORMW 의 입으로부터 페니스밴드를 뽑아 냈다

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER68(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 70

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE68

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%들
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF CUM_GET_COUNT:(MTAR:0):사정부위_입 > 0
	LOCALS:2 = 먹인 정액을 토해내지 않게
ELSE
	LOCALS:2 = 
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 입에 페니스밴드가 붙은 재갈을 %TARGET_STR("매달았다", "매달게 했다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에 페니스밴드가 붙은 재갈을 잡게 하면, %LOCALS:2%자신의 입에 달게 했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, 페니스밴드가 붙은 재갈로 %조사처리(LOCALS:1,"를")% 꾸짖도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 입에 페니스밴드가 붙은 재갈을 달았다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST68

;--------------------------------------------------------
;장착시와 계속 상태에서의 공통 처리
;--------------------------------------------------------
@COM_COMMON68(ARG:0)
SELECTCASE ABL:(ARG:0):Ｍ감
	CASE 0
		SOURCE:(ARG:0):고통 += 300
	CASE 1
		SOURCE:(ARG:0):고통 += 100
	CASE 2
		SOURCE:(ARG:0):고통 += 30
ENDSELECT

IF PALAM:(ARG:0):윤활 < PALAMLV:1
	TIMES SOURCE:(ARG:0):쾌Ｍ, 0.05
	SOURCE:(ARG:0):고통 = SOURCE:(ARG:0):고통 * 4 + 1000
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:3
	TIMES SOURCE:(ARG:0):쾌Ｍ, 0.20
	SOURCE:(ARG:0):고통 = SOURCE:(ARG:0):고통 * 2 + 300
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:5
	TIMES SOURCE:(ARG:0):쾌Ｍ, 0.60
	TIMES SOURCE:(ARG:0):고통, 1.00
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:7
	TIMES SOURCE:(ARG:0):쾌Ｍ, 0.85
	TIMES SOURCE:(ARG:0):고통, 0.70
ELSE
	TIMES SOURCE:(ARG:0):쾌Ｍ, 1.00
	TIMES SOURCE:(ARG:0):고통, 0.50
ENDIF

IF TALENT:(ARG:0):체격 == 체격_작은체형
	TIMES SOURCE:(ARG:0):고통, 1.50
ENDIF

IF TALENT:(ARG:0):처녀 == 1
	SOURCE:(ARG:0):고통 = SOURCE:(ARG:0):고통 * 2 + 500
ENDIF

IF ARG:0 != MASTER
	IF !(TALENT:(ARG:0):연모 || TALENT:(ARG:0):친구)
		IF TALENT:(ARG:0):정조관념
			SOURCE:(ARG:0):반감 = 1000
		ELSEIF TALENT:(ARG:0):정조관념없음
			SOURCE:(ARG:0):반감 = 300
		ELSE
			SOURCE:(ARG:0):반감 = 2500
		ENDIF
		IF TALENT:(ARG:0):연인
			TIMES SOURCE:(ARG:0):반감, 0.10
		ENDIF
	ENDIF

	;호감도·종속도·지배도의 영향
	LOCAL:0 = MAX(CFLAG:(ARG:0):2, CFLAG:(ARG:0):4, CFLAG:(ARG:0):5)
	IF LOCAL:0 < 100
		TIMES SOURCE:(ARG:0):반감, 1.00
	ELSEIF LOCAL:0 < 300
		TIMES SOURCE:(ARG:0):반감, 0.90
	ELSEIF LOCAL:0 < 500
		TIMES SOURCE:(ARG:0):반감, 0.80
	ELSEIF LOCAL:0 < 800
		TIMES SOURCE:(ARG:0):반감, 0.40
	ELSEIF LOCAL:0 < 1500
		TIMES SOURCE:(ARG:0):반감, 0.15
	ELSE
		SOURCE:(ARG:0):반감 = SOURCE:(ARG:0):반감 / ((LOCAL:0 - 1500) / 50 + 10)
	ENDIF
	
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN68
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_68()
RETURN POWER(2, 성적기호_도구를사용하고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_68()
RETURN POWER(2, 성적기호_도구를사용되고싶다) + POWER(2, 성적기호_시달리고싶다)

