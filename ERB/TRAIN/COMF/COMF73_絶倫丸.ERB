;절륜환

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME73
LOCALS:0 = 절륜환

RESULTS:0 = %조사처리(LOCALS:0,"를")% 먹인다
RESULTS:1 = %조사처리(LOCALS:0,"를")% 음당한다
RESULTS:2 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:3 = %조사처리(LOCALS:0,"를")% 먹게 된다
RESULTS:4 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:5 = %LOCALS:0%사용 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE73
;공통 부분
CALL COM_ABLE_COMMON(73)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;절륜환이 필요
SIF ITEM:절륜환 < MTAR_NUM && ITEM:A_절륜환 <= MTAR_NUM
	RETURN 0

;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;플레이어가 풋잡·전기안마중이라면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	;이미 타겟이 절륜환을 사용되고 있다면 불가
	SIF TCVAR:(MTAR:LOCAL):66
		RETURN 0
	;타겟의 입이 막히고 있다면 불가
	SIF IS_M_HOLD(MTAR:LOCAL)
		RETURN 0
	;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
	SIF GROUPMATCH(GET_SEX_POSITION(MTAR:LOCAL, MPLY:0), 2, 4)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM73
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;절륜환을 감소시킨다(추가 아이템 사용시는 감소시키지 않다)
IF ITEM:A_절륜환 <= 0
	ITEM:절륜환 = MAX(ITEM:절륜환 - MTAR_NUM, 0)
ENDIF

;●플레이어측의 처리

;●타겟측의 처리
FOR LOCAL, 0, MTAR_NUM
	;절륜환사용 플래그를 세운다
	TCVAR:(MTAR:LOCAL):66 = 1

	SELECTCASE ABL:(MTAR:LOCAL):욕망
		CASE 0
			SOURCE:(MTAR:LOCAL):반감 = 150
			SOURCE:(MTAR:LOCAL):불안 = 400
		CASE 1
			SOURCE:(MTAR:LOCAL):반감 = 100
			SOURCE:(MTAR:LOCAL):불안 = 300
		CASE 2
			SOURCE:(MTAR:LOCAL):반감 = 50
			SOURCE:(MTAR:LOCAL):불안 = 150
		CASE 3
			SOURCE:(MTAR:LOCAL):반감 = 20
			SOURCE:(MTAR:LOCAL):불안 = 50
	ENDSELECT

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:LOCAL, 30, 20)

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MPLY:0, MTAR:LOCAL, 0)
NEXT
;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 1

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

;경과시간을 무리하게 1줄인다
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER73(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 65

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE73
IF MTAR_NUM > 1
	LOCALS:0 = 들
ELSE
	LOCALS:0 = 
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)% %LOCALS:0%에 절륜환을 먹%TARGET_STR("였다", "여 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 절륜환을 먹이도록(듯이) 명령했다
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)% %LOCALS:0%에 절륜환을 삼키게 했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)% %LOCALS:0%에 절륜환을 먹이도록(듯이) 명령했다
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)% %LOCALS:0%에 절륜환을 삼키게 했다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN73
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_73()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_73()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

