;방방귀뀐다

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME150
LOCALS:0 = 방귀

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE150
;공통 부분
CALL COM_ABLE_COMMON(150)
SIF RESULT == 0
	RETURN 0
;플레이어는 1명 이상
SIF MPLY_NUM <= 0
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;다대 다는 불가
SIF MPLY_NUM >= 2 && MTAR_NUM >= 2
	RETURN 0

LOCAL:1 = -1
;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;비잔량이 충분하지 않으면 불가
	SIF TCVAR:(MPLY:(LOCAL:0)):55 < 1000
		RETURN 0
	;Ａ가 메워지고 있다면 불가
	SIF IS_A_HOLD(MPLY:(LOCAL:0)) && !IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 102) && !IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 105)
		RETURN 0
	;안면승마중이라면 불가
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM150
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;전플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):배설경험치 += 1

	LOCAL:3 = 300
	LOCAL:4 = 250

	SELECTCASE ABL:(LOCAL:2):배설
		CASE 0
			TIMES LOCAL:3, 0.00
			TIMES LOCAL:4, 1.00
		CASE 1
			TIMES LOCAL:3, 0.10
			TIMES LOCAL:4, 0.60
		CASE 2
			TIMES LOCAL:3, 0.20
			TIMES LOCAL:4, 0.25
		CASE 3
			TIMES LOCAL:3, 0.40
			TIMES LOCAL:4, 0.05
		CASE 4
			TIMES LOCAL:3, 0.70
			TIMES LOCAL:4, 0.00
		CASEELSE
			LOCAL:3 = LOCAL:3 * (100 + (ABL:(LOCAL:2):배설 - 5) * 2) / 100
			LOCAL:4 = 0
	ENDSELECT

	SOURCE:(LOCAL:2):노출 = 1000
	SOURCE:(LOCAL:2):일탈 = 400
	SOURCE:(LOCAL:2):중독충족 = LOCAL:3
	SOURCE:(LOCAL:2):반감 = LOCAL:4

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 150)

	;비잔량을 0으로 한다
	TCVAR:(LOCAL:2):55 = 0
NEXT

;주도권의 소재를 취득
LOCAL:11 = GET_COM_INITIATIVE()

;전 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;타겟으로 주도권
	IF LOCAL:11 == 1 || LOCAL:11 == 2
		SOURCE:(LOCAL:1):기학 = 200
		SOURCE:(LOCAL:1):일탈 = 200
	;타겟으로 주도권이 없다
	ELSE
		SOURCE:(LOCAL:1):일탈 = 400
		SOURCE:(LOCAL:1):공포 = MAX(100 - 30 * ABL:(LOCAL:1):욕망, 0)
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 100, 100)
NEXT

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 3

;레즈·ＢＬ경험기준치
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP150
RETURN 0

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER150(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"노출 Lv{ABL:(ARG:0):노출}", ABL:(ARG:0):노출 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"배설 Lv{ABL:(ARG:0):배설}", ABL:(ARG:0):배설 * 7)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -12)
ENDIF
IF TALENT:(ARG:0):수치심옅음
	CALL COM_ORDER_ELEMENT(ARG:0, "수치심옅음", 8)
ENDIF
IF TALENT:(ARG:0):자제심
	CALL COM_ORDER_ELEMENT(ARG:0, "자제심", -6)
ENDIF
IF TALENT:(ARG:0):주목받기원함
	CALL COM_ORDER_ELEMENT(ARG:0, "주목받기원함", 6)
ENDIF
IF TALENT:(ARG:0):도착적
	CALL COM_ORDER_ELEMENT(ARG:0, "도착적", 8)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE150
IF MPLY_NUM == 1
	LOCALS:1 = %ANAME(MPLY:0)%
ELSEIF MPLY_NUM == 2
	LOCALS:1 = %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%
ELSE
	LOCALS:1 = %ANAME(MPLY:0)%들
ENDIF

IF MTAR_NUM == 1
	LOCALS:2 = %ANAME(MTAR:0)%
ELSEIF MTAR_NUM == 2
	LOCALS:2 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
ELSE
	LOCALS:2 = %ANAME(MTAR:0)%들
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(LOCALS:1,"는")% %LOCALS:2%에 엉덩이를 향하여 방귀를 %TARGET_STR("했다", "해 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(LOCALS:2,"는")% 자신의 눈앞에서 %LOCALS:1%에 방귀를 시켰다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:1%에게, %LOCALS:2%에 향하여 방귀를 하도록(듯이) 명령했다
		PRINTFORMW %조사처리(LOCALS:1,"는")% (듣)묻는 대로 %LOCALS:2%에 엉덩이를 향하여 방귀했다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST150
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):배설 >= 2
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 부끄러움과 해방감에, 흥분한 표정을 띄우고 있다
		ELSEIF ABL:(LOCAL:1):배설 >= 1
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 아직 긴장한 모습이지만, 방귀의 해방감에 무심코 얼굴을 느슨하게했다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 너무나 부끄러워서 얼굴을 새빨갛게 하고 있다
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):로주 + CUP:(LOCAL:1):액주

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):욕정 >= 500
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 숨을 거칠게 해, 흥분한 모습으로 방귀의 냄새를 맡고 있다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 감돌아 오는 악취에 무심코 얼굴을 찡그렸다
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN150
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_150()
RETURN POWER(2, 성적기호_욕보이고싶다)

@COM_PREFERENCE_TARGET_150()
RETURN POWER(2, 성적기호_욕보여지고싶다)

