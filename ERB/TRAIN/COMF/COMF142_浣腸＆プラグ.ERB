;관장＆플러그

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME142
LOCALS:0 = 관장＆플러그

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE142
;공통 부분
CALL COM_ABLE_COMMON(142)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;관장이 필요
SIF ITEM:관장 <= 0 && ITEM:A_관장 <= 0
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
;엉덩이 막히고 있으면 안 돼
SIF IS_A_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM142
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;주도권의 소재를 취득
LOCAL:11 = GET_COM_INITIATIVE()

;●플레이어측의 처리
SOURCE:(MPLY:0):성행동 = 90

;플레이어에 주도권
IF LOCAL:11 == 0 || LOCAL:11 == 2
	SOURCE:(MPLY:0):기학 = 200
	SOURCE:(MPLY:0):일탈 = 300
;플레이어에 주도권이 없다
ELSE
	SOURCE:(MPLY:0):일탈 = 600
	SOURCE:(MPLY:0):공포 = MAX(150 - 40 * ABL:(MPLY:0):욕망, 0)
ENDIF

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 80)

;●타겟측의 처리
LOCAL:3 = 1000
LOCAL:4 = 600

SELECTCASE ABL:(MTAR:0):배설
	CASE 0
		TIMES LOCAL:3, 0.00
		TIMES LOCAL:4, 1.00
	CASE 1
		TIMES LOCAL:3, 0.10
		TIMES LOCAL:4, 0.60
	CASE 2
		TIMES LOCAL:3, 0.20
		TIMES LOCAL:4, 0.25
	CASE 3
		TIMES LOCAL:3, 0.40
		TIMES LOCAL:4, 0.05
	CASE 4
		TIMES LOCAL:3, 0.70
		TIMES LOCAL:4, 0.00
	CASEELSE
		LOCAL:3 = LOCAL:3 * (100 + (ABL:(MTAR:0):배설 - 5) * 2) / 100
		LOCAL:4 = 0
ENDSELECT

SOURCE:(MTAR:0):노출 = 1400
SOURCE:(MTAR:0):일탈 = 600
SOURCE:(MTAR:0):중독충족 = LOCAL:3
SOURCE:(MTAR:0):반감 = LOCAL:4
SOURCE:(MTAR:0):성행동 = 150

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 80, 170)

;비잔량을 제로로 한다
TCVAR:(MTAR:0):55 = 0

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 6

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP142
;계속 커맨드 한편 필터링 불가
RETURN 2

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP142(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

LOCAL:5 = 500
LOCAL:6 = 300

SELECTCASE ABL:(LOCAL:3):배설
	CASE 0
		TIMES LOCAL:5, 0.00
		TIMES LOCAL:6, 1.00
	CASE 1
		TIMES LOCAL:5, 0.10
		TIMES LOCAL:6, 0.60
	CASE 2
		TIMES LOCAL:5, 0.20
		TIMES LOCAL:6, 0.25
	CASE 3
		TIMES LOCAL:5, 0.40
		TIMES LOCAL:6, 0.05
	CASE 4
		TIMES LOCAL:5, 0.70
		TIMES LOCAL:6, 0.00
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(MTAR:0):배설 - 5) * 2) / 100
		LOCAL:6 = 0
ENDSELECT

SOURCE:(LOCAL:3):고통 += 80
SOURCE:(LOCAL:3):일탈 += 200
SOURCE:(LOCAL:3):중독충족 += LOCAL:5
SOURCE:(LOCAL:3):반감 += LOCAL:6

IF GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_마조)
	SOURCE:(LOCAL:3):중독충족 += 300
ENDIF

;도착도 변화
TCVAR:(LOCAL:3):50 += 4

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE142(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%의 애널에 플러그를 삽입중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP142(ARG:0)
PRINTFORML %조사처리(EQUIP_TARGET_ANAME(ARG:0),"는")% 플러그로 항문을 막을 수 있어 괴로운 듯이 하고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP142(ARG:0)
LOCAL:0 = MEQUIP_TARGET:(ARG:0):0

PRINTFORMW %ANAME(LOCAL:0)%의 항문으로부터 플러그를 뽑아 냈다

;이벤트 구상의 표시
CALL KOJO_EVENT(LOCAL:0, 151)

PRINTL 
PRINTFORML %ANAME(LOCAL:0), 8%:배설경험치+2

EXP:(LOCAL:0):배설경험치 += 2
CALL TRAIN_AUTO_ABLUP(LOCAL:0)
WAIT

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER142(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"노출 Lv{ABL:(ARG:0):노출}", ABL:(ARG:0):노출 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"배설 Lv{ABL:(ARG:0):배설}", ABL:(ARG:0):배설 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):도착적
	CALL COM_ORDER_ELEMENT(ARG:0, "도착적", 8)
ENDIF
IF TALENT:(ARG:0):보수적
	CALL COM_ORDER_ELEMENT(ARG:0, "보수적", -8)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE142
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 애널에 관장액을 주입%TARGET_STR("했다", "해 왔다")%
		PRINTFORMW 한층 더%조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 엉덩이 구멍에 플러그로 마개했다…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 %STR_BODY("엉덩이:외관:장", MTAR:0)%를 향하여, 관장하도록(듯이) 요구했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 관장액을 %ANAME(MTAR:0)%의 애널에 주입해, 항문에 플러그로 마개했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)%에 관장해 플러그를 가리도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 관장액을 %ANAME(MTAR:0)%의 애널에 주입해, 항문에 플러그로 마개했다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST142
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):로주

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):배설 >= 2
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 배설의 해방감을 요구해, 아첨하는 것 같은 시선으로 %조사처리(ANAME(MPLY:0),"를")% 응시하고 있다
		ELSEIF LOCAL:2 < 100
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 플러그로 항문을 막을 수 있어 고민의 표정을 띄우고 있다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 괴로운 듯이 배를 누르면서, 원망스러운 듯이 %조사처리(ANAME(MPLY:0),"를")% 노려보고 있다
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN142
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_142()
RETURN POWER(2, 성적기호_욕보이고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_142()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_시달리고싶다)

