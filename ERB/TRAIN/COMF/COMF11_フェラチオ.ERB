;펠라치오

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME11
;삽입중
FOR LOCAL, 0, MTAR_NUM
	IF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ") == 1
		LOCALS:1 = 청소
		BREAK
	ELSEIF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ") == 2
		LOCALS:1 = Ａ청소
		BREAK
	ELSE
		LOCALS:1 = 
	ENDIF
NEXT

IF MTAR_NUM > 3
	LOCALS:0 = %LOCALS:1%고리 페라
ELSEIF MTAR_NUM == 3
	LOCALS:0 = %LOCALS:1%3개 페라
ELSEIF MTAR_NUM == 2
	LOCALS:0 = %LOCALS:1%2 개 페라
ELSEIF MPLY_NUM == 3 && MTAR_NUM == 1
	LOCALS:0 = Ｔ%LOCALS:1%페라
ELSEIF MPLY_NUM == 2 && MTAR_NUM == 1
	LOCALS:0 = Ｗ%LOCALS:1%페라
ELSE
	;플레이어로부터 타겟으로 안면승마중 or 타겟으로부터 플레이어에 쿤닐링구스중
	IF IS_RIDE(MPLY:0, MTAR:0) || SEARCH_EQUIP(2, MTAR:0, MPLY:0) >= 0
		LOCALS:0 = 식스나인
	ELSEIF SEARCH_EQUIP(12, MPLY:0, MTAR:0) >= 0
		LOCALS:0 = 파이즈리 페라
	;타겟으로부터 플레이어에 페라중
	ELSEIF SEARCH_EQUIP(11, MTAR:0, MPLY:0) >= 0
		LOCALS:0 = 상호페라
	ELSE
		LOCALS:0 = %LOCALS:1%페라
	ENDIF
ENDIF

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE11
;공통 부분
CALL COM_ABLE_COMMON(11)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 3명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;타겟이 필요
SIF MTAR_NUM <= 0
	RETURN 0

FOR LOCAL, 0, MPLY_NUM
	;행동불능이라면 불가
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;구속중이라면 실행자는 1만
	SIF IS_BIND(MPLY:(LOCAL:0)) && MPLY_NUM >= 2
		RETURN 0

	;안면승마 되고 있는, 키스중, 재갈 장착중이라면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0)) || IS_EQUIP(MPLY:(LOCAL:0), 20) || IS_EQUIP_TARGET(MPLY:(LOCAL:0), 86)
		RETURN 0
	;가슴애무·다리 빨고 안은 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 1, 104)
		RETURN 0
	FOR LOCAL:1, 0, MTAR_NUM
		;식스나인 또는 상호페라가 될 때는, 플레이어도 타겟도 혼자서는 없으면 안 돼
		SIF (MPLY_NUM > 1 || MTAR_NUM > 1) && (IS_RIDE(MPLY:LOCAL, MTAR:(LOCAL:1)) || SEARCH_EQUIP(2, MTAR:(LOCAL:1), MPLY:LOCAL) >= 0 || SEARCH_EQUIP(11, MPLY:LOCAL, MTAR:(LOCAL:1)) >= 0)
			RETURN 0
		;플레이어로부터 타겟으로 삽입중이라면 불가
		SIF GROUPMATCH(IS_INSERT_MUTUAL(MPLY:(LOCAL), MTAR:(LOCAL:1)), 1, 2)
			RETURN 0
		;성기술이 2 이하의 경우, 삽입되고 있는 상대에게로의 페라는 1-1만 가능
		SIF IS_INSERT_SINGLE(MTAR:(LOCAL:1), "전") && MPLY_NUM > 1 && ABL:(MPLY:(LOCAL:0)):성기술 <= 2
			RETURN 0
		;타겟으로부터 플레이어에 다리 빨고 안이라면 불가
		SIF SEARCH_EQUIP(104, MTAR:(LOCAL:1), MPLY:(LOCAL:0)) >= 0
			RETURN 0
		;플레이어와 타겟이 서로 조개맞대기중이라면 불가
		SIF SEARCH_EQUIP_IC(21, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어와 타겟이 서로 쌍두바이브로 결합중이라면 불가
		SIF SEARCH_EQUIP_IC(22, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어로부터 타겟／타겟으로부터 플레이어에 엉덩이코키중이라면 불가
		SIF SEARCH_EQUIP_IC(15, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어로부터 타겟／타겟으로부터 플레이어에 전기안마중이라면 불가
		SIF SEARCH_EQUIP_IC(103, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;자지가 메워지고 있어 페라도 파이즈리도 아니었으면 안 돼
		SIF IS_P_HOLD(MTAR:LOCAL) && SEARCH_EQUIP(12, MPLY:0, MTAR:0) == -1 && SEARCH_EQUIP(11, MTAR:0, MPLY:0) == -1
			RETURN 0
	NEXT
NEXT

FOR LOCAL, 0, MTAR_NUM
	;되는 측 전원에게 페니스가 필요
	SIF !HAS_PENIS(MTAR:LOCAL)
		RETURN 0
NEXT

IF MPLY_NUM == 1 && MTAR_NUM == 1
	;플레이어와 타겟이 함께 구속중이라면 불가
	SIF IS_BIND(MPLY:0) && IS_BIND(MTAR:0)
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;쾌Ｍ소스의 배율을 취득하는 함수 ARG:0=PLAYER의 캐릭터 번호
;-------------------------------------------------
@COM11_RATE_M(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):봉사
	CASE 0
		TIMES LOCAL:5, 0.00
		;TIMES SOURCE:(LOCAL:2):불결, 4.00
	CASE 1
		TIMES LOCAL:5, 0.10
		;TIMES SOURCE:(LOCAL:2):불결, 2.50
	CASE 2
		TIMES LOCAL:5, 0.30
		;TIMES SOURCE:(LOCAL:2):불결, 1.50
	CASE 3
		TIMES LOCAL:5, 0.80
		;TIMES SOURCE:(LOCAL:2):불결, 1.00
	CASE 4
		TIMES LOCAL:5, 1.00
		;TIMES SOURCE:(LOCAL:2):불결, 0.50
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(ARG:0):봉사 - 5) * 3) / 100
		;TIMES SOURCE:(LOCAL:2):불결, 0.10
ENDSELECT

SELECTCASE ABL:(ARG:0):성기술
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(ARG:0):성기술 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM11
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

TFLAG:16 = 0
;삽입중
FOR LOCAL, 0, MTAR_NUM
	IF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		TFLAG:16 = IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		BREAK
	ENDIF
NEXT


LOCAL:1 = -1
;파이즈리 상황을 대피
IF MPLY_NUM == 1 && MTAR_NUM == 1
	LOCAL:1 = SEARCH_EQUIP(12, MPLY:0, MTAR:0)
ENDIF

FOR LOCAL:0, 0, MTAR_NUM
	;타겟의 삽입 상태를 해제
	CALL CLEAR_INSERT_FLAG(MTAR:(LOCAL:0), "Ｐ")

	;타겟의 이라마를 해제
	CALL RELEASE_EQUIP_EX(90, MTAR:(LOCAL:0), -1)
NEXT

FOR LOCAL:0, 0, MPLY_NUM
	;플레이어의 쿤닐링구스를 해제
	CALL RELEASE_EQUIP(SEARCH_EQUIP(2, MPLY:(LOCAL:0), -1), 1, 0)
	;플레이어의 유두흡 있고를 해제
	CALL RELEASE_EQUIP(SEARCH_EQUIP(9, MPLY:(LOCAL:0), -1), 1, 0)
	;플레이어의 애널핥기를 해제
	CALL RELEASE_EQUIP(SEARCH_EQUIP(8, MPLY:0, -1), 1, 0)
NEXT


;대Ｐ봉사계 커맨드의 계속 플래그를 해제
CALL RELEASE_SERVE_P

;파이즈리 상황을 복원
IF LOCAL:1 >= 0
	CALL SET_EQUIP(12, 1)
ENDIF
		
	

;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 120

	EXP:(LOCAL:2):성기술경험치 += MAX(MTAR_NUM / 2 + 1, 1)
	EXP:(LOCAL:2):구음경험 += 1

	SOURCE:(LOCAL:2):봉사 = SERVE_HOUSHI(LOCAL:2, 400)
	SOURCE:(LOCAL:2):접촉 = 50
	SOURCE:(LOCAL:2):쾌Ｍ = 600 * COM11_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):성행동 = 300

	;주도권에 응한 우월·수동의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 120)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;전 타겟으로 주는 쾌감계 소스를 계산
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):혀놀림
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):쾌Ｐ += LOCAL:4

		;사정 개소와 대상을 세트
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 사정부위_입 # 사정부위_얼굴)
	NEXT
	IF MTAR_NUM == 1
		;페니스에의 키스
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%의 페니스", GET_SITUATION_BY_TRAIN_MODE())
	ELSE
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%들의 페니스", (GET_SITUATION_BY_TRAIN_MODE() == "화간" ? "난교" # "윤간"))
	ENDIF

	;봉사정신ＬＶ2이상, 성기술ＬＶ2이상이라면 더러움을 없는 취한다
	;IF ABL:(LOCAL:1):봉사 >= 2 && ABL:(LOCAL:1):성기술 >= 2
	;	STAIN:PLAYER:2 = 2
	;ENDIF

	;노예의 손가락⇔조교자의Ｐ의 더러움이 이동
	;IF HAS_PENIS(PLAYER)
	;	STAIN:(LOCAL:1):1 |= STAIN:PLAYER:2
	;	STAIN:PLAYER:2 |= STAIN:(LOCAL:1):1
	;노예의 손가락⇔조교자의Ｖ의 더러움이 이동
	;ELSE
	;	STAIN:(LOCAL:1):3 |= STAIN:PLAYER:2
	;	STAIN:PLAYER:2 |= STAIN:(LOCAL:1):3
	;ENDIF
NEXT

;●전 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;●소스의 계산
	DOWNBASE:(LOCAL:1):체력 += 60

	SOURCE:(LOCAL:1):접촉 = 50
	SOURCE:(LOCAL:1):성행동 = 180

	;주도권에 응한 우월·수동의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 150, 70)
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP11
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP11(ARG:0)
;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 20

	EXP:(LOCAL:2):성기술경험치 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):구음경험 += 1

	SOURCE:(LOCAL:2):봉사 += SERVE_HOUSHI(LOCAL:2, 150)
	SOURCE:(LOCAL:2):접촉 += 25
	SOURCE:(LOCAL:2):쾌Ｍ += 300 * COM11_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):성행동 += 100

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;전 타겟으로 주는 쾌감계 소스를 계산
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):혀놀림
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):쾌Ｐ += LOCAL:4

		;사정 개소와 대상을 세트
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 사정부위_입 # 사정부위_얼굴)
	NEXT
NEXT

;●전 타겟으로 붙어 판정
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):체력 += 10

	SOURCE:(LOCAL:1):접촉 += 25
	SOURCE:(LOCAL:1):성행동 += 60
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE11(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%에 페라중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP11(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%의 페니스를 입으로 물어 핥아대고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP11(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER11(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 85

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"정애Lv{ABL:(ARG:0):정애}", ABL:(ARG:0):정애 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "미약", 6)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):악취둔감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취둔감", 1)
ENDIF
IF TALENT:(ARG:0):악취민감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취민감", -3)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF
IF TALENT:(ARG:0):남성혐오
	CALL COM_ORDER_ELEMENT(ARG:0, "남성혐오", -12)
ENDIF
IF TALENT:(ARG:0):키스미경험
	IF TALENT:(ARG:0):선을넘지않음
		CALL COM_ORDER_ELEMENT(ARG:0, "키스미경험", -15)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "키스미경험", -10)
	ENDIF
ENDIF
;더러움의 판정
;IF ! TALENT:(ARG:0):불결무시
;	LOCAL:0 = 0
;	;애액의 더러움
;	SIF STAIN:PLAYER:2 & 1
;		LOCAL:0 += 1
;	;정액의 더러움
;	SIF STAIN:PLAYER:2 & 4
;		LOCAL:0 += 3
;	;애널의 더러움
;	SIF STAIN:PLAYER:2 & 8
;		LOCAL:0 += 7
;	SIF STAIN:PLAYER:2 & 16
;		LOCAL:0 += 15
;
;	SIF TALENT:(ARG:0):악취둔감
;		LOCAL:0/= 3
;	SIF TALENT:(ARG:0):악취민감
;		LOCAL:0 *= 2
;
;	;서로 더러워져
;	IF LOCAL:0 > 0
;		CALL COM_ORDER_ELEMENT(ARG:0, "서로 더러워져", -LOCAL:0)
;	ENDIF
;ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE11
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(11, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)

IF MTAR_NUM == 1
	LOCALS:6 = 
ELSE
	LOCALS:6 = 들
ENDIF

IF TFLAG:16 == 1
	LOCALS:7 = 질로부터 뽑아 내진지 얼마 안된
ELSEIF TFLAG:16 == 2
	LOCALS:7 = 애널로부터 뽑아 내진지 얼마 안된
ELSE
	LOCALS:7 = 
ENDIF

IF HAS_PENIS(MTAR:0)
	LOCALS:3 = 장대
	LOCALS:4 = 페니스
	LOCALS:5 = 페니스를 입으로 기분 좋게 한다
ELSE
	LOCALS:3 = 딜도
	LOCALS:4 = 딜도
	LOCALS:5 = 딜도에 페라한다
ENDIF

;3인
IF MPLY_NUM == 3
	LOCAL:0 = RAND:3
	LOCAL:1 = (LOCAL:0 + 1) % 3
	LOCAL:2 = (LOCAL:0 + 2) % 3
	IF IS_PIERCED(MTAR:0, 피어스_페니스)
		LOCALS:1 = 피어스가 장착된
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			IF HAS_PENIS(MTAR:0)
				PRINTFORML %조사처리(ANAME(MPLY:(LOCAL:1)),"와")% %ANAME(MPLY:(LOCAL:2))%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1%장대에 혀를 기게 해 자극%TARGET_STR("했다", "해 왔다")%
				PRINTFORMW 한층 더 %조사처리(ANAME(MPLY:(LOCAL:0)),"가")% 정면에서 귀두를 핥아대 자극하고 있다…
			ELSE
				PRINTFORML %조사처리(ANAME(MPLY:(LOCAL:1)),"와")% %ANAME(MPLY:(LOCAL:2))%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 딜도에 추잡하게 혀를 걸%TARGET_STR("었다", "어 왔다")%
				PRINTFORMW 한층 더 %조사처리(ANAME(MPLY:(LOCAL:0)),"가")% 정면에서 딜도의 앞을 졸졸 혀끝에서 핥아대고 있다…
			ENDIF
		;타겟으로 주도권
		CASE 1
			PRINTFORML %조사처리(ANAME(MPLY:(LOCAL:1)),"와")% %ANAME(MPLY:(LOCAL:2))%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %LOCALS:3%에 혀를 걸어 봉사했다
			PRINTFORMW 한층 더 %조사처리(ANAME(MPLY:(LOCAL:0)),"가")% 정면에서 %LOCALS:3%의 끝 쪽의 부분에 졸졸 혀를 기게 하고 있다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%의 세 명에게, %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:5%같게 명령했다
			IF HAS_PENIS(MTAR:0)
				PRINTFORML %조사처리(ANAME(MPLY:(LOCAL:1)),"와")% %ANAME(MPLY:(LOCAL:2))%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1%장대에 혀를 기게 했다
				PRINTFORMW 한층 더 %조사처리(ANAME(MPLY:(LOCAL:0)),"가")% 정면에서 귀두를 핥아대 자극하고 있다…
			ELSE
				PRINTFORML %조사처리(ANAME(MPLY:(LOCAL:1)),"와")% %ANAME(MPLY:(LOCAL:2))%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 딜도에 혀를 기게 했다
				PRINTFORMW 한층 더 %조사처리(ANAME(MPLY:(LOCAL:0)),"가")% 정면에서 딜도의 앞을 졸졸 혀끝에서 핥아대고 있다…
			ENDIF
	ENDSELECT

;2인
ELSEIF MPLY_NUM == 2
	IF IS_PIERCED(MTAR:0, 피어스_페니스)
		LOCALS:1 = 피어스가 장착된
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			IF HAS_PENIS(MTAR:0)
				PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %LOCALS:4%에 혀를 기게 해 자극%TARGET_STR("했다", "해 왔다")%…
			ELSE
				PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %조사처리(LOCALS:4,"를")%, 과시하도록(듯이) 추잡하게 핥%TARGET_STR("았다", "아 왔다")%…
			ENDIF
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 협력해 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% 빨았다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에, %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:5%같게 명령했다
			IF HAS_PENIS(MTAR:0)
				PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:4%에 혀를 기게 해 자극했다…
			ELSE
				PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %조사처리(LOCALS:4,"를")%, 과시하도록(듯이) 추잡하게 핥아댔다…
			ENDIF
	ENDSELECT

;1인(식스나인)
ELSEIF LOCAL:5 >= 0 || LOCAL:6 >= 0 || LOCAL:7 >= 0 || LOCAL:8 >= 0
	IF LOCAL:6 >= 0
		LOCALS:0 = 애널
	ELSEIF LOCAL:5 >= 0 && !HAS_VAGINA(MPLY:0)
		LOCALS:0 = 구슬
	ELSEIF LOCAL:7 >= 0
		LOCALS:0 = %BAR_NAME(MPLY:0)%
	ELSE
		LOCALS:0 = 비순
	ENDIF
	IF IS_PIERCED(MTAR:0, 피어스_페니스)
		LOCALS:1 = 피어스가 장착된
	ELSE
		LOCALS:1 = 
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에 자신의 %조사처리(LOCALS:0,"를")% 빨게 하면서, %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:3,"를")% 핥%TARGET_STR("았다", "아 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%의 %조사처리(LOCALS:0,"를")% 빨면서, %LOCALS:7% %ANAME(MPLY:0)%에 자신의 %LOCALS:1% %조사처리(LOCALS:3,"를")% 물게 했다…
		;제삼자에게 주도권
		CASEELSE
			IF LOCAL:5 >= 0  && !HAS_VAGINA(MPLY:0)
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에 대해, %ANAME(MTAR:0)%의 %LOCALS:1% %LOCALS:5%같게 명령했다
			ELSEIF LOCAL:7 >= 0
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MTAR:0)%에, 서로의 남성기를 서로 빨도록(듯이) 명령했다
			ELSE
				PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MTAR:0)%에, 식스나인의 몸의 자세로 얽히도록(듯이) 명령했다
			ENDIF
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에 자신의 %조사처리(LOCALS:0,"를")% 빨게 하면서, %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:3,"를")% 핥아댔다…
	ENDSELECT
	;안면승마·페라·쿤닐링구스의 지문을 컷
	IF LOCAL:5 >= 0
		MEQUIP_CUT:(LOCAL:5) = 1
	ENDIF
	IF LOCAL:6 >= 0
		MEQUIP_CUT:(LOCAL:6) = 1
	ENDIF
	IF LOCAL:7 >= 0
		MEQUIP_CUT:(LOCAL:7) = 1
	ENDIF
	IF LOCAL:8 >= 0
		MEQUIP_CUT:(LOCAL:8) = 1
	ENDIF

;플레이어가 1명＆구속중
ELSEIF IS_BIND(MPLY:0)
	IF TALENT:(MPLY:0):체격 == 체격_아이 || TALENT:(MPLY:0):체격 == 체격_작은체형
		LOCALS:0 = 작은 입
	ELSE
		LOCALS:0 = 입
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 자신의 %LOCALS:0%에 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% 넣게 해 줄줄 소리를 내 빨았다…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)% %조사처리(LOCALS:6,"는")% 동작이 잡히지 않는 %ANAME(MPLY:0)%의 %LOCALS:0%에 억지로 %LOCALS:7% %LOCALS:1% %조사처리(LOCALS:4,"를")% 밀어넣어 빨게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% 입으로 기분 좋게 하도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 내밀어진 %LOCALS:7% %ANAME(MTAR:0)% %LOCALS:6%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %조사처리(LOCALS:0,"로")% 물어, 줄줄 소리를 내 빨았다…
	ENDSELECT

	;밧줄로 묶는 것 지문을 컷 한다
	LOCAL:0 = SEARCH_EQUIP(85, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
	LOCAL:0 = SEARCH_EQUIP(208, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF


;1인
ELSE
	IF TALENT:(MPLY:0):체격 == 체격_아이 || TALENT:(MPLY:0):체격 == 체격_작은체형
		LOCALS:0 = 작은 입으로 필사적으로
	ELSE
		LOCALS:0 = 입으로
	ENDIF
	SELECTCASE MTAR_NUM
		CASE 1
			IF IS_PIERCED(MTAR:0, 피어스_페니스)
				LOCALS:1 = 피어스가 장착된
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;플레이어에 주도권
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ENDIF
				;타겟으로 주도권
				CASE 1
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 봉사하고 있다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 봉사하고 있다…
					ENDIF
				;제삼자에게 주도권
				CASEELSE
					PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)%의 %조사처리(LOCALS:4,"를")% 입으로 기분 좋게 하도록(듯이) 명령했다
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 빨았다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 핥아댔다…
					ENDIF
			ENDSELECT
		;한사람이 두 명의 장대를
		CASE 2
			IF IS_PIERCED(MTAR:0, 피어스_페니스) && IS_PIERCED(MTAR:1, 피어스_페니스)
				LOCALS:1 = 피어스가 장착된
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;플레이어에 주도권
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ENDIF
				;타겟으로 주도권
				CASE 1
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 봉사하고 있다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 봉사하고 있다…
					ENDIF
				;제삼자에게 주도권
				CASEELSE
					PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%의 %조사처리(LOCALS:4,"를")% 입으로 기분 좋게 하도록(듯이) 명령했다
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 빨았다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 핥아댔다…
					ENDIF
			ENDSELECT
		;한사람이 세 명의 장대를
		CASE 3
			IF IS_PIERCED(MTAR:0, 피어스_페니스) && IS_PIERCED(MTAR:1, 피어스_페니스) && IS_PIERCED(MTAR:2, 피어스_페니스)
				LOCALS:1 = 피어스가 장착된
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;플레이어에 주도권
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %조사처리(ANAME(MTAR:0),"와")% %조사처리(ANAME(MTAR:1),"와")% %ANAME(MTAR:2)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ENDIF
				;타겟으로 주도권
				CASE 1
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %조사처리(ANAME(MTAR:0),"와")% %조사처리(ANAME(MTAR:1),"와")% %ANAME(MTAR:2)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 봉사하고 있다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 봉사하고 있다…
					ENDIF
				;제삼자에게 주도권
				CASEELSE
					PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %조사처리(ANAME(MTAR:0),"와")% %조사처리(ANAME(MTAR:1),"와")% %ANAME(MTAR:2)%의 %조사처리(LOCALS:4,"를")% 입으로 기분 좋게 하도록(듯이) 명령했다
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 빨았다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 핥아댔다…
					ENDIF
			ENDSELECT
		CASEELSE
			IF IS_PIERCED(MTAR:0, 피어스_페니스) && IS_PIERCED(MTAR:1, 피어스_페니스) && IS_PIERCED(MTAR:2, 피어스_페니스)
				LOCALS:1 = 피어스가 장착된
			ELSE
				LOCALS:1 = 
			ENDIF
			SELECTCASE GET_COM_INITIATIVE()
				;플레이어에 주도권
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%들의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
					ENDIF
				;타겟으로 주도권
				CASE 1
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%들의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 봉사하고 있다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 봉사하고 있다…
					ENDIF
				;제삼자에게 주도권
				CASEELSE
					PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)%들의 %조사처리(LOCALS:4,"를")% 입으로 기분 좋게 하도록(듯이) 명령했다
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %LOCALS:7% %ANAME(MTAR:0)%들의 %LOCALS:1% %조사처리(LOCALS:4,"를")% %LOCALS:0%물어,
					IF ABL:(MPLY:0):성기술 >= 3
						PRINTFORMW 교묘한 혀놀림으로 빨았다…
					ELSE
						PRINTFORMW 서투른 혀놀림으로 핥아댔다…
					ENDIF
			ENDSELECT
	ENDSELECT
ENDIF


IF (PALAM:(MPLY:0):욕정 >= PALAMLV:10 || (PALAM:(MPLY:0):욕정 >= PALAMLV:7 && TCVAR:(MPLY:0):60)) && ((IS_FEMALE(MPLY:0) && ABL:(MPLY:0):욕망 >= 랭크역치:랭크_그외:랭크_D) || ABL:(MPLY:0):정애 >= 랭크역치:랭크_그외:랭크_C)
	SELECTCASE RAND:2
		CASE 0
			SELECTCASE RAND:2
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %SPLIT_RAND("흥분으로 콧김을 난폭하게 하면서/정욕에 자극을 받는 대로", 1)%、
				CASE 1
					PRINTFORM %SPLIT_RAND("흥분해 버린 모습의/발정해 버린 모습의", 1)% %조사처리(ANAME(MPLY:0),"는")%,
			ENDSELECT
			PRINTFORML %SPLIT_RAND("와 소리를 내/천한 소리를 내/군침을 늘어뜨려/격렬하게 머리를 전후 시키면서/열중해/필사적으로", 1)% %ANAME(MTAR:0)%의 페니스%SPLIT_RAND("에 빨아 붙어 있다/에 달라붙고 있다", 1)%…
		CASE 1
			PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %SPLIT_RAND("흥분으로 숨을 거칠게 하면서/녹은 것 같은 얼굴을 하면서/열중한 것 같은 얼굴을 하면서", 1)%、
			PRINTFORML %SPLIT_RAND("맛보도록(듯이)/형태를 확인하도록(듯이)", 1)% %ANAME(MTAR:0)%의 페니스%SPLIT_RAND("를 빨고 있다/를 가득 넣고 있다/에 혀를 기게 하고 있다/를 핥아대고 있다", 1)%…
	ENDSELECT
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN11
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_11()
RETURN POWER(2, 성적기호_봉사하고싶다)

@COM_PREFERENCE_TARGET_11()
RETURN POWER(2, 성적기호_봉사되고싶다)

