;호신술 훈련

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME302
RESULTS:0 = 호신술을 배운다
RESULTS:1 = 호신술을 배운다

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE302
;공통 부분
CALL COM_ABLE_COMMON(302)
SIF RESULT == 0
	RETURN 0
;포로 회화에서는 불가
SIF FLAG:조교모드 == 6
	RETURN 0
;주인공이 플레이어 때 한정
SIF MPLY:0 != MASTER
	RETURN 0
;데이트중에서 장소가 강변, 꽃밭, 삼림 이외라면 무리
SIF TFLAG:54 && !GROUPMATCH(GET_PLACENAME(TFLAG:54), "강변", "꽃밭", "삼림")
	RETURN 0
;임월 or상처라면 불가
SIF CFLAG:(MTAR:0):행동불능상태 == 행동불능_임월 || CFLAG:(MTAR:0):행동불능상태 == 3
	RETURN 0
;키스중은 불가
SIF IS_EQUIP(MPLY:0, 342)
	RETURN 0
RETURN 1


;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM302
CALL COM_SLG_TRAIN_MAIN_PROCESS(GETNUM(ABL, "방위"), 0)
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE302
;주도권이 주인공측
IF IS_INITIATIVE(MASTER)
	PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %조사처리(ANAME(MTAR:0),"와")% 호신술을 배웠다
ELSE
	;상대보다 무투가 10이상 뒤떨어지고 있다
	IF ABL:(MPLY:0):방위 + 10 <= ABL:(MTAR:0):방위
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에게 호신술을 배웠다
	ELSE
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에게 호신술을 배웠다
	ENDIF
ENDIF

IF TFLAG:18 == 1
	PRINTFORML 유익한 시간이었다……
ELSEIF TFLAG:18 == -1
	PRINTFORML ……하지만 그다지 잘 되지 않았다
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST302
;일상 커맨드에 공통의 지문
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN302
RETURN 커맨드_일상
