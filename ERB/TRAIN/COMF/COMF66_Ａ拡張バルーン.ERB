;ＡＡ확장벌룬

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME66
LOCALS:0 = Ａ확장벌룬

RESULTS:0 = %LOCALS:0%로 꾸짖는다
RESULTS:1 = %LOCALS:0%로 봉사한다
RESULTS:2 = %LOCALS:0%로 봉사시킨다
RESULTS:3 = %LOCALS:0%로 꾸짖을 수 있다
RESULTS:4 = %LOCALS:0%로 꾸짖게 한다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE66
;공통 부분
CALL COM_ABLE_COMMON(66)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;확장 벌룬이 필요
SIF !ITEM:확장벌룬 && !ITEM:A_확장벌룬
	RETURN 0
;타겟의Ａ가 막히고 있으면 불가
SIF IS_A_HOLD(MTAR:0)
	RETURN 0
;플레이어가 안면승마 되고 있다면 불가
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;타겟이 안면승마중이라면 불가
SIF IS_RIDE(MTAR:0)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;플레이어로부터 타겟으로 삽입중의 경우, 체위가 후배위·배면기승위 이외라면 불가
SIF !GROUPMATCH(GET_SEX_POSITION(MPLY:0, MTAR:0), 0, 2, 6)
	RETURN 0
;타겟으로부터 플레이어에 삽입중의 경우, 체위가 배면기승위 이외라면 불가
SIF !GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 0, 6)
	RETURN 0
;플레이어로부터 타겟／타겟으로부터 플레이어에 전기안마중이라면 불가
SIF SEARCH_EQUIP_IC(103, MPLY:0, MTAR:0) >= 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM66
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;타겟의Ａ확장경험에 의한 여러 가지의 배율
LOCAL:10 = MIN(100 + EXP:(MTAR:0):Ａ확장경험 * 10 + MIN(ABL:(MTAR:0):Ａ감, 6) * 40, 600)

;●플레이어에 대해 처리
EXP:(MPLY:0):성기술경험치 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):접촉 = 10
SOURCE:(MPLY:0):일탈 = 150
SOURCE:(MPLY:0):성행동 = 90

IF IS_INITIATIVE(MPLY:0)
	SOURCE:(MPLY:0):기학 = 150
ENDIF

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 50)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

;●타겟으로 붙어 처리
EXP:(MTAR:0):Ａ확장경험 += 1

SOURCE:(MTAR:0):노출 = 90
SOURCE:(MTAR:0):일탈 = 400
SOURCE:(MTAR:0):접촉 = 10
SOURCE:(MTAR:0):성행동 = 150

IF TALENT:(MPLY:0):기사
	SOURCE:(MTAR:0):쾌Ａ += SENSE_HOUSHI(MPLY:0, MTAR:0, 400) * (LOCAL:10 + 50) / 150
	SOURCE:(MTAR:0):고통 = 1500 * 100 / LOCAL:10
ELSE
	SOURCE:(MTAR:0):쾌Ａ += SENSE_HOUSHI(MPLY:0, MTAR:0, 300) * (LOCAL:10 + 50) / 150
	SOURCE:(MTAR:0):고통 = 2000 * 100 / LOCAL:10
ENDIF

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 50, 110)

;Ａ확장벌룬의 공통 처리
CALL COM_COMMON66(MTAR:0)

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 3

;레즈·ＢＬ경험기준치
TFLAG:51 = 3

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP66
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP66(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;소스를 퇴피
CALL PUTOUT_SOURCE(LOCAL:2)
CALL PUTOUT_SOURCE(LOCAL:3)

SOURCE:(LOCAL:2):성행동 += 30

;타겟의Ａ확장경험에 의한 여러 가지의 배율
LOCAL:10 = MIN(100 + EXP:(LOCAL:3):Ａ확장경험 * 10 + MIN(ABL:(LOCAL:3):Ａ감, 6) * 40, 600)

EXP:(LOCAL:3):Ａ확장경험 += 1

SOURCE:(LOCAL:3):노출 += 40
SOURCE:(LOCAL:3):일탈 += 140
SOURCE:(LOCAL:3):성행동 += 50

IF TALENT:(LOCAL:2):기사
	SOURCE:(LOCAL:3):쾌Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 200) * (LOCAL:10 + 50) / 150
	SOURCE:(LOCAL:3):고통 = 750 * 100 / LOCAL:10
ELSE
	SOURCE:(LOCAL:3):쾌Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 150) * (LOCAL:10 + 50) / 150
	SOURCE:(LOCAL:3):고통 = 1000 * 100 / LOCAL:10
ENDIF

;도착도 변화
TCVAR:(LOCAL:3):50 += 1

;Ａ확장벌룬의 공통 처리
CALL COM_COMMON66(LOCAL:3)

;퇴피한 소스를 가산
CALL SUM_SOURCE(LOCAL:2)
CALL SUM_SOURCE(LOCAL:3)

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE66(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%의 애널에 확장 벌룬을 삽입중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP66(ARG:0)
PRINTFORML %EQUIP_TARGET_ANAME(ARG:0)%의 애널은 확장 벌룬으로 펴넓혀지고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP66(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%의 애널로부터 확장 벌룬을 꺼냈다

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER66(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE66
IF ABL:(MTAR:0):Ａ감 >= 3
	LOCALS:0 = 충분히 개발 된 애널
ELSEIF ABL:(MTAR:0):Ａ감 >= 2
	LOCALS:0 = 개발 도상의 애널
ELSE
	LOCALS:0 = 힘들게 닫은 애널
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 %LOCALS:0%에 확장 벌룬을 삽입해, 공기를 보내 내부를 펴넓%TARGET_STR("혔다", "혀 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 확장 벌룬을 갖게하면, 그것을 자신의 %조사처리(LOCALS:0,"로")% 삽입시켜 내부를 확장시켰다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)%의 엉덩이 구멍을 벌룬으로 확장하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 %LOCALS:0%에 확장 벌룬을 삽입해, 공기를 보내 내부를 펴넓혔다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST66

;--------------------------------------------------------
;장착시와 계속 상태에서의 공통 처리
;--------------------------------------------------------
@COM_COMMON66(ARG:0)
IF PALAM:(ARG:0):윤활 < PALAMLV:1
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.05
	TIMES SOURCE:(ARG:0):고통, 3.00
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:3
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.20
	TIMES SOURCE:(ARG:0):고통, 2.20
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:5
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.60
	TIMES SOURCE:(ARG:0):고통, 1.50
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:7
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.85
	TIMES SOURCE:(ARG:0):고통, 1.20
ELSE
	TIMES SOURCE:(ARG:0):쾌Ａ, 1.00
	TIMES SOURCE:(ARG:0):고통, 1.00
ENDIF

IF TALENT:(ARG:0):체격 == 체격_작은체형
	TIMES SOURCE:(ARG:0):고통, 1.30
ENDIF

CALL TIGHTNESS_DECREASE_A(ARG:0, RAND(5, 8))
CALL VIRGIN_COMMON_A(ARG:0, "애널 확장 벌룬", GET_SITUATION_BY_TRAIN_MODE())

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN66
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_66()
RETURN POWER(2, 성적기호_도구를사용하고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_66()
RETURN POWER(2, 성적기호_도구를사용되고싶다) + POWER(2, 성적기호_시달리고싶다)

