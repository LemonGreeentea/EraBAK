;번갈아Ａ삽입한다

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME58
LOCALS:0 = 번갈아Ａ삽입

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE58
;공통 부분
CALL COM_ABLE_COMMON(58)
SIF RESULT == 0
	RETURN 0
;플레이어는 2명 이상
SIF MPLY_NUM < 2
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM < 1
	RETURN 0

;모든 플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	;플레이어와 타겟중 양쪽 모두에 행동불능 또는 구속중의 캐릭터가 있다면 불가
	IF !IS_PLAYABLE(MPLY:(LOCAL:0)) || IS_BIND(MPLY:(LOCAL:0))
		FOR LOCAL:1, 0, MTAR_NUM
			IF !IS_PLAYABLE(MTAR:(LOCAL:1)) || IS_BIND(MTAR:(LOCAL:1))
				RETURN 0
			ENDIF
		NEXT
	ENDIF

	;플레이어에 장대가 없고, 페니스밴드장착 그 중에서 없으면 불가
	SIF !HAS_PENIS(MPLY:(LOCAL:0)) && !IS_EQUIP_PLAYER((MPLY:(LOCAL:0)), 50)
		RETURN 0
	;플레이어가 안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;플레이어가 안면승마중이라면 불가
	SIF IS_RIDE(MPLY:(LOCAL:0))
		RETURN 0
	;플레이어가 각종 봉사를 받고 있는·각종 봉사를 실시하고 있다면 불가
	SIF IS_EQUIP(MPLY:(LOCAL:0), 1, 2, 3, 4, 20, 21, 22) || IS_EQUIP(MPLY:(LOCAL:0), 10, 11, 12, 13, 14, 15, 16, 17) || IS_EQUIP(MPLY:(LOCAL:0), 87, 88, 90, 103, 104, 106, 107)
		RETURN 0
	;플레이어의 자지가 메워지고 있다면 안 돼
	SIF IS_P_HOLD(MPLY:0)
		RETURN 0
	;플레이어가 바닥에 엎드리며 있다면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 110)
		RETURN 0
NEXT

;모든 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	;Ａ가 메워지고 있다면 불가
	SIF IS_A_HOLD(MTAR:(LOCAL:0))
		RETURN 0
	;플레이어와 타겟의 삽입 관계를 취득
	LOCAL:1 = IS_INSERT_MUTUAL(MPLY:0, MTAR:(LOCAL:0))
	;타겟으로부터 플레이어에 삽입중이라면 불가
	SIF LOCAL:1 == 3 || LOCAL:1 == 4
		RETURN 0
	;안면승마중이라면 불가
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;쿤닐링구스되고 있다면 불가
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 2)
		RETURN 0
	;조개맞대기중·쌍두바이브 사용중이라면 불가
	SIF IS_EQUIP(MTAR:(LOCAL:0), 21, 22)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM58
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;삽입 관계를 일시 리셋트

FOR LOCAL:0, 0, MPLY_NUM
	CALL CLEAR_INSERT_FLAG(MPLY:(LOCAL:0), "Ｐ")

	;플레이어가 행동불능 또는 구속중이라면 기승위 삽입으로 한다
	LOCAL:6 = !IS_PLAYABLE(MPLY:(LOCAL:0)) || IS_BIND(MPLY:(LOCAL:0))

	;●플레이어측의 처리
	EXP:(MPLY:(LOCAL:0)):성기술경험치 += 2
	EXP:(MPLY:(LOCAL:0)):성교경험치 += 3

	LOCAL:5 = 0
	FOR LOCAL:1, 0, MTAR_NUM
		IF LOCAL:6
			SOURCE:(MPLY:(LOCAL:0)):쾌Ｐ = SENSE_SEX_TARGET_P(MTAR:(LOCAL:1), MPLY:(LOCAL:0), 1500)
		ELSE
			SOURCE:(MPLY:(LOCAL:0)):쾌Ｐ = SENSE_SEX_PLAYER_P(MTAR:(LOCAL:1), MPLY:(LOCAL:0), 1500)
		ENDIF

		CALL DOUTEI_COMMON(MPLY:(LOCAL:0), @"%ANAME(MTAR:(LOCAL:1))%들의 애널", "난교")

		;성교계의 공통 처리
		CALL COM_COMMON_ASEX_P(MPLY:(LOCAL:0), MTAR:(LOCAL:1))

		LOCAL:5 += SOURCE:(MPLY:(LOCAL:0)):쾌Ｐ
		SOURCE:(MPLY:(LOCAL:0)):쾌Ｐ = 0
	NEXT
	SOURCE:(MPLY:(LOCAL:0)):쾌Ｐ = LOCAL:5 / MPLY_NUM
	SOURCE:(MPLY:(LOCAL:0)):노출 = 100
	SOURCE:(MPLY:(LOCAL:0)):성행동 = 450

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:(LOCAL:0), 200, 100)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(MPLY:(LOCAL:0)):4 = 1

	;플레이어의 사정처를 강제 리셋트
	CALL RESET_STACK_SPERM(MPLY:(LOCAL:0))

	FOR LOCAL:1, 0, MTAR_NUM
		CALL STACK_SPERM(MPLY:(LOCAL:0), MTAR:(LOCAL:1), 2, 1)
	NEXT
NEXT

;●타겟측의 처리
FOR LOCAL:1, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:1)
	LOCAL:3 = MPLY:(RAND:MPLY_NUM)

	EXP:(LOCAL:2):성기술경험치 += 2
	EXP:(LOCAL:2):성교경험치 += 3
	EXP:(LOCAL:2):윤간경험 += MPLY_NUM / 2
	IF LOCAL:6
		SOURCE:(LOCAL:2):쾌Ａ = SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):쾌Ａ = SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):노출 = 400
	SOURCE:(LOCAL:2):성행동 = 450

	CALL VIRGIN_COMMON_A(LOCAL:2, @"%ANAME(MPLY:0)%들", (GET_SITUATION_BY_TRAIN_MODE() == "화간" ? "난교" # "윤간"))

	;성교계의 공통 처리
	CALL COM_COMMON_ASEX_A(LOCAL:2, LOCAL:3)
	CALL TIGHTNESS_DECREASE_A(LOCAL:2, MPLY_NUM / 2)

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;타겟의Ｖ⇔플레이어의Ｐ의 더러움이 이동
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT


;체위 플래그세트
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP58(ARG:0)
;전플레이어에게 처리
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)
	DOWNBASE:(LOCAL:1):체력 += 50

	;플레이어가 행동불능 또는 구속중이라면 기승위 삽입으로 한다
	LOCAL:6 = !IS_PLAYABLE(LOCAL:1) || IS_BIND(LOCAL:1)

	;●플레이어측의 처리
	EXP:(LOCAL:1):성기술경험치 += 2
	EXP:(LOCAL:1):성교경험치 += 2

	LOCAL:5 = 0
	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:2)
		IF LOCAL:6
			SOURCE:(LOCAL:1):쾌Ｐ = SENSE_SEX_TARGET_P(LOCAL:3, LOCAL:1, 750)
		ELSE
			SOURCE:(LOCAL:1):쾌Ｐ = SENSE_SEX_PLAYER_P(LOCAL:3, LOCAL:1, 750)
		ENDIF

		;성교계의 공통 처리
		CALL COM_COMMON_ASEX_P(LOCAL:1, LOCAL:3)

		LOCAL:5 += SOURCE:(LOCAL:1):쾌Ｐ
		SOURCE:(LOCAL:1):쾌Ｐ = 0
	NEXT
	SOURCE:(LOCAL:1):쾌Ｐ = LOCAL:5 / MEQUIP_PLAYER_NUM:(ARG:0)
	SOURCE:(LOCAL:1):노출 += 100
	SOURCE:(LOCAL:1):성행동 += 450

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 200, 100)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:1):4 = 1


	;플레이어의 사정처를 강제 리셋트
	CALL RESET_STACK_SPERM(LOCAL:1)

	FOR LOCAL:2, 0, MEQUIP_TARGET_NUM:(ARG:0)
		CALL STACK_SPERM(LOCAL:1, MEQUIP_TARGET:(ARG:0):(LOCAL:2), 2, 1)
	NEXT
NEXT
;체위 플래그세트
;TCVAR:(MPLY:0):31 = 1
;TCVAR:(MTAR:0):33 = 1

;●타겟측의 처리
FOR LOCAL:1, 0,  MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 =  MEQUIP_TARGET:(ARG:0):(LOCAL:1)
	LOCAL:3 = MEQUIP_PLAYER:(ARG:0):(RAND:(MEQUIP_PLAYER_NUM:(ARG:0)))

	EXP:(LOCAL:2):성기술경험치 += 2
	EXP:(LOCAL:2):성교경험치 += 2
	EXP:(LOCAL:2):윤간경험 += MEQUIP_PLAYER_NUM:(ARG:0) / 2

	IF LOCAL:6
		SOURCE:(LOCAL:2):쾌Ａ += SENSE_SEX_PLAYER(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ELSE
		SOURCE:(LOCAL:2):쾌Ａ += SENSE_SEX_TARGET(LOCAL:2, LOCAL:3, MAX(1200 - MTAR_NUM * 75, 600))
	ENDIF
	SOURCE:(LOCAL:2):노출 += 400
	SOURCE:(LOCAL:2):성행동 += 450

	;성교계의 공통 처리
	CALL COM_COMMON_ASEX_A(LOCAL:2, LOCAL:3)
	CALL TIGHTNESS_DECREASE_A(LOCAL:2, MEQUIP_PLAYER_NUM / 2)

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 100)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;타겟의Ｖ⇔플레이어의Ｐ의 더러움이 이동
	;STAIN:(LOCAL:2):3 |= STAIN:(MPLY:0):2
	;STAIN:(MPLY:0):2 |= STAIN:(LOCAL:2):3
NEXT

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP58
RETURN 1

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE58(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%에 대신하는 대신하는 애널 삽입중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP58(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"는")% %EQUIP_TARGET_ANAME(ARG:0)%의 애널을 번갈아 범하고 있다……

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP58(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"는")% %조사처리(EQUIP_TARGET_ANAME(ARG:0),"로")%부터 멀어졌다

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER58(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;질성교 공통
CALL COM_ORDER_ASEX_P(ARG:0)
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE58
IF MTAR_NUM >= 3
	LOCALS:0 = %ANAME(MTAR:0)%들
	LOCALS:2 = 옆일렬에 늘어놓고
	LOCALS:3 = 차례로
ELSEIF MTAR_NUM == 2
	LOCALS:0 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
	LOCALS:2 = 좌우에 늘어놓고
	LOCALS:3 = 교대로
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
	LOCALS:2 = 이쪽에 향해라
	LOCALS:3 = 난폭하게
ENDIF

IF MPLY_NUM >= 3
	LOCALS:4 = %ANAME(MPLY:0)%들
ELSE
	LOCALS:4 = %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(LOCALS:4,"는")% %조사처리(LOCALS:0,"를")% %조사처리(LOCALS:2,"와")% %LOCALS:3%스스로의 장대를 찔러 넣어 갔다…
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(LOCALS:0,"는")% 일제히 %조사처리(LOCALS:4,"를")% 쓰러트리고 %LOCALS:3%상에 걸쳐 각각의 엉덩이 구멍으로 장대를 찔러 넣%PLAYER_STR("었다", "어 왔다")%…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:4%에, %조사처리(LOCALS:0,"를")% %LOCALS:3%범하도록(듯이) 명령했다
		PRINTFORMW %조사처리(LOCALS:4,"는")% %LOCALS:2% %LOCALS:0%의 엉덩이 구멍에, %LOCALS:3%장대를 삽입해 갔다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN58
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_58()
RETURN POWER(2, 성적기호_성교하고싶다)

@COM_PREFERENCE_TARGET_58()
RETURN POWER(2, 성적기호_성교되고싶다) + POWER(2, 성적기호_윤간되고싶다)

