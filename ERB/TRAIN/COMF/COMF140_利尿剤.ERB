;이뇨제

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME140
LOCALS:0 = 이뇨제

RESULTS:0 = %조사처리(LOCALS:0,"를")% 먹인다
RESULTS:1 = %조사처리(LOCALS:0,"를")% 음당한다
RESULTS:2 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:3 = %조사처리(LOCALS:0,"를")% 먹게 된다
RESULTS:4 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:5 = %LOCALS:0%사용 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE140
;공통 부분
CALL COM_ABLE_COMMON(140)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;이뇨제가 필요
SIF ITEM:이뇨제 <= 0 && ITEM:A_이뇨제 <= 0
	RETURN 0
;이미 이뇨제를 사용 종료라면 불가
SIF TCVAR:(MTAR:0):62
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM140
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;이뇨제를 1개 감소시킨다(추가 아이템 사용시는 감소시키지 않다)
IF ITEM:A_이뇨제 <= 0
	ITEM:이뇨제 = MAX(ITEM:이뇨제 - 1, 0)
ENDIF

;●플레이어측의 처리
SOURCE:(MPLY:0):일탈 = 50

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 30, 20)

;●타겟측의 처리
LOCAL:4 = 80

SELECTCASE ABL:(LOCAL:2):배설
	CASE 0
		TIMES LOCAL:4, 1.00
	CASE 1
		TIMES LOCAL:4, 0.60
	CASE 2
		TIMES LOCAL:4, 0.25
	CASE 3
		TIMES LOCAL:4, 0.05
	CASEELSE
		LOCAL:4 = 0
ENDSELECT

SOURCE:(MTAR:0):일탈 = 100
SOURCE:(MTAR:0):반감 = LOCAL:4

;이뇨제 사양 플래그를 ON
TCVAR:(MTAR:0):62 = 1

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 30, 40)

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER140(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 80

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"노출 Lv{ABL:(ARG:0):노출}", ABL:(ARG:0):노출 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"배설 Lv{ABL:(ARG:0):배설}", ABL:(ARG:0):배설 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):도착적
	CALL COM_ORDER_ELEMENT(ARG:0, "도착적", 8)
ENDIF
IF TALENT:(ARG:0):보수적
	CALL COM_ORDER_ELEMENT(ARG:0, "보수적", -8)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE140
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에 이뇨제를 먹%TARGET_STR("였다", "여 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 이뇨제를 먹이도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)%의 입에 이뇨제를 쏟았다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)%에 이뇨제를 먹이도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)%의 입에 이뇨제를 따라, 삼키게 했다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN140
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_140()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_140()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

