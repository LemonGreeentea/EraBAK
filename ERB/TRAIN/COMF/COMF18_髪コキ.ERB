;오나홀대딸

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME18
IF MPLY_NUM == 2
	LOCALS:0 = Ｗ머리카락코키
ELSE
	LOCALS:0 = 머리카락코키
ENDIF

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE18
;공통 부분
CALL COM_ABLE_COMMON(18)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 2명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 2
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0

;전플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;행동불능이라면 불가
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;구속중이라면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;삽입중은 플레이어가 한사람의 경우만 허가
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "전") && MPLY_NUM >= 2
		RETURN 0
	;미들 이상이 필요
	SIF TALENT:(MPLY:(LOCAL:0)):머리길이 < 4
		RETURN 0
	;페라, 파이즈리, 스마타, 풋잡, 엉덩이코키, 꼬리 나호, 꼬리중은 무리
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 13, 14, 15, 16, 17)
		RETURN 0
NEXT
;타겟으로 장대가 필요
SIF !HAS_PENIS(MTAR:0)
	RETURN 0
;타겟의 장대가 삽입중이라면 불가
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ")
	RETURN 0
;타겟의 자지가 메워지고 있으면 안 돼
SIF IS_P_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM18
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;대Ｐ봉사계 커맨드의 계속 플래그를 해제
CALL RELEASE_SERVE_P

;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	EXP:(LOCAL:2):성기술경험치 += 1

	SOURCE:(LOCAL:2):봉사 = SERVE_HOUSHI(LOCAL:2, 250)
	SOURCE:(LOCAL:2):접촉 = 40
	SOURCE:(LOCAL:2):성행동 = 210

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 180, 80)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;타겟으로 주는 쾌감계 소스를 계산
	SOURCE:(MTAR:0):쾌Ｐ += SENSE_HOUSHI_P(LOCAL:2, MTAR:0, 500) * LOCAL:10 / 100

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MTAR:0, LOCAL:2, RAND:3 ? 사정부위_머리카락 # 사정부위_얼굴)
NEXT

;●타겟으로 붙어 판정
SOURCE:(MTAR:0):접촉 = 20
SOURCE:(MTAR:0):성행동 = 180

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 60)

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP18
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP18(ARG:0)
;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	EXP:(LOCAL:2):성기술경험치 += 1

	SOURCE:(LOCAL:2):봉사 += SERVE_HOUSHI(LOCAL:2, 60)
	SOURCE:(LOCAL:2):접촉 += 20
	SOURCE:(LOCAL:2):성행동 += 70

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;타겟으로 주는 쾌감계 소스를 계산
	SOURCE:(LOCAL:3):쾌Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 사정부위_머리카락 # 사정부위_얼굴)
NEXT

;●타겟으로 붙어 판정
SOURCE:(LOCAL:3):접촉 += 10
SOURCE:(LOCAL:3):성행동 += 60

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE18(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%에 머리카락코키중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP18(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%의 페니스에 머리카락을 걸어 흔들어 올리고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP18(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER18(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 75

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"정애Lv{ABL:(ARG:0):정애}", ABL:(ARG:0):정애 * 1)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):악취둔감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취둔감", 1)
ENDIF
IF TALENT:(ARG:0):악취민감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취민감", -3)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF
IF TALENT:(ARG:0):남성혐오
	CALL COM_ORDER_ELEMENT(ARG:0, "남성혐오", -7)
ENDIF

;더러움의 판정
;IF ! TALENT:(ARG:0):불결무시
;	LOCAL:0 = 0
;	;애액의 더러움
;	SIF STAIN:PLAYER:2 & 1
;		LOCAL:0 += 1
;	;정액의 더러움
;	SIF STAIN:PLAYER:2 & 4
;		LOCAL:0 += 3
;	;애널의 더러움
;	SIF STAIN:PLAYER:2 & 8
;		LOCAL:0 += 7
;	SIF STAIN:PLAYER:2 & 18
;		LOCAL:0 += 15
;
;	SIF TALENT:(ARG:0):악취둔감
;		LOCAL:0/= 3
;	SIF TALENT:(ARG:0):악취민감
;		LOCAL:0 *= 2
;
;	;더러움의 영향이 작다
;	LOCAL:0/= 3
;
;	;서로 더러워져
;	IF LOCAL:0 > 0
;		CALL COM_ORDER_ELEMENT(ARG:0, "서로 더러워져", -LOCAL:0)
;	ENDIF
;ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE18
;플레이어가 2명
IF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 각각 머리카락을 %ANAME(MTAR:0)%의 페니스에 관련되어, 추잡하게 흔들%TARGET_STR("었다", "어 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MTAR:0),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에, 머리카락을 페니스에 얽히게 할 수 있는 훑게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에, %ANAME(MTAR:0)%의 페니스에 머리카락을 걸어 훑도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 각각 머리카락을 %ANAME(MTAR:0)%의 페니스에 관련되어, 추잡하게 흔들었다…
	ENDSELECT

;플레이어가 1명
ELSE
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락을 %ANAME(MTAR:0)%의 페니스에 관련되어, 추잡하게 흔들%TARGET_STR("었다", "어 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에, 머리카락을 페니스에 얽히게 할 수 있는 훑게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)%의 페니스에 머리카락을 걸어 훑도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 머리카락을 %ANAME(MTAR:0)%의 페니스에 관련되어, 추잡하게 흔들었다…
	ENDSELECT
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN18
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_18()
RETURN POWER(2, 성적기호_봉사하고싶다)

@COM_PREFERENCE_TARGET_18()
RETURN POWER(2, 성적기호_봉사되고싶다)

