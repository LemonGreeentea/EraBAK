;스킨쉽

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME320
;온천이라면 알몸의 스킨쉽
IF TFLAG:54 == 7
	RESULTS:0 = 스킨쉽을 한다(알몸)
	RESULTS:1 = 스킨쉽을 해준다(알몸)
	RESULTS:2 = 스킨쉽을 요청한다(알몸)
	RESULTS:3 = 스킨쉽을 받는다(알몸)
ELSE
	RESULTS:0 = 스킨쉽을 한다
	RESULTS:1 = 스킨쉽을 해준다
	RESULTS:2 = 스킨쉽을 요청한다
	RESULTS:3 = 스킨쉽을 받는다
ENDIF

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE320
;공통 부분
CALL COM_ABLE_COMMON(320)
SIF RESULT == 0
	RETURN 0
;주인공 이외가 실행하는 경우, 호감도가 500이상 필요
SIF MPLY:0 != MASTER && CFLAG:(MPLY:0):2 < 500
	RETURN 0
;상처라면 불가
SIF CFLAG:(MTAR:0):행동불능상태 == 3
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM320
;실행 판정
CALL COM_ORDER_COMMON
IF RESULT == 0
	RETURN 0
ENDIF

IF MPLY:0 == MASTER
	;커맨드의 성공 여부를 TFLAG:18에 세트
	CALL JUDGE_COM_RESULT(MTAR:0, 5, 10)
	LOCAL:1 = MTAR:0
ELSE
	;커맨드의 성공 여부를 TFLAG:18에 세트
	CALL JUDGE_COM_RESULT(MPLY:0, 5, 10)
	LOCAL:1 = MPLY:0
ENDIF

;주도권에 응한 우월·수동의 소스 추가
CALL ADD_SOURCE_INITIATIVE_N(MPLY:0, 100, 100)
CALL ADD_SOURCE_INITIATIVE_N(MTAR:0, 100, 100)

;온천이라면 알몸에서의 스킨쉽
IF TFLAG:54 == 7
	TFLAG:17 = 2

	SOURCE:(MPLY:0):접촉 = 450
	SOURCE:(MTAR:0):접촉 = 450
	SOURCE:(MPLY:0):노출 = 50
	SOURCE:(MTAR:0):노출 = 50

	;친밀하게 응한 환락의 소스 추가
	SOURCE:(MPLY:0):환락 = -30
	SOURCE:(MTAR:0):환락 = -30
	CALL ADD_SOURCE_KANRAKU(MPLY:0, 150)
	CALL ADD_SOURCE_KANRAKU(MTAR:0, 150)
	SOURCE:(MPLY:0):환락 = MAX(SOURCE:(MPLY:0):환락, 0)
	SOURCE:(MTAR:0):환락 = MAX(SOURCE:(MTAR:0):환락, 0)

	;친밀하게 응한 애정의 소스 추가
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 130)
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 130)

	;주도권에 응한 소스의 추가
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 30, 30)
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 30, 30)

	;레즈·ＢＬ경험기준치
	TFLAG:51 = 2

ELSE
	;연인 or낙인으로 농후한 스킨쉽
	IF TALENT:(LOCAL:1):연인 || TALENT:(LOCAL:1):낙인
		TFLAG:17 = 1

		;레즈·ＢＬ경험기준치
		TFLAG:51 = 1
	ELSE
		TFLAG:17 = 0
	ENDIF

	SOURCE:(MPLY:0):접촉 = 150
	SOURCE:(MTAR:0):접촉 = 150

	;친밀하게 응한 환락의 소스 추가
	SOURCE:(MPLY:0):환락 = -30
	SOURCE:(MTAR:0):환락 = -30
	CALL ADD_SOURCE_KANRAKU(MPLY:0, 140)
	CALL ADD_SOURCE_KANRAKU(MTAR:0, 140)
	SOURCE:(MPLY:0):환락 = MAX(SOURCE:(MPLY:0):환락, 0)
	SOURCE:(MTAR:0):환락 = MAX(SOURCE:(MTAR:0):환락, 0)

	;친밀하게 응한 애정의 소스 추가
	CALL ADD_SOURCE_AIZYOU(MPLY:0, 90)
	CALL ADD_SOURCE_AIZYOU(MTAR:0, 90)

	;주도권에 응한 소스의 추가
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 20, 20)
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 20, 20)
ENDIF

;실패
IF TFLAG:18 == -1
	TIMES SOURCE:(MPLY:0):환락, 0.10
	TIMES SOURCE:(MPLY:0):우월, 0.50
	TIMES SOURCE:(MPLY:0):애정, 0.50
	TIMES SOURCE:(MTAR:0):환락, 0.10
	TIMES SOURCE:(MTAR:0):우월, 0.50
	TIMES SOURCE:(MTAR:0):애정, 0.50
	SOURCE:(MPLY:0):불만 += 500
	SOURCE:(MTAR:0):불만 += 500
	TFLAG:37 -= 5
;성공
ELSEIF TFLAG:18 == 0

;대성공
ELSE
	TIMES SOURCE:(MPLY:0):환락, 2.00
	TIMES SOURCE:(MPLY:0):우월, 2.00
	TIMES SOURCE:(MPLY:0):애정, 2.00
	TIMES SOURCE:(MTAR:0):환락, 2.00
	TIMES SOURCE:(MTAR:0):우월, 2.00
	TIMES SOURCE:(MTAR:0):애정, 2.00
	TFLAG:37 += 5
ENDIF

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = -1

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER320(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 35

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 3)

;호감도
IF CFLAG:(ARG:0):2 < 400
	IF CFLAG:(ARG:0):2 < 50
		LOCAL:0 = -40
	ELSEIF CFLAG:(ARG:0):2 < 150
		LOCAL:0 = -20
	ELSEIF CFLAG:(ARG:0):2 < 300
		LOCAL:0 = -10
	ELSE
		LOCAL:0 = -5
	ENDIF
	CALL COM_ORDER_ELEMENT(ARG:0, @"호감도 부족", LOCAL:0)
ELSE
	LOCAL:0 = CFLAG:(ARG:0):2 / 75
	CALL COM_ORDER_ELEMENT(ARG:0, @"호감도", LOCAL:0)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -3)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 5)
ENDIF
IF TFLAG:54
	CALL COM_ORDER_ELEMENT(ARG:0, "데이트중", 4)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE320
;키스중
IF IS_EQUIP(MPLY:0, 342)
	LOCALS:0 = 키스를 하면서
	;지문 컷
	LOCAL:0 = SEARCH_EQUIP_IC(342, MPLY:0, MTAR:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
ELSE
	LOCALS:0 = 
ENDIF

;온천에서 데이트중의 경우
IF TFLAG:54 == 7
	;플레이어가 주인공측
	IF MPLY:0 == MASTER
		;주도권이 주인공측
		IF IS_INITIATIVE(MASTER)
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 알몸인 상태로 %LOCALS:0% %조사처리(ANAME(MTAR:0),"와")% 피부를 포갰다
		;주도권이 상대측
		ELSE
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%에게 이끌려 알몸 상태로 %LOCALS:0% 피부을 맞닿는다
		ENDIF

		IF TFLAG:18 == 1
			PRINTFORML %조사처리(ANAME(MTAR:0),"는")% 뭔가 기쁜것 같아 보인다…
		ELSEIF TFLAG:18 == -1
			PRINTFORML 하지만 %조사처리(ANAME(MTAR:0),"는")% 기분이 좋지 않아 보인다…
		ENDIF
	;플레이어가 상대측
	ELSE
		;주도권이 주인공측
		IF IS_INITIATIVE(MASTER)
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 요구에 응해, 알몸 상태로 %LOCALS:0%피부를 서로 맞닿는다
		;주도권이 상대측
		ELSE
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:0% %ANAME(MTAR:0)%의 팔을 잡아, 알몸 상태로 피부를 맞대었다
		ENDIF

		IF TFLAG:18 == 1
			PRINTFORML %조사처리(ANAME(MPLY:0),"는")% 매우 즐거운 듯 하다…
		ELSEIF TFLAG:18 == -1
			PRINTFORML %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 반응이 마음에 안든건지 뾰로통해있다…
		ENDIF
	ENDIF

ELSE
	IF TFLAG:17 == 1
		LOCALS:1 = 조금 농후한 
	ENDIF

	;플레이어가 주인공측
	IF MPLY:0 == MASTER
		;주도권이 주인공측
		IF IS_INITIATIVE(MASTER)
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:0% %조사처리(ANAME(MTAR:0),"와")%의 %LOCALS:1%스킨쉽을 했다
		;주도권이 상대측
		ELSE
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:0% %ANAME(MTAR:0)%의 요청에 %LOCALS:1%스킨쉽을 했다
		ENDIF

		IF TFLAG:18 == 1
			PRINTFORML %조사처리(ANAME(MTAR:0),"는")% 왠지 기쁜것 같다…
		ELSEIF TFLAG:18 == -1
			PRINTFORML 아무래도, 너무 허물없게 한 것 같다…
		ENDIF
	;플레이어가 상대측
	ELSE
		;주도권이 주인공측
		IF IS_INITIATIVE(MASTER)
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 요구에 응해, %LOCALS:0% %LOCALS:1%스킨쉽을 꾀했다
		;주도권이 상대측
		ELSE
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:0% %ANAME(MTAR:0)%의 몸을 대어, %LOCALS:1%스킨쉽을 꾀해 왔다
		ENDIF

		IF TFLAG:18 == 1
			PRINTFORML %조사처리(ANAME(MPLY:0),"는")% 매우 즐거운 듯 하다…
		ELSEIF TFLAG:18 == -1
			PRINTFORML %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 반응이 마음에 안든건지 뾰로통해있다…
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------
;지문(파라미터·각인 변동 후)
;-------------------------------------------------
@COM_TEXT_LAST320
;일상 커맨드에 공통의 지문
CALL COM_TEXT_LAST_LIFE

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN320
RETURN 커맨드_일상
