;관장

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME141
LOCALS:0 = 관장

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE141
;공통 부분
CALL COM_ABLE_COMMON(141)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;관장이 필요
SIF ITEM:관장 <= 0 && ITEM:A_관장 <= 0
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
SIF GROUPMATCH(GET_SEX_POSITION(MTAR:0, MPLY:0), 2, 4)
	RETURN 0
;엉덩이 막히고 있으면 안 돼
SIF IS_A_HOLD(MTAR:0)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM141
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;주도권의 소재를 취득
LOCAL:11 = GET_COM_INITIATIVE()

;●플레이어측의 처리
SOURCE:(MPLY:0):성행동 = 90

;플레이어에 주도권
IF LOCAL:11 == 0 || LOCAL:11 == 2
	SOURCE:(MPLY:0):기학 = 200
	SOURCE:(MPLY:0):일탈 = 300
;플레이어에 주도권이 없다
ELSE
	SOURCE:(MPLY:0):일탈 = 600
	SOURCE:(MPLY:0):공포 = MAX(150 - 40 * ABL:(MPLY:0):욕망, 0)
ENDIF

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 100, 80)

;●타겟측의 처리
EXP:(MTAR:0):배설경험치 += 2

LOCAL:3 = 1000
LOCAL:4 = 600

SELECTCASE ABL:(MTAR:0):배설
	CASE 0
		TIMES LOCAL:3, 0.00
		TIMES LOCAL:4, 1.00
	CASE 1
		TIMES LOCAL:3, 0.10
		TIMES LOCAL:4, 0.60
	CASE 2
		TIMES LOCAL:3, 0.20
		TIMES LOCAL:4, 0.25
	CASE 3
		TIMES LOCAL:3, 0.40
		TIMES LOCAL:4, 0.05
	CASE 4
		TIMES LOCAL:3, 0.70
		TIMES LOCAL:4, 0.00
	CASEELSE
		LOCAL:3 = LOCAL:3 * (100 + (ABL:(MTAR:0):배설 - 5) * 2) / 100
		LOCAL:4 = 0
ENDSELECT

SOURCE:(MTAR:0):노출 = 1400
SOURCE:(MTAR:0):일탈 = 600
SOURCE:(MTAR:0):중독충족 = LOCAL:3
SOURCE:(MTAR:0):반감 = LOCAL:4
SOURCE:(MTAR:0):성행동 = 150

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 80, 170)

;비잔량을 제로로 한다
TCVAR:(MTAR:0):55 = 0

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 6

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER141(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"노출 Lv{ABL:(ARG:0):노출}", ABL:(ARG:0):노출 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"배설 Lv{ABL:(ARG:0):배설}", ABL:(ARG:0):배설 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):도착적
	CALL COM_ORDER_ELEMENT(ARG:0, "도착적", 8)
ENDIF
IF TALENT:(ARG:0):보수적
	CALL COM_ORDER_ELEMENT(ARG:0, "보수적", -8)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE141
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 애널에 관장액을 주입%TARGET_STR("했다", "해 왔다")%
		PRINTFORMW %ANAME(MTAR:0)%의 엉덩이 구멍으로부터, 천한 소리와 함께 갈색빛 나게 탁해진 액체가 토해내졌다…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %ANAME(MPLY:0)%에 엉덩이를 향하여, 관장하도록(듯이) 요구했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"가")% 관장액을 주입하면, %ANAME(MTAR:0)%의 엉덩이 구멍으로부터 천한 소리와 함께 갈색빛 나게 탁해진 액체가 토해내졌다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)%에 관장하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"가")% %ANAME(MTAR:0)%의 애널에 관장액을 주입하면, %ANAME(MTAR:0)%의 엉덩이 구멍으로부터 천한 소리와 함께 갈색빛 나게 탁해진 액체가 토해내졌다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST141
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:1 = MPLY:(LOCAL:0)
	LOCAL:2 = CUP:(LOCAL:1):로주 + CUP:(LOCAL:1):액주

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF LOCAL:2 < 0 && CUP:(LOCAL:1):욕정 >= 500
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 배설물의 냄새에 넋을 잃으면서, 요염하게 미소를 띄우고 있다
		ELSEIF LOCAL:2 < 0
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 얼굴을 붉히면서 배설의 모습을 가만히 응시하고 있다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 배설물의 냄새에 얼굴을 비뚤어지게 해, 불쾌감을 드러냈다
		ENDIF
	ENDIF
NEXT
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	IF LOCAL:1 != MASTER && !TCVAR:(MTAR:0):52
		IF ABL:(LOCAL:1):배설 >= 2
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 너무나 부끄러워서 흥분했는지, 완전히 얼굴을 녹게 하고 있다
		ELSEIF ABL:(LOCAL:1):배설 >= 1
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 수치인 나머지 얼굴을 숙이고 있지만, 어딘가 흥분한 모습으로 숨을 난폭하게 하고 있다
		ELSE
			PRINTFORMW %조사처리(ANAME(LOCAL:1),"는")% 수치인 나머지 얼굴을 숙여, 당장 울 듯한 모습이다
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN141
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_141()
RETURN POWER(2, 성적기호_욕보이고싶다) + POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_141()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_시달리고싶다)

