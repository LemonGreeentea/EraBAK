;노출플레이

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME114
LOCALS:0 = 신체에낙서
RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE114
;공통 부분
CALL COM_ABLE_COMMON(114)
SIF RESULT == 0
	RETURN 0
;플레이어는 1명
SIF MPLY_NUM != 1
	RETURN 0

;타겟 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

SIF ITEM:매직 == 0 && ITEM:A_매직 == 0
	RETURN 0


;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;키스중이라면 불가
	SIF IS_EQUIP(MPLY:(LOCAL:0), 20)
		RETURN 0
	;페라·파이즈리·귀두 페라중이라면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 107)
		RETURN 0
	;이라마·촉수펠라 되고 있다면 불가
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 90, 210)
		RETURN 0
	;구속되고 있다면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;삽입중이라면 불가
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "전")
		RETURN 0
	;타겟이 이미 낙서가 끝난 상태라면 불가
	SIF IS_EQUIP_TARGET(MTAR:0, 114)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM114
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●전플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 30

	SOURCE:(LOCAL:2):기학 = 30
	SOURCE:(LOCAL:2):노출 = 20
	SOURCE:(LOCAL:2):일탈 = 20
	SOURCE:(LOCAL:2):불안 = 30

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 100)
NEXT

;●전 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 40

	SOURCE:(LOCAL:2):일탈 = 80
	SOURCE:(LOCAL:2):노출 = 1000
	SOURCE:(LOCAL:2):불안 = 30

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 140, 80)
NEXT

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

RETURN 1


;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP114
;계속 커맨드 한편 필터링 불가
RETURN 2
;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP114(ARG:0)
;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 10

	SOURCE:(LOCAL:2):기학 = 10
	SOURCE:(LOCAL:2):노출 = 300
	SOURCE:(LOCAL:2):일탈 = 10
	SOURCE:(LOCAL:2):불안 = 15

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;도착도 변화 기준치
	TCVAR:(LOCAL:2):50 += 2
NEXT

;●전 타겟으로 붙어 판정
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 30

	SOURCE:(LOCAL:2):일탈 = 10
	SOURCE:(LOCAL:2):노출 = 600
	SOURCE:(LOCAL:2):불안 = 10
	
	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 80, 20)

	;도착도 변화 기준치
	TCVAR:(LOCAL:2):50 += 2
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE114(ARG:0)
LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%
RESULTS = %LOCALS:0%의 신체에 추잡한 낙서가 되고 있다

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP114(ARG:0)
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%
PRINTFORML %LOCALS:0%의 피부에 그려진 추잡한 낙서가 눈을 끈다……

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP114(ARG:0)
	LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%

PRINTFORMW %LOCALS:0%의 낙서를 잘 닦아내 주었다

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER114(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 105

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드)
	CALL COM_ORDER_ELEMENT(ARG:0, "새드", 10)
ENDIF
IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -5)
ENDIF
IF TALENT:(ARG:0):수치심옅음
	CALL COM_ORDER_ELEMENT(ARG:0, "수치심옅음", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE114

LOCALS:0 = %ANAME(MPLY:0)%
;타겟이 2명 이상
IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%들
;타겟이 1명
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(LOCALS:0,"는")% %LOCALS:1%의 신체에, 추잡한 낙서를 베풀었다……
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(LOCALS:1,"는")% %LOCALS:0%에, 신체에 추잡한 낙서를 베풀어 받았다……
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %LOCALS:0%에, %LOCALS:1%의 신체에 추잡한 낙서를 베풀도록(듯이) 명령했다
		PRINTFORMW %조사처리(LOCALS:0,"는")% (듣)묻는 대로, %LOCALS:1%에 추잡한 낙서를 베풀어 갔다……
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN114
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_114()
RETURN POWER(2, 성적기호_욕보이고싶다)

@COM_PREFERENCE_TARGET_114()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_노출하고싶다)

