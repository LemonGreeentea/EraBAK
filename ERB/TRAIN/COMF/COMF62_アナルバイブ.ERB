;애널바이브

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME62
LOCALS:0 = 애널바이브

RESULTS:0 = %LOCALS:0%로 꾸짖는다
RESULTS:1 = %LOCALS:0%로 봉사한다
RESULTS:2 = %LOCALS:0%로 봉사시킨다
RESULTS:3 = %LOCALS:0%로 꾸짖을 수 있다
RESULTS:4 = %LOCALS:0%로 꾸짖게 한다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE62
;공통 부분
CALL COM_ABLE_COMMON(62)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;애널바이브가 필요
SIF !ITEM:애널바이브 && !ITEM:A_애널바이브
	RETURN 0

FOR LOCAL:0, 0, MTAR_NUM
	;타겟의Ａ가 막히고 있으면 불가
	SIF IS_A_HOLD(MTAR:(LOCAL:0))
		RETURN 0
	FOR LOCAL:1, 0, MPLY_NUM
		;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
		SIF GROUPMATCH(GET_SEX_POSITION(MTAR:(LOCAL:0), MPLY:(LOCAL:1)), 2, 4)
			RETURN 0
	NEXT
NEXT

FOR LOCAL:0, 0, MPLY_NUM
	;플레이어가 안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;플레이어가 구속중이라면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	FOR LOCAL:1, 0, MTAR_NUM
		;플레이어로부터 타겟／타겟으로부터 플레이어에 전기안마중이라면 불가
		SIF SEARCH_EQUIP_IC(103, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM62
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
DOWNBASE:(MPLY:0):체력 += 80

EXP:(MPLY:0):성기술경험치 += MTAR_NUM / 2 + 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(MPLY:0, 200)
SOURCE:(MPLY:0):접촉 = 10
SOURCE:(MPLY:0):성행동 = 90

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 180, 20)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)
	;●타겟으로 붙어 처리
	DOWNBASE:(LOCAL:2):체력 += 80

	SOURCE:(LOCAL:2):노출 = 70
	SOURCE:(LOCAL:2):일탈 = 300
	SOURCE:(LOCAL:2):접촉 = 10
	SOURCE:(LOCAL:2):성행동 = 150
	IF TALENT:(MPLY:0):기사
		SOURCE:(LOCAL:2):쾌Ａ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 1500)
	ELSE
		SOURCE:(LOCAL:2):쾌Ａ = SENSE_HOUSHI(MPLY:0, LOCAL:2, 1000)
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 20, 100)

	;Ａ바이브의 공통 처리
	CALL COM_COMMON62(LOCAL:2)

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MTAR:0, MPLY:0, 0)

NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 3

;레즈·ＢＬ경험기준치
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP62
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP62(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0
;소스를 퇴피
CALL PUTOUT_SOURCE(LOCAL:2)
SOURCE:(LOCAL:2):성행동 += 30
;퇴피한 소스를 가산
CALL SUM_SOURCE(LOCAL:2)

FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)
	CALL PUTOUT_SOURCE(LOCAL:3)

	IF TALENT:(LOCAL:2):기사
		SOURCE:(LOCAL:3):쾌Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 900)
	ELSE
		SOURCE:(LOCAL:3):쾌Ａ += SENSE_HOUSHI(LOCAL:2, LOCAL:3, 600)
	ENDIF
	SOURCE:(LOCAL:3):노출 += 20
	SOURCE:(LOCAL:3):일탈 += 200
	SOURCE:(LOCAL:3):성행동 += 50

	;도착도 변화
	TCVAR:(LOCAL:3):50 += 3

	;바이브의 공통 처리
	CALL COM_COMMON62(LOCAL:3)

	;퇴피한 소스를 가산
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE62(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%
RESULTS += "에 애널바이브를 삽입중"

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP62(ARG:0)
PRINTFORM %EQUIP_TARGET_ANAME(ARG:0)%
PRINTFORML 의 엉덩이 구멍으로 바이브가 진동하고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP62(ARG:0)
PRINTFORM %EQUIP_TARGET_ANAME(ARG:0)%
PRINTFORMW 의 엉덩이 구멍으로부터 바이브를 뽑아 냈다
;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER62(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 75

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE62
IF ABL:(MTAR:0):Ａ감 >= 3
	LOCALS:0 = 충분히 개발 된 애널
ELSEIF ABL:(MTAR:0):Ａ감 >= 2
	LOCALS:0 = 개발 도상의 애널
ELSE
	LOCALS:0 = 힘들게 닫은 애널
ENDIF

IF MTAR_NUM >= 2
	LOCALS:1 = %ANAME(MTAR:0)%들
ELSE
	LOCALS:1 = %ANAME(MTAR:0)%
ENDIF

IF TALENT:(MTAR:0):예쁜엉덩이
	LOCALS:2 = %STR_BODY("엉덩이:엉덩이살:장:애무:감촉", MTAR:0)%를 나누어 열어,
ELSEIF GET_HIPSIZE(MTAR:0) >= 1
	LOCALS:2 = %STR_BODY("엉덩이살:장", MTAR:0)%를 써 (뜻)이유,
ELSE
	LOCALS:2 = 
ENDIF


IF CUM_GET_COUNT:(MTAR:0):사정부위_애널 > 0
	LOCALS:3 = 항내에 낸 정액이 흘러넘치지 않게
ELSE
	LOCALS:3 = 
ENDIF

SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 %LOCALS:2% %LOCALS:0%에 떨리는 딜도를 꽉 눌러, %LOCALS:3%안쪽으로 밀어넣%TARGET_STR("었다", "어 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORMW %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에 애널바이브를 잡게 하면, %LOCALS:3%자신의 %조사처리(LOCALS:0,"를")% 관철하게 했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, 바이브로 %LOCALS:1%의 엉덩이 구멍을 꾸짖도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 %LOCALS:2% %LOCALS:0%에 떨리는 딜도를 꽉 눌러, %LOCALS:3%안쪽으로 밀어넣었다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST62

;--------------------------------------------------------
;장착시와 계속 상태에서의 공통 처리
;--------------------------------------------------------
@COM_COMMON62(ARG:0)
SELECTCASE ABL:(ARG:0):Ａ감
	CASE 0
		SOURCE:(ARG:0):고통 += 1200
	CASE 1
		SOURCE:(ARG:0):고통 += 500
	CASE 2
		SOURCE:(ARG:0):고통 += 200
	CASE 3
		SOURCE:(ARG:0):고통 += 80
	CASE 4
		SOURCE:(ARG:0):고통 += 30
ENDSELECT

IF PALAM:(ARG:0):윤활 < PALAMLV:1
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.05
	SOURCE:(ARG:0):고통 = SOURCE:(ARG:0):고통 * 3 + 1000
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:3
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.20
	SOURCE:(ARG:0):고통 = SOURCE:(ARG:0):고통 * 3 / 2 + 300
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:5
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.60
	TIMES SOURCE:(ARG:0):고통, 1.00
ELSEIF PALAM:(ARG:0):윤활 < PALAMLV:7
	TIMES SOURCE:(ARG:0):쾌Ａ, 0.85
	TIMES SOURCE:(ARG:0):고통, 0.70
ELSE
	TIMES SOURCE:(ARG:0):쾌Ａ, 1.00
	TIMES SOURCE:(ARG:0):고통, 0.50
ENDIF

IF TALENT:(ARG:0):체격 == 체격_작은체형
	TIMES SOURCE:(ARG:0):고통, 1.50
ENDIF

;대상의 질이 느슨해진다
CALL TIGHTNESS_DECREASE_A(ARG:0, RAND(1, 4))
CALL VIRGIN_COMMON_A(ARG:0, "애널바이브", GET_SITUATION_BY_TRAIN_MODE())

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN62
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_62()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_62()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

