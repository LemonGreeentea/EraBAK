;펠라치오

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME19
	;플레이어로부터 타겟으로 페라중
	IF SEARCH_EQUIP(11, MPLY:0, MTAR:0) >= 0
		LOCALS:0 = 페라구슬빨기
	;대딸중
	ELSEIF SEARCH_EQUIP(10, MPLY:0, MTAR:0) >= 0
		LOCALS:0 = 대딸구슬빨기
	;애널핥기중
	ELSEIF SEARCH_EQUIP(8, MPLY:0, MTAR:0) >= 0
		LOCALS:0 = Ａ구슬빨기
	ELSE
		LOCALS:0 = 구슬빨기
	ENDIF

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE19
;공통 부분
CALL COM_ABLE_COMMON(19)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 3명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 3
	RETURN 0
;타겟은 혼자
SIF MTAR_NUM != 1
	RETURN 0

FOR LOCAL, 0, MPLY_NUM
	;행동불능이라면 불가
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;구속중이라면 실행자는 1만
	SIF IS_BIND(MPLY:(LOCAL:0)) && MPLY_NUM >= 2
		RETURN 0

	;구가 막히고 있으면 불가(페라, 애널핥기는 놓친다)
	SIF IS_M_HOLD(MPLY:(LOCAL:0)) && SEARCH_EQUIP(11, MPLY:(LOCAL:0), MTAR:0) == -1 && SEARCH_EQUIP(8, MPLY:(LOCAL:0), MTAR:0) == -1
		RETURN 0
	FOR LOCAL:1, 0, MTAR_NUM
		;어느 쪽인지가 어딘가에 삽입중이라면 불가
		SIF IS_INSERT_MUTUAL(MPLY:(LOCAL), MTAR:(LOCAL:1)) != 0
			RETURN 0
		;타겟으로부터 플레이어에 다리 빨고 안이라면 불가
		SIF SEARCH_EQUIP(104, MTAR:(LOCAL:1), MPLY:(LOCAL:0)) >= 0
			RETURN 0
		;플레이어로부터 타겟으로 파이즈리중이라면 불가
		SIF SEARCH_EQUIP(12, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0

		;플레이어와 타겟이 서로 조개맞대기중이라면 불가
		SIF SEARCH_EQUIP_IC(21, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어와 타겟이 서로 쌍두바이브로 결합중이라면 불가
		SIF SEARCH_EQUIP_IC(22, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어로부터 타겟／타겟으로부터 플레이어에 엉덩이코키중이라면 불가
		SIF SEARCH_EQUIP_IC(13, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어로부터 타겟／타겟으로부터 플레이어에 엉덩이코키중이라면 불가
		SIF SEARCH_EQUIP_IC(15, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
		;플레이어로부터 타겟／타겟으로부터 플레이어에 전기안마중이라면 불가
		SIF SEARCH_EQUIP_IC(103, MPLY:(LOCAL:0), MTAR:(LOCAL:1)) >= 0
			RETURN 0
	NEXT
NEXT

;자지가 필요합니다
SIF !HAS_PENIS(MTAR:0)
	RETURN 0

IF MPLY_NUM == 1 && MTAR_NUM == 1
	;플레이어와 타겟이 함께 구속중이라면 불가
	SIF IS_BIND(MPLY:0) && IS_BIND(MTAR:0)
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;쾌Ｍ소스의 배율을 취득하는 함수 ARG:0=PLAYER의 캐릭터 번호
;-------------------------------------------------
@COM19_RATE_M(ARG:0)
#FUNCTION
LOCAL:5 = 1000
SELECTCASE ABL:(ARG:0):봉사
	CASE 0
		TIMES LOCAL:5, 0.00
		;TIMES SOURCE:(LOCAL:2):불결, 4.00
	CASE 1
		TIMES LOCAL:5, 0.10
		;TIMES SOURCE:(LOCAL:2):불결, 2.50
	CASE 2
		TIMES LOCAL:5, 0.30
		;TIMES SOURCE:(LOCAL:2):불결, 1.50
	CASE 3
		TIMES LOCAL:5, 0.80
		;TIMES SOURCE:(LOCAL:2):불결, 1.00
	CASE 4
		TIMES LOCAL:5, 1.00
		;TIMES SOURCE:(LOCAL:2):불결, 0.50
	CASEELSE
		LOCAL:5 = LOCAL:5 * (100 + (ABL:(ARG:0):봉사 - 5) * 3) / 100
		;TIMES SOURCE:(LOCAL:2):불결, 0.10
ENDSELECT

SELECTCASE ABL:(ARG:0):성기술
	CASE 0
		TIMES LOCAL:5, 1.00
	CASE 1
		TIMES LOCAL:5, 1.10
	CASE 2
		TIMES LOCAL:5, 1.20
	CASE 3
		TIMES LOCAL:5, 1.30
	CASE 4
		TIMES LOCAL:5, 1.40
	CASEELSE
		LOCAL:5 = LOCAL:5 * ((ABL:(ARG:0):성기술 - 5) * 2 + 150) / 100
ENDSELECT

RETURNF LOCAL:5

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM19
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

TFLAG:16 = 0
;삽입중
FOR LOCAL, 0, MTAR_NUM
	IF IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		TFLAG:16 = IS_INSERT_SINGLE(MTAR:LOCAL, "Ｐ")
		BREAK
	ENDIF
NEXT


;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MPLY_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MTAR_NUM
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 120

	EXP:(LOCAL:2):성기술경험치 += MAX(MTAR_NUM / 2 + 1, 1)
	EXP:(LOCAL:2):구음경험 += 1

	SOURCE:(LOCAL:2):봉사 = SERVE_HOUSHI(LOCAL:2, 600)
	SOURCE:(LOCAL:2):접촉 = 50
	SOURCE:(LOCAL:2):쾌Ｍ = 300 * COM19_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):성행동 = 300

	;주도권에 응한 우월·수동의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 150, 120)

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;전 타겟으로 주는 쾌감계 소스를 계산
	FOR LOCAL:1, 0, MTAR_NUM
		LOCAL:3 = MTAR:(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 1500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):혀놀림
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):쾌Ｐ += LOCAL:4

		;사정 개소와 대상을 세트
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 사정부위_입 # 사정부위_얼굴)
	NEXT
	IF MTAR_NUM == 1
		;페니스에의 키스
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%의 불알", GET_SITUATION_BY_TRAIN_MODE())
	ELSE
		CALL KISS_COMMON(LOCAL:2, @"%ANAME(MTAR:0)%들의 불알", (GET_SITUATION_BY_TRAIN_MODE() == "화간" ? "난교" # "윤간"))
	ENDIF
	;봉사정신ＬＶ2이상, 성기술ＬＶ2이상이라면 더러움을 없는 취한다
	;IF ABL:(LOCAL:1):봉사 >= 2 && ABL:(LOCAL:1):성기술 >= 2
	;	STAIN:PLAYER:2 = 2
	;ENDIF

	;노예의 손가락⇔조교자의Ｐ의 더러움이 이동
	;IF HAS_PENIS(PLAYER)
	;	STAIN:(LOCAL:1):1 |= STAIN:PLAYER:2
	;	STAIN:PLAYER:2 |= STAIN:(LOCAL:1):1
	;노예의 손가락⇔조교자의Ｖ의 더러움이 이동
	;ELSE
	;	STAIN:(LOCAL:1):3 |= STAIN:PLAYER:2
	;	STAIN:PLAYER:2 |= STAIN:(LOCAL:1):3
	;ENDIF
NEXT

;●전 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	;●소스의 계산
	DOWNBASE:(LOCAL:1):체력 += 60

	SOURCE:(LOCAL:1):접촉 = 50
	SOURCE:(LOCAL:1):성행동 = 180

	;주도권에 응한 우월·수동의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 150, 70)
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 0

;레즈·ＢＬ경험기준치
TFLAG:51 = 7

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP19
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP19(ARG:0)
;●인원수 보정의 설정
LOCAL:10 = 100

SELECTCASE MEQUIP_PLAYER_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
	CASE 3
		TIMES LOCAL:10, 0.60
ENDSELECT

SELECTCASE MEQUIP_TARGET_NUM:(ARG:0)
	CASE 2
		TIMES LOCAL:10, 0.75
ENDSELECT

;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 20

	EXP:(LOCAL:2):성기술경험치 += MAX(MEQUIP_TARGET_NUM:(ARG:0) / 2 + 1, 1)
	EXP:(LOCAL:2):구음경험 += 1

	SOURCE:(LOCAL:2):봉사 += SERVE_HOUSHI(LOCAL:2, 200)
	SOURCE:(LOCAL:2):접촉 += 25
	SOURCE:(LOCAL:2):쾌Ｍ += 150 * COM19_RATE_M(LOCAL:2) / 1000
	SOURCE:(LOCAL:2):성행동 += 100

	;봉사 경험치를 얻을 수 있는 커맨드의 플래그
	TCVAR:(LOCAL:2):4 = 1

	;전 타겟으로 주는 쾌감계 소스를 계산
	FOR LOCAL:1, 0, MEQUIP_TARGET_NUM:(ARG:0)
		LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:1)
		LOCAL:4 = SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 500) * LOCAL:10 / 100
		IF TALENT:(LOCAL:2):혀놀림
			TIMES LOCAL:4, 1.50
		ENDIF
		SOURCE:(LOCAL:3):쾌Ｐ += LOCAL:4

		;사정 개소와 대상을 세트
		CALL STACK_SPERM(LOCAL:3, LOCAL:2, RAND:3 ? 사정부위_입 # 사정부위_얼굴)
	NEXT
NEXT

;●전 타겟으로 붙어 판정
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:1 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:1):체력 += 10

	SOURCE:(LOCAL:1):접촉 += 25
	SOURCE:(LOCAL:1):성행동 += 60
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE19(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%에 구슬빨기 안

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP19(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%의 불알을 핥아대고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP19(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER19(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 80

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)
CALL COM_ORDER_ELEMENT(ARG:0, @"정애Lv{ABL:(ARG:0):정애}", ABL:(ARG:0):정애 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 3, 30))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "미약", 6)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):악취둔감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취둔감", 1)
ENDIF
IF TALENT:(ARG:0):악취민감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취민감", -3)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF
IF TALENT:(ARG:0):남성혐오
	CALL COM_ORDER_ELEMENT(ARG:0, "남성혐오", -12)
ENDIF
IF TALENT:(ARG:0):키스미경험
	IF TALENT:(ARG:0):선을넘지않음
		CALL COM_ORDER_ELEMENT(ARG:0, "키스미경험", -15)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "키스미경험", -10)
	ENDIF
ENDIF
;더러움의 판정
;IF ! TALENT:(ARG:0):불결무시
;	LOCAL:0 = 0
;	;애액의 더러움
;	SIF STAIN:PLAYER:2 & 1
;		LOCAL:0 += 1
;	;정액의 더러움
;	SIF STAIN:PLAYER:2 & 4
;		LOCAL:0 += 3
;	;애널의 더러움
;	SIF STAIN:PLAYER:2 & 8
;		LOCAL:0 += 7
;	SIF STAIN:PLAYER:2 & 16
;		LOCAL:0 += 15
;
;	SIF TALENT:(ARG:0):악취둔감
;		LOCAL:0/= 3
;	SIF TALENT:(ARG:0):악취민감
;		LOCAL:0 *= 2
;
;	;서로 더러워져
;	IF LOCAL:0 > 0
;		CALL COM_ORDER_ELEMENT(ARG:0, "서로 더러워져", -LOCAL:0)
;	ENDIF
;ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE19
LOCAL:5 = SEARCH_EQUIP(101, MPLY:0, MTAR:0)
LOCAL:6 = SEARCH_EQUIP(102, MPLY:0, MTAR:0)
LOCAL:7 = SEARCH_EQUIP(19, MTAR:0, MPLY:0)
LOCAL:8 = SEARCH_EQUIP(2, MTAR:0, MPLY:0)

IF MTAR_NUM == 1
	LOCALS:6 = 
ELSE
	LOCALS:6 = 들
ENDIF
;3인
IF MPLY_NUM == 3
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORML %ANAME(MPLY:(LOCAL:1))%들은 좌우로부터 얼굴을 대어, 세 명으로 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 자극%TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORML %ANAME(MPLY:(LOCAL:1))%들은 좌우로부터 얼굴을 대어, 세 명으로 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 봉사했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%、%ANAME(MPLY:1)%、%ANAME(MPLY:2)%의 세 명에게, %ANAME(MTAR:0)% %LOCALS:6%의 구슬을 빨도록(듯이) 명령했다
			PRINTFORML %ANAME(MPLY:(LOCAL:1))%들은 좌우로부터 얼굴을 대어, 세 명으로 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 자극%TARGET_STR("했다", "해 왔다")%…
	ENDSELECT

;2인
ELSEIF MPLY_NUM == 2
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 자극%TARGET_STR("했다", "해 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 협력해 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 봉사했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에, %ANAME(MTAR:0)% %LOCALS:6%의 구슬을 빨도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 좌우로부터 얼굴을 대어, 둘이서 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 봉사했다…
	ENDSELECT


;플레이어가 1명＆구속중
ELSEIF IS_BIND(MPLY:0)

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 자신의 얼굴에 %ANAME(MTAR:0)% %LOCALS:6%의 구슬을 강압하게 해 줄줄 소리를 내 빨았다…
		;타겟으로 주도권
		CASE 1
			PRINTFORMW %ANAME(MTAR:0)% %조사처리(LOCALS:6,"는")% 동작이 잡히지 않는 %ANAME(MPLY:0)%의 얼굴에 억지로 구슬을 강압하고 빨게 했다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)% %LOCALS:6%의 구슬을 입으로 기분 좋게 하도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% 강압할 수 있는 %ANAME(MTAR:0)% %LOCALS:6%의 구슬에 혀를 기게 해 줄줄 소리를 내 빨았다…
	ENDSELECT

	;밧줄로 묶는 것 지문을 컷 한다
	LOCAL:0 = SEARCH_EQUIP(85, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF
	LOCAL:0 = SEARCH_EQUIP(208, -1, MPLY:0)
	IF LOCAL:0 >= 0
		MEQUIP_CUT:(LOCAL:0) = 1
	ENDIF


;1인
ELSE
	IF TALENT:(MPLY:0):체격 == 체격_아이 || TALENT:(MPLY:0):체격 == 체격_작은체형
		LOCALS:0 = 작은 입으로 필사적으로
	ELSE
		LOCALS:0 = 입으로
	ENDIF
	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 구슬에 혀를 기게 해
			IF ABL:(MPLY:0):성기술 >= 3
				PRINTFORMW 교묘한 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
			ELSE
				PRINTFORMW 서투른 혀놀림으로 자극%TARGET_STR("했다", "해 왔다")%…
			ENDIF
		;타겟으로 주도권
		CASE 1
			PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 구슬에 혀를 기게 해
			IF ABL:(MPLY:0):성기술 >= 3
				PRINTFORMW 교묘한 혀놀림으로 봉사하고 있다…
			ELSE
				PRINTFORMW 서투른 혀놀림으로 봉사하고 있다…
			ENDIF
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에, %ANAME(MTAR:0)%의 구슬을 입으로 기분 좋게 하도록(듯이) 명령했다
			PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)%의 구슬에 혀를 기게 해
			IF ABL:(MPLY:0):성기술 >= 3
				PRINTFORMW 교묘한 혀놀림으로 빨았다…
			ELSE
				PRINTFORMW 서투른 혀놀림으로 핥아댔다…
			ENDIF
	ENDSELECT
ENDIF


IF (PALAM:(MPLY:0):욕정 >= PALAMLV:10 || (PALAM:(MPLY:0):욕정 >= PALAMLV:7 && TCVAR:(MPLY:0):60)) && ((IS_FEMALE(MPLY:0) && ABL:(MPLY:0):욕망 >= 랭크역치:랭크_그외:랭크_D) || ABL:(MPLY:0):정애 >= 랭크역치:랭크_그외:랭크_C)
	SELECTCASE RAND:2
		CASE 0
			SELECTCASE RAND:2
				CASE 0
					PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %SPLIT_RAND("흥분으로 콧김을 난폭하게 하면서/정욕에 자극을 받는 대로", 1)%、
				CASE 1
					PRINTFORM %SPLIT_RAND("흥분해 버린 모습의/발정해 버린 모습의", 1)% %조사처리(ANAME(MPLY:0),"는")%,
			ENDSELECT
			PRINTFORML %SPLIT_RAND("줄줄 소리를 내/천한 소리를 내/군침을 늘어뜨려/격렬하게 코로 호흡하면서/열중해/필사적으로", 1)% %ANAME(MTAR:0)%의 불알%SPLIT_RAND("를 핥아대고 있다/에 달라붙고 있다", 1)%…
		CASE 1
			PRINTFORM %조사처리(ANAME(MPLY:0),"는")% %SPLIT_RAND("흥분으로 숨을 거칠게 하면서/녹은 것 같은 얼굴을 하면서/열중한 것 같은 얼굴을 하면서", 1)%、
			PRINTFORML %SPLIT_RAND("맛보도록(듯이)/형태를 확인하도록(듯이)", 1)% %ANAME(MTAR:0)%의 불알%SPLIT_RAND("를 빨고 있다/를 가득 넣고 있다/에 혀를 기게 하고 있다/를 핥아대고 있다", 1)%…
	ENDSELECT
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN19
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_19()
RETURN POWER(2, 성적기호_봉사하고싶다) + POWER(2, 성적기호_더럽혀지고싶다)

@COM_PREFERENCE_TARGET_19()
RETURN POWER(2, 성적기호_봉사되고싶다)

