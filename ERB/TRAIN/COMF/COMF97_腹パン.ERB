;스팽킹

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME97
LOCALS:0 = 배빵

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE97
;공통 부분
CALL COM_ABLE_COMMON(97)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 2명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 2
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;플레이어가Ｖ인가Ａ에 삽입되고 있다면 불가
SIF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	RETURN 0
;플레이어가 안면승마 되고 있다면 불가
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;플레이어가 바닥에 엎드리며 있다면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 110)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;타겟이 안면승마중이라면 불가
	SIF IS_RIDE(MTAR:(LOCAL:0))
		RETURN 0
	;타겟이 바닥에 엎드리며 있다면 불가
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 110)
		RETURN 0
	;플레이어로부터 타겟으로 엉덩이코키중이라면 불가
	SIF SEARCH_EQUIP(15, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;플레이어로부터 타겟／타겟으로부터 플레이어에 풋잡중이라면 불가
	SIF SEARCH_EQUIP_IC(14, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;플레이어로부터 타겟／타겟으로부터 플레이어에 전기안마중이라면 불가
	SIF SEARCH_EQUIP_IC(103, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
	;플레이어로부터 타겟／타겟으로부터 플레이어에 다리 빨고 안이라면 불가
	SIF SEARCH_EQUIP_IC(104, MPLY:0, MTAR:(LOCAL:0)) >= 0
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM97
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
DOWNBASE:(MPLY:0):체력 += 100

SOURCE:(MPLY:0):기학 = 150
SOURCE:(MPLY:0):일탈 = 50
SOURCE:(MPLY:0):접촉 = 30
EXP:(MPLY:0):기학경험치 += RAND:3 + 1

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 150, 50)

;●전 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)

	DOWNBASE:(LOCAL:1):체력 += 100

	SOURCE:(LOCAL:1):고통 = 150 + MIN(ABL:(MPLY:0):무투, 90) * 3
	SOURCE:(LOCAL:1):노출 = 50
	SOURCE:(LOCAL:1):일탈 = 80
	SOURCE:(LOCAL:1):접촉 = 30
	EXP:(LOCAL:1):피학경험치 += RAND:3 + 1

	;타겟이 기절하고 있는 경우
	IF TCVAR:(LOCAL:1):52
		SOURCE:(LOCAL:1):기절회복 = 300
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:1, 0, 150)

	IF GETBIT(TALENT:(MPLY:0):음란계, 소질_음란_새드)
		TIMES SOURCE:(LOCAL:1):고통, 1.50
	ENDIF
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 2

RETURN 1

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER97(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 90

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"새드 Lv{ABL:(ARG:0):새드}", ABL:(ARG:0):새드 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):얌전함
	CALL COM_ORDER_ELEMENT(ARG:0, "얌전함", -3)
ENDIF
IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ기질
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ기질", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드)
	CALL COM_ORDER_ELEMENT(ARG:0, "새드", 40)
ELSE
	IF ABL:(ARG:0):주도도Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 15)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 10)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 5)
	ELSEIF ABL:(ARG:0):주도도Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", -5)
	ENDIF

	IF ABL:(ARG:0):도착도 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 20)
	ELSEIF ABL:(ARG:0):도착도 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 15)
	ELSEIF ABL:(ARG:0):도착도 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 10)
	ELSEIF ABL:(ARG:0):도착도 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE97
;타겟이 2명
IF MTAR_NUM == 2
	LOCALS:0 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%

;타겟이 1명
ELSE
	LOCALS:0 = %ANAME(MTAR:0)%
ENDIF
IF TALENT:(MTAR:0):임신
	LOCALS:1 = %조사처리(ANAME(MTAR:0),"가")% 임신하고 있는 것에도 불구하고,
ELSEIF TALENT:(MTAR:0):촉수임신 || TALENT:(MTAR:0):Ａ촉수임신
	LOCALS:1 = %조사처리(ANAME(MTAR:0),"가")% 촉수의 아이를 품고 있는 것에도 불구하고,
ELSEIF BASE:(MTAR:0):체력 <= 0
	LOCALS:1 = 체력의 한계를 맞이한 %조사처리(ANAME(MTAR:0),"를")% 질타 하도록(듯이),
ELSEIF BASE:(MTAR:0):기력 <= 0
	LOCALS:1 = 기력의 한계를 맞이한 %조사처리(ANAME(MTAR:0),"를")% 질타 하도록(듯이),
ELSEIF MTAR_NUM == 2 && TALENT:(MTAR:1):임신
	LOCALS:1 = %조사처리(ANAME(MTAR:1),"가")% 임신하고 있는 것에도 불구하고,
ELSEIF MTAR_NUM == 2 && (TALENT:(MTAR:1):촉수임신 || TALENT:(MTAR:1):Ａ촉수임신)
	LOCALS:1 = %조사처리(ANAME(MTAR:1),"가")% 촉수의 아이를 품고 있는 것에도 불구하고,
ELSEIF MTAR_NUM == 2 && BASE:(MTAR:1):체력 <= 0
	LOCALS:1 = 체력의 한계를 맞이한 %조사처리(ANAME(MTAR:1),"를")% 질타 하도록(듯이),
ELSEIF MTAR_NUM == 2 && BASE:(MTAR:1):기력 <= 0
	LOCALS:1 = 기력의 한계를 맞이한 %조사처리(ANAME(MTAR:1),"를")% 질타 하도록(듯이),
ELSE
	LOCALS:1 = 
ENDIF


SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1% %LOCALS:0%의 배에, 힘껏 주먹을 내던%TARGET_STR("졌다", "져 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %LOCALS:1% %조사처리(LOCALS:0,"는")% %ANAME(MPLY:0)%에, 배를 때리도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %LOCALS:0%의 배에 주먹을 내던졌다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %LOCALS:1% %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %LOCALS:0%의 배를 때리도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %LOCALS:0%의 배에 주먹을 내던졌다…
	ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST97
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:1 = MTAR:(LOCAL:0)
	IF LOCAL:1 != MASTER
		LOCAL:2 = CUP:(LOCAL:1):포주
		LOCAL:3 = CUP:(LOCAL:1):로주
		IF TALENT:(LOCAL:1):임신
			PRINTFORML 자를 지키려고, %조사처리(ANAME(LOCAL:1),"는")% 순간에 배를 감쌌다…
		ELSEIF TALENT:(LOCAL:1):촉수임신 || TALENT:(LOCAL:1):Ａ촉수임신
			PRINTFORML %ANAME(LOCAL:1)%의 배로부터, 고기의 무너지는 것 같은 싫은 소리가 울렸다…
		ELSEIF BASE:(LOCAL:1):체력 <= 0 || BASE:(LOCAL:1):기력 <= 0
			PRINTFORML 기절하고 있는 %조사처리(ANAME(LOCAL:1),"는")%, 아픔에 신음했다…
		ELSEIF ABL:(LOCAL:1):마조 >= 5
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 아픔을 느끼고 있는 것에도 불구하고 기쁜듯이 달콤한 소리를 높이고 있다
		ELSEIF ABL:(LOCAL:1):마조 >= 3
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 고민의 신음을 지르지만, 그 소리에는 달콤한 색이 섞이고 있다
		ELSEIF LOCAL:2 >= 3000
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 지나친 아픔과 공포에 오열을 흘리면서 구르고 돌고 있다
		ELSEIF LOCAL:2 >= 1500
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 아픔에 계속 참지 못하고, 입으로부터 신음을 흘려, 붕괴되었다
		ELSEIF LOCAL:2 >= 500
			PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 아픔의 너무 웅크리고 있다
		ELSE
			IF LOCAL:3 >= 200 && !IS_INITIATIVE(LOCAL:1)
				PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 아픔에 참으면서, %조사처리(ANAME(MPLY:0),"를")% 노려봤다
			ELSE
				PRINTFORML %조사처리(ANAME(LOCAL:1),"는")% 이를 악물어 아픔에 참고 있다
			ENDIF
		ENDIF
	ENDIF
NEXT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN97
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_97()
RETURN POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_97()
RETURN POWER(2, 성적기호_시달리고싶다)

