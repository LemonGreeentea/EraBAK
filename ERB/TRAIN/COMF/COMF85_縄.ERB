;밧줄

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME85
LOCALS:0 = 밧줄 묶기

RESULTS:0 = %LOCALS:0%
RESULTS:1 = %LOCALS:0%등 다투어진다
RESULTS:2 = %LOCALS:0%등 다툰다
RESULTS:3 = %LOCALS:0%
RESULTS:4 = %LOCALS:0%등 다툰다
RESULTS:5 = 밧줄 묶어 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE85
;공통 부분
CALL COM_ABLE_COMMON(85)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;밧줄이 필요
SIF !ITEM:밧줄 && !ITEM:A_밧줄
	RETURN 0
;플레이어가Ｖ인가Ａ에 삽입되고 있다면 불가
SIF IS_INSERT_SINGLE(MPLY:0, "ＶＡ")
	RETURN 0
;플레이어가 안면승마 되고 있다면 불가
SIF IS_RIDDEN(MPLY:0)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;타겟이 이미 구속중이라면 불가
SIF IS_BIND(MTAR:0)
	RETURN 0
;플레이어가 봉사중이라면 불가(꼬리계 제외함)
SIF IS_EQUIP_PLAYER(MPLY:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MPLY:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MPLY:0, 103, 104, 106, 107)
	RETURN 0
;타겟이 봉사중이라면 불가(꼬리계 제외함)
SIF IS_EQUIP_PLAYER(MTAR:0, 1, 2, 3, 4, 5) || IS_EQUIP_PLAYER(MTAR:0, 10, 11, 12, 13, 14, 15, 16) || IS_EQUIP_PLAYER(MTAR:0, 100, 103, 104, 106, 107, 116, 117)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM85
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;●플레이어에 대해 처리
DOWNBASE:(MPLY:0):체력 += 100
EXP:(MPLY:0):기학경험치 += 1

SOURCE:(MPLY:0):기학 = 300
SOURCE:(MPLY:0):일탈 = 100
SOURCE:(MPLY:0):접촉 = 30

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 50)

;●타겟으로 붙어 처리
DOWNBASE:(MTAR:0):체력 += 60

EXP:(MTAR:0):긴박경험 += 1
EXP:(MTAR:0):피학경험치 += 1

SOURCE:(MTAR:0):고통 = 80 + MIN(ABL:(MPLY:0):무투, 90) / 2
SOURCE:(MTAR:0):노출 = 50
SOURCE:(MTAR:0):일탈 = 200
SOURCE:(MTAR:0):접촉 = 30

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 0, 170)

IF GETBIT(TALENT:(MPLY:0):음란계, 소질_음란_새드)
	TIMES SOURCE:(MTAR:0):고통, 1.50
ENDIF

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 6

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP85
;계속 커맨드 한편 필터링 불가
RETURN 2

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP85(ARG:0)
LOCAL:3 = MEQUIP_TARGET:(ARG:0):0

DOWNBASE:(LOCAL:3):체력 += 10

EXP:(LOCAL:3):긴박경험 += 1
EXP:(LOCAL:3):피학경험치 += 1

SOURCE:(LOCAL:3):고통 += 30
SOURCE:(LOCAL:3):일탈 += 150

;도착도 변화
TCVAR:(LOCAL:3):50 += 4

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE85(ARG:0)
RESULTS = %조사처리(EQUIP_TARGET_ANAME(ARG:0),"를")% 밧줄로 구속중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP85(ARG:0)
PRINTFORML %조사처리(EQUIP_TARGET_ANAME(ARG:0),"는")% 밧줄로 속박되어 동작할 수 없다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP85(ARG:0)
PRINTFORMW %EQUIP_TARGET_ANAME(ARG:0)%의 밧줄을 풀었다

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER85(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 110

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"새드 Lv{ABL:(ARG:0):새드}", ABL:(ARG:0):새드 * 3)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):얌전함
	CALL COM_ORDER_ELEMENT(ARG:0, "얌전함", -3)
ENDIF
IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 3)
ENDIF
IF TALENT:(ARG:0):Ｓ기질
	CALL COM_ORDER_ELEMENT(ARG:0, "Ｓ기질", 6)
ENDIF

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드)
	CALL COM_ORDER_ELEMENT(ARG:0, "새드", 40)
ELSE
	IF ABL:(ARG:0):주도도Ｕ >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 15)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 10)
	ELSEIF ABL:(ARG:0):주도도Ｕ >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 5)
	ELSEIF ABL:(ARG:0):주도도Ｕ > -100
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", 0)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "주도도", -5)
	ENDIF

	IF ABL:(ARG:0):도착도 >= 800
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 20)
	ELSEIF ABL:(ARG:0):도착도 >= 500
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 15)
	ELSEIF ABL:(ARG:0):도착도 >= 300
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 10)
	ELSEIF ABL:(ARG:0):도착도 >= 100
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 5)
	ELSE
		CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 0)
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE85
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %조사처리(ANAME(MTAR:0),"를")% 밧줄로 %TARGET_STR("묶었다", "묶어 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")%, %ANAME(MPLY:0)%에게 밧줄로 자신을 묶도록(듯이) 명령했다
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %조사처리(ANAME(MTAR:0),"를")% 밧줄로 묶었다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %조사처리(ANAME(MTAR:0),"를")% 밧줄로 구속하도록(듯이) 명령했다
		PRINTFORML %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %ANAME(MTAR:0)%의 몸에 밧줄을 휘감고 붙들어매었다…
ENDSELECT

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN85
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_85()
RETURN POWER(2, 성적기호_학대하고싶다)

@COM_PREFERENCE_TARGET_85()
RETURN POWER(2, 성적기호_시달리고싶다) + POWER(2, 성적기호_속박되고싶다)

