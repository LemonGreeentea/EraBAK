;노노출플레이

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME112
LOCALS:0 = 노출플레이
RESULTS:0 = %LOCALS:0%에 나간다
RESULTS:1 = %LOCALS:0%에에 걸치게 하고
RESULTS:2 = %LOCALS:0%에 나가게 한다
RESULTS:3 = %LOCALS:0%에 나갈 수 있다
RESULTS:4 = %LOCALS:0%에 나가게 한다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE112
;공통 부분
CALL COM_ABLE_COMMON(112)
SIF RESULT == 0
	RETURN 0
;플레이어는 1명
SIF MPLY_NUM != 1
	RETURN 0

;플레이어에 주도권이 필요
SIF FLAG:주도권소유자 != MPLY:0
	RETURN 0

;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0

;궁전 이외 or 이미 노출플레이중이라면 불가
SIF TFLAG:54 > 0
	RETURN 0

SIF ITEM:목걸이 == 0
	RETURN 0


;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;재갈 장착중이라면 불가
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 86)
		RETURN 0
	;키스중이라면 불가
	SIF IS_EQUIP(MPLY:(LOCAL:0), 20)
		RETURN 0
	;페라·파이즈리·귀두 페라중이라면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 11, 12, 107)
		RETURN 0
	;이라마·촉수펠라 되고 있다면 불가
	SIF IS_EQUIP_TARGET(MPLY:(LOCAL:0), 90, 210)
		RETURN 0
	;구속되고 있다면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM112
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

IF MPLY:0 == MASTER
	PRINTFORML 어디에 가자……?

	FOR LOCAL, 0, VARSIZE("IAN_PLACE")
		PRINTFORML [{LOCAL}] %IAN_PLACE:LOCAL%
	NEXT
	PRINTFORML [99] 그만둔다

	$INPUT_LOOP
	INPUT
ELSE
	RESULT = RAND(0, VARSIZE("IAN_PLACE"))
	PRINTFORM %조사처리(ANAME(MPLY:0),"는")%
ENDIF

IF RESULT == 99
	PRINTFORML 역시 그만두었다
	RETURN 0
ELSEIF INRANGE(RESULT, 0, VARSIZE("IAN_PLACE"))
	PRINTFORML %IAN_PLACE:RESULT%에 가기로 했다
	TFLAG:105 = RESULT
	TSTR:0 = %TRAIN_PLACE%
	TRAIN_PLACE = %IAN_PLACE:RESULT%
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF


;●전플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 40

	SOURCE:(LOCAL:2):기학 = 20
	SOURCE:(LOCAL:2):노출 = 800
	SOURCE:(LOCAL:2):일탈 = 20
	SOURCE:(LOCAL:2):불안 = 30

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 50, 160)
NEXT

;●플레이어 이외의 전참가자에 대해 처리
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE

	DOWNBASE:LOCAL:체력 += 80

	SOURCE:LOCAL:일탈 = 80
	SOURCE:LOCAL:노출 = 1500
	SOURCE:LOCAL:불안 = 30

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 160, 80)
NEXT

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

TFLAG:54 = 8
TFLAG:103 = 0

RETURN 1


;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP112
;계속 커맨드 한편 필터링 불가
RETURN 2
;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP112(ARG:0)
;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	DOWNBASE:(LOCAL:2):체력 += 10

	SOURCE:(LOCAL:2):기학 += 10
	SOURCE:(LOCAL:2):노출 += 500 + 50 * TFLAG:104
	SOURCE:(LOCAL:2):일탈 += 10
	SOURCE:(LOCAL:2):불안 += 15

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 10, 50)

	;도착도 변화 기준치
	TCVAR:(LOCAL:2):50 += 3
NEXT


;●플레이어 이외의 전참가자에 대해 처리
FOR LOCAL:0, 0, CHARANUM
	SIF !IS_PARTICIPATE_TRAIN(LOCAL) || LOCAL:0 == MPLY:0
		CONTINUE

	DOWNBASE:LOCAL:체력 += 40

	SOURCE:LOCAL:일탈 = 10
	SOURCE:LOCAL:노출 = 1000 + 100 * TFLAG:104
	SOURCE:LOCAL:불안 = 10

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL, 80, 20)

	;도착도 변화 기준치
	TCVAR:LOCAL:50 += 3
NEXT

;계속 턴 증가
TFLAG:103 ++

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE112(ARG:0)
RESULTS = 노출플레이중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP112(ARG:0)

LOCALS:0 = %EQUIP_TARGET_ANAME(ARG:0)%

IF TFLAG:44 >= 8
	RETURN 
ENDIF

IF RAND:MAX(TFLAG:103, 1) * 4 - TFLAG:104 > RAND:50
	CALL CREATE_IAN_MOB(TFLAG:105, 1, 0)
	LOCAL:2 = CHARANUM - 1
	TALENT:(LOCAL:2):노출플레이모브 = 1
	PRINTFORML 소란을 우연히 들었던 %조사처리(ANAME(LOCAL:2),"가")% 참가했다…
	TFLAG:104 ++
	TFLAG:44 ++
ENDIF

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP112(ARG:0)
TFLAG:54 = 0
TFLAG:103 = 0
TFLAG:104 = 0
;모브는 여기서 삭제한다
LOCAL:2 = CHARANUM
FOR LOCAL:0, 0, LOCAL:2
	LOCAL:1 = LOCAL:2 - LOCAL:0 - 1
	IF TALENT:(LOCAL:1):노출플레이모브
		SIF FLAG:주도권소유자 == LOCAL:1
			FLAG:주도권소유자 = -1
		CALL RELEASE_ALL_EQUIP(LOCAL:1)
		CALL DELETE_CHARA(LOCAL:1)
	ENDIF
NEXT
PRINTFORMW 옥내로 돌아갔다
TRAIN_PLACE = %TSTR:0%

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER112(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 110

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):수치심옅음
	CALL COM_ORDER_ELEMENT(ARG:0, "수치심옅음", 5)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE112

LOCALS:0 = %ANAME(MPLY:0)%

PRINTFORMW %조사처리(LOCALS:0,"는")% 옥외에서 행위를 하기로 했다…


;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN112
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_112()
RETURN POWER(2, 성적기호_욕보이고싶다)

@COM_PREFERENCE_TARGET_112()
RETURN POWER(2, 성적기호_욕보여지고싶다) + POWER(2, 성적기호_노출하고싶다)

