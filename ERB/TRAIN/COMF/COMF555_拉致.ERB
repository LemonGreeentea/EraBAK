;납치

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME555
RESULTS:0 = 납치

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE555
;공통 부분
CALL COM_ABLE_COMMON(555)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;주인공이 플레이어 때 한정
SIF MPLY:0 != MASTER
	RETURN 0
;자국의 관직에 오름은 불가
SIF CFLAG:(MTAR:0):소속 == CFLAG:(MPLY:0):소속
	RETURN 0
;요바이 중한정
SIF !FLAG:요바이
	RETURN 0
;플레이어의 입이 막히고 있다면 불가
SIF IS_M_HOLD(MPLY:0)
	RETURN 0
;방랑중이라면 안 돼
SIF !CFLAG:MASTER:소속
	RETURN 0
;상대가 군주라면 불가
FOR LOCAL:0, 0, MAX_COUNTRY
	SIF COUNTRY_BOSS:(LOCAL:0) == GET_ID(MTAR:0)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM555
;●실행치의 계산
CVARSET TCVAR, 24, -10000
CVARSET TCVAR, 25, 10000

TCVAR:(MTAR:0):25 = 120

CALL COM_ORDER_BASE(MTAR:0)

SIF TCVAR:(MTAR:0):51
	CALL COM_ORDER_ELEMENT(MTAR:0, "피로", 10)
SIF TCVAR:(MTAR:0):52
	CALL COM_ORDER_ELEMENT(MTAR:0, "실신", 10)

IF TALENT:(MTAR:0):반항적
	CALL COM_ORDER_ELEMENT(MTAR:0, "반항적", -5)
ENDIF
IF TALENT:(MTAR:0):꿋꿋함
	CALL COM_ORDER_ELEMENT(MTAR:0, "꿋꿋함", -5)
ENDIF
IF TALENT:(MTAR:0):솔직함
	CALL COM_ORDER_ELEMENT(MTAR:0, "솔직함", 5)
ENDIF
IF TALENT:(MTAR:0):프라이드높음
	CALL COM_ORDER_ELEMENT(MTAR:0, "프라이드높음", -15)
ENDIF
IF TALENT:(MTAR:0):프라이드낮음
	CALL COM_ORDER_ELEMENT(MTAR:0, "프라이드낮음", 5)
ENDIF
IF TALENT:(MTAR:0):호기심
	CALL COM_ORDER_ELEMENT(MTAR:0, "호기심", 5)
ENDIF
IF TALENT:(MTAR:0):보수적
	CALL COM_ORDER_ELEMENT(MTAR:0, "보수적", -8)
ENDIF
IF TALENT:(MTAR:0):고고함
	CALL COM_ORDER_ELEMENT(MTAR:0, "고고함", -(20 * KOKOU_RATE(MTAR:0) / 100))
ENDIF
IF TALENT:(MTAR:0):선을넘지않음
	CALL COM_ORDER_ELEMENT(MTAR:0, "선을넘지않음", -15)
ENDIF
IF GETBIT(TALENT:(MTAR:0):음란계, 소질_음란_음란) && FLAG:우후후플래그
	CALL COM_ORDER_ELEMENT(MTAR:0, "음란", 5)
ENDIF

IF TALENT:(MTAR:0):친애
	CALL COM_ORDER_ELEMENT(MTAR:0, "친애", 50)
ELSEIF TALENT:(MTAR:0):연모
	CALL COM_ORDER_ELEMENT(MTAR:0, "연모", 20)
ELSEIF TALENT:(MTAR:0):친구
	CALL COM_ORDER_ELEMENT(MTAR:0, "친구", 20)
ENDIF

IF TALENT:(MTAR:0):연인
	CALL COM_ORDER_ELEMENT(MTAR:0, "연인", 10)
ENDIF

IF TALENT:(MTAR:0):예속
	CALL COM_ORDER_ELEMENT(MTAR:0, "예속", 60)
ELSEIF TALENT:(MTAR:0):복종
	CALL COM_ORDER_ELEMENT(MTAR:0, "복종", 30)
ENDIF

IF TALENT:(MTAR:0):낙인
	CALL COM_ORDER_ELEMENT(MTAR:0, "낙인", 10)
ENDIF

IF TALENT:(MTAR:0):소유자
	CALL COM_ORDER_ELEMENT(MTAR:0, "소유자", 25)
ELSEIF TALENT:(MTAR:0):주인
	CALL COM_ORDER_ELEMENT(MTAR:0, "주인", 10)
ENDIF


LOCAL:0 = GET_PALAMLV(PALAM:(MTAR:0):욕정)
IF LOCAL:0 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 30))
ENDIF

IF TALENT:(MTAR:0):프라이드높음
	LOCAL:0 = 4
ELSEIF TALENT:(MTAR:0):프라이드낮음
	LOCAL:0 = 1
ELSE
	LOCAL:0 = 2
ENDIF

IF MARK:(MTAR:0):기성사실 >= 1
	CALL COM_ORDER_ELEMENT(MTAR:0, @"기성 사실 Lv{MARK:(MTAR:0):기성사실}", MARK:(MTAR:0):기성사실 * 3 * LOCAL:0)
ENDIF

IF FLAG:조교모드 == 6
	CALL COM_ORDER_ELEMENT(MTAR:0, "포로 회화", -180)
ELSEIF CFLAG:(MTAR:0):소속 != 0
	;CALL COM_ORDER_ELEMENT(MTAR:0, "시중들고 있는 군주가 있다", -45)
	;지금의 군주에의 좋은 인상-악인상을 10으로 나눈 것
	LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):소속)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):소속))) / 10
	IF LOCAL:0 >= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"지금의 군주의 %ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):소속))%에 좋은 인상", -(LOCAL:0))
	ELSEIF LOCAL:0 <= 0
		CALL COM_ORDER_ELEMENT(MTAR:0, @"지금의 군주의 %ANAME(GET_COUNTRY_BOSS(CFLAG:(MTAR:0):소속))%에 악인상", -(LOCAL:0))
	ENDIF
ENDIF

;권해진 세력의 군주에의 좋은 인상-악인상을 10으로 나눈 것
LOCAL:0 = (REL_LIKE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:소속)) - REL_HATE:(MTAR:0):(GET_COUNTRY_BOSS(CFLAG:MASTER:소속))) / 10
IF LOCAL:0 >= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"군주의 %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:소속))%에 좋은 인상", LOCAL:0)
ELSEIF LOCAL:0 <= 0
	CALL COM_ORDER_ELEMENT(MTAR:0, @"군주의 %ANAME(GET_COUNTRY_BOSS(CFLAG:MASTER:소속))%에 악인상", LOCAL:0)
ENDIF

;IF CFLAG:(MTAR:0):2 < 300
;	CALL COM_ORDER_ELEMENT(MTAR:0, "호감도 부족", -20)
;ELSEIF CFLAG:(MTAR:0):2 < 500
;	CALL COM_ORDER_ELEMENT(MTAR:0, "호감도 부족", -10)
;ELSEIF CFLAG:(MTAR:0):2 < 800
;	CALL COM_ORDER_ELEMENT(MTAR:0, "호감도 부족", -5)
;ENDIF

;판정 결과를 표시
PRINTFORMW  = {TCVAR:(MTAR:0):24} %TOSTR_COMPARE(TCVAR:(MTAR:0):24, TCVAR:(MTAR:0):25)% 실행치{TCVAR:(MTAR:0):25}

;판정 성공
IF TCVAR:(MTAR:0):24 >= TCVAR:(MTAR:0):25
	PRINTL 
	IF !IS_SLAVE(MTAR:0)
		PRINTFORML %조사처리(ANAME(MASTER),"는")% 발버둥 치는 %조사처리(ANAME(MTAR:0),"를")% 봉투에 담아 납치했다……
		PRINTFORMW ……%조사처리(ANAME(MTAR:0),"가")% 우리 나라의 포로가 되었습니다
		;소속국가의 변경
		CALL CAPTURE(MTAR:0, CFLAG:MASTER:소속)
	ELSE
		PRINTFORML %조사처리(ANAME(MASTER),"는")% %ANAME(MTAR:0)%에, 자신에게 대하도록(듯이) 명령했다
		PRINTFORMW ……%조사처리(ANAME(MTAR:0),"는")% 명령을 받아들였습니다
		CALL CHANGE_COUNTRY(MTAR:0, CFLAG:MASTER:소속, 1)
	ENDIF
	;강제 종료 플래그를 세운다
	TFLAG:38 = 1
;판정 실패
ELSE
	PRINTFORML %조사처리(ANAME(MASTER),"는")% %조사처리(ANAME(MTAR:0),"를")% 봉투에 담아 납치하려고 했다……
	PRINTFORMW ……저항되어 버렸다
	IF CFLAG:(MTAR:0):포로처 == CFLAG:MASTER:소속
		SOURCE:(MTAR:0):반감 = 300
	ELSE
		SOURCE:(MTAR:0):반감 = 3000
	ENDIF
ENDIF

RETURN 1

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN555
RETURN 커맨드_공통
