;촉수대딸

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME206
LOCALS:0 = 촉수대딸

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE206
;공통 부분
CALL COM_ABLE_COMMON(206)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;플레이어가 촉수소환중이 아니면 불가
SIF !IS_EQUIP_PLAYER(MPLY:0, 200)
	RETURN 0
;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MTAR_NUM
	;장대가 필요
	SIF !HAS_PENIS(MTAR:(LOCAL:0))
		RETURN 0
	;타겟을 장대를 꾸짖어지고 있다면 불가
	SIF IS_EQUIP_TARGET(MTAR:(LOCAL:0), 10, 11, 12, 13, 14, 15, 103)
		RETURN 0
	;타겟이 자위중이라면 불가
	SIF IS_EQUIP_PLAYER(MTAR:(LOCAL:0), 100)
		RETURN 0
	;♥삽입중은 불가
	SIF IS_INSERT_SINGLE(MTAR:(LOCAL:0), "Ｐ")
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM206
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

FOR LOCAL:0, 0, MTAR_NUM
	;타겟의 촉수오나홀을 해제
	CALL RELEASE_EQUIP(SEARCH_EQUIP(205, -1, MTAR:(LOCAL:0)), 1, 0)
NEXT

;●플레이어에 대해 처리
EXP:(MPLY:0):성기술경험치 += 1
EXP:(MPLY:0):신비경험치 += 1
EXP:(MPLY:0):촉수경험치 += 1

SOURCE:(MPLY:0):봉사 = SERVE_HOUSHI(MPLY:0, 150)
SOURCE:(MPLY:0):기학 = 30
SOURCE:(MPLY:0):일탈 = 80
SOURCE:(MPLY:0):촉수 = 30
SOURCE:(MPLY:0):성행동 = 210

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 130, 30)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MPLY:0):4 = 1

;●모든 타겟으로 붙어 처리
FOR LOCAL:0, 0, MTAR_NUM
	LOCAL:2 = MTAR:(LOCAL:0)

	EXP:(LOCAL:2):촉수경험치 += 1

	SOURCE:(LOCAL:2):쾌Ｐ = SENSE_HOUSHI_P(MPLY:0, LOCAL:2, 400 + ABL:(MPLY:0):신비 * 4) + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 200)
	SOURCE:(LOCAL:2):일탈 = 300 - (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 150)
	SOURCE:(LOCAL:2):촉수 = 300 + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 150)
	SOURCE:(LOCAL:2):성행동 = 180 + (GETBIT(TALENT:(LOCAL:2):음란계, 소질_음란_모판) * 90)

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(LOCAL:2, 60, 70)
NEXT

;주도도 변화 기준치
TFLAG:49 = 2

;도착도 변화 기준치
TFLAG:50 = 3

;레즈·ＢＬ경험기준치
TFLAG:51 = 1

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP206
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP206(ARG:0)
LOCAL:2 = MEQUIP_PLAYER:(ARG:0):0

SOURCE:(LOCAL:2):성행동 += 70

;모든 타겟으로 붙어 판정
FOR LOCAL:0, 0, MEQUIP_TARGET_NUM:(ARG:0)
	LOCAL:3 = MEQUIP_TARGET:(ARG:0):(LOCAL:0)

	;소스를 퇴피
	CALL PUTOUT_SOURCE(LOCAL:3)

	EXP:(LOCAL:3):촉수경험치 += 1

	SOURCE:(LOCAL:3):쾌Ｐ += SENSE_HOUSHI_P(LOCAL:2, LOCAL:3, 300 + ABL:(LOCAL:2):신비 * 3) + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 150)
	SOURCE:(LOCAL:3):일탈 += 120 - (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 60)
	SOURCE:(LOCAL:3):촉수 += 150 + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 750)
	SOURCE:(LOCAL:3):성행동 += 60 + (GETBIT(TALENT:(LOCAL:3):음란계, 소질_음란_모판) * 30)

	;도착도 변화
	TCVAR:(LOCAL:3):50 += 2

	;퇴피한 소스를 가산
	CALL SUM_SOURCE(LOCAL:3)
NEXT

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE206(ARG:0)
RESULTS = %EQUIP_TARGET_ANAME(ARG:0)%에게 %EQUIP_PLAYER_ANAME(ARG:0)%의 촉수가 촉수대딸중

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP206(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% 조종하는 촉수가 %EQUIP_TARGET_ANAME(ARG:0)%의 페니스에 감겨 희롱하고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP206(ARG:0)

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER206(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 120

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 2)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 3)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):촉수}", ABL:(ARG:0):촉수 * 6)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -2)
ENDIF
IF TALENT:(ARG:0):호기심
	CALL COM_ORDER_ELEMENT(ARG:0, "호기심", 2)
ENDIF
IF TALENT:(ARG:0):보수적
	CALL COM_ORDER_ELEMENT(ARG:0, "보수적", -7)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 4)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -2)
ENDIF
IF TALENT:(ARG:0):남성혐오
	CALL COM_ORDER_ELEMENT(ARG:0, "남성혐오", -6)
ENDIF

IF GETBIT(TALENT:(ARG:0):음란계, 소질_음란_새드) || GETBIT(TALENT:(ARG:0):음란계, 소질_음란_마조) || ABL:(ARG:0):도착도 >= 800
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 32)
ELSEIF ABL:(ARG:0):도착도 >= 500
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 24)
ELSEIF ABL:(ARG:0):도착도 >= 300
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 16)
ELSEIF ABL:(ARG:0):도착도 >= 100
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 8)
ELSE
	CALL COM_ORDER_ELEMENT(ARG:0, "도착도", 0)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE206
LOCAL:1 = 1
FOR LOCAL:0, 0, MTAR_NUM
	IF SEARCH_EQUIP(206, MPLY:0, MTAR:(LOCAL:0)) == -1
		LOCAL:1 = 0
	ENDIF
NEXT

;전원이 이미 촉수오나홀중이라면 분기
IF LOCAL:1
	;계속중의 지문을 컷
	FOR LOCAL:0, 0, MTAR_NUM
		LOCAL:2 = SEARCH_EQUIP(206, MPLY:0, MTAR:(LOCAL:0))
		IF LOCAL:2 >= 0
			MEQUIP_CUT:(LOCAL:2) = 1
		ENDIF
	NEXT

	IF MTAR_NUM >= 3
		LOCALS:1 = %ANAME(MTAR:0)%들
	ELSEIF MTAR_NUM == 2
		LOCALS:1 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
	ELSE
		LOCALS:1 = %ANAME(MTAR:0)%
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 페니스를, 미끌거리는 촉수로 한층 더 격렬하게 흔들%TARGET_STR("었다", "어 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORML %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에, 페니스에 감긴 촉수를 한층 더 움직여 봉사하도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 페니스를 계속 훑는 촉수를 보다 격렬하게 전후시켜, 장대 전체를 쓸어올렸다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %LOCALS:1%의 페니스에 감긴 촉수를 보다 격렬하게 움직이도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (들)물은 대로, %LOCALS:1%의 페니스를 미끌거리는 촉수로 한층 더 격렬하게 흔들었다…
	ENDSELECT

ELSE
	IF MTAR_NUM >= 3
		LOCALS:0 = 페니스
		LOCALS:1 = %ANAME(MTAR:0)%들
		LOCALS:2 = 동시에
		LOCALS:3 = 전원의
	ELSEIF MTAR_NUM == 2
		LOCALS:0 = 페니스
		LOCALS:1 = %조사처리(ANAME(MTAR:0),"와")% %ANAME(MTAR:1)%
		LOCALS:2 = 동시에
		LOCALS:3 = 두 명의
	ELSE
		IF PALAM:(MTAR:0):욕정 >= PALAMLV:7
			LOCALS:0 = 흥분해 뒤로 젖힌 페니스
		ELSE
			LOCALS:0 = 페니스
		ENDIF

		LOCALS:1 = %ANAME(MTAR:0)%
		LOCALS:2 = 격렬하게
		LOCALS:3 = 자신의
	ENDIF

	SELECTCASE GET_COM_INITIATIVE()
		;플레이어에 주도권
		CASE 0
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 %LOCALS:0%에 촉수를 감아, %LOCALS:2%상하에 흔들%TARGET_STR("었다", "어 왔다")%…
		;타겟으로 주도권
		CASE 1
			PRINTFORML %조사처리(LOCALS:1,"는")% %ANAME(MPLY:0)%에, 촉수를 사용해 페니스에 봉사하도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로, %LOCALS:1%의 페니스에 촉수를 휘감아 장대를 흔들었다…
		;제삼자에게 주도권
		CASEELSE
			PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, 촉수를 사용해 %LOCALS:1%의 페니스를 꾸짖도록(듯이) 명령했다
			PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %LOCALS:1%의 %LOCALS:0%에 촉수를 감아, %LOCALS:2%상하에 흔들었다…
	ENDSELECT
ENDIF

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST206

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN206
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_206()
RETURN POWER(2, 성적기호_촉수로범하고싶다) + POWER(2, 성적기호_봉사하고싶다)

@COM_PREFERENCE_TARGET_206()
RETURN POWER(2, 성적기호_촉수로범해지고싶다) + POWER(2, 성적기호_봉사되고싶다)

