;Ｗ안면승마

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME105
LOCALS:0 = Ｗ안면승마

RESULTS:0 = %LOCALS:0%한다
RESULTS:1 = %LOCALS:0%당한다
RESULTS:2 = %LOCALS:0%시킨다
RESULTS:3 = %LOCALS:0%된다
RESULTS:4 = %LOCALS:0%시킨다
RESULTS:5 = %LOCALS:0%과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE105
;공통 부분
CALL COM_ABLE_COMMON(105)
SIF RESULT == 0
	RETURN 0
;플레이어가 2명 때 한정
SIF MPLY_NUM != 2
	RETURN 0
;타겟은 최대로 1명까지
SIF MTAR_NUM <= 0 || MTAR_NUM > 1
	RETURN 0
;타겟이Ｐ삽입중 또한 체위가 기승위 이외라면 불가
SIF IS_INSERT_SINGLE(MTAR:0, "Ｐ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｐ"), 5)
	RETURN 0
;타겟이Ｖ삽입중 또한 체위가 정상위 이외라면 불가
SIF IS_INSERT_SINGLE(MTAR:0, "Ｖ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ｖ"), 1)
	RETURN 0
;타겟이Ｖ삽입중 또한 체위가 정상위 이외라면 불가
SIF IS_INSERT_SINGLE(MTAR:0, "Ａ") && !GROUPMATCH(GET_SEX_POSITION_SINGLE(MTAR:0, "Ａ"), 1)
	RETURN 0

;모든 플레이어에 대해 판정
FOR LOCAL:0, 0, MPLY_NUM
	;행동불능이라면 불가
	SIF !IS_PLAYABLE(MPLY:(LOCAL:0))
		RETURN 0
	;구속중이라면 불가
	SIF IS_BIND(MPLY:(LOCAL:0))
		RETURN 0
	;삽입중은 불가
	SIF IS_INSERT_SINGLE(MPLY:(LOCAL:0), "전")
		RETURN 0
	;엉덩이코키중이라면 불가
	SIF IS_EQUIP_PLAYER(MPLY:(LOCAL:0), 15)
		RETURN 0
	;플레이어가 안면승마 되고 있다면 불가
	SIF IS_RIDDEN(MPLY:(LOCAL:0))
		RETURN 0
	;플레이어가 이미 다른 상대에게 안면승마 하고 있다면 불가
	IF IS_RIDE(MPLY:(LOCAL:0))
		IF !(SEARCH_EQUIP(101, MPLY:(LOCAL:0), MTAR:0) >= 0 || SEARCH_EQUIP(102, MPLY:(LOCAL:0), MTAR:0) >= 0 || SEARCH_EQUIP(105, MPLY:(LOCAL:0), MTAR:0) >= 0)
			RETURN 0
		ENDIF
	ENDIF
	;플레이어로부터 타겟으로 다리 빨고 안이라면 불가
	SIF SEARCH_EQUIP(104, MPLY:(LOCAL:0), MTAR:0) >= 0
		RETURN 0
NEXT
;타겟이 안면승마 되고 있는 경우, 적어도 다른 한쪽이 이번 플레이어가 아니면 불가
IF IS_RIDDEN(MTAR:0)
	LOCAL:0 = 0
	IF SEARCH_EQUIP(101, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(102, MPLY:0, MTAR:0) >= 0 || SEARCH_EQUIP(105, MPLY:0, MTAR:0) >= 0
		LOCAL:0 = 1
	ENDIF
	IF SEARCH_EQUIP(101, MPLY:1, MTAR:0) >= 0 || SEARCH_EQUIP(102, MPLY:1, MTAR:0) >= 0 || SEARCH_EQUIP(105, MPLY:1, MTAR:0) >= 0
		LOCAL:0 = 1
	ENDIF
	SIF !LOCAL:0
		RETURN 0
ENDIF
;타겟이 안면승마중이라면 불가
SIF IS_RIDE(MTAR:0)
	RETURN 0
;타겟이 키스중이라면 불가
SIF IS_EQUIP(MTAR:0, 20)
	RETURN 0
;타겟이 다리 빨고 안이라면 불가
SIF IS_EQUIP_PLAYER(MTAR:0, 104)
	RETURN 0
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM105
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;쿤닐링구스, 안면승마의 플래그를 해제
FOR LOCAL:0, 0, MPLY_NUM
	FOR LOCAL:1, 0, MTAR_NUM
		CALL RELEASE_EQUIP_EX(2, MTAR:(LOCAL:1), -1)
		CALL RELEASE_EQUIP_EX(101, MPLY:(LOCAL:0), -1)
		CALL RELEASE_EQUIP_EX(102, MPLY:(LOCAL:0), -1)
	NEXT
NEXT

IF SEARCH_EQUIP(105, MPLY:0, MTAR:0) == -1 || SEARCH_EQUIP(105, MPLY:1, MTAR:0) == -1
	CALL RELEASE_EQUIP_EX(105, MPLY:0, -1)
	CALL RELEASE_EQUIP_EX(105, MPLY:1, -1)
ENDIF

;●플레이어에 대해 처리
FOR LOCAL:0, 0, MPLY_NUM
	LOCAL:2 = MPLY:(LOCAL:0)

	SOURCE:(LOCAL:2):노출 = 200
	SOURCE:(LOCAL:2):일탈 = 150
	SOURCE:(LOCAL:2):접촉 = 40
	SOURCE:(LOCAL:2):성행동 = 150
	EXP:(LOCAL:2):기학경험치 += 1

	;주도권이 플레이어 측에 있는 경우
	IF GET_COM_INITIATIVE() == 0
		SOURCE:(MPLY:0):기학 = 130
	ENDIF

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MPLY:0, 170, 120)
NEXT

;●타겟으로 붙어 처리
EXP:(MTAR:0):성기술경험치 += 1
EXP:(MTAR:0):피학경험치 += 1

SOURCE:(MTAR:0):봉사 = SERVE_HOUSHI(MTAR:0, 300)
SOURCE:(MTAR:0):일탈 = 150
SOURCE:(MTAR:0):접촉 = 60
SOURCE:(MTAR:0):성행동 = 150

;주도권에 응한 우월·굴종의 소스 추가
CALL ADD_SOURCE_INITIATIVE_U(MTAR:0, 120, 140)

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(MTAR:0):4 = 1

;주도도 변화 기준치
TFLAG:49 = 3

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 4

RETURN 1

;-------------------------------------------------
;계속 커맨드인가 어떤가를 설정
;-------------------------------------------------
@COM_IS_EQUIP105
RETURN 1

;-------------------------------------------------
;계속 상태의 처리
;-------------------------------------------------
@COM_EQUIP105(ARG:0)
;●전플레이어에 대해 판정
FOR LOCAL:0, 0, MEQUIP_PLAYER_NUM:(ARG:0)
	LOCAL:2 = MEQUIP_PLAYER:(ARG:0):(LOCAL:0)

	SOURCE:(LOCAL:2):노출 += 100
	SOURCE:(LOCAL:2):일탈 += 80
	SOURCE:(LOCAL:2):접촉 += 20
	SOURCE:(LOCAL:2):성행동 += 50
	EXP:(LOCAL:2):기학경험치 += 1

	;도착도 변화
	TCVAR:(LOCAL:2):50 += 2
NEXT

;●타겟으로 붙어 판정
LOCAL:2 = MEQUIP_TARGET:(ARG:0):0

EXP:(LOCAL:2):성기술경험치 += 1
EXP:(LOCAL:2):피학경험치 += 1

SOURCE:(LOCAL:2):봉사 += SERVE_HOUSHI(LOCAL:2, 100)
SOURCE:(LOCAL:2):일탈 += 80
SOURCE:(LOCAL:2):접촉 += 30
SOURCE:(LOCAL:2):성행동 += 50

;도착도 변화
TCVAR:(LOCAL:2):50 += 2

;봉사 경험치를 얻을 수 있는 커맨드의 플래그
TCVAR:(LOCAL:2):4 = 1

;-------------------------------------------------
;계속중의 표시
;-------------------------------------------------
@EQUIP_MESSAGE105(ARG:0)
RESULTS = %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%에 안면승마중(엉덩이)

;-------------------------------------------------
;계속중의 지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE_EQUIP105(ARG:0)
PRINTFORML %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"가")% %EQUIP_TARGET_ANAME(ARG:0)%의 얼굴 위에 걸치고 있다…

;-------------------------------------------------
;계속을 해제했을 때의 지문
;-------------------------------------------------
@COM_TEXT_RELEASE_EQUIP105(ARG:0)
PRINTFORMW %조사처리(EQUIP_PLAYER_ANAME(ARG:0),"는")% %EQUIP_TARGET_ANAME(ARG:0)%의 얼굴로부터 허리를 올렸다…

;-------------------------------------------------
;고유의 실행 판정
;-------------------------------------------------
@COM_ORDER_PLAYER105(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 75

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 2, 20))

IF TCVAR:(ARG:0):60
	CALL COM_ORDER_ELEMENT(ARG:0, "미약", 6)
ENDIF

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):악취둔감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취둔감", 1)
ENDIF
IF TALENT:(ARG:0):악취민감
	CALL COM_ORDER_ELEMENT(ARG:0, "악취민감", -3)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF
IF TALENT:(ARG:0):여성혐오
	CALL COM_ORDER_ELEMENT(ARG:0, "여성혐오", -7)
ENDIF

;합의
IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF
RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE105
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 %ANAME(MTAR:0)%의 얼굴에 걸쳐, 두 명 분의 엉덩이로 찌부러뜨리도록(듯이) 자리%TARGET_STR("했다", "해 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %조사처리(ANAME(MTAR:0),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에, 둘이서 얼굴 위에 걸쳐 엉덩이를 강압하도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 (듣)묻는 대로 %ANAME(MTAR:0)%의 얼굴 위에 주저앉아, 두 명 분의 엉덩이를 강압했다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%에게, %ANAME(MTAR:0)%의 얼굴 위에 앉도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"와")% %ANAME(MPLY:1)%는 (듣)묻는 대로 %ANAME(MTAR:0)%의 얼굴 위에 걸쳐, 두 명 분의 엉덩이를 강압했다…
ENDSELECT

;--------------------------------------------------------
;지문(파라미터·각인 변동 후)
;--------------------------------------------------------
@COM_TEXT_LAST105

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN105
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_105()
RETURN POWER(2, 성적기호_욕보여지고싶다)

@COM_PREFERENCE_TARGET_105()
RETURN POWER(2, 성적기호_욕보이고싶다)

