;미약

;-------------------------------------------------
;커맨드 명칭
;-------------------------------------------------
@COM_NAME71
LOCALS:0 = 미약

RESULTS:0 = %조사처리(LOCALS:0,"를")% 먹인다
RESULTS:1 = %조사처리(LOCALS:0,"를")% 음당한다
RESULTS:2 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:3 = %조사처리(LOCALS:0,"를")% 먹게 된다
RESULTS:4 = %조사처리(LOCALS:0,"를")% 먹이게 한다
RESULTS:5 = %LOCALS:0%사용 과시하고

;-------------------------------------------------
;선택 가부 판정
;-------------------------------------------------
@COM_ABLE71
;공통 부분
CALL COM_ABLE_COMMON(71)
SIF RESULT == 0
	RETURN 0
;플레이어는 최대로 1명까지
SIF MPLY_NUM <= 0 || MPLY_NUM > 1
	RETURN 0
;타겟은 1명 이상
SIF MTAR_NUM <= 0 
	RETURN 0
;플레이어가 행동불능이라면 불가
SIF !IS_PLAYABLE(MPLY:0)
	RETURN 0
;미약이 필요
SIF ITEM:미약 < MTAR_NUM && ITEM:A_미약 < MTAR_NUM
	RETURN 0

;플레이어가 구속중이라면 불가
SIF IS_BIND(MPLY:0)
	RETURN 0
;플레이어가 풋잡·전기안마중이라면 불가
SIF IS_EQUIP_PLAYER(MPLY:0, 14, 103)
	RETURN 0
FOR LOCAL, 0, MTAR_NUM
	;이미 타겟이 미약을 사용되고 있다면 불가
	SIF TCVAR:(MTAR:LOCAL):60
		RETURN 0
	;타겟의 입이 막히고 있다면 불가
	SIF IS_M_HOLD(MTAR:LOCAL)
		RETURN 0
	;타겟으로부터 플레이어에 삽입중에서 체위가 후배위·배면좌위라면 불가
	SIF GROUPMATCH(GET_SEX_POSITION(MTAR:LOCAL, MPLY:0), 2, 4)
		RETURN 0
NEXT
RETURN 1

;-------------------------------------------------
;메인 처리
;-------------------------------------------------
@COM71
;실행 판정
CALL COM_ORDER_COMMON
SIF RESULT == 0
	RETURN 0

;미약을 감소시킨다(추가 아이템 사용시는 감소시키지 않다)
IF ITEM:A_미약 <= 0
	ITEM:미약 = MAX(ITEM:미약 - MTAR_NUM, 0)
ENDIF

;●플레이어측의 처리

FOR LOCAL, 0, MTAR_NUM

	;●타겟측의 처리
	;미약사용 플래그를 세운다
	TCVAR:(MTAR:LOCAL):60 = 1

	SOURCE:(MTAR:LOCAL):일탈 = 100

	SELECTCASE ABL:(MTAR:LOCAL):욕망
		CASE 0
			SOURCE:(MTAR:LOCAL):반감 = 400
			SOURCE:(MTAR:LOCAL):불안 = 800
		CASE 1
			SOURCE:(MTAR:LOCAL):반감 = 300
			SOURCE:(MTAR:LOCAL):불안 = 600
		CASE 2
			SOURCE:(MTAR:LOCAL):반감 = 250
			SOURCE:(MTAR:LOCAL):불안 = 500
		CASE 3
			SOURCE:(MTAR:LOCAL):반감 = 150
			SOURCE:(MTAR:LOCAL):불안 = 300
		CASE 4
			SOURCE:(MTAR:LOCAL):반감 = 80
			SOURCE:(MTAR:LOCAL):불안 = 160
		CASEELSE
			SOURCE:(MTAR:LOCAL):반감 = MAX(50 - (ABL:(MTAR:LOCAL):욕망 - 5) * 20, 0)
			SOURCE:(MTAR:LOCAL):불안 = MAX(100 - (ABL:(MTAR:LOCAL):욕망 - 5) * 20, 0)
	ENDSELECT

	;주도권에 응한 우월·굴종의 소스 추가
	CALL ADD_SOURCE_INITIATIVE_U(MTAR:LOCAL, 20, 80)

	;사정 개소와 대상을 세트
	CALL STACK_SPERM(MPLY:0, MTAR:LOCAL, 0)

NEXT

;주도도 변화 기준치
TFLAG:49 = 1

;도착도 변화 기준치
TFLAG:50 = 2

;레즈·ＢＬ경험기준치
TFLAG:51 = 0

;경과시간을 무리하게 1줄인다
TFLAG:55 --

RETURN 1

;-------------------------------------------------
;고유의 실행 판정(플레이어측)
;-------------------------------------------------
@COM_ORDER_PLAYER71(ARG:0)
;실행치의 설정
TCVAR:(ARG:0):25 = 65

;공통 부분
CALL COM_ORDER(ARG:0)

CALL COM_ORDER_ELEMENT(ARG:0, @"욕망 Lv{ABL:(ARG:0):욕망}", ABL:(ARG:0):욕망 * 1)
CALL COM_ORDER_ELEMENT(ARG:0, @"봉사 Lv{ABL:(ARG:0):봉사}", ABL:(ARG:0):봉사 * 4)

LOCAL:0 = GET_PALAMLV(PALAM:(ARG:0):욕정)
CALL COM_ORDER_ELEMENT(ARG:0, @"욕정 Lv{LOCAL:0}", MIN(LOCAL:0 * 1, 10))

IF TALENT:(ARG:0):수줍음
	CALL COM_ORDER_ELEMENT(ARG:0, "수줍음", -1)
ENDIF
IF TALENT:(ARG:0):헌신적
	CALL COM_ORDER_ELEMENT(ARG:0, "헌신적", 6)
ENDIF
IF TALENT:(ARG:0):쾌감을부정
	CALL COM_ORDER_ELEMENT(ARG:0, "쾌감을부정", -1)
ENDIF

IF !TALENT:(ARG:0):합의 && !TALENT:(ARG:0):친구
	CALL COM_ORDER_ELEMENT(ARG:0, "합의 없음", -10)
ENDIF

RETURN 1

;-------------------------------------------------
;지문(전문)
;-------------------------------------------------
@COM_TEXT_BEFORE71
IF MTAR_NUM > 1
	LOCALS:0 = 들
ELSE
	LOCALS:0 = 
ENDIF
SELECTCASE GET_COM_INITIATIVE()
	;플레이어에 주도권
	CASE 0
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% %ANAME(MTAR:0)% %LOCALS:0%에 미약을 먹%TARGET_STR("였다", "여 왔다")%…
	;타겟으로 주도권
	CASE 1
		PRINTFORML %ANAME(MTAR:0)% %조사처리(LOCALS:0,"는")% %ANAME(MPLY:0)%에 미약을 먹이도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)% %LOCALS:0%의 입에 미약을 쏟았다…
	;제삼자에게 주도권
	CASEELSE
		PRINTFORML %조사처리(ANAME(TFLAG:45),"는")% %ANAME(MPLY:0)%에게, %ANAME(MTAR:0)% %LOCALS:0%에 미약을 먹이도록(듯이) 명령했다
		PRINTFORMW %조사처리(ANAME(MPLY:0),"는")% (듣)묻는 대로 %ANAME(MTAR:0)% %LOCALS:0%의 입에 미약을 따라, 삼키게 했다…
ENDSELECT
;-------------------------------------------------
;지문(실행 후)
;-------------------------------------------------
@COM_TEXT_AFTER71
IF MTAR_NUM > 1
	LOCALS:0 = 들
ELSE
	LOCALS:0 = 
ENDIF
IF MTAR:0 != MASTER
	IF (TCVAR:(MTAR:0):52) || (TCVAR:(MTAR:0):53 == 1)
		PRINTFORML 의식을 잃은 채로 먹게 된 약에 의해, %ANAME(MTAR:0)% %LOCALS:0%의 몸은 뜨겁게 달아오르기 시작하고 있다
	ELSE
		SIF TCVAR:(MTAR:0):51
			PRINT 지쳐 동작하지 못한 채
		SIF TALENT:(MTAR:0):신비지식 || TALENT:(MTAR:0):기사
			PRINT 먹게 된 약의 효과에 마음에 짚여,
		IF IS_LOVER(MTAR:0) && MPLY:0 == MASTER
			PRINTFORML 달아오르기 시작한 육체를 %ANAME(MTAR:0)% %조사처리(LOCALS:0,"는")% 주체 못하고 있다
		;포로 조교의 메인 타겟
		ELSEIF FLAG:조교모드 == 2 && FINDELEMENT(PRISONER_TARGET, MTAR:0) != -1
			PRINTFORML %ANAME(MTAR:0)% %조사처리(LOCALS:0,"는")% %ANAME(MPLY:0)%에 사용된 미약에 의해, 자신의 의사 (와)는 관계없는 것으로 신체가 쑤시기 시작하고 있다
		ELSE
			PRINTFORML %ANAME(MTAR:0)% %LOCALS:0%의 몸은 미약에 의해, 민감하게 되어 있다
		ENDIF
	ENDIF
ENDIF

;-------------------------------------------------

;커맨드 구분
;-------------------------------------------------

@COM_AVAILABLE_WHEN71
RETURN 커맨드_우후후

;--------------------
;성 기호용 함수
;--------------------
@COM_PREFERENCE_PLAYER_71()
RETURN POWER(2, 성적기호_도구를사용하고싶다)

@COM_PREFERENCE_TARGET_71()
RETURN POWER(2, 성적기호_도구를사용되고싶다)

