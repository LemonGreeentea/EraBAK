;===========================================================================
;	자원 표시 관련으로 사용하는 함수군
;
;PRINT_TARGET_IMAGE				타겟의 화상을 인원수분 표시　AA의 장소에서 부른다
;PRINT_STATE_IMAGE				상태화면의 표시 전용
;PRINT_IMAGE					어디라도 부르면 그림표시 해 주는 함수
;PRINT_IMAGE_ANIMATION			연번 화상 애니메이션
;PRINT_IMAGE_ANIMATION_정상위	정상위의 애니메이션 그림표시
;PRINT_IMAGE_ANIMATION_후배위	후배위의 애니메이션 그림표시
;PRINT_IMAGE_ANIMATION_사정후	사정후의 애니메이션 그림표시
;
;===========================================================================

;=========================================================
;   복장 변경/기타 조건에 따른 화상변화 적용 - 레몬녹차
; 
;   대상(ARG)의 다른 의상이 있다면 이곳에서 설정한다.
;   리소스 폴더 내 CSV에도 화상 지정 필요
;   
;=========================================================
@PRINT_IMAGE_OTHER_CLOTHES(대상)
#DIM 대상
#DIM 옷대상
#DIMS clothes

;대상이 플레이어면 복장 참고 대상을 0번 캐릭터로
IF 대상 == FLAG:주인공화상
	옷대상 = 0
ELSE
	옷대상 = 대상
ENDIF

SELECTCASE 대상
	CASE 1
		;시로코(라이딩, 수영복, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 8
			clothes = _라이딩
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 2
		;호시노(수영복, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 3
		;세리카(수영복, 정월, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월				
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 4
		;노노미(수영복, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 5
		;아야네
		IF CLOTH:옷대상:의상부위_상하의 == 12
			clothes = _샬레

		ELSEIF CLOTH:옷대상:의상부위_악세사리 == 1
				;아야네(수영복, 체육복)
			IF CLOTH:옷대상:의상부위_상하의 == 10
				clothes = _안경수영복
			ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
				clothes = _안경체육복
			ELSE
				clothes = _안경의복
			ENDIF
		ELSE
			;아야네(수영복, 체육복)
			IF CLOTH:옷대상:의상부위_상하의 == 10
				clothes = _수영복
			ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
				clothes = _체육복
			ELSE
				clothes = _의복
			ENDIF
		ENDIF
	CASE 9
		;아루(정월)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSE
			clothes = _의복
		ENDIF
	CASE 10
		;무츠키(정월)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSE
			clothes = _의복
		ENDIF
	CASE 11
		;카요코(정월)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSE
			clothes = _의복
		ENDIF
	CASE 12
		;카요코(정월,샬레)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 12
			clothes = _샬레
		ELSE
			clothes = _의복
		ENDIF
	CASE 13
		;히나(수영복, 파자마)
		IF CLOTH:옷대상:의상부위_상하의 == 6
			clothes = _학교수영복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 13
			clothes = _파자마
		ELSE
			clothes = _의복
		ENDIF
	CASE 14
		;아코(샬레, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 12
			clothes = _샬레
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 15
		;이오리(수영복)
		IF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSE
			clothes = _의복
		ENDIF
	CASE 16
		;치나츠(온천) 
		IF CLOTH:옷대상:의상부위_상하의 == 3
			clothes = _온천
		ELSE
			clothes = _의복
		ENDIF
	CASE 17
		;하루나(정월, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 18
		;준코(정월, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 19
		;이즈미(정월, 체육복, 수영복)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 10
			clothes = _수영복
		ELSE
			clothes = _의복
		ENDIF
	CASE 20
		;아카리(정월, 체육복)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 9
			clothes = _체육복
		ELSE
			clothes = _의복
		ENDIF
	CASE 21
		;후우카(정월)
		IF CLOTH:옷대상:의상부위_상하의 == 2
			clothes = _정월
		ELSE
			clothes = _의복
		ENDIF

	CASE 42
		;아스나(바니걸, 교복) 
		IF CLOTH:옷대상:의상부위_상하의 == 1
			clothes = _바니걸
		ELSEIF CLOTH:옷대상:의상부위_상하의 == 11
			clothes = _교복
		ELSE
			clothes = _의복
		ENDIF
	CASEELSE
		clothes = _의복
ENDSELECT
RESULTS = %clothes%
RETURN

;======================================================
;  ☆ 이미지 출력 함수
;  따로따로 퍼져있는 화상출력을 한 함수로 통합함(by. 레몬녹차)
;  대상 : 캐릭터번호
;  종류 : 스탠딩 혹은 얼굴
;  표정 : 통상, 웃음, 화남
;  출력예시 : 얼굴_화남_통상_13
;======================================================
@PRINT_IMAGE_CHARA(대상, 종류, 표정)
#DIM 대상
#DIMS 표정
#DIMS 종류
#DIMS sName
IF 대상 == -1
	sName = 더미캐릭터

ELSEIF IS_RANDOM_CHARA(대상)
		IF IS_FEMALE(대상)
			sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
		ELSE
			sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
		ENDIF
	ELSE
		SELECTCASE 대상
			;플레이어 일 때
			CASE 0
				;커스텀 페이스 적용 유무
				IF TALENT:대상:커스텀페이스
					;커스텀페이스(남캐)
					IF IS_MALE(대상)
						sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
					;커스텀페이스(여캐)
					ELSE
						sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
					ENDIF
				;주인공이 고정캐릭일 때
				ELSEIF !FLAG:주인공화상 == 0
						CALL PRINT_IMAGE_OTHER_CLOTHES(FLAG:주인공화상)
						sName = %종류%%RESULTS%_%표정%_{FLAG:주인공화상}
				ELSE
					;기본 남캐 화상
					IF IS_MALE(대상)
						sName = 스탠딩_의복_통상_남
					;기본 여캐 화상
					ELSE
						sName = 스탠딩_의복_통상_여
					ENDIF
				ENDIF
			CASE 1 TO CHARANUM
				CALL PRINT_IMAGE_OTHER_CLOTHES(대상)
				sName = %종류%%RESULTS%_%표정%_{대상}
			CASEELSE
				IF TALENT:대상:커스텀페이스
					IF IS_FEMALE(대상)
						sName = 커스텀_F_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
					ELSE
						sName = 커스텀_M_얼굴_의복_통상_{TALENT:대상:커스텀페이스}
					ENDIF
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_도둑)
					sName = 스탠딩_의복_통상_도둑
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_홉고블린)
					sName = 스탠딩_의복_통상_홉고블린
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_외래인)
					sName = 스탠딩_의복_통상_외래인
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_촉수)
					sName = 스탠딩_의복_통상_촉수
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_자경단)
					sName = 스탠딩_의복_통상_자경단
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_서큐버스)
					sName = 스탠딩_의복_통상_서큐버스
				ELSEIF CFLAG:대상:소속 == GET_COUNTRY_FROM_ID(SP_COUNTRY_ID:특수세력_광신도)
					sName = 스탠딩_의복_통상_광신도
				ENDIF
		ENDSELECT
	ENDIF


RESULTS = %sName%
RETURN



;=========================================================
;	갈아입기의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;=========================================================
@PRINT_STATE_IMAGE(iID, eText="통상", iSize=1000)
#DIM iID
#DIM iSize
#DIMS sPrint_Text
#DIMS sTemp, 3
#DIMS eText
#DIM iFont_Hei_mag
#DIM iEnter_Con

;SIF !iID
;	RETURN RESULT

iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

sPrint_Text = <p align='left'>
CALL PRINT_IMAGE_CHARA(iID, "스탠딩", "통상")
sPrint_Text += @"<img src='%RESULTS%' height='{iFont_Hei_mag}'>"
HTML_PRINT sPrint_Text
iEnter_Con = 1

IF FLAG:화상범위표시
	sPrint_Text =  <p align='left'><nobr>
	sPrint_Text += @"<img src='프레임_%eText%' height='{iFont_Hei_mag}'"
	sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF

FOR LOCAL, iEnter_Con+1, (iSize/100)
	HTML_PRINT "<br>"
NEXT
HTML_PRINT "<br>"
HTML_PRINT "<br>"
RETURN RESULT

;=========================================================
;	조교 화면의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	iEnter_Wid	: 그림의 X축 위치 배율
;=========================================================
@PRINT_PLAY_IMAGE(iSize=1000)
#DIM iID
#DIM iSize
#DIMS sTemp, 3
#DIMS sPrint_Text
#DIMS eMotion_Text
#DIM iFont_Hei_mag
#DIM iEnter_Con
#DIM iEnter_Wid
#DIM dIsplay_Num

dIsplay_Num = 0
iEnter_Con = 0
iEnter_Wid = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

VARSET sTemp



;뒷부분 최대 캐릭터 수
FOR iID, 1, CHARANUM
;	IF CFLAG:(iID):조교참가플래그
	IF IS_PARTICIPATE_TRAIN(iID)
		;-----------------
		;복장 변경에 따른 화상변경 (조교시)
		;-----------------

		SIF FLAG:그림표시 == dIsplay_Num
			BREAK
		IF dIsplay_Num == 5
			iEnter_Con = 0
			iEnter_Wid = 0
		ENDIF
		eMotion_Text = %TOSTR_EMOTION(iID)%
		IF GROUPMATCH(eMotion_Text, "행복", "기쁨")
			sTemp:1 = 미소
		ELSEIF GROUPMATCH(eMotion_Text, "원한", "분노", "분개")
			sTemp:1 = 분노
		ELSE
			sTemp:1 = 통상
		ENDIF

		IF FLAG:스탠딩종류 == 0
			sTemp:2 = 스탠딩
		ELSEIF FLAG:스탠딩종류 == 1
			sTemp:2 = 얼굴
		ENDIF

		sPrint_Text = <p align='left'><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
		;화상표시 함수
		CALL PRINT_IMAGE_CHARA(iID, sTemp:2, sTemp:1)
		sPrint_Text += @"<img src='%RESULTS%' height='{iFont_Hei_mag}' ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"

		HTML_PRINT sPrint_Text
		iEnter_Con++
		IF FLAG:화상범위표시
			sPrint_Text =  <p align='left'><nobr><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
			sPrint_Text += @"<img src='프레임_%sTemp:1%' height='{iFont_Hei_mag}'"
			sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
			HTML_PRINT sPrint_Text
		ELSE
			sPrint_Text =  <p align='left'><nobr><shape type = 'space'param = '{1000 * (iEnter_Wid)}'>
			sPrint_Text += @"<img src='프레임_공백' height='{iFont_Hei_mag}'"
			sPrint_Text += @"ypos='-{(iEnter_Con) * iFont_Hei_mag / 10+1}'>"
			HTML_PRINT sPrint_Text
		ENDIF
		iEnter_Con++
		iEnter_Wid++
		dIsplay_Num++
	ENDIF
NEXT

IF dIsplay_Num > 5
	FOR LOCAL, dIsplay_Num - 5, 5
		HTML_PRINT "<br>"
		HTML_PRINT "<br>"
	NEXT
ELSE
	FOR LOCAL, dIsplay_Num, 5
		HTML_PRINT "<br>"
		HTML_PRINT "<br>"
	NEXT
ENDIF

RETURN RESULT

;=========================================================
;	자원의 표시
;	표시 후 사이즈에 맞추어 개행을 실시한다
;	PRINT_IMG 의 확장같은 느낌 문중의 삽화적으로 사용할 수 있다고 생각한다
;
;	sRes_Name	：자원의 이름 캐릭터라인
;	iSize		：일행의 높이에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	sAlign		：align 속성 생략으로 왼쪽 맞춤　left, center, right의 삼종을 지정할 수 있습니다
;=========================================================
@PRINT_IMAGE(sRes_Name, iSize=1000, sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag

SIF GETCONFIGS("描画インターフェース") == "WINAPI"
	THROW 描画インターフェース를 TextRenderer로 변경해 주세요

SIF !FLAG:그림표시 || sRes_Name == ""
	RETURN RESULT

iEnter_Con = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5? 1#0)

HTML_PRINT @"<p align='%sAlign%'><nobr><img src='%sRes_Name%' height='{iFont_Hei_mag}'>"
iEnter_Con++

IF FLAG:화상범위표시
	sPrint_Text =  <p align='%sAlign%'><nobr>
	sPrint_Text += @"<img src='프레임_통상' height='{iFont_Hei_mag}'"
	sPrint_Text += @"ypos='-{iEnter_Con * iFont_Hei_mag / 10+1}'>"
	HTML_PRINT sPrint_Text
	iEnter_Con++
ENDIF


FOR LOCAL, iEnter_Con-1, (iSize/100)-1
	HTML_PRINT "<br>"
NEXT
RETURN RESULT

;=========================================================
;	연번 화상 애니메이션
;
;	sRes_Name	：자원의 이름 캐릭터라인 이 이름의 뒤에 연번 숫자를 붙인다
;	iStart		：개시 번호
;	iEnd		：개시부터의 묘화 매수
;	iAnime_Num	：전체의 애니메이션 반복 회수 생략 0으로 옵션 설정한 플래그 회수가 됩니다
;	iRollback	：롤백(rollback) 시킬까 플래그
;	iWait		：1매의 표시 시간
;	iSize		：일행의 높이에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;	sAlign		：align 속성 생략으로 왼쪽 맞춤　left, center, right의 삼종을 지정할 수 있습니다
;=========================================================
@PRINT_IMAGE_ANIMATION(sRes_Name,iStart=0,iEnd=4,iAnime_Num=0,iRollback=1,iWait=100,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iStart
#DIM iEnd
#DIM iAnime_Num
#DIM iRollback
#DIM iWait
#DIM iSize
#DIMS sAlign


SIF !FLAG:그림표시 || sRes_Name == ""
	RETURN RESULT

;SIF !FLAG:애니메이션
;	RETURN RESULT

SIF !iAnime_Num
	iAnime_Num = FLAG:애니메이션

FOR LOCAL,0,iAnime_Num
	FOR LOCAL:1,iStart,(iStart+iEnd)
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
	
	;ロールバックさせるか
	SIF !iRollback
		CONTINUE

	FOR LOCAL:1,(iStart+iEnd)-2,iStart-1,-1
		CALL PRINT_IMAGE(@"%sRes_Name%{LOCAL:1}",iSize,sAlign)
		TWAIT iWait, 0
		CLEARLINE iSize/100
	NEXT
NEXT
RETURN RESULT
[SKIPSTART]
;=========================================================
;	정상위のアニメーション画像表示
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_정상위(sRes_Name,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign

#DIM iStart
#DIMS sPrint_Res_Name
#DIMS sAnime_Res_Name

SIF !FLAG:그림표시 || sRes_Name == "" || !FLAG:삽화표시
	RETURN RESULT

IF sRes_Name == "정상위_사정"
	sPrint_Res_Name = %sRes_Name%_{FLAG:사정화상종류}
ELSE
	sPrint_Res_Name = %sRes_Name%
ENDIF

IF !FLAG:애니메이션
	CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
	RETURN RESULT
ENDIF

sAnime_Res_Name = 정상위_삽입_

IF sRes_Name == "정상위_사정" || sRes_Name == "정상위_절정"
	iStart = 1
ELSE
	iStart= 0
ENDIF

CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,iStart,4-iStart,FLAG:애니메이션,1,,iSize,sAlign)
CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,iStart,4-iStart,1,0,,iSize,sAlign)

IF sRes_Name == "정상위_사정"
	sAnime_Res_Name = 정상위_사정_{FLAG:사정화상종류}_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,2,FLAG:애니메이션,0,,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,2,2,FLAG:애니메이션,1,,iSize,sAlign)
ENDIF
CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)

RETURN RESULT

;=========================================================
;	후배위のアニメーション画像表示
;	コマ数など変わったので別で
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_후배위(sRes_Name,대상=0,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM 대상
#DIM iSize
#DIMS sAlign

#DIMS sPrint_Res_Name
#DIMS sAnime_Res_Name
#DIM iAnimeCon

SIF !FLAG:그림표시 || sRes_Name == "" || !FLAG:삽화표시
	RETURN RESULT

IF sRes_Name == "후배위_사정"
	sPrint_Res_Name = %sRes_Name%_{FLAG:사정화상종류}
	SIF NOWEX:대상:사정 > 6
		sPrint_Res_Name = %sRes_Name%_{FLAG:사정화상종류}_9
ELSE
	sPrint_Res_Name = %sRes_Name%
ENDIF

IF !FLAG:애니메이션
	CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
	RETURN RESULT
ENDIF


IF sRes_Name == "후배위_삽입"
	
	iAnimeCon = FLAG:애니메이션/2?FLAG:애니메이션/2#1
	CALL PRINT_IMAGE_ANIMATION("후배위_삽입_",0,6,iAnimeCon,1,,iSize,sAlign)
	
	iAnimeCon = (FLAG:애니메이션-iAnimeCon)?FLAG:애니메이션-iAnimeCon#-1
	CALL PRINT_IMAGE_ANIMATION("후배위_삽입2_",0,12,iAnimeCon,0)
ELSE
	IF sRes_Name == "후배위_절정"
		iAnimeCon = FLAG:애니메이션/2?FLAG:애니메이션/2#1
		CALL PRINT_IMAGE_ANIMATION("후배위_삽입2_",0,12,iAnimeCon,0)
	ENDIF
	
	CALL PRINT_IMAGE_ANIMATION("후배위_절정_",0,4,FLAG:애니메이션,0)
	CALL PRINT_IMAGE_ANIMATION("후배위_절정_",0,2,1,0)
ENDIF
IF sRes_Name == "후배위_사정"
	sAnime_Res_Name = 후배위_사정_{FLAG:사정화상종류}_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,8,1,0,,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,6,2,FLAG:애니메이션,1,,iSize,sAlign)
	
	;大量なら追加で
	IF NOWEX:대상:사정 > 6
		CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,8,2,FLAG:애니메이션,1,,iSize,sAlign)
	ENDIF
	
ENDIF
CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)

RETURN RESULT
;=========================================================
;	후배위のアニメーション画像表示
;	コマ数など変わったので別で
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_파이즈리(sRes_Name,iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign

#DIMS sPrint_Res_Name
#DIMS sAnime_Res_Name
#DIM iAnimeCon

SIF !FLAG:그림표시 || sRes_Name == "" || !FLAG:삽화표시
	RETURN RESULT

IF sRes_Name == "파이즈리_사정"
	sPrint_Res_Name = %sRes_Name%_{FLAG:사정화상종류}
ELSE
	sPrint_Res_Name = %sRes_Name%
ENDIF

sAnime_Res_Name = 파이즈리_삽입_

IF !FLAG:애니메이션
	CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)
	RETURN RESULT
ENDIF

CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,5,FLAG:애니메이션,1,,iSize,sAlign)
CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,5,1,0,,iSize,sAlign)

IF sRes_Name == "파이즈리_사정"
	sAnime_Res_Name = 파이즈리_사정_{FLAG:사정화상종류}_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,5,FLAG:애니메이션,0,,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,4,1,FLAG:애니메이션,1,,iSize,sAlign)
ENDIF

CALL PRINT_IMAGE(sPrint_Res_Name,iSize,sAlign)

RETURN RESULT
;=========================================================
;	사정後のアニメーション画像表示
;
;	sRes_Name	：最後に表示する画像のリソース名
;	iSize		：一行の高さに対するサイズ％数値　縦基準　縦横比維持　省略で1000(10倍)
;	sAlign		：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;=========================================================
@PRINT_IMAGE_ANIMATION_사정후(sRes_Name="정상위_질내사정",iSize=1000,sAlign="left")
#DIMS sRes_Name
#DIM iSize
#DIMS sAlign

#DIM iWait
#DIMS sAnime_Res_Name
#DIMS sType
#DIM SAVE_RESULT

SIF !FLAG:그림표시 || sRes_Name == "" || !FLAG:삽화표시
	RETURN RESULT

IF !FLAG:애니메이션
	CALL PRINT_IMAGE(sRes_Name,iSize,sAlign)
	RETURN RESULT
ENDIF
SAVE_RESULT = RESULT
SPLIT sRes_Name,"_",sType

IF sType == "후배위"
	sAnime_Res_Name = %sType%_사정_{FLAG:사정화상종류}_\@NOWEX:MASTER:사정>1?9#7\@
ELSE
	sAnime_Res_Name = %sType%_사정_{FLAG:사정화상종류}_3
ENDIF

iWait = 500
CALL PRINT_IMAGE(@"%sAnime_Res_Name%",iSize,sAlign)
sAnime_Res_Name = %sType%_삽입_
TWAIT iWait, 0
CLEARLINE iSize/100

iWait = 100
FOR LOCAL,3,0,-1
	CALL PRINT_IMAGE(@"%sAnime_Res_Name%{LOCAL}",iSize,sAlign)
	TWAIT iWait, 0
	CLEARLINE iSize/100
NEXT

;후배위のみアニメ
IF sType == "후배위"
	sAnime_Res_Name = %sType%_사정후_
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,0,2,1,0,iWait,iSize,sAlign)
	CALL PRINT_IMAGE_ANIMATION(sAnime_Res_Name,2,2,0,0,iWait,iSize,sAlign)
ENDIF

CALL PRINT_IMAGE(sRes_Name,iSize,sAlign)

RESULT = SAVE_RESULT
RETURN RESULT

;홍조する条件
;@BLUSH(ARG)
;#FUNCTION
;SIF TCVAR:ARG:만취 || TCVAR:ARG:미약 || TCVAR:ARG:발정
;	RETURNF 1
;SIF GETPALAMLV(PALAM:ARG:호의, 15) >= 10
;	RETURNF 1
;SIF GETPALAMLV(PALAM:ARG:욕정, 15) + CFLAG:ARG:욕구불만도 / 200 >= 8
;	RETURNF 1
;SIF CFLAG:ARG:우후후 && (GETPALAMLV(PALAM:ARG:욕정, 15) + CFLAG:ARG:욕구불만도 / 200 >= 5)
;	RETURNF 1
;RETURNF 0
;
;@TARGET_BLUSH_EXCEPTION(ARG)
;#FUNCTION
;SELECTCASE ARG
;	CASE 29,69
;		SIF INRANGE(CFLAG:ARG:현재위치, 200, 299) && TEQUIP:ARG:하반신착의상황 && TEQUIP:ARG:상반신착의상황
;			RETURNF 1
;ENDSELECT
;SIF CFLAG:ARG:モブ顔パターン == 5
;	RETURNF 1
;RETURNF 0

@GET_TARGETNUM()
#FUNCTION
RETURNF MAX(0, FINDELEMENT(TARGET, 0) - 1)
[SKIPEND]
;=========================================================
;	VS 화면의 그림표시
;
;	iID		：캐릭터 ID와 같은 자원명을 표시합니다
;	iSize	：폰트에 대한 사이즈%수치세로 기준 종횡비유지 생략으로 1000(10배)
;=========================================================
@PRINT_VS(VS_1, VS_2)
#DIM VS_1
#DIM VS_2
#DIMS TEMP_1
#DIMS TEMP_2
#DIMS sPrint_Text,3

IF FLAG:화상설정
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"

	;공격자 표시
	CALL PRINT_IMAGE_CHARA(VS_1, "스탠딩", "통상")
	sPrint_Text:0 = <p align='left'>
	sPrint_Text:0 += @"<shape type='space' param='0'><img src='%RESULTS%' height='1000' ypos='-400'>"
	HTML_PRINT sPrint_Text:0

	;VS 표시
	sPrint_Text:1 = <p align='left'>
	sPrint_Text:1 += @"<shape type='space' param='900'><img src='VS_P' height='1000' ypos='-500'>"
	HTML_PRINT sPrint_Text:1

	;방어자 표시
	CALL PRINT_IMAGE_CHARA(VS_2, "스탠딩", "통상")
	sPrint_Text:2 = <p align='left'>
	sPrint_Text:2 += @"<shape type='space' param='1350'><img src='%RESULTS%' height='1000' ypos='-600'>"
	HTML_PRINT sPrint_Text:2

	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
	HTML_PRINT "<br>"
ELSE

ENDIF
RETURN


@FACE_IMAGE(ARG, iSize, iSpace = 0, iChk = 0)
#DIMS sImageName
#DIMS sFrame
#DIMS sWeapon
#DIM iSize
#DIM iPadding
#DIM iSpace
#DIM iVar
#DIM iChk

sImageName = %GET_IMAGE_NAME(ARG)%
sFrame = Frame_1
sWeapon = 웨폰_{TALENT:ARG:웨폰}

iPadding = iSize - 3

FOR iVar, 0, iPadding
	HTML_PRINT "<br>"
NEXT

CALL PRINT_HTML_IMAGE(sImageName, iSize, iPadding, iSpace)
CALL PRINT_HTML_IMAGE(sFrame, iSize, iPadding + 1, iSpace)
SIF iChk == 0
	CALL PRINT_HTML_IMAGE(sWeapon, iSize, iPadding + 2, iSpace + 4)

RETURN

@PRINT_HTML_IMAGE(sImageName, iHeight, iPos, iSpace)
#DIMS sImageName
#DIM iHeight
#DIM iPos
#DIM iSpace
#DIM iSizeFactor
#DIMS sHtml

iSizeFactor = 100 * GETCONFIG("一行の高さ") / GETCONFIG("フォントサイズ")
iHeight *= iSizeFactor
iPos *= iSizeFactor
iSpace *= iSizeFactor
HTML_PRINT @"<shape type='space' param='{iSpace+10}'><img src='%sImageName%' height='{iHeight}' ypos='-{iPos}'>"

RETURN

@GET_IMAGE_NAME(iID)
#FUNCTIONS
#DIM iID
#DIMS sName
IF TALENT:(iID):커스텀페이스 > 0
	sName = 커스텀
	;성별전환 커스텀페이스
	IF IS_MALE(iID)
		sName = %sName%_M
	ELSEIF IS_FEMALE(iID)
		sName = %sName%_F
	ENDIF
	sName = %sName%_얼굴_의복_통상_{TALENT:(iID):커스텀페이스}
ELSEIF IS_MALE(iID)
	sName = F_남
ELSEIF IS_FEMALE(iID)
	sName = F_여
ELSE
	sName = F_DUMMY
ENDIF

RETURNF sName
