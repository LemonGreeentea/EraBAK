;-------------------------
;주인공  라고 할까 「당신」를 에디트 한다
;방침  MASTER_EDIT는 한 번 가상적으로 캐릭터를 생성해, 그 캐릭터의 스테이터스를 만진다
;최종적으로 완성된 캐릭터를 「당신」로서 굉장히 돌아간다
;-------------------------
@MASTER_EDIT(대상)
#DIM 대상
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT
REDRAW 0
$SHOW_LOOP
CALL MASTER_EDIT_UI(대상)

INPUT

SELECTCASE RESULT
	;이름
	CASE 0 TO 10
		CALL MASTER_EDIT_PROCESS_NAME(대상, RESULT)
	;종족
;	CASE 4000 TO 4999
;		CALL MASTER_EDIT_PROCESS_TAG(대상, RESULT - 4000)
	;성별
	CASE 100 TO 110
		CALL MASTER_EDIT_PROCESS_SEX(대상, RESULT - 100)
	;성 경험
	CASE 200 TO 210
		CALL MASTER_EDIT_PROCESS_SEX_EXP(대상, RESULT - 200)
	;능력
	CASE 300 TO 310
		CALL MASTER_EDIT_PROCESS_SLG_ABL(대상, RESULT - 300)
	CASE 400 TO 410
		CALL MASTER_EDIT_PROCESS_FACE_WEAPON_ABL(대상, RESULT - 400)
	;성적 능력
	CASE 1000 TO 1999
		CALL MASTER_EDIT_PROCESS_SEX_ABL(대상, RESULT - 1000)
	;소질
	CASE 2000 TO 2999
		CALL MASTER_EDIT_PROCESS_TALENT(대상, RESULT - 2000)
	;그 외
	CASE 3000 TO 3999
		CALL MASTER_EDIT_PROCESS_MISC(대상, RESULT - 3000)
	;수동 능력 설정
	CASE 100001
		CALL MASTER_EDIT_SLG_ABL_EDIT(대상)
	CASE 100002
		SIF MASTER == NAME_TO_CHARA("당신")
			CALL MASTER_EDIT_RELATION_SETTING(대상)
	CASE 100000
		REDRAW 1
		PRINTFORML 이 주인공을 보존합니까?
		CALL ASK_YN()
		IF RESULT == 0
			CALL SAVE_CUSTOM_DATA(-2, 대상)
		ENDIF
		CALL MASTER_EDIT_RELATION_CONFIRM(대상)
		RETURN 대상
ENDSELECT
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

;-------------------------
;이름 처리
;-------------------------
@MASTER_EDIT_PROCESS_NAME(대상, 번호)
#DIM 대상
#DIM 번호
SELECTCASE 번호
	CASE 0
		PRINTFORML 성을 입력
		INPUTS
		CSTR:대상:0 = %RESULTS%
	CASE 1
		PRINTFORML 명을 입력
		INPUTS
		CSTR:대상:1 = %RESULTS%
		NAME:대상 = %RESULTS%
		CALLNAME:대상 = %RESULTS%
	CASE 2
		PRINTFORML 애칭을 입력
		INPUTS
		CSTR:대상:98 = %RESULTS%
ENDSELECT


;-------------------------
;태그 처리
;-------------------------
;@MASTER_EDIT_PROCESS_TAG(대상, 번호)
;#DIM 대상
;#DIM 번호
;SIF !INRANGE(번호, 0, VARSIZE("TAG_NAME"))
;	RETURN 0
;ALL INVERT_TAG(대상, 번호)

;-------------------------
;성별 처리
;-------------------------
@MASTER_EDIT_PROCESS_SEX(대상, 번호)
#DIM 대상
#DIM 번호
TALENT:대상:성별 = 번호
TALENT:대상:커스텀페이스 = 0

;-------------------------
;성 경험 처리
;-------------------------
@MASTER_EDIT_PROCESS_SEX_EXP(대상, 번호)
#DIM 대상
#DIM 번호
SELECTCASE 번호
	CASE 0
		TALENT:대상:동정 = !TALENT:대상:동정
		IF TALENT:대상:동정
			SEXUAL_EXPERIENCE:대상:첫체험_동정 = ----
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_동정 = ----
		ELSE
			SEXUAL_EXPERIENCE:대상:첫체험_동정 = 불명
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_동정 = 불명
		ENDIF
	CASE 1
		TALENT:대상:키스미경험 = !TALENT:대상:키스미경험
		IF TALENT:대상:키스미경험
			SEXUAL_EXPERIENCE:대상:첫체험_키스 = ----
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_키스 = ----
		ELSE
			SEXUAL_EXPERIENCE:대상:첫체험_키스 = 불명
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_키스 = 불명
		ENDIF
	CASE 2
		TALENT:대상:처녀 = !TALENT:대상:처녀
		IF TALENT:대상:처녀
			SEXUAL_EXPERIENCE:대상:첫체험_처녀 = ----
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_처녀 = ----
		ELSE
			SEXUAL_EXPERIENCE:대상:첫체험_처녀 = 불명
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_처녀 = 불명
		ENDIF
	CASE 3
		TALENT:대상:애널처녀 = !TALENT:대상:애널처녀
		IF TALENT:대상:애널처녀
			SEXUAL_EXPERIENCE:대상:첫체험_애널처녀 = ----
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_애널처녀 = ----
		ELSE
			SEXUAL_EXPERIENCE:대상:첫체험_애널처녀 = 불명
			SEXUAL_LAST_EXPERIENCE:대상:첫체험_애널처녀 = 불명
		ENDIF
ENDSELECT

;-------------------------
;SLG 처리
;-------------------------
@MASTER_EDIT_PROCESS_SLG_ABL(대상, 번호)
#DIM 대상
#DIM 번호
#DIM 보정, 6
VARSET 보정

IF 번호 == 1
	TALENT:대상:성장형 = ROUND_INCREMENT(TALENT:대상:성장형, 0, 9)
ELSEIF 번호 == 0
	;기대치 합계를 270 전후로
	SELECTCASE TALENT:대상:성장형
		;만능
		CASE 0
			ABL:대상:무투 = RAND(40, 70)
			ABL:대상:방위 = RAND(40, 70)
			ABL:대상:지략 = RAND(40, 70)
			ABL:대상:정치 = RAND(40, 70)
			ABL:대상:요리 = RAND(20, 30)
			ABL:대상:가창 = RAND(20, 30)
		;지장 무투방위 지략
		CASE 1
			ABL:대상:무투 = RAND(50, 80)
			ABL:대상:방위 = RAND(50, 80)
			ABL:대상:지략 = RAND(50, 80)
			ABL:대상:정치 = RAND(30, 60)
			ABL:대상:요리 = RAND(10, 20)
			ABL:대상:가창 = RAND(10, 20)
		;학사 지략 정치
		CASE 2
			ABL:대상:무투 = RAND(30, 60)
			ABL:대상:방위 = RAND(30, 60)
			ABL:대상:지략 = RAND(55, 80)
			ABL:대상:정치 = RAND(55, 80)
			ABL:대상:요리 = RAND(20, 30)
			ABL:대상:가창 = RAND(20, 30)
		;양도 무투방위 정치
		CASE 3
			ABL:대상:무투 = RAND(50, 80)
			ABL:대상:방위 = RAND(50, 80)
			ABL:대상:지략 = RAND(30, 60)
			ABL:대상:정치 = RAND(50, 80)
			ABL:대상:요리 = RAND(10, 20)
			ABL:대상:가창 = RAND(10, 20)
		;무관 무투방위
		CASE 4
			ABL:대상:무투 = RAND(70, 85)
			ABL:대상:방위 = RAND(70, 85)
			ABL:대상:지략 = RAND(25, 55)
			ABL:대상:정치 = RAND(25, 55)
			ABL:대상:요리 = RAND(10, 25)
			ABL:대상:가창 = RAND(10, 25)
		;군사 지략
		CASE 5
			ABL:대상:무투 = RAND(30, 60)
			ABL:대상:방위 = RAND(30, 60)
			ABL:대상:지략 = RAND(70, 85)
			ABL:대상:정치 = RAND(55, 70)
			ABL:대상:요리 = RAND(15, 25)
			ABL:대상:가창 = RAND(15, 25)
		;문관  정치
		CASE 6
			ABL:대상:무투 = RAND(30, 60)
			ABL:대상:방위 = RAND(30, 60)
			ABL:대상:지략 = RAND(55, 70)
			ABL:대상:정치 = RAND(70, 85)
			ABL:대상:요리 = RAND(15, 25)
			ABL:대상:가창 = RAND(15, 25)
		;예능
		CASE 7
			ABL:대상:무투 = RAND(30, 60)
			ABL:대상:방위 = RAND(30, 60)
			ABL:대상:지략 = RAND(30, 60)
			ABL:대상:정치 = RAND(30, 60)
			ABL:대상:요리 = RAND(25, 35)
			ABL:대상:가창 = RAND(50, 70)
		;요리
		CASE 8
			ABL:대상:무투 = RAND(30, 60)
			ABL:대상:방위 = RAND(30, 60)
			ABL:대상:지략 = RAND(30, 60)
			ABL:대상:정치 = RAND(30, 60)
			ABL:대상:요리 = RAND(50, 70)
			ABL:대상:가창 = RAND(25, 35)
		;천재 기대치 300
		CASE 9
			ABL:대상:무투 = RAND(45, 75)
			ABL:대상:방위 = RAND(45, 75)
			ABL:대상:지략 = RAND(45, 75)
			ABL:대상:정치 = RAND(45, 75)
			ABL:대상:요리 = RAND(25, 35)
			ABL:대상:가창 = RAND(25, 35)
	ENDSELECT
ENDIF

;-------------------------
;성적 능력 처리
;-------------------------
@MASTER_EDIT_PROCESS_SEX_ABL(대상, 번호)
#DIM 대상
#DIM 번호
{
#DIM 편집가능능력 = 
GETNUM(ABL, "Ｃ감"), GETNUM(ABL, "Ｂ감"), GETNUM(ABL, "Ｖ감"), GETNUM(ABL, "Ａ감"), GETNUM(ABL, "Ｍ감"),
GETNUM(ABL, "성지식"), GETNUM(ABL, "욕망"), GETNUM(ABL, "성기술"), GETNUM(ABL, "봉사"), GETNUM(ABL, "주량")
}

SIF FINDELEMENT(편집가능능력, 번호) == -1
	RETURN

IF 번호 == GETNUM(ABL, "성지식")
	PRINTL 치를 입력(0~5)
	INPUT
	ABL:대상:성지식 = LIMIT(RESULT, 0, 5)
ELSE
	PRINTL 치를 입력(0~3)
	INPUT
	ABL:대상:번호 = LIMIT(RESULT, 0, 3)
ENDIF

;-------------------------
;프로필 웨폰 처리
;-------------------------
@MASTER_EDIT_PROCESS_FACE_WEAPON_ABL(대상, 번호)
#DIM 대상
#DIM 번호

IF 번호 == 0
	IF TALENT:대상:커스텀페이스 == 0
		IF IS_MALE(대상)
			TALENT:대상:커스텀페이스 = MOB_PACE_M
		ELSEIF IS_FEMALE(대상)
			TALENT:대상:커스텀페이스 = MOB_PACE_F
		ENDIF
	ELSEIF TALENT:대상:커스텀페이스 == 1
		TALENT:대상:커스텀페이스 = 101
	ELSEIF TALENT:대상:커스텀페이스 == 101
		TALENT:대상:커스텀페이스 = 0
	ELSE
		TALENT:대상:커스텀페이스 --
	ENDIF
ELSEIF 번호 == 1
	IF (IS_MALE(대상) && TALENT:대상:커스텀페이스 == CUSTOM_PACE_M + 100) || (IS_FEMALE(대상) && TALENT:대상:커스텀페이스 == CUSTOM_PACE_F + 100)
		TALENT:대상:커스텀페이스 = 1
	ELSEIF TALENT:대상:커스텀페이스 == 0
		TALENT:대상:커스텀페이스 = 101
	ELSEIF (IS_MALE(대상) && TALENT:대상:커스텀페이스 == MOB_PACE_M) || (IS_FEMALE(대상) && TALENT:대상:커스텀페이스 == MOB_PACE_F)
		TALENT:대상:커스텀페이스 = 0
	ELSE
		TALENT:대상:커스텀페이스 ++
	ENDIF
ELSEIF 번호 == 2
	CALL WEAPON_MENU(대상)
ENDIF

;-------------------------
;소질 처리
;-------------------------
@MASTER_EDIT_PROCESS_TALENT(대상, 번호)
#DIM 대상
#DIM 번호
{
SIF !PASS_CHARA_CHECK_SHOW_TALENT(번호) || TALENTNAME:번호 == "" ||
GROUPMATCH(번호, GETNUM(TALENT, "수수께끼의매력"), GETNUM(TALENT, "절륜"), GETNUM(TALENT, "신비지식"), GETNUM(TALENT, "하늘의사자"), GETNUM(TALENT,"기사"),
				GETNUM(TALENT, "폭주가"), GETNUM(TALENT, "군주의그릇"), GETNUM(TALENT, "구심력"))
}
	RETURN

TALENT:대상:번호 = !TALENT:대상:번호

;-------------------------
;그 외 처리
;-------------------------
@MASTER_EDIT_PROCESS_MISC(대상, 번호)
#DIM 대상
#DIM 번호

SELECTCASE 번호
	CASE 200
		TALENT:대상:음모현재치 = ROUND_INCREMENT(TALENT:대상:음모현재치, -1, 10)
		TALENT:대상:음모목표치 = TALENT:대상:음모현재치
	CASE 202
		TALENT:대상:겨드랑이털현재치 = ROUND_INCREMENT(TALENT:대상:겨드랑이털현재치, -1, 9)
		TALENT:대상:겨드랑이털목표치 = TALENT:대상:겨드랑이털현재치
	CASE 201
		CALL BEAUTIFY(대상)
ENDSELECT

;-------------------------
;UI부분
;-------------------------
@MASTER_EDIT_UI(대상)
#DIM 대상
CALL SINGLE_DRAWLINE
PRINTFORML 주인공의 소질이나 능력을 설정합니다

CALL SINGLE_DRAWLINE
PRINTFORML 이름
CALL MASTER_EDIT_NAME(대상)

;CALL SINGLE_DRAWLINE
;PRINTFORML 종족
;CALL MASTER_EDIT_TAG(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 성별
CALL MASTER_EDIT_SEX(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 프로필 사진/웨폰
CALL MASTER_EDIT_FACE_WEAPON(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 성 경험
CALL MASTER_EDIT_SEX_EXP(대상)

CALL SINGLE_DRAWLINE
PRINTFORML SLG 능력
CALL MASTER_EDIT_SLG_ABL(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 성적 능력
CALL MASTER_EDIT_SEX_ABL(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 소질
CALL MASTER_EDIT_TALENT(대상)

CALL SINGLE_DRAWLINE
PRINTFORML 그 외
CALL MASTER_EDIT_MISC(대상)

CALL SINGLE_DRAWLINE
IF MASTER == NAME_TO_CHARA("당신")
	PRINTBUTTON "[혈연 설정]", 100002
	PRINTL
ENDIF
PRINTBUTTON "[완료]", 100000

;-------------------------
;이름 UI
;-------------------------
@MASTER_EDIT_NAME(대상)
#DIM 대상
PRINTFORM   성 - %CSTR:대상:0, 20, LEFT%
PRINTBUTTON "[입력]", 0
PRINTL 
PRINTFORM   명 - %CSTR:대상:1, 20, LEFT%
PRINTBUTTON "[입력]", 1
PRINTL 
PRINTFORM 애칭 - %CSTR:대상:98, 20, LEFT%
PRINTBUTTON "[입력]", 2
PRINTL
PRINTFORML ※이름이 너무 길면 게임 내 표시 등이 깨질 수 있습니다.

;-------------------------
;태그 UI
;-------------------------
@MASTER_EDIT_TAG(대상)
#DIM 대상
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 4000 + LOCAL, HAS_TAG(대상, LOCAL))
NEXT
PRINTL
PRINTFORML ※최대{MAX_TAG}개까지
PRINTFORML ※모순된 태그를 부여할 수 있어?   아무튼 신경쓰지 마

;-------------------------
;성별 UI
;-------------------------
@MASTER_EDIT_SEX(대상)
#DIM 대상
CALL PRINT_SELECT_BUTTON("[남]", 100, TALENT:대상:성별 == 0)
CALL PRINT_SELECT_BUTTON("[녀]", 101, TALENT:대상:성별 == 1)
CALL PRINT_SELECT_BUTTON("[여성(후타)]", 102, TALENT:대상:성별 == 2)
CALL PRINT_SELECT_BUTTON("[남성(후타)]", 103, TALENT:대상:성별 == 3)
CALL PRINT_SELECT_BUTTON("[오토코노코]", 104, TALENT:대상:성별 == 4)
CALL PRINT_SELECT_BUTTON("[오토코노코(후타)]", 105, TALENT:대상:성별 == 5)
PRINTL

;-------------------------
;성별 UI
;-------------------------
@MASTER_EDIT_FACE_WEAPON(대상)
#DIM 대상
SIF TALENT:대상:웨폰 == 0
	TALENT:대상:웨폰 = 100
CALL FACE_IMAGE(대상, 7, 2)
PRINTBUTTON "[◁]", 400
PRINT [프로필사진]
PRINTBUTTON "[▷]", 401
PRINT     
PRINTBUTTON @"[웨폰:%WEAPON_NAME(TALENT:대상:웨폰)%]", 402
PRINTL

;-------------------------
;성 경험 UI
;-------------------------
@MASTER_EDIT_SEX_EXP(대상)
#DIM 대상
PRINTBUTTON TALENT:대상:동정 ? "[ 동정 ]" # "[비동정]", 200
PRINTBUTTON TALENT:대상:키스미경험 ? "[ 키스 미경험 ]" # "[ 키스 경험함 ]", 201
PRINTBUTTON TALENT:대상:처녀 ? "[ 처녀 ]" # "[비처녀]", 202
PRINTBUTTON TALENT:대상:애널처녀 ? "[ Ａ처녀 ]" # "[Ａ비처녀]", 203
PRINTL

;-------------------------
;SLG 능력 UI
;-------------------------
@MASTER_EDIT_SLG_ABL(대상)
#DIM 대상
#DIM 표시능력 = GETNUM(ABL, "무투"), GETNUM(ABL, "방위"), GETNUM(ABL, "지략"), GETNUM(ABL, "정치"), GETNUM(ABL, "가창"), GETNUM(ABL, "요리")

PRINTBUTTON "[변경]", 301
PRINTFORM 성장형:%TOSTR_GROWTYPE(TALENT:대상:성장형)%(%TOSTR_GROWTYPE_B(TALENT:대상:성장형)%에 적성)
PRINTL
PRINTBUTTON "[수동 설정]", 100001
PRINT   
PRINTBUTTON "[리로르]", 300
PRINT   
FOR LOCAL, 0, VARSIZE("표시능력")
	PRINTFORM %ABLNAME:(표시능력:LOCAL)%:
	CALL PRINT_ALPHABET_RANK(랭크_ＳＬＧ, ABL:대상:(표시능력:LOCAL))
	PRINTFORM {ABL:대상:(표시능력:LOCAL), 3, RIGHT}  
NEXT
PRINTL

;-------------------------
;성적 능력 UI
;-------------------------
@MASTER_EDIT_SEX_ABL(대상)
#DIM 대상
{
#DIM 표시능력 = 
GETNUM(ABL, "Ｃ감"), GETNUM(ABL, "Ｂ감"), GETNUM(ABL, "Ｖ감"), GETNUM(ABL, "Ａ감"), GETNUM(ABL, "Ｍ감"),
GETNUM(ABL, "성지식"), GETNUM(ABL, "욕망"), GETNUM(ABL, "성기술"), GETNUM(ABL, "봉사"), GETNUM(ABL, "주량")
}

FOR LOCAL, 0, VARSIZE("표시능력")
	PRINTFORM %ABLNAME:(표시능력:LOCAL)%:
	IF 표시능력:LOCAL == GETNUM(ABL, "성지식")
		CALL PRINT_ALPHABET_RANK(랭크_성지식, ABL:대상:(표시능력:LOCAL))
	ELSE
		CALL PRINT_ALPHABET_RANK(랭크_그외, ABL:대상:(표시능력:LOCAL))
	ENDIF
	PRINTFORM {ABL:대상:(표시능력:LOCAL), 2, RIGHT}
	PRINTBUTTON "[설정]", 1000 + 표시능력:LOCAL
	PRINT   
	SIF LOCAL % 5 == 0 && LOCAL > 0
		PRINTFORML
NEXT
PRINTL
CALL KILL_EMPTY_LINE
;-------------------------
;소질 UI
;-------------------------
@MASTER_EDIT_TALENT(대상)
#DIM 대상
LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF !PASS_CHARA_CHECK_SHOW_TALENT(LOCAL) || TALENTNAME:LOCAL == "" || GROUPMATCH(LOCAL, GETNUM(TALENT, "수수께끼의매력"), GETNUM(TALENT, "절륜"), GETNUM(TALENT, "신비지식"), GETNUM(TALENT, "하늘의사자"), GETNUM(TALENT,"기사"), GETNUM(TALENT, "폭주가"), GETNUM(TALENT, "합의"), GETNUM(TALENT, "군주의그릇"), GETNUM(TALENT, "구심력"))
		CONTINUE
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:LOCAL + "]", 14, LEFT%", 2000 + LOCAL, TALENT:대상:LOCAL)
	PRINT   
	LOCAL:1 ++
	SIF LOCAL:1 % 6 == 0 && LOCAL:1 > 0
		PRINTFORML
NEXT
PRINTL

;-------------------------
;그 외 UI
;-------------------------
@MASTER_EDIT_MISC(대상)
#DIM 대상

PRINTFORM 체격:%TOSTR_BODYSIZE(TALENT:(대상):체격), 12, LEFT%
PRINTFORM 체형:%TOSTR_BODYTYPE(TALENT:(대상):체형), 12, LEFT%
PRINTL

SIF HAS_VAGINA(대상)
	PRINTFORM 유방:%TOSTR_BUSTSIZE(GET_BUSTSIZE(대상)), 12, LEFT%
SIF HAS_PENIS(대상)
	PRINTFORM 음경:%TOSTR_PENISSIZE(GET_PENISSIZE(대상)), 12, LEFT%
PRINTFORM 엉덩이:%TOSTR_HIPSIZE(GET_HIPSIZE(대상)), 12, LEFT%

IF HAS_VAGINA(대상)
	PRINTFORM 질  :%TOSTR_TIGHTNESS(TALENT:(대상):Ｖ조임), 12, LEFT% 
ENDIF
PRINTFORM 애널:%TOSTR_TIGHTNESS(TALENT:(대상):Ａ조임), 12, LEFT% 
PRINTL

PRINTFORM 머리색:
CALL PRINT_HAIR_COLOR(TALENT:(대상):머리색)
PRINTFORM 머리길이:%TOSTR_HAIR_LENGTH(TALENT:(대상):머리길이), 12, LEFT% 
PRINTL
CALL SHOW_BODYSIZE(대상)
PRINTBUTTON "[보디 사이즈 설정]", 3201
PRINTL
PRINTFORM 음모:
PRINTBUTTON @"[%GET_INMOU_NAME(TALENT:대상:음모현재치)%]", 3200
PRINTFORM 겨드랑이털:
PRINTBUTTON @"[%GET_ARMPIT_NAME(TALENT:대상:겨드랑이털현재치)%]", 3202

PRINTL 


;-------------------------
;능력의 수동 설정
;-------------------------
@MASTER_EDIT_SLG_ABL_EDIT(대상)
#DIM 대상
#DIM 표시능력 = GETNUM(ABL, "무투"), GETNUM(ABL, "방위"), GETNUM(ABL, "지략"), GETNUM(ABL, "정치"), GETNUM(ABL, "가창"), GETNUM(ABL, "요리")
#DIM 포인트
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

포인트 = 240
FOR LOCAL, 0, VARSIZE("표시능력")
	포인트 -= ABL:대상:(표시능력:LOCAL)
NEXT

CALL SINGLE_DRAWLINE
PRINTFORML 능력을 수동으로 설정합니다
PRINTFORML 포인트가 허락하는 한, 0～100으로 지정할 수 있습니다
PRINTFORML 다만 리로르의 기대치 쪽이, 포인트 합계보다 위입니다
PRINTFORML 나머지 포인트:{포인트}
CALL SINGLE_DRAWLINE
FOR LOCAL, 0, VARSIZE("표시능력")
	PRINTFORML [{LOCAL}] %ABLNAME:(표시능력:LOCAL)% - {ABL:대상:(표시능력:LOCAL)}
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [99] 돌아온다

INPUT

IF RESULT == 99
	IF 포인트 < 0
		PRINTFORMW 포인트 부족합니다
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	ENDIF
	RETURN 0
ELSEIF !INRANGE(RESULT, 0, VARSIZE("표시능력") - 1)
	CLEARLINE LINECOUNT - FIRST_LINE
	RESTART
ENDIF

LOCAL = RESULT
PRINTFORML %ABLNAME:(표시능력:LOCAL)%의 능력치를 입력(0~{MAX(ABL:대상:(표시능력:LOCAL) + 포인트, 0)})

$INPUT_LOOP

INPUT

SIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP

ABL:대상:(표시능력:LOCAL) = RESULT

CLEARLINE LINECOUNT - FIRST_LINE

RESTART


;-------------------------
;혈연 설정
;-------------------------
@MASTER_EDIT_RELATION_SETTING(대상)
#DIM 대상
#DIM FIRST_LINE

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTBUTTON "[부친의 설정]", 0
IF ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE) > 0
	PRINTFORML  현재:%ANAME(ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE))%
ELSE
	PRINTFORML  현재:없음
ENDIF
PRINTBUTTON "[모친의 설정]", 1
IF ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE) > 0
	PRINTFORML  현재:%ANAME(ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE))%
ELSE
	PRINTFORML  현재:없음
ENDIF

PRINTBUTTON "[형제의 설정]", 2
IF MASTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	PRINTFORM 현재:
	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		SIF ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL) > 0
			PRINTFORM %ANAME(ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 현재:없음
ENDIF

PRINTBUTTON "[아들·딸의 설정]", 3
IF MASTER_EDIT_CHILD_CANDIDATE_NUM > 0
	PRINTFORM 현재:
	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		SIF ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL) > 0
			PRINTFORM %ANAME(ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL))% 
	NEXT
	PRINTFORML
ELSE
	PRINTFORML 현재:없음
ENDIF

PRINTBUTTON "[돌아온다]", 999

INPUT

IF RESULT == 0
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("MASTER_EDIT_FATHER", "NONE")
	MASTER_EDIT_FATHER_CANDIDATE = GET_ID(RESULT)

	;배타적 연산
	SIF MASTER_EDIT_MOTHER_CANDIDATE == MASTER_EDIT_FATHER_CANDIDATE
		MASTER_EDIT_MOTHER_CANDIDATE = 0
	
	LOCAL = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_BROTHER_CANDIDATE_NUM --
	ENDIF

	LOCAL = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_CHILD_CANDIDATE_NUM --
	ENDIF

ELSEIF RESULT == 1
	CALL SELECT_CHARA_LIST_ONLY_LOGIC_SLG("MASTER_EDIT_MOTHER", "NONE")
	MASTER_EDIT_MOTHER_CANDIDATE = GET_ID(RESULT)

	;배타적 연산
	SIF MASTER_EDIT_MOTHER_CANDIDATE == MASTER_EDIT_FATHER_CANDIDATE
		MASTER_EDIT_FATHER_CANDIDATE = 0
	
	LOCAL = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_BROTHER_CANDIDATE_NUM --
	ENDIF

	LOCAL = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)
	IF LOCAL != -1
		ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL
		MASTER_EDIT_CHILD_CANDIDATE_NUM --
	ENDIF

ELSEIF RESULT == 2
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MASTER_EDIT_CANDIDATE_MAX, "MASTER_EDIT_BROTHER", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		MASTER_EDIT_BROTHER_CANDIDATE:LOCAL = GET_ID(SELECTED_CHARA:LOCAL)
	NEXT
	MASTER_EDIT_BROTHER_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;배타적 연산
	SIF FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		MASTER_EDIT_FATHER_CANDIDATE = 0

	SIF FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM) != -1
		MASTER_EDIT_MOTHER_CANDIDATE = 0

	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = MASTER_EDIT_BROTHER_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, LOCAL:1, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT MASTER_EDIT_CHILD_CANDIDATE, -1, 0, LOCAL:2
			MASTER_EDIT_CHILD_CANDIDATE_NUM --
		ENDIF
	NEXT


ELSEIF RESULT == 3
	CALL SELECT_CHARA_LIST_MULTI_ONLY_LOGIC_SLG(MASTER_EDIT_CANDIDATE_MAX, "MASTER_EDIT_CHILD", "NONE")
	FOR LOCAL, 0, SELECTED_CHARA_NUM
		LOCAL:1 = SELECTED_CHARA:LOCAL
		MASTER_EDIT_CHILD_CANDIDATE:LOCAL = GET_ID(LOCAL:1)
	NEXT
	MASTER_EDIT_CHILD_CANDIDATE_NUM = SELECTED_CHARA_NUM

	;배타적 연산
	SIF FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_FATHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		MASTER_EDIT_FATHER_CANDIDATE = 0

	SIF FINDELEMENT(MASTER_EDIT_CHILD_CANDIDATE, MASTER_EDIT_MOTHER_CANDIDATE, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM) != -1
		MASTER_EDIT_MOTHER_CANDIDATE = 0

	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = MASTER_EDIT_CHILD_CANDIDATE:LOCAL
		LOCAL:2 = FINDELEMENT(MASTER_EDIT_BROTHER_CANDIDATE, LOCAL:1, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM)

		IF LOCAL:2 != -1
			ARRAYSHIFT MASTER_EDIT_BROTHER_CANDIDATE, -1, 0, LOCAL:2
			MASTER_EDIT_BROTHER_CANDIDATE_NUM --
		ENDIF
	NEXT

ELSEIF RESULT == 999
	RETURN 0
ENDIF

CLEARLINE LINECOUNT - FIRST_LINE
RESTART


@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_FATHER(대상)
#DIM 대상
RETURN HAS_PENIS(대상) && IS_FIXED_CHARA(대상) && 대상 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_MOTHER(대상)
#DIM 대상
RETURN HAS_VAGINA(대상) && IS_FIXED_CHARA(대상) && 대상 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_BROTHER(대상)
#DIM 대상
RETURN IS_FIXED_CHARA(대상) && 대상 != MASTER

@SELECT_CHARA_LIST_SHOW_LOGIC_MASTER_EDIT_CHILD(대상)
#DIM 대상
RETURN IS_FIXED_CHARA(대상) && 대상 != MASTER


@MASTER_EDIT_RELATION_CONFIRM(대상)
#DIM 대상
#DIM 후보, 3000
#DIM 후보수
#DIM 아버지후보
#DIM 어머니후보
VARSET 후보, -1
VARSET 후보수, 0

아버지후보 = ID_TO_CHARA(MASTER_EDIT_FATHER_CANDIDATE)
어머니후보 = ID_TO_CHARA(MASTER_EDIT_MOTHER_CANDIDATE)

IF 아버지후보 >= 0
	CFLAG:대상:부친 = GET_ID(아버지후보)
	CALL VIRGINIZE_LOST(아버지후보, 첫체험_동정, 어머니후보 >= 0 ? ANAME(어머니후보) + "의 질" # "불명")
	EXP:아버지후보:Ｖ삽입경험 += 5
ENDIF

IF 어머니후보 >= 0
	CFLAG:대상:모친 = GET_ID(어머니후보)
	CALL VIRGINIZE_LOST(어머니후보, 첫체험_처녀, 아버지후보 >= 0 ? ANAME(아버지후보) # "불명")
	EXP:어머니후보:Ｖ성교경험 += 5
	EXP:어머니후보:질내사정경험 += 5
	EXP:어머니후보:출산경험 += 1
ENDIF

IF MASTER_EDIT_BROTHER_CANDIDATE_NUM > 0
	;형제 후보의 형제도, 형제로서 추가한다
	FOR LOCAL, 0, MASTER_EDIT_BROTHER_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA(MASTER_EDIT_BROTHER_CANDIDATE:LOCAL)
		IF LOCAL:1 >= 0
			후보:후보수 = LOCAL:1
			후보수 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(후보, LOCAL:2, 0, 후보수) == -1
					후보:후보수 = LOCAL:2
					후보수 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	IF 아버지후보 < 0 && 어머니후보 < 0
		FOR LOCAL, 0, SPERM_MAX_ADDRESS
			IF SPERM_NAME_ARRAY:LOCAL == ""
				LOCAL:2 = LOCAL
				LOCAL --
				BREAK
			ENDIF
		NEXT
		SIF LOCAL == SPERM_MAX_ADDRESS
			THROW SPERM_NAME_ARRAY가 가득입니다

		SPERM_NAME_ARRAY:(LOCAL:2) = 불명
		BROTHER_SPERM_FLAG:(LOCAL:2) = 1
	ENDIF

	FOR LOCAL, 0, 후보수
		CFLAG:(후보:LOCAL):부친 = 아버지후보 >= 0 ? GET_ID(아버지후보) # LOCAL:2 * -1
		CFLAG:(후보:LOCAL):모친 = 어머니후보 >= 0 ? GET_ID(어머니후보) # LOCAL:2 * -1
	NEXT
	CFLAG:대상:부친 = 아버지후보 >= 0 ? GET_ID(아버지후보) # LOCAL:2 * -1
	CFLAG:대상:모친 = 어머니후보 >= 0 ? GET_ID(어머니후보) # LOCAL:2 * -1
	VARSET 후보
	VARSET 후보수
ENDIF

IF MASTER_EDIT_CHILD_CANDIDATE_NUM > 0
	;아이 후보의 아이도, 아이 후보로서 추가한다
	FOR LOCAL, 0, MASTER_EDIT_CHILD_CANDIDATE_NUM
		LOCAL:1 = ID_TO_CHARA(MASTER_EDIT_CHILD_CANDIDATE:LOCAL)
		IF LOCAL:1 >= 0
			후보:후보수 = LOCAL:1
			후보수 ++
			FOR LOCAL:2, 0, CHARANUM
				IF IS_PURE_BROTHER(LOCAL:1, LOCAL:2) && FINDELEMENT(후보, LOCAL:2, 0, 후보수)
					후보:후보수 = LOCAL:2
					후보수 ++
				ENDIF
			NEXT
		ENDIF
	NEXT

	FOR LOCAL, 0, SPERM_MAX_ADDRESS
		IF SPERM_NAME_ARRAY:LOCAL == ""
			LOCAL:2 = LOCAL
			LOCAL --
			BREAK
		ENDIF
	NEXT
	SIF LOCAL == SPERM_MAX_ADDRESS
		THROW SPERM_NAME_ARRAY가 가득입니다

	SPERM_NAME_ARRAY:(LOCAL:2) = 불명
	BROTHER_SPERM_FLAG:(LOCAL:2) = 1

	FOR LOCAL, 0, 후보수
		IF IS_MALE(대상)
			CFLAG:(후보:LOCAL):부친 = GET_ID(대상)
			CFLAG:(후보:LOCAL):모친 = LOCAL:2 * -1
			CALL VIRGINIZE_LOST(대상, 첫체험_동정, "불명")
			EXP:대상:Ｖ삽입경험 += 5

		ELSE
			CFLAG:(후보:LOCAL):부친 = LOCAL:2 * -1
			CFLAG:(후보:LOCAL):모친 = GET_ID(대상)
			CALL VIRGINIZE_LOST(대상, 첫체험_처녀, "불명")
			EXP:대상:Ｖ성교경험 += 5
			EXP:대상:질내사정경험 += 5
			EXP:대상:출산경험 += 1
		ENDIF
	NEXT
	VARSET 후보
	VARSET 후보수
ENDIF
