;-------------------------------------------------
;전 캐릭터의 현재 위치 이동 처리
;-------------------------------------------------
@MOVE_CHARA_CITY()
#DIM 대상
#DIM 대상현재위치
#DIM 후보위치
#DIM 대상소속
#DIM 후보위치주인
#DIM 현재위치주인
#DIM 포로처주인
#DIM 라이벌_ID

#DIM 세력
#DIM 부대

라이벌_ID = NO_TO_CHARA(RIVAL_ID)

CALL RIVAL_MOVE

FOR 대상, 0, CHARANUM
	대상현재위치 = CFLAG:대상:현재위치
	대상소속 = CFLAG:대상:소속
	현재위치주인 = CITY_OWNER:대상현재위치
	포로처주인 = CFLAG:대상:포로처
	
	;마스터는 스킵
	SIF 대상 == MASTER
		CONTINUE
	SIF 대상 == 라이벌_ID
		CONTINUE
	;사망이면 스킵
	SIF GROUPMATCH(CFLAG:대상:특수상태, 특수상태_사망) > 0
		CONTINUE
	
	;포로인 경우 현재위치가 포로 영지일 때 스킵
	IF 포로처주인 > 0 && CITY_OWNER:대상현재위치 == 포로처주인
		CONTINUE
	ENDIF
	
	;포로의 경우 현재위치가 포로영지가 아닐 때 포로 위치의 도시를 랜덤으로 설정
	IF 포로처주인 > 0 && CITY_OWNER:대상현재위치 != 포로처주인
		SIF GET_OWN_CITY(포로처주인) <= 0
			CONTINUE
			
		LOCAL:1 = RAND:(GET_OWN_CITY(포로처주인)) + 1
		LOCAL:2 = 0
		FOR 후보위치, 0, MAX_CITY
			SIF CITY_OWNER:(후보위치) == 포로처주인
				LOCAL:2 ++
			IF LOCAL:1 == LOCAL:2
				CFLAG:대상:현재위치 = 후보위치
				BREAK
			ENDIF
		NEXT
		CONTINUE
	ENDIF
	
	;방랑의 경우(MASTER는 예외)
	IF CFLAG:대상:특수상태 == 특수상태_방랑
		IF 대상현재위치 <= 0
			CFLAG:대상:현재위치 = RAND(0, CITY_NUM) + 1
			대상현재위치 = CFLAG:대상:현재위치
		ENDIF
		FOR LOCAL:0, 0, 10
			후보위치 = CITY_ROUTE:대상현재위치:(LOCAL:0)
			후보위치주인 = CITY_OWNER:(후보위치)
			SIF 후보위치 <= 0 || 후보위치주인 < 0
				CONTINUE
			
			IF RAND:100 > 40
				CFLAG:대상:현재위치 = 후보위치
				BREAK
			ENDIF
		NEXT
		CONTINUE
	ENDIF
	
	;현재위치가 0 이하이면서 특수상태가 정상적으로 속하고 있는 경우
	IF 대상현재위치 <= 0 && CFLAG:대상:특수상태 == 특수상태_소속중 && 대상소속 > 0
		SIF GET_OWN_CITY(대상소속) <= 0
			CONTINUE
		
		LOCAL:1 = RAND:(GET_OWN_CITY(대상소속)) + 1
		LOCAL:2 = 0
		FOR 후보위치, 0, MAX_CITY
			SIF CITY_OWNER:(후보위치) == 대상소속
				LOCAL:2 ++
			IF LOCAL:1 == LOCAL:2
				CFLAG:대상:현재위치 = 후보위치
				BREAK
			ENDIF
		NEXT
		CONTINUE
	ENDIF

	;자국 밖에 있을 경우 자국 영지로 랜덤 리셋
	IF CITY_OWNER:대상현재위치 != 대상소속 && 포로처주인 == 0 && CHECK_COUNTRY_RELATION_F(대상소속, 현재위치주인) <= 2
		SIF GET_OWN_CITY(대상소속) <= 0
			CONTINUE
		
		LOCAL:1 = RAND:(GET_OWN_CITY(대상소속)) + 1
		LOCAL:2 = 0
		FOR 후보위치, 0, MAX_CITY
			SIF CITY_OWNER:(후보위치) == 대상소속
				LOCAL:2 ++
			IF LOCAL:1 == LOCAL:2
				CFLAG:대상:현재위치 = 후보위치
				BREAK
			ENDIF
		NEXT
		CONTINUE
	ENDIF
	;도시에 있는 인접 이동 가능 도시로 이동하거나 머무른다
	FOR LOCAL:0, 0, 10
		후보위치 = CITY_ROUTE:대상현재위치:(LOCAL:0)
		후보위치주인 = CITY_OWNER:(후보위치)
		SIF 후보위치 <= 0 || 후보위치주인 < 0
			CONTINUE
		SIF 후보위치 == -1
			BREAK
		IF CHECK_COUNTRY_RELATION_F(대상소속, 후보위치주인) > 2
			IF RAND:100 > 40
				CFLAG:대상:현재위치 = 후보위치
				BREAK
			ENDIF
		ENDIF
	NEXT
NEXT

;부대에 소속되어 있는 경우
FOR 세력, 0, MAX_COUNTRY
	FOR 부대, 0, MAX_UNIT
		IF UNIT_SOLDIER:세력:부대 > 0
			FOR LOCAL:2, 0, MAX_UNIT_COMMANDER
				IF GET_UNIT_COMMANDER(세력, 부대, LOCAL:2) > -1
					대상 = GET_UNIT_COMMANDER(세력, 부대, LOCAL:2)
					;MASTER는 스킵
					SIF 대상 == MASTER
						CONTINUE
                    ;라이벌은 스킵
                    SIF 대상 == 라이벌_ID
                        CONTINUE
					;사망은 스킵
					SIF GROUPMATCH(CFLAG:대상:특수상태, 특수상태_사망) > 0
						CONTINUE
					CFLAG:대상:현재위치 = UNIT_POSITION:세력:부대
				ENDIF
			NEXT
		ENDIF
	NEXT
NEXT

;장수의 경우
FOR 후보위치, 0, MAX_CITY
	FOR 부대, 0, MAX_CITY_COMMANDER
		IF GET_CITY_COMMANDER(후보위치, 부대) == 대상
			대상 = GET_CITY_COMMANDER(후보위치, 부대)
			;MASTER는 스킵
			SIF 대상 == MASTER
				CONTINUE
            ;라이벌은 스킵
            SIF 대상 == 라이벌_ID
                CONTINUE
			;사망은 스킵
			SIF GROUPMATCH(CFLAG:대상:특수상태, 특수상태_사망) > 0
				CONTINUE
			CFLAG:대상:현재위치 = 후보위치
		ENDIF
	NEXT
NEXT

@RIVAL_MOVE
#DIM 라이벌_ID
#DIM 대상현재위치
#DIM 대상소속
#DIM 현재위치주인
#DIM 후보위치
#DIM 후보위치주인

라이벌_ID = NO_TO_CHARA(RIVAL_ID)


대상현재위치 = CFLAG:라이벌_ID:현재위치
대상소속 = CFLAG:라이벌_ID:소속
현재위치주인 = CITY_OWNER:대상현재위치

SIF CONFIG:501 == 1
	GOTO LOOP_0

;자국 밖에 있을 경우 자국 영지로 랜덤 리셋
IF CITY_OWNER:대상현재위치 != 대상소속
	SIF GET_OWN_CITY(대상소속) <= 0
		RETURN

	LOCAL:1 = RAND:(GET_OWN_CITY(대상소속)) + 1
	LOCAL:2 = 0
	FOR 후보위치, 0, MAX_CITY
		SIF CITY_OWNER:(후보위치) == 대상소속
			LOCAL:2 ++
		IF LOCAL:1 == LOCAL:2
			CFLAG:라이벌_ID:현재위치 = 후보위치
			LOCALS = %CITY_NAME:(CFLAG:라이벌_ID:현재위치)%
			IF CONFIG:509 == 1
				CALL SINGLE_DRAWLINE
				PRINTFORMW 라이벌이 %조사처리(LOCALS, "로")% 이동했습니다.
			ENDIF
			BREAK
		ENDIF
	NEXT
	RETURN
ENDIF
$LOOP_0
;도시에 있는 인접 이동 가능 도시로 이동하거나 머무른다
FOR LOCAL:0, 0, 10
	후보위치 = CITY_ROUTE:대상현재위치:(LOCAL:0)
	후보위치주인 = CITY_OWNER:(후보위치)
	SIF 후보위치 <= 0 || 후보위치주인 < 0
		RETURN
	
	SIF 후보위치 == -1
		RETURN
	IF CONFIG:501 == 0
		IF CHECK_COUNTRY_RELATION_F(대상소속, 후보위치주인) > 2
			IF RAND:100 > 40
				CFLAG:라이벌_ID:현재위치 = 후보위치
				IF CONFIG:509 == 1
					LOCALS = %CITY_NAME:(CFLAG:라이벌_ID:현재위치)%
					CALL SINGLE_DRAWLINE
					PRINTFORMW 라이벌이 %조사처리(LOCALS, "로")% 이동했습니다.
				ENDIF
				RETURN
			ENDIF
		ENDIF
	ELSE
		IF RAND:100 > 40
			CFLAG:라이벌_ID:현재위치 = 후보위치
			IF CONFIG:509 == 1
				LOCALS = %CITY_NAME:(CFLAG:라이벌_ID:현재위치)%
				CALL SINGLE_DRAWLINE
				PRINTFORMW 라이벌이 %조사처리(LOCALS, "로")% 이동했습니다.
			ENDIF
			RETURN
		ENDIF
	ENDIF
NEXT