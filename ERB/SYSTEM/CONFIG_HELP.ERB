@CREATE_HELP_MAP()
VARSET HELP_CONTENT_NAME
VARSET HELP_CONTENT_NAME_ENG
VARSET HELP_CONTENT_NUM

CALL REGISTER_HELP("게임의 흐름"           , "ABOUT_GAME_FLOW")
CALL REGISTER_HELP("능력치의 의미"           , "ABOUT_ABL")
CALL REGISTER_HELP("소질의 의미"          	, "ABOUT_TALENT")
CALL REGISTER_HELP("거점 페이즈의 커맨드" , "ABOUT_LIFE_PHASE_COMMAND")
CALL REGISTER_HELP("전략 페이즈의 커맨드" , "ABOUT_SLG_PHASE_COMMAND")
CALL REGISTER_HELP("초심자전용 가이드"       , "QUICKSTART")
CALL REGISTER_HELP("기본 전술"               , "BASIC_STRATEGY")
CALL REGISTER_HELP("사관을 늘린다"           , "GET_OFFICER")
CALL REGISTER_HELP("쿨 타임"           , "ABOUT_COOLTIME")
CALL REGISTER_HELP("외교에 대해"           , "ABOUT_DIPLOMACY")
CALL REGISTER_HELP("포로 조교의 사용방법"     , "HOW_TO_TRAIN_HOSTAGE")
CALL REGISTER_HELP("사관의 함락"             , "GET_TALENT")
CALL REGISTER_HELP("함락소질"               , "SPECIAL_TALENT")
CALL REGISTER_HELP("데일리 이벤트"       , "ABOUT_DAILY_EVENT")
CALL REGISTER_HELP("사관의 단련법"         , "HOW_TO_BUILD_CHARA")
CALL REGISTER_HELP("사관이 쉽게 화를 냄"   , "THEY_GET_MAD")
CALL REGISTER_HELP("사관이 쉽게 기절함"     , "THEY_EASYLY_GET_UNCONSCIOUS")
CALL REGISTER_HELP("호감도 보정"             , "ABOUT_LOVE_POWER")
CALL REGISTER_HELP("사관이 포로가 되었다"     , "THEY_GET_CAPTURED")
CALL REGISTER_HELP("각 세력에 굉장히 미움받는다" , "THEY_HATE_ME")
CALL REGISTER_HELP("특수한 세력"             , "SP_COUNTRY")
CALL REGISTER_HELP("병이 부족함"           , "SOLDIER_DOESNT_INCREASE")
CALL REGISTER_HELP("외교 요구가 통하지 않음"     , "THEY_WONT_ACCEPT_REQUEST")
CALL REGISTER_HELP("모반과 봉기"             , "THEY_REBEL_AGAINST_ME")
CALL REGISTER_HELP("주의해야 할 캐릭터"       , "BEWARE_HER")

@REGISTER_HELP(HELPNAME, HELPNAME_ENG, HELPPAGENUM)
#DIMS HELPNAME
#DIMS HELPNAME_ENG
#DIM HELPPAGENUM
HELP_CONTENT_NAME:HELP_CONTENT_NUM '= HELPNAME
HELP_CONTENT_NAME_ENG:HELP_CONTENT_NUM '= HELPNAME_ENG
HELP_CONTENT_NUM ++
;-------------------------------------------------
;공략 정보 메뉴
;-------------------------------------------------
@CONFIG_HELPMENU
#DIM FIRST_LINE
#DIM FIRST_LINE2

;지금 보고 있는 메뉴의 페이지
#DIM MENUPAGE
;지금 보고 있는 헬프의 HELP_CONTENT_계 변수에 있어서의 인덱스
#DIM HELPINDEX
;지금 보고 있는 헬프 항목의 페이지
#DIM HELPPAGE
;현재의 REDRAW를 보존
#DIM NOWREDRAW

;메뉴의 페이지수
#DIM PAGENUM

;헬프의 행수
#DIM MAXHELPLINE
;페이지 근처의 컨텐츠수
#DIM CONTENTPERPAGE

FIRST_LINE = LINECOUNT
MENUPAGE = 1
HELPINDEX = 0
HELPPAGE = 1
CURRENTREDRAW
NOWREDRAW = RESULT:0

CONTENTPERPAGE = 5
PAGENUM = HELP_CONTENT_NUM / CONTENTPERPAGE + (HELP_CONTENT_NUM % CONTENTPERPAGE ? 1 # 0)

;커스터마이즈용……헬프의 페이지 적중행수
MAXHELPLINE = 20

VARSET HELP_CONTENT_LINE
VARSET HELP_CONTENT_LINE_NUM

;초기화 처리
CALLFORM PREPARE_HELP_CONTENT_%HELP_CONTENT_NAME_ENG:HELPINDEX%()

$SHOW_LOOP
REDRAW 0

CALL SINGLE_DRAWLINE
PRINT ●공략 정보●
PRINTBUTTON "    1000[이전페이지]", 1000
PRINTFORM     페이지 {MENUPAGE} / {PAGENUM}
PRINTBUTTON "    1001[다음페이지]", 1001
PRINTL 

CALL SINGLE_DRAWLINE

FOR LOCAL, CONTENTPERPAGE * (MENUPAGE - 1), MIN(HELP_CONTENT_NUM, CONTENTPERPAGE * MENUPAGE)
	CALL HELP_COLORBUTTON(HELPINDEX, LOCAL, HELP_CONTENT_NAME:LOCAL)
NEXT

IF LINECOUNT - FIRST_LINE < CONTENTPERPAGE + 3
	LOCAL:1 = (CONTENTPERPAGE + 3) - (LINECOUNT - FIRST_LINE)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTL 
	NEXT
ENDIF

CALL SINGLE_DRAWLINE
FIRST_LINE2 = LINECOUNT
CALL SHOW_HELP_CONTENT_VIEW(HELPINDEX, HELPPAGE, MAXHELPLINE)

IF LINECOUNT - FIRST_LINE2 < MAXHELPLINE + 3
	LOCAL:1 = MAXHELPLINE - (LINECOUNT - FIRST_LINE2)
	FOR LOCAL:0, 0, LOCAL:1
		PRINTL 
	NEXT
ENDIF
CALL SINGLE_DRAWLINE
PRINTBUTTON "    2000[이전페이지]", 2000
PRINTFORM     페이지 {HELPPAGE} / {HELP_CONTENT_LINE_NUM / MAXHELPLINE + 1}
PRINTBUTTON "    2001[다음페이지]", 2001
PRINTL
CALL SINGLE_DRAWLINE
PRINTBUTTON " 999[ 돌아온다]", 999
PRINTL 

$INPUT_LOOP
INPUT

IF RESULT == 999
	REDRAW NOWREDRAW
	CLEARLINE LINECOUNT - FIRST_LINE
	RETURN

ELSEIF RESULT == 1000
	MENUPAGE -= 1
	IF MENUPAGE < 1
		MENUPAGE = PAGENUM
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 1001
	MENUPAGE += 1
	IF MENUPAGE > PAGENUM
		MENUPAGE = 1
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 2000
	HELPPAGE --
	SIF HELPPAGE < 1
		HELPPAGE = (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
	CLEARLINE LINECOUNT - FIRST_LINE

ELSEIF RESULT == 2001
	HELPPAGE ++
	SIF HELPPAGE > (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
		HELPPAGE = 1
	CLEARLINE LINECOUNT - FIRST_LINE
ELSEIF INRANGE(RESULT, 0, HELP_CONTENT_NUM - 1)
	HELPINDEX = RESULT
	HELPPAGE = 1
	VARSET HELP_CONTENT_LINE
	VARSET HELP_CONTENT_LINE_NUM
	CALLFORM PREPARE_HELP_CONTENT_%HELP_CONTENT_NAME_ENG:HELPINDEX%()
	CLEARLINE LINECOUNT - FIRST_LINE
ELSE
	GOTO INPUT_LOOP
ENDIF
GOTO SHOW_LOOP

;-------------------------------------------------
;헬프의 컨텐트 부분의 표시
;-------------------------------------------------
@SHOW_HELP_CONTENT_VIEW(HELPINDEX, HELPPAGE, MAXHELPLINE)
#DIM HELPINDEX
#DIM HELPPAGE
#DIM MAXHELPLINE

SIF !INRANGE(HELPINDEX, 0, HELP_CONTENT_NUM - 1)
	HELPINDEX = 0
SIF HELPPAGE > (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1
	HELPPAGE = 1
SIF HELPPAGE < 1
	HELPPAGE = (HELP_CONTENT_LINE_NUM - 1) / MAXHELPLINE + 1

PRINTFORML %HELP_CONTENT_NAME:HELPINDEX%
CALL SINGLE_DRAWLINE
FOR LOCAL, (HELPPAGE - 1) * MAXHELPLINE, HELPPAGE * MAXHELPLINE
	CALL PRINT_HELP_CONTENT(HELP_CONTENT_LINE:LOCAL, LOCAL == 0)
NEXT


;-------------------------------------------------
;헬프의 내용을 추가하는 함수
;-------------------------------------------------
@ADD_HELP_LINE(LINE)
#DIMS LINE
HELP_CONTENT_LINE:HELP_CONTENT_LINE_NUM '= LINE
HELP_CONTENT_LINE_NUM ++

;-------------------------------------------------
;헬프의 각 행을 묘화 하는 함수
;-------------------------------------------------
@PRINT_HELP_CONTENT(ORIGSTR, INDENTRESET)
#DIMS ORIGSTR
#DIMS SEPSTR, 1000
#DIM SHORTHANDFLAG, 1000
#DIM POINTER
#DIM SEPNUM
#DIM SHORTHANDPOINT,3
#DIM SHORTHANDAREA, 2
#DIM SHORTHANDTYPE
#DIM CONST 쇼트핸드_랭크 = 1
#DIM CONST 쇼트핸드_컨텐트 = 2
#DIM CONST 쇼트핸드_수치 = 3
#DIMS CONST SHORTHAND_START_CHARA = "", "<", "`", "'"
#DIMS CONST SHORTHAND_END_CHARA = "", ">", "`", "'"
#DIM INDENTFLAG
#DIM INDENTRESET

VARSET SEPSTR
VARSET SHORTHANDFLAG
VARSET POINTER
VARSET SEPNUM
VARSET SHORTHANDPOINT
VARSET SHORTHANDAREA
VARSET SHORTHANDTYPE

PRINT    

;빈 문자열 또는 인덴트 리셋트의 경우, 인덴트를 해제한다
IF ORIGSTR == "" || INDENTRESET == 1
	INDENTFLAG = 0
	IF ORIGSTR == ""
		PRINTFORML
		RETURN
	ENDIF
ENDIF


WHILE 1

	FOR LOCAL, 0, VARSIZE("SHORTHANDPOINT")
		SHORTHANDPOINT:LOCAL = STRFINDU(ORIGSTR, SHORTHAND_START_CHARA:(LOCAL + 1), POINTER)
	NEXT

	;이제 쇼트 핸드가 없다
	IF ALLSAMES(SHORTHANDPOINT:0, SHORTHANDPOINT:1, SHORTHANDPOINT:2)
		SUBSTRINGU ORIGSTR, POINTER, -1
		IF RESULTS != ""
			SEPSTR:SEPNUM = %RESULTS%
			SEPNUM ++
		ENDIF
		BREAK
	ENDIF

	FOR LOCAL, 0, VARSIZE("SHORTHANDPOINT")
		SIF SHORTHANDPOINT:LOCAL == -1
			SHORTHANDPOINT:LOCAL = __INT_MAX__
	NEXT

	;쇼트 핸드가 있는 경우
	SHORTHANDAREA:0 = MINARRAY(SHORTHANDPOINT)
	;쇼트 핸드의 종류를 취득
	SHORTHANDTYPE = FINDELEMENT(SHORTHAND_START_CHARA, SUBSTRINGU(ORIGSTR, SHORTHANDAREA:0, 1))
	;쇼트 핸드의 종료 위치를 취득
	SHORTHANDAREA:1 = STRFINDU(ORIGSTR, SHORTHAND_END_CHARA:SHORTHANDTYPE, SHORTHANDAREA:0 + 1)

	SUBSTRINGU ORIGSTR, POINTER, SHORTHANDAREA:0 - POINTER
	SEPSTR:SEPNUM = %RESULTS%
	SEPNUM ++
	SUBSTRINGU ORIGSTR, SHORTHANDAREA:0 + 1, SHORTHANDAREA:1 - SHORTHANDAREA:0 - 1
	SEPSTR:SEPNUM = %RESULTS%
	SHORTHANDFLAG:SEPNUM = SHORTHANDTYPE
	SEPNUM ++
	POINTER = SHORTHANDAREA:1 + 1
WEND


SIF INDENTFLAG && FINDELEMENT(SHORTHANDFLAG, 쇼트핸드_컨텐트) == -1
	PRINTFORM %HELP_CONTENTS_TOSTR_SPACE()%


FOR LOCAL, 0, SEPNUM
	SELECTCASE SHORTHANDFLAG:LOCAL
		CASE 쇼트핸드_컨텐트
			INDENTFLAG = 1
			PRINTFORM %HELP_CONTENTS(SEPSTR:LOCAL)%
		CASE 쇼트핸드_수치
			PRINTFORM %HELP_NUMBERCOLOR(SEPSTR:LOCAL)%
		CASE 쇼트핸드_랭크
			PRINTFORM %HELP_RANKCOLOR(SEPSTR:LOCAL)%
		CASEELSE
			PRINTFORM %SEPSTR:LOCAL%
	ENDSELECT
NEXT

PRINTL

;-------------------------------------------------
;디자인 통일용 함수
;표시중의 메뉴를 색인화성으로 하는 버튼
; ARG:0 =표시중의 페이지
; ARG:1 =버튼 번호
; ARGS:2 =타이틀
;-------------------------------------------------
@HELP_COLORBUTTON(ARG:0, ARG:1, ARGS:2)
IF ARG:0 == ARG:1
	SETCOLOR 칼라_선택중
ENDIF
PRINTFORML [{ARG:1, 2}] %ARGS:2%
RESETCOLOR
RETURN

;-------------------------------------------------
;디자인 통일용 식 안의 함수
;소질명등을 좌측으로 타이틀로서 표시할 때
; ARGS:0 =타이틀
; %HELP_CONTENTS("연모")%
;-------------------------------------------------
@HELP_CONTENTS(ARGS:0)
#FUNCTIONS
SETCOLOR 칼라_선택불가
PRINT · 
RESETCOLOR
LOCALS:0 = %ARGS:0%
PRINTFORM %LOCALS:0, 14, LEFT%
SETCOLOR 칼라_선택불가
PRINT  - 
RESETCOLOR
RETURNF

;-------------------------------------------------
;디자인 통일용 식 안의 함수
;소질명등을 좌측으로 타이틀로서 표시할 때
;설명문이 2행에 걸치는 경우에 왼쪽에 비워두는 공간
; %HELP_CONTENTS_TOSTR_SPACE()%
;-------------------------------------------------
@HELP_CONTENTS_TOSTR_SPACE()
#FUNCTIONS
PRINTFORM %TOSTR_SPACE(20)%
RETURNF

;-------------------------------------------------
;디자인 통일용 식 안의 함수
;숫자를 강조한다
; ARGS:0 =숫자
; %HELP_RANK 칼라 _S%
;-------------------------------------------------
@HELP_NUMBERCOLOR(ARGS:0)
#FUNCTIONS
SETCOLOR 칼라_선택중
PRINTFORM %ARGS:0%
RESETCOLOR
RETURNF

;-------------------------------------------------
;디자인 통일용 식 안의 함수
;랭크 표시에 색을 칠한다（@PRINT_ALPHABET_RANK에 준거）
; ARGS:0 =랭크（전각）
; %HELP_RANK 칼라 _S%
;-------------------------------------------------
@HELP_RANKCOLOR(ARGS:0)
#FUNCTIONS

SETCOLOR GET_RANK_COLOR(ARGS:0)
PRINTFORM %ARGS:0%
RESETCOLOR
RETURNF

