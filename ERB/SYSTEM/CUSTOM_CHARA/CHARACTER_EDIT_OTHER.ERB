@CHARACTER_EDIT_UI_OTHER(대상)
#DIM 대상

IF FLAG:주회수 > 0
	PRINTFORM 소속:
	IF CFLAG:대상:소속 == 0
		IF CFLAG:대상:특수상태 == 특수상태_사망
			PRINTFORM 무소속(사망)          
		ELSEIF CFLAG:대상:특수상태 == 특수상태_방랑
			PRINTFORM 무소속(방랑)          
		ELSE
			PRINTFORM 무소속(미등장)        
		ENDIF
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
		SETCOLOR 칼라_선택불가
		PRINTPLAIN ※군주는 변경 불가※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[변경] ", 0)
		CALL PRINTBUTTON_EX("[일람] ", 1)
	ENDIF
	PRINTL

	PRINTFORM 포로:
	IF CFLAG:대상:포로처 == 0
		PRINTFORM %"없음", MAX_CHARANAME_LENGTH + 4, LEFT%
	ELSE
		PRINTFORM %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:포로처)), MAX_CHARANAME_LENGTH + 4, LEFT%
	ENDIF
	IF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
		SETCOLOR 칼라_선택불가
		PRINTPLAIN ※군주는 변경 불가※
		RESETCOLOR
	ELSE
		CALL PRINTBUTTON_EX("[변경] ", 10)
		CALL PRINTBUTTON_EX("[일람] ", 11)
	ENDIF
	PRINTL
	CALL COLOR_LINE
ENDIF
;특수 처리
PRINTBUTTON "[      성별]:", 100
PRINTFORML %TOSTR_SEX(TALENT:대상:GETNUM(TALENT, "성별"))%
PRINTBUTTON "[      음모]:", 105
PRINTFORML %GET_INMOU_NAME(TALENT:대상:음모현재치)%
PRINTBUTTON "[겨드랑이털]:", 106
PRINTFORML %GET_ARMPIT_NAME(TALENT:대상:겨드랑이털현재치)%
PRINTBUTTON "[      문신]:", 107
CALL PRINT_TATTOO(대상)
PRINTL
PRINTBUTTON "[    피어스]:", 108
CALL PRINT_PIERCE(대상)
PRINTL
CALL COLOR_LINE
SIF TALENT:대상:웨폰 == 0
	TALENT:대상:웨폰 = 100
CALL FACE_IMAGE(대상, 7, 2)
PRINTBUTTON "[◁]", 109
PRINT [프로필사진]
PRINTBUTTON "[▷]", 110
PRINT     
PRINTBUTTON @"[웨폰:%WEAPON_NAME(TALENT:대상:웨폰)%]", 111
PRINTL
CALL COLOR_LINE
PRINTFORM 태그:
FOR LOCAL, 0, VARSIZE("TAG_NAME")
	CALL PRINT_TOGGLE_BUTTON(@"[%TAG_NAME:LOCAL%]", 1000 + LOCAL, HAS_TAG(대상, LOCAL))
NEXT
PRINTL
CALL COLOR_LINE

CALL SHOW_BODYSIZE(대상)
SIF HAS_VAGINA(대상)
	PRINTFORM 유방:%TOSTR_BUSTSIZE(GET_BUSTSIZE(대상)), 12, LEFT%
SIF HAS_PENIS(대상)
	PRINTFORM 음경:%TOSTR_PENISSIZE(GET_PENISSIZE(대상)), 12, LEFT%
PRINTFORML 엉덩이:%TOSTR_HIPSIZE(GET_HIPSIZE(대상)), 12, LEFT%

IF HAS_VAGINA(대상)
	PRINTFORM 질  :%TOSTR_TIGHTNESS(TALENT:(대상):Ｖ조임), 12, LEFT% 
ENDIF
PRINTFORM 애널:%TOSTR_TIGHTNESS(TALENT:(대상):Ａ조임), 12, LEFT% 
PRINTL
PRINT 머리색:
CALL PRINT_HAIR_COLOR(TALENT:대상:머리색) 
PRINTFORML 머리길이:%TOSTR_HAIR_LENGTH(TALENT:대상:머리길이)%
PRINTBUTTON "[보디 설정]", 101

@CHARACTER_EDIT_INPUT_OTHER(대상, 입력치)
#DIM 대상
#DIM 입력치

SELECTCASE RESULT
	;성별
	CASE 100
		LOCAL:1 = HAS_PENIS(대상)
		LOCAL:2 = IS_MALE(대상)
		TALENT:대상:성별 = ROUND_INCREMENT(TALENT:대상:성별, 0, 5)
		SIF LOCAL:2 != IS_MALE(대상)
			CALL SET_BODYSIZE(대상)
		SIF LOCAL:1 == 0 && HAS_PENIS(대상)
			CALL SET_PENISSIZE(대상)
		TALENT:대상:커스텀페이스 = 0
	;가슴
	CASE 101
		CALL BEAUTIFY(대상)
	;음모
	CASE 105
		CALL ASK_MULTI("백보지", "깔끔", "손질됨", "솜털", "살짝", "표준", "많은", "주렁주렁", "무식하게", "강모", "엉덩이까지", "♥")
		TALENT:대상:음모현재치 = RESULT -1
		TALENT:대상:음모목표치 = RESULT - 1
	;겨드랑이털
	CASE 106
		CALL ASK_MULTI("제모", "깔끔", "손질됨", "손질됨", "살짝", "보통", "무정", "주렁주렁", "무식하게", "강모", "밀림")
		TALENT:대상:겨드랑이털현재치 = RESULT -1
		TALENT:대상:겨드랑이털목표치 = RESULT - 1
	CASE 107
		CALL TATTOO_MENU(대상, 1)
	CASE 108
		CALL PIERCE_MENU(대상, 1)
	CASE 109
		IF TALENT:대상:커스텀페이스 == 0
			IF IS_MALE(대상)
				TALENT:대상:커스텀페이스 = MOB_PACE_M
			ELSEIF IS_FEMALE(대상)
				TALENT:대상:커스텀페이스 = MOB_PACE_F
			ENDIF
		ELSEIF TALENT:대상:커스텀페이스 == 1
			TALENT:대상:커스텀페이스 = 101
		ELSEIF TALENT:대상:커스텀페이스 == 101
			TALENT:대상:커스텀페이스 = 0
		ELSE
			TALENT:대상:커스텀페이스 --
		ENDIF
	CASE 110
		IF (IS_MALE(대상) && TALENT:대상:커스텀페이스 == CUSTOM_PACE_M + 100) || (IS_FEMALE(대상) && TALENT:대상:커스텀페이스 == CUSTOM_PACE_F + 100)
			TALENT:대상:커스텀페이스 = 1
		ELSEIF TALENT:대상:커스텀페이스 == 0
			TALENT:대상:커스텀페이스 = 101
		ELSEIF (IS_MALE(대상) && TALENT:대상:커스텀페이스 == MOB_PACE_M) || (IS_FEMALE(대상) && TALENT:대상:커스텀페이스 == MOB_PACE_F)
			TALENT:대상:커스텀페이스 = 0
		ELSE
			TALENT:대상:커스텀페이스 ++
		ENDIF
	CASE 111
		CALL WEAPON_MENU(대상)
	CASE 1000 TO 1000 + VARSIZE("TAG_NAME")
		CALL INVERT_TAG(대상, 입력치 - 1000)
ENDSELECT

IF FLAG:주회수 > 0
	SELECTCASE 입력치
		CASE 0
			SIF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
				RETURN
			IF CFLAG:대상:소속 == 0 && CFLAG:대상:특수상태 == 0 && !CFLAG:대상:포로처
				CFLAG:대상:특수상태 = 특수상태_방랑
			ELSEIF CFLAG:대상:소속 == 0 && CFLAG:대상:특수상태 == 특수상태_방랑
				CFLAG:대상:특수상태 = 특수상태_사망
			ELSE
				LOCAL:1 = 0
				FOR LOCAL, CFLAG:대상:소속 + 1, MAX_COUNTRY
					SIF !IS_COUNTRY(LOCAL)
						CONTINUE
					CALL CHANGE_COUNTRY(대상, LOCAL)
					LOCAL:1 = 1
					BREAK
				NEXT
				IF LOCAL:1 == 0
					CALL CHANGE_COUNTRY(대상, 0)
					CFLAG:대상:특수상태 = 0
				ENDIF
			ENDIF
		CASE 1
			SIF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] 무소속(미등장)
			PRINTFORML [{MAX_COUNTRY + 1}] 무소속(방랑)
			PRINTFORML [{MAX_COUNTRY + 2}] 무소속(사망)
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CHANGE_COUNTRY(대상, RESULT, 0)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CHANGE_COUNTRY(대상, 0, 1)
				CFLAG:대상:특수상태 = 0
			ELSEIF RESULT == MAX_COUNTRY + 1
				CALL CHANGE_COUNTRY(대상, 0, 1)
			ELSEIF RESULT == MAX_COUNTRY + 2
				CALL CHANGE_COUNTRY(대상, 0, 1, 1)
			ENDIF
		CASE 10
			SIF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
				RETURN
			LOCAL:1 = 0
			FOR LOCAL:0, CFLAG:대상:포로처 + 1, MAX_COUNTRY
				IF IS_COUNTRY(LOCAL)
					CALL CAPTURE(대상, LOCAL)
					LOCAL:1 = 1
					BREAK
				ENDIF
			NEXT
			SIF LOCAL:1 == 0
				CALL CAPTURE(대상, 0)
		CASE 11
			SIF GET_COUNTRY_BOSS(CFLAG:대상:소속) == 대상
				RETURN
			FOR LOCAL, 0, MAX_COUNTRY
				SIF !IS_COUNTRY(LOCAL)
					CONTINUE
				PRINTFORML [{LOCAL, 2, RIGHT}] %ANAME(GET_COUNTRY_BOSS(LOCAL))%
			NEXT
			PRINTFORML [{MAX_COUNTRY}] 없음
			INPUT
			IF INRANGE(RESULT, 0, MAX_COUNTRY - 1)
				CALL CAPTURE(대상, RESULT)
			ELSEIF RESULT == MAX_COUNTRY
				CALL CAPTURE(대상, 0)
			ENDIF      
	ENDSELECT
ENDIF
