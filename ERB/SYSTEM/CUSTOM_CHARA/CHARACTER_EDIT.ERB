@CHARACTER_EDIT(대상, 변환가능 = 0)
#DIM 대상
#DIM 변환가능
#DIM 현재탭
#DIMS CONST 탭영명 = "NAME", "BASE", "ABL",  "TALENT", "EXP",  "MARK", "SKILL", "OTHER", "INFO"
#DIMS CONST 탭명   = "이름", "기초", "능력", "소질",   "경험", "함락", "스킬", "외", "정보"
#DIM FIRST_LINE, 2
#DIM CREDRAW

CREDRAW = CURRENTREDRAW()
REDRAW 0

$SHOW_LOOP

FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML %조사처리(ANAME(대상),"를")% 편집중
CALL SINGLE_DRAWLINE
FIRST_LINE:1 = LINECOUNT
CALLFORM CHARACTER_EDIT_UI_%탭영명:현재탭%(대상)
FOR LOCAL, 0, FIRST_LINE:1 + 25 - LINECOUNT
	PRINTL
NEXT
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, VARSIZE("탭명")
	PRINTBUTTON @"[%탭명:LOCAL%]", 10000 + LOCAL
	PRINT  
NEXT
PRINTL

IF 변환가능
	CALL SINGLE_DRAWLINE
	PRINTBUTTON "[이전캐릭터]", 20000
	PRINT  
	PRINTBUTTON "[다음캐릭터]", 20001
	PRINTL
ENDIF

CALL SINGLE_DRAWLINE
PRINTBUTTON "[돌아온다]", 30000

INPUT

IF RESULT == 30000
	REDRAW CREDRAW
	RETURN
ELSEIF RESULT == 20000 && 변환가능
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				대상 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 대상
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 --
		SIF LOCAL:0 < 0
			LOCAL:0 = CHARANUM - 1
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF RESULT == 20001 && 변환가능
	LOCAL:0 = 0
	LOCAL:1 = 0
	WHILE 1
		LOCAL:2 = SHOP_CHARA_LIST:(LOCAL:0)
		IF LOCAL:1
			IF LOCAL:2 != -1
				대상 = SHOP_CHARA_LIST:(LOCAL:0)
				BREAK
			ENDIF
		ELSEIF LOCAL:2 == 대상
			LOCAL:1 = 1
		ENDIF
		LOCAL:0 ++
		SIF LOCAL:0 >= CHARANUM
			LOCAL:0 = 0
	WEND
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ELSEIF INRANGE(RESULT, 10000, 10000 + VARSIZE("탭명") - 1)
	현재탭 = RESULT - 10000
	IF 탭명:현재탭 == "정보"
		현재탭 = 0
		CALL SHOW_INFO_WITH_UI(대상)
	ELSEIF 탭명:현재탭 == "스킬"
		현재탭 = 0
		CALL SHOP_LIFE_USERSHOP41(대상, 1)
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

CALLFORM CHARACTER_EDIT_INPUT_%탭영명:현재탭%(대상, RESULT)

CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP

