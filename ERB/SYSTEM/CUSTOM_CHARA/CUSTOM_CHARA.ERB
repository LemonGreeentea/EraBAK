;---------------------------------------
;커스텀 캐릭터용 메뉴
;---------------------------------------
@CUSTOM_CHARA_MENU
#DIM 대상
#DIM FIRST_LINE
#DIM 페이지
LOADGLOBAL
;랜덤 캐릭터의 이름 후보 리스트를 작성
CALL CREATE_NAME_LIST
;랭크 역치의 생성
CALL INIT_RANK
;능력치의 최대 최소의 설정
CALL SET_ABL_MIN_MAX
;문신의 랜덤 입력 내용 리스트를 작성
CALL CREATE_TATTOO_LIST

;모든 고정 캐릭터와 「당신」를 작성한다
FOR LOCAL:0, 0, MAX_FIXED_CHARA
	;CSV 파일의 존재 판정
	SIF EXISTCSV(LOCAL:0)
		CALL ADD_CHARA_FROM_CSV(LOCAL:0)
NEXT

$END_INIT
FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML 커스텀 캐릭터 관리 화면
CALL SINGLE_DRAWLINE

CALL LISTUP_CUSTOM_CHARACTER(0, 페이지, 20)
CALL SINGLE_DRAWLINE
PRINTBUTTON "[종료]", 10000


INPUT

IF RESULT == 10000
	DELALLCHARA
	RETURN
ELSEIF RESULT == MAX_CUSTOM_CHARA
	페이지 = ROUND_DECREMENT(페이지, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF RESULT == MAX_CUSTOM_CHARA + 1
	페이지 = ROUND_INCREMENT(페이지, 0, MAX_CUSTOM_CHARA / 20)
ELSEIF INRANGE(RESULT, 0, MAX_CUSTOM_CHARA)
	LOCAL = RESULT
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CALL LOAD_CUSTOM_DATA(LOCAL)
		CALL EDIT_CUSTOM_DATA(LOCAL, RESULT)
	ELSE
		CALL INIT_CUSTOM_DATA()
		CALL EDIT_CUSTOM_DATA(LOCAL, RESULT)
	ENDIF
ENDIF
CLEARLINE FIRST_LINE - LINECOUNT
GOTO END_INIT

;---------------------------------------
;커스텀 캐릭터의 리스트업
;---------------------------------------
@LISTUP_CUSTOM_CHARACTER(번호 = 0, 페이지 = 0, 최대수 = 20)
#DIM 번호
#DIM 페이지
#DIM 최대수
FOR LOCAL, 페이지 * 최대수, MIN(페이지 * 최대수 + 최대수, MAX_CUSTOM_CHARA)
	IF FIND_CHARADATA(TOSTR(LOCAL))
		CHKCHARADATA RESULTS:0
		PRINTBUTTON @"[%RESULTS:0%]", LOCAL + 번호
	ELSE
		PRINTBUTTON "[신규 작성]", LOCAL + 번호
	ENDIF
	PRINTL
NEXT
PRINTBUTTON "[이전페이지]", 번호 + MAX_CUSTOM_CHARA
PRINTFORM {페이지 + 1}/{MAX_CUSTOM_CHARA / 최대수 + 1}
PRINTBUTTON "[다음페이지]", 번호 + MAX_CUSTOM_CHARA + 1
PRINTL
