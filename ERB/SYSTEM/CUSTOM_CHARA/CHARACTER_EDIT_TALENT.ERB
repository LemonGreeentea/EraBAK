@CHARACTER_EDIT_UI_TALENT(대상)
#DIM 대상
LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF CHARACTER_EDIT_IGNORE_TALENT(LOCAL)
		CONTINUE
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	CALL PRINT_TOGGLE_BUTTON(@"%"[" + TALENTNAME:(LOCAL) + "]" , 14, LEFT%", LOCAL, TALENT:대상:LOCAL == 1)
	PRINT  
	LOCAL:1 ++

	SIF LOCAL:1 != 0 && LOCAL:1 % 6 == 0
		PRINTL
NEXT
PRINTL

CALL COLOR_LINE
PRINTBUTTON "[성장형]:", GETNUM(TALENT, "성장형")
PRINTFORML %TOSTR_GROWTYPE(TALENT:대상:GETNUM(TALENT, "성장형"))%
CALL PRINT_TOGGLE_BUTTON("[쵸로인]", GETNUM(TALENT, "쵸로인"), TALENT:대상:쵸로인)
PRINTL
PRINTBUTTON "[위험일]:", GETNUM(TALENT, "위험일")
PRINTFORML %TOFULL(TOSTR(TALENT:대상:위험일))%, %TOFULL(TOSTR(TALENT:대상:위험일 + 5))%
CALL PRINT_TOGGLE_BUTTON("[키스]", GETNUM(TALENT, "키스미경험"), TALENT:대상:키스미경험)
CALL PRINT_TOGGLE_BUTTON("[처녀]", GETNUM(TALENT, "처녀"), TALENT:대상:처녀)
CALL PRINT_TOGGLE_BUTTON("[애널처녀]", GETNUM(TALENT, "애널처녀"), TALENT:대상:애널처녀)
CALL PRINT_TOGGLE_BUTTON("[동정]", GETNUM(TALENT, "동정"), TALENT:대상:동정)
PRINTL

@CHARACTER_EDIT_INPUT_TALENT(대상, 입력치)
#DIM 대상
#DIM 입력치
;가드절
SIF (CHARACTER_EDIT_IGNORE_TALENT(입력치) && !GROUPMATCH(입력치, GETNUM(TALENT, "처녀"), GETNUM(TALENT, "애널처녀"), GETNUM(TALENT, "키스미경험"), GETNUM(TALENT, "동정"), GETNUM(TALENT, "위험일"))) || TALENTNAME:입력치 == ""
	RETURN 0

IF 입력치 == GETNUM(TALENT, "성장형")
	TALENT:대상:입력치 = ROUND_INCREMENT(TALENT:대상:입력치, 0, 9)
ELSEIF 입력치 == GETNUM(TALENT, "위험일")
	PRINTL 0～4로 입력
	INPUT
	TALENT:대상:위험일 = LIMIT(RESULT, 0, 4)
ELSEIF 입력치 == GETNUM(TALENT, "쵸로인")
	TALENT:대상:입력치 = !TALENT:대상:입력치
ELSEIF GROUPMATCH(입력치, GETNUM(TALENT, "처녀"), GETNUM(TALENT, "애널처녀"), GETNUM(TALENT, "키스미경험"), GETNUM(TALENT, "동정"))
	TALENT:대상:입력치 = !TALENT:대상:입력치
	SELECTCASE 입력치
		CASE GETNUM(TALENT, "처녀")
			IF TALENT:대상:처녀
				CALL VIRGINIZE(대상, 첫체험_처녀)
			ELSE
				CALL VIRGINIZE_LOST(대상, 첫체험_처녀)
			ENDIF
		CASE GETNUM(TALENT, "애널처녀")
			IF TALENT:대상:애널처녀
				CALL VIRGINIZE(대상, 첫체험_애널처녀)
			ELSE
				CALL VIRGINIZE_LOST(대상, 첫체험_애널처녀)
			ENDIF
		CASE GETNUM(TALENT, "키스미경험")
			IF TALENT:대상:키스미경험
				CALL VIRGINIZE(대상, 첫체험_키스)
			ELSE
				CALL VIRGINIZE_LOST(대상, 첫체험_키스)
			ENDIF
		CASE GETNUM(TALENT, "동정")
			IF TALENT:대상:동정
				CALL VIRGINIZE(대상, 첫체험_동정)
			ELSE
				CALL VIRGINIZE_LOST(대상, 첫체험_동정)
			ENDIF
	ENDSELECT
ELSEIF INRANGE(입력치, 0, 299) && 입력치 != GETNUM(TALENT, "성별")
	TALENT:대상:입력치 = !TALENT:대상:입력치
ENDIF

SIF !TALENT:대상:신비지식
	ABL:대상:신비 = 0

SIF !TALENT:대상:모유체질
	CFLAG:대상:일시적모유체질 = 0

@CHARACTER_EDIT_IGNORE_TALENT(ARG:0)
#FUNCTION
{
RETURNF GROUPMATCH(ARG:0, GETNUM(TALENT, "성별"), GETNUM(TALENT, "처녀"), GETNUM(TALENT, "애널처녀"), GETNUM(TALENT, "키스미경험"), GETNUM(TALENT, "동정"), GETNUM(TALENT, "연모"), GETNUM(TALENT, "친애"), GETNUM(TALENT, "연인"), GETNUM(TALENT, "복종"), GETNUM(TALENT, "예속"), GETNUM(TALENT, "낙인"), GETNUM(TALENT, "친구"), GETNUM(TALENT,"주인"), GETNUM(TALENT,"소유자"),
GETNUM(TALENT, "붕괴"), GETNUM(TALENT, "공허"), GETNUM(TALENT, "정실"), GETNUM(TALENT, "첩"), GETNUM(TALENT, "임신"), GETNUM(TALENT, "육아중"), GETNUM(TALENT, "유아"),
GETNUM(TALENT, "촉수임신"), GETNUM(TALENT, "Ａ촉수임신"), GETNUM(TALENT, "아이인질"), GETNUM(TALENT, "잉태가능"), GETNUM(TALENT, "모성"), GETNUM(TALENT, "위험일"), GETNUM(TALENT, "합의"))
}