;-------------------------------------------------
;콘피그의 초기설정
;-------------------------------------------------
@INIT_CONFIG_DAILY
DAILY_EVENT_COUNT = 2
KOJO_DAILY_EVENT_COUNT = 2
DAILY_EVENT_ODDS = 1
KOJO_DAILY_EVENT_ODDS = 1

;-------------------------------------------------
;ＳＬＧ부분에 관한 설정
;-------------------------------------------------
@CONFIG_EVENT_DAILY
#DIM 콘피그소속, DAILY_GENRE_NUM, 299
#DIM 이벤트수, DAILY_GENRE_NUM
#DIM 표시, DAILY_GENRE_NUM
#DIMS CONST 장르명 = "호색가", "고용", "트래이너", "외교", "특수 세력", "그 외"
#DIM 선택항목
#DIM 선택번호
#DIMS EVENT_NAME_ENG
#DIMS EVENT_NAME_JAP
#DIM FIRST_LINE
VARSET 콘피그소속, -1
VARSET 이벤트수
VARSET 표시
FOR LOCAL, 0, VARSIZE("DAILY_EVENT_NAME_ENG", 1)
	EVENT_NAME_ENG = %DAILY_DISABLE_NAME:LOCAL%
	TRYCCALLFORM EVENT_DAILY_%EVENT_NAME_ENG%_GENRE
	콘피그소속:RESULT:(이벤트수:RESULT) = LOCAL
	이벤트수:RESULT++
	DEBUGPRINTFORML %EVENT_NAME_ENG%:{RESULT}
	CATCH
		DEBUGPRINTFORML %EVENT_NAME_ENG%발견되지 않고
	ENDCATCH
NEXT


FIRST_LINE = LINECOUNT

$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 데일리의 콘피그를 설정합니다
CALL SINGLE_DRAWLINE

FOR LOCAL, 0, DAILY_GENRE_NUM
	PRINTFORM ◇　%장르명:LOCAL, 10, LEFT% 
	PRINTBUTTON @"[변환]", LOCAL * 300 + 99
	PRINTFORM %TOSTR_SPACE(5)%
	PRINTBUTTON @"[모두 오프]", LOCAL * 300 + 98
	PRINTFORM %TOSTR_SPACE(5)%
	PRINTBUTTON @"[모두   온]", LOCAL * 300 + 97
	PRINTL
	SIF 표시:LOCAL == 0
		CONTINUE
	FOR LOCAL:1, 0, VARSIZE("콘피그소속", 1)
		SIF 콘피그소속:LOCAL:(LOCAL:1) == -1
			BREAK
		SIF GET_DAILY_INDEX_FROM_ENG(데일리_통상, DAILY_DISABLE_NAME:(콘피그소속:LOCAL:(LOCAL:1))) == -1
			CONTINUE
		EVENT_NAME_JAP = %DAILY_EVENT_NAME:데일리_통상:GET_DAILY_INDEX_FROM_ENG(데일리_통상, DAILY_DISABLE_NAME:(콘피그소속:LOCAL:(LOCAL:1)))%
		IF DAILY_DISABLE:(콘피그소속:LOCAL:(LOCAL:1))
			SETCOLOR 칼라_선택불가
		ENDIF
		PRINTBUTTON @"\@ DAILY_DISABLE:(콘피그소속:LOCAL:(LOCAL:1)) ? [오프] # [온] \@", LOCAL * 300 + LOCAL:1
		PRINTFORM %TOSTR_SPACE(5)%
		PRINTFORM %EVENT_NAME_JAP, 30, LEFT%
		PRINTL
		RESETCOLOR
	NEXT
NEXT
CALL SINGLE_DRAWLINE
PRINTFORML [9990] 데일리 발생 배율 - {DAILY_EVENT_ODDS}
PRINTFORML [9991] 데일리 발생 개수 - {DAILY_EVENT_COUNT}
PRINTFORML [9992] 구상 데일리 배율 - {KOJO_DAILY_EVENT_ODDS}
PRINTFORML [9993] 구상 데일리 개수 - {KOJO_DAILY_EVENT_COUNT}
PRINTFORML [9999] 돌아온다
INPUT
IF RESULT == 9999
	RETURN
ELSEIF GROUPMATCH(RESULT, 9990, 9991, 9992, 9993)
	SELECTCASE RESULT
		CASE 9990
			DAILY_EVENT_ODDS = ROUND_INCREMENT(DAILY_EVENT_ODDS, 0, 10)
		CASE 9991
			DAILY_EVENT_COUNT = ROUND_INCREMENT(DAILY_EVENT_COUNT, 0, 10)
		CASE 9992
			KOJO_DAILY_EVENT_ODDS = ROUND_INCREMENT(KOJO_DAILY_EVENT_ODDS, 0, 10)
		CASE 9993
			KOJO_DAILY_EVENT_COUNT = ROUND_INCREMENT(KOJO_DAILY_EVENT_COUNT, 0, 10)
	ENDSELECT
ELSE
	선택항목 = RESULT / 300
	선택번호 = RESULT % 300
	PRINTFORML {선택항목}
	PRINTFORML {선택번호}
	IF !INRANGE(선택항목, 0, VARSIZE("콘피그소속", 0))
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
	ENDIF
	IF 선택번호 == 99
		표시:선택항목 = !표시:선택항목
	ELSEIF 선택번호 == 98 || 선택번호 == 97
		FOR LOCAL:1, 0, VARSIZE("콘피그소속", 1)
			SIF 콘피그소속:선택항목:(LOCAL:1) == -1
				BREAK
			DAILY_DISABLE:(콘피그소속:선택항목:(LOCAL:1)) = 선택번호 - 97
		NEXT
	ELSEIF 콘피그소속:선택항목:선택번호 == -1
	ELSE
		DAILY_DISABLE:(콘피그소속:선택항목:선택번호) = !DAILY_DISABLE:(콘피그소속:선택항목:선택번호)
	ENDIF
	CLEARLINE LINECOUNT - FIRST_LINE
	GOTO SHOW_LOOP
ENDIF

;죽어 분기
CLEARLINE LINECOUNT - FIRST_LINE
GOTO SHOW_LOOP
