;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@EVENT_DAILY_MERCENARY_RATE()
RETURN 30


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@EVENT_DAILY_MERCENARY_DECISION()
;미검수 OFF면 스킵
SIF FLAG:데일리이벤트_미검수 == 0
	RETURN 0
RETURN DAY >= 5

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@EVENT_DAILY_MERCENARY_GENRE()
RETURN 데일리_장르_고용

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@EVENT_DAILY_MERCENARY()
#DIM 병수
#DIM 고용액


병수 = RAND(1000, 5000)
고용액 = MAX(SLG_EXTRA_DRAFT_CALC_RATE(CFLAG:MASTER:소속) * 병수 / 2, 1)

PRINTFORML 비서가 손님이 왔다고 알렸다.
PRINTFORMW 아무래도, 용병단이 참가하고 싶하고 있는 것 같다
CALL ICPRINT(@"용병<{병수}>의 고용으로, <{고용액}>크레딧을 요구하고 있다", "W", 칼라_주의)
PRINTFORML 현재의 자금:{MONEY}

CALL ASK_MULTI_JUDGE("고용한다", MONEY >= 고용액, "고용하지 않는다", 1)
IF RESULT == 0 && MONEY >= 고용액
	PRINTFORMW 그들을 고용하기로 했다……
	COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += 병수
	MONEY -= 고용액
ELSEIF RESULT == 1
	PRINTFORMW 그만두었다
ENDIF
RETURN 1
