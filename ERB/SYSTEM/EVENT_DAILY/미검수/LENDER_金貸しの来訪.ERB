;---------------------
;발생 확률(1000분 율 100으로 10%)
;---------------------
@EVENT_DAILY_LENDER_RATE()
	RETURN 20

;---------------------
;확률 이외의 발생 판정
;---------------------
@EVENT_DAILY_LENDER_DECISION()
;미검수 OFF면 스킵
SIF FLAG:데일리이벤트_미검수 == 0
	RETURN 0
	
SIF DVAR:대금업자_발생플래그 == -1
	RETURN 0

SIF DAY < 15
	RETURN 0

;반제기간이 있다（빌리고 있다）또는 현재액이 있다（반제중）라면 스킵
SIF !(DVAR:대금업자_반제기간 == 0 && DVAR:대금업자_나머지융자액 == 0) 
	RETURN 0

RETURN 1

;---------------------
;장르
;---------------------
@EVENT_DAILY_LENDER_GENRE()
RETURN 데일리_장르_에로


@EVENT_DAILY_LENDER()
#DIM LCOUNT, 3
#DIM 융자액
#DIM 반제액
#DIM 반제기간
#DIM 담보
VARSET 융자액
VARSET 반제액
VARSET 반제기간

;빌리지 않은 패턴
IF DVAR:대금업자_반제기간 == 0 && DVAR:대금업자_나머지융자액 == 0 && DVAR:대금업자_융자총액 == 0
	;융자액과 반제기간의 계산
	;융자액은 현재 자금의 10% + 봉급 * 기한일분
	;반제액은 그 2 할증
	반제기간 = RAND(5, 10)
	융자액 = MONEY / 10 + GET_SUM_ECONOMY(CFLAG:MASTER:소속) * (ABL:MASTER:무투 + ABL:MASTER:방위 + ABL:MASTER:지략 + ABL:MASTER:정치 + 100) / 40000 * (반제기간)
	반제액 = 융자액 * 12 / 10
	PRINTFORMW %조사처리(ANAME(MASTER),"가")% 일을 하던 도중, 비서가 손님이 왔다고 알렸다.
	PRINTFORMW  누구냐고 묻자,「풍채가 좋은, 어딘가 사람이 좋은 것 같은 중년 남성입니다.」라고 한다
	PRINTFORMW 무슨 용무일까하고 의심하면서, %조사처리(ANAME(MASTER),"는")%, 만나기로 해 보았다
	PRINTFORML ·
	PRINTFORML ·
	PRINTFORML ·
	PRINTFORMW 「처음 뵙겠습니다. %ANAME(MASTER)% 님. 만날 수 있어 영광입니다」
	PRINTFORMW 손을 맞대고 비비면서 나타난 것은, 말 그대로 매우 포근한 인상의 중년 남성이다.
	PRINTFORMW 그 옷차림은 범상치 않아, 높은 사회적 지위에 있는 것을 눈치챈다. 위세도 좋을 것 같다
	PRINTFORMW 「저는 대금업자를 하고 있어서. 이번에 %ANAME(MASTER)%님을 거들고 싶어 찾아왔습니다.」
	PRINTFORMW 즉, 자금원조의 의사표현일 것이다. 그렇다고 해도 대금업자다. 당연, 융자라는 형태가 되겠지만……
	PRINTFORM 「그렇습니다, 크레딧
	CALL COLOR_PRINT(@"{융자액}", 칼라_주의)
	PRINTFORM 의 융자로, 반제액은
	CALL COLOR_PRINT(@"{반제액}", 칼라_주의)
	PRINTFORM , 반제 시기는
	CALL COLOR_PRINT(@"{반제기간}기", 칼라_주의)
	PRINTFORMW 로 어떻습니까?」
	PRINTFORMW 「%ANAME(MASTER)%님도, 자본이 많을 수록 좋지 않습니까?」
	PRINTFORMW 그 말대로다. 용병에 훈련, 외교, 무슨 일에도 돈은 필요하다
	PRINTFORMW 빚은 그다지 지고 싶지 않지만, 살아 남기 위해서는 어떤 수단이라도 강구해야만 한다.
	PRINTFORMW 자, 그럼, 어떻게 하지……
	CALL SINGLE_DRAWLINE
	PRINTFORML 현재의 돈:{MONEY}
	CALL ASK_MULTI(@"{융자액} 크레딧을 빌린다", "그만둔다", "짜증나기 때문에 쏜다")
	IF RESULT == 1
		PRINTFORMW 「흠. 필요하지 않다고 말씀하신다면, 어쩔 수 없습니다」
		PRINTFORMW 「그럼 저는 이만. 또 들리겠습니다」
		PRINTFORMW 남자은 조속히 떠나 갔다……
		RETURN 1
	ELSEIF RESULT == 0
		PRINTFORMW 「계약 성립입니다」
		PRINTFORM 「반제액은
		CALL COLOR_PRINT(@"{반제액}", 칼라_주의)
		PRINTFORM , 반제기간은
		CALL COLOR_PRINT(@"{반제기간}기", 칼라_주의)
		PRINTFORMW 이기 때문에. 틀림없이 부탁드립니다.」
		PRINTFORMW 여러 가지의 서류에 기입시킨 뒤, 남자는 떠나 갔다……
		PRINTFORM 크레딧
		CALL COLOR_PRINT(@"{융자액}", 칼라_주의)
		PRINTFORMW 를 손에 넣었다
		MONEY += 융자액
		DVAR:대금업자_반제기간 = 반제기간
		DVAR:대금업자_나머지융자액 = 반제액
		DVAR:대금업자_융자총액 = 반제액
		RETURN 1
	ELSEIF RESULT == 2
		융자액 = RAND(100000, 300000)
		PRINTFORMW 「이런이런! 총부림은 위험합니다!   다치면 곤란하겠지요」
		PRINTFORMW 참지 못하고 총을 쏘았지만, %조사처리(ANAME(MASTER),"이지만")%, 남자는 겉모습으로부터 상상할 수 없는 민첩함으로 피했다.
		PRINTFORM 「전혀 믿을 수 없는 행동이군요. 위자료로서 크레딧
		CALL COLOR_PRINT(@"{융자액}", 칼라_주의)
		PRINTFORMW 를 지불해 받습니까?
		VARSET DAILY_TARGET
		VARSET DAILY_TARGET_NUM
		CALL EVENT_DAILY_DERIVATION_LENDER_REPAYMENT_SETTARGET()
		IF DAILY_TARGET_NUM > 0
			담보 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)
		ELSE
			담보 = -1
		ENDIF
		DVAR:대금업자_발생플래그 = 1
		IF MONEY >= 융자액
			PRINTFORM 남자는
			CALL COLOR_PRINT(@"{융자액}", 칼라_주의)
			PRINTFORMW 크레딧을 가지고 떠났다……
			MONEY -= 융자액
			RETURN 1
		ELSEIF 담보 != -1 && !CONFIG:200
			PRINTFORMW 「……뭐?   크레딧이 부족해?」
			PRINTFORMW 「그럼%ANAME(담보)%를 우리 가게로 데려가겠습니다.」
			PRINTFORMW 「금액을 채우면 돌려보내긴 하겠습니다. 후후후……」
			PRINTFORMW 남자는 그렇게 말해, %ANAME(담보)%를 데려가 버렸다……
			DVAR:대금업자_반제기간 = -1
			DVAR:대금업자_어깨ID = GET_ID(담보)
			DVAR:대금업자_융자총액 = 융자액
			DVAR:대금업자_나머지융자액 = 융자액
			CALL CHANGE_COUNTRY(담보, 0)
			CFLAG:담보:특수상태 = 0
			RETURN 1
		ELSE
			PRINTFORMW 「……뭐?  크레딧이 부족해?」
			PRINTFORMW 「하아, 어쩔 수 없죠. 이번은 특별히 깎아 드립니다」
			PRINTFORMW 「하지만, 다음은 없기에!」
			PRINTFORMW 남자는 떠나 갔다……
			RETURN 1
		ENDIF
	ENDIF
ENDIF
