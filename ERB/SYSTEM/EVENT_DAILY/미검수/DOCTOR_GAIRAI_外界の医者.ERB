;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_RATE()
RETURN 35


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_DECISION()
;미검수 OFF면 스킵
SIF FLAG:데일리이벤트_미검수 == 0
	RETURN 0
RETURN 1

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI_GENRE()
RETURN 데일리_장르_트래이너

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@EVENT_DAILY_DOCTOR_GAIRAI()
;외계의 의사에 의한 치료
IF CFLAG:MASTER:소속 != 0 && CFLAG:MASTER:포로처 == 0
	;[공허][붕괴]가 붙은 캐릭터의 수를 조사한다(자국 한정)
	LOCAL:2 = 0
	FOR LOCAL:0, 0, CHARANUM
		IF (CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 || CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속) && (TALENT:(LOCAL:0):공허 || TALENT:(LOCAL:0):붕괴)
			LOCAL:2 ++
		ENDIF
	NEXT

	;[공허][붕괴]의 캐릭터가 있다
	IF LOCAL:2

		PRINTFORML 시스터후드의 마리가 방문해 왔다
		PRINTFORML

		PRINTFORMW 「안녕하세요. 시스터후드의 마리입니다.」
		PRINTFORMW 「여기에 마음의 상처를 가진 어린 양이 있다고 들었습니다.」
		PRINTFORMW 「가슴 아픈 일이에요......」
		PRINTFORMW 「마음의 상처를 치유하는 것도 성직자의 의무. 부디 저에게 맡겨주실 수 있을까요?」
		PRINTL 
		PRINTFORML [공허]또는[붕괴]가 붙은 학생을 1명 치료할 수 있습니다.
		PRINTFORML ★치료할 학생을 선택해 주세요★
		CALL SINGLE_DRAWLINE

		;전원의 조교참가플래그를 클리어
		CVARSET CFLAG, 6, 0

		;페이지수의 계산
		LOCAL:6 = (LOCAL:2 - 1) / 44 + 1
		LOCAL:7 = 1

		$SHOW_LOOP

		;캐릭터 리스트의 표시
		LOCAL:8 = 0
		LOCAL:9 = 0
		LOCAL:10 = (LOCAL:7 - 1) * 44
		LOCAL:11 = LOCAL:7 * 44
		LOCAL:12 = 4
		FOR LOCAL:0, 0, CHARANUM
			IF (CFLAG:(LOCAL:0):소속 == CFLAG:MASTER:소속 || CFLAG:(LOCAL:0):포로처 == CFLAG:MASTER:소속) && (TALENT:(LOCAL:0):공허 || TALENT:(LOCAL:0):붕괴)
				IF LOCAL:8 >= LOCAL:10 && LOCAL:8 < LOCAL:11
					IF LOCAL:9 % 2 != 0
						PRINTPLAIN 　
					ELSEIF LOCAL:9 >= 1
						PRINTL 
						LOCAL:12 ++
					ENDIF
					CALL PRINT_PARTNER_DATA(LOCAL:0)
					LOCAL:9 ++
				ENDIF
				LOCAL:8 ++
			ENDIF
		NEXT
		PRINTL 
		CALL SINGLE_DRAWLINE

		IF LOCAL:6 >= 2
			LOCAL:12 += 2
			IF LOCAL:7 > 1
				PRINT [  8] 이전페이지            
			ELSE
				PRINT                             
			ENDIF
			LOCALS:0 = page{LOCAL:7}/{LOCAL:6}
			PRINTPLAINFORM %LOCALS:0, 28, LEFT%
			IF LOCAL:7 < LOCAL:6
				PRINT [  9] 다음페이지
			ENDIF
			PRINTL 
			CALL SINGLE_DRAWLINE
		ENDIF

		PRINTL [  0] 치료하지 않는다

		REDRAW 0

		$INPUT_LOOP
		INPUT

		;치료하지 않는다
		IF RESULT == 0
			REDRAW 1
			PRINTFORML %조사처리(ANAME(MASTER),"는")% 치료를 거절했다
			PRINTFORMW 「그, 그렇습니까…. 뭔가 특수한 사정이 있다고 한다면, 저도 어쩔 수 없지요…」
		;이전페이지
		ELSEIF RESULT == 8 && LOCAL:7 > 1
			LOCAL:7 = MAX(1, LOCAL:7 - 1)
			CLEARLINE LOCAL:12
			GOTO SHOW_LOOP
		;다음페이지
		ELSEIF RESULT == 9 && LOCAL:7 < LOCAL:6
			LOCAL:7 = MIN(LOCAL:6, LOCAL:7 + 1)
			CLEARLINE LOCAL:12
			GOTO SHOW_LOOP
		ELSE
			LOCAL:5 = NO_TO_CHARA(RESULT - 100)
			IF LOCAL:5 >= 0 && (CFLAG:(LOCAL:5):소속 == CFLAG:MASTER:소속 || CFLAG:(LOCAL:5):포로처 == CFLAG:MASTER:소속) && (TALENT:(LOCAL:5):공허 || TALENT:(LOCAL:5):붕괴)
				REDRAW 1
				PRINTFORMW 「%ANAME(LOCAL:5)%씨 입니까. 알겠습니다.」
				PRINTFORMW 「괜찮습니다. %ANAME(LOCAL:5)%씨, 마음을 가라앉히고 저에게 모든 것을 이야기해주세요......」
				SETCOLOR 칼라_주의
				IF TALENT:(LOCAL:5):붕괴
					PRINTFORMW %조사처리(ANAME(LOCAL:5),"는")%<붕괴>를 잃었다
				ELSE
					PRINTFORMW %조사처리(ANAME(LOCAL:5),"는")%<공허>를 잃었다
				ENDIF
				RESETCOLOR
				TALENT:(LOCAL:5):붕괴 = 0
				TALENT:(LOCAL:5):공허 = 0
				CFLAG:(LOCAL:5):붕괴 = MIN(CFLAG:(LOCAL:5):붕괴, 1000)
				PRINTFORMW 「%ANAME(LOCAL:5)%씨는 이제 괜찮습니다. 조금 안정을 취하면 괜찮아질 거에요.」
			ELSE
				CLEARLINE 1
				GOTO INPUT_LOOP
			ENDIF
		ENDIF

	;[공허][붕괴]의 캐릭터가 없는 경우(클리어가 끝난 상태라면 아무것도 일어나지 않는다)
	ELSEIF !FLAG:클리어플래그
		SETCOLOR 칼라_주의
		PRINTFORMW 시스터후드의 마리가 방문해 왔습니다
		RESETCOLOR

		PRINTFORMW 「마음이 아프신 분이 계시다고 들었는데......」
		PRINTFORMW ………………
		;징병 한계를 넘어 군사의 확보가 가능
		LOCAL:0 = MIN(GET_SUM_ECONOMY(CFLAG:MASTER:소속) / 200, 2000)
		COUNTRY_SOLDIER:(CFLAG:MASTER:소속) += LOCAL:0
		SETCOLOR 칼라_주의
		PRINTFORMW 마리의 인기로 우리 학교의 신입생이 {LOCAL:0} 증가했습니다
		RESETCOLOR
		LOCAL:2 = 1
	ENDIF
ENDIF

RETURN LOCAL:2 > 0
