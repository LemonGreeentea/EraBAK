;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@EVENT_DAILY_STEMA_RATE()
RETURN 70


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@EVENT_DAILY_STEMA_DECISION()
;미검수 OFF면 스킵
SIF FLAG:데일리이벤트_미검수 == 0
	RETURN 0
RETURN 1

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@EVENT_DAILY_STEMA_GENRE()
RETURN 데일리_장르_그외

;---------------------
;특정의 조건을 채우는 캐릭터를 랜덤에 선택하는 경우에 이용
;다른 함수는 필수이지만, 이것만은 없어도 좋다  라고 할까 퍼포먼스에 영향을 주므로 불필요하면 만들어서는 안 된다
;대상이 존재하지 않고 데일리를 개시 할 수 없는 경우는 0을 돌려주는 것으로 데일리의 발생을 캔슬한다
;---------------------
@EVENT_DAILY_STEMA_SETTARGET()
;타국에 소속해 있는 자신에게 연모 or복종을 가지는 캐릭터로부터 랜덤 선택, 대상이 없는 경우는 이벤트 캔슬
FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):소속 != CFLAG:MASTER:소속 && IS_COUNTRY(CFLAG:LOCAL:소속) && (IS_LOVER(LOCAL) || IS_SLAVE(LOCAL)) && !CFLAG:(LOCAL):포로처 && !IS_ANIMAL(LOCAL)
		DAILY_TARGET:DAILY_TARGET_NUM = LOCAL 
		DAILY_TARGET_NUM ++
	ENDIF
NEXT

SIF DAILY_TARGET_NUM == 0
	RETURN 0

RETURN 1

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@EVENT_DAILY_STEMA()
#DIM 대상


대상 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)

PRINTFORML %ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))% 의 아래에 있는 %조사처리(ANAME(대상),"는")%, 동료들에게
PRINTFORM %조사처리(ANAME(MASTER),"을")%
IF IS_SLAVE(대상)
	PRINTFORM 도취하는 것 같은 표정으로 열정적으로
ELSE
	PRINTFORM 뺨을 붉히면서 열심히
ENDIF
PRINTFORMW 홍보하고 있다

FOR LOCAL, 0, CHARANUM
	IF CFLAG:(LOCAL):소속 == CFLAG:(대상):소속 && !CFLAG:(LOCAL):포로처 && !IS_ANIMAL(LOCAL)
		CFLAG:(LOCAL):호감도 += (51 + RAND:100)
	ENDIF
NEXT
CALL CHANGE_RELATION_C_TO_O(CFLAG:대상:소속, MASTER, 50, -50)
CALL COLOR_PRINT(@"%ANAME(GET_COUNTRY_BOSS(CFLAG:대상:소속))% 세력에 소속하는 무장들의 %ANAME(MASTER)%에 대한 평가가 올랐다", 칼라_주의)
PRINTFORMW 

RETURN 1
