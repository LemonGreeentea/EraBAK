;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_RATE()
RETURN 75


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_DECISION()
RETURN DAY >= SLG_PP:1 && CFLAG:MASTER:소속 && !IS_SP_COUNTRY(CFLAG:MASTER:소속)

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_GENRE()
RETURN 데일리_장르_고용

;---------------------
;특정의 조건을 채우는 캐릭터를 랜덤에 선택하는 경우에 이용
;다른 함수는 필수이지만, 이것만은 없어도 좋다  라고 할까 퍼포먼스에 영향을 주므로 불필요하면 만들어서는 안 된다
;대상이 존재하지 않고 데일리를 개시 할 수 없는 경우는 0을 돌려주는 것으로 데일리의 발생을 캔슬한다
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA_SETTARGET()
;방랑하고 있는 캐릭터.
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:특수상태 != 특수상태_방랑
		CONTINUE
	SIF REL_LIKE:LOCAL:MASTER - REL_HATE:LOCAL:MASTER < -1000
		CONTINUE
	SIF RAND:3
		CONTINUE
	DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
	DAILY_TARGET_NUM ++
NEXT

SIF DAILY_TARGET_NUM < 1
	RETURN 0

RETURN 1


;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@EVENT_DAILY_MEETING_WITH_ZAIYA()
#DIM 대상
#DIM 고용액
#DIM FIRST_LINE

대상 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)

PRINTFORMW 재야 무장의 %조사처리(ANAME(대상),"가")% 면회를 요구해 왔다……

;특수한 패턴. 공짜로 고용할 수가 있다
IF TALENT:대상:친애 || TALENT:대상:예속 || TALENT:대상:소유자
	PRINTFORML 지금은 방랑의 몸이며 자유로운 것으로, 꼭%ANAME(MASTER)%의 옆에 있으면 좋다, 라고다
	PRINTFORML 저쪽에서 사관을 요구해 온다고는 고마운 것이지만……
	PRINTFORML 
	CALL ASK_YN("받아들인다", "거부한다")
	IF RESULT == 0
		PRINTFORML 거절할 합당한 이유가 없다
		PRINTFORMW %조사처리(ANAME(대상),"를")% 사관으로서 맞아들이기로 했다……
		CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 동료가 되었습니다", 칼라_주의)
		CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속)
	ELSE
		PRINTFORML 그렇게 말할 수는 없다
		PRINTFORML 단호히거절했다……
	ENDIF
	RETURN 1
ENDIF

;기본 싸다
고용액 = SCOUT_CALC_PRICE_BASIS(대상) * 2 / 3

;연모등 있으면 게다가 싸고
SIF IS_LOVER(대상) || IS_SLAVE(대상)
	고용액 /= 2

CALL ICPRINT(@"지금은 방랑의 몸이며 자유로운 것이지만, 자신을 돈<{고용액}>로 고용해 보지 않겠는가, 라고다", "L", 칼라_주의)
SIF IS_LOVER(대상) || IS_SLAVE(대상) || IS_SLAVED_BY(대상) || IS_SLAVED_BY(대상)
	PRINTFORML 상대가 %조사처리(ANAME(MASTER),"라")%는 일로, 준비금을 상당히 소극적으로 해 준 것 같다
PRINTFORML 자신만만하게 말하고 있지만, 실력은 확실할 것이다……

FIRST_LINE = LINECOUNT
$SHOW_LOOP

CALL SHOW_SIMPLE_INFO(대상)

PRINTL
CALL ASK_MULTI_JUDGE(@"금{고용액}로 고용하는 현재:{MONEY}", MONEY >= 고용액, "그만둔다", 1, "능력표시", 2)

SELECTCASE RESULT
	CASE 0
		PRINTFORML 전력은 아무리 있어도 많다는 것은 없다
		PRINTFORMW %조사처리(ANAME(대상),"를")% 사관으로서 맞아들이기로 했다……
		CALL COLOR_PRINTW(@"%조사처리(ANAME(대상),"가")% 동료가 되었습니다", 칼라_주의)
		CALL CHANGE_COUNTRY(대상, CFLAG:MASTER:소속)
		MONEY -= 고용액
	CASE 1
		PRINTFORML 지금은 필요가 없다
		PRINTFORML 거절하기로 했다……
	CASE 2
		CALL SHOW_INFO_WITH_UI(대상, 0)
		CLEARLINE LINECOUNT - FIRST_LINE
		GOTO SHOW_LOOP
ENDSELECT

RETURN 1

@EVENT_DAILY_MEETING_WITH_ZAIYA_ALLOW_WHEN_WANDERING
RETURN 1

