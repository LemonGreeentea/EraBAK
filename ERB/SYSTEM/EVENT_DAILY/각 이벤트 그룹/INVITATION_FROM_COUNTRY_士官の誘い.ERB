;---------------------
;기본적인 발생 확률(1000분 율 100으로 10%)
;이것에 콘피그 항목의 발생 빈도를 걸칠 수 있으므로, 반드시 이 대로는 되지 않는다
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_RATE()
RETURN (ABL:MASTER:무투 + ABL:MASTER:지략 + ABL:MASTER:정치 + ABL:MASTER:방위) 


;---------------------
;확률 이외의 발생 판정
;0기 이후가 되면 발생한다든가, 데일리측에서 이용하고 있는 변수가-1이라면 일으키지 않는다  같은 튕기는 방법을 할 경우에 사용한다
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_DECISION()
;벌써 어디엔가 소속 OR포로라면 안 돼
SIF CFLAG:MASTER:소속 || CFLAG:MASTER:포로처
	RETURN 0
SIF FLAG:클리어플래그
	RETURN 0
;비권유 남고 있으면 안 돼
SIF DVAR:관직에오름의권유_비권유남은턴
	RETURN 0
RETURN 1

;---------------------
;장르
;콘피그 설정으로 사용
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_GENRE()
RETURN 데일리_장르_고용


;---------------------
;특정의 조건을 채우는 캐릭터를 랜덤에 선택하는 경우에 이용
;다른 함수는 필수이지만, 이것만은 없어도 좋다  라고 할까 퍼포먼스에 영향을 주므로 불필요하면 만들어서는 안 된다
;대상이 존재하지 않고 데일리를 개시 할 수 없는 경우는 0을 돌려주는 것으로 데일리의 발생을 캔슬한다
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY_SETTARGET()
FOR LOCAL, 0, MAX_COUNTRY
	SIF !IS_COUNTRY(LOCAL) || IS_SP_COUNTRY(LOCAL) || FINDELEMENT(COUNTRY_PLAYER_BELONGED, LOCAL) != -1
		CONTINUE
	DAILY_TARGET:DAILY_TARGET_NUM = LOCAL
	DAILY_TARGET_NUM ++
NEXT

SIF DAILY_TARGET_NUM < 1
	RETURN 0

RETURN 1

;---------------------
;본체
;이벤트가 발생했을 경우는 1, 발생하지 않았던 경우는 0을 돌려준다
;발생하지 않았다는 것은 예를 들면, 특정 조건을 채우는 캐릭터로부터 랜덤에 한사람 선택하는 데일리로, 원래 그 조건을 채우는 캐릭터가 한사람도 없는 것 같은 때
;구사양으로 만든 적 있는 사람용으로 말하면 「구사양의 데일리 본체 첫머리에서-1을 돌려주는 것 같은 상황」
;---------------------
@EVENT_DAILY_INVITATION_FROM_COUNTRY()
#DIM 고용확률
#DIM 준비금
#DIM 세력
#DIM 수령



;준비금의 계산
세력 = DAILY_TARGET:(RAND:DAILY_TARGET_NUM)
수령 = GET_COUNTRY_BOSS(세력)
준비금 = 고용확률 * 20 / 1000 * 1000 + RAND:1000 + RAND:(MAX(1, CFLAG:수령:호감도))

PRINTFORMW %ANAME(수령)%로부터, 관직에 오름 하지 않을것인가 라는 권유가 왔다
PRINTFORM 준비금으로 해서 돈
SETCOLOR 칼라_주의
PRINTFORM {준비금}
RESETCOLOR
PRINTFORMW 를 준비해 있는 것 같다
PRINTFORML 이 난세, 어디엔가 시중들어 힘을 길러 두는 것은 하나의 손이다
PRINTFORML 다만, 일부러 권해진 이상, 곧바로 하야 하는 것은 인정되지 않는다
PRINTFORM 최악이어도
SETCOLOR 칼라_주의
PRINTFORM 10
RESETCOLOR
PRINTFORMW 기 정도는 맡지 않으면 안 된다
PRINTFORML 어떻게 하지……
PRINTFORML
CALL ASK_YN("권유를 받는다", "정중하게 사퇴한다")
IF RESULT == 0
	PRINTFORML %ANAME(수령)%를 시중들기로 했다
	PRINTFORMW 부하의 무장들은 %조사처리(ANAME(MASTER),"를")% 환영하고 있는 것 같다……
	PRINTFORMW 준비금{준비금}를 받았다
	CALL CHANGE_COUNTRY(MASTER, 세력, 1)
	MONEY += 준비금
	FOR LOCAL:2, 0, CHARANUM
		IF CFLAG:(LOCAL:2):소속 == CFLAG:MASTER:소속
			CFLAG:(LOCAL:2):호감도 += RAND(300, 600)
		ENDIF
	NEXT
	PLAYER_HIRED_COUNTER = 10
ELSE
	PRINTFORMW 권유를 정중하게 거절했다
	DVAR:관직에오름의권유_비권유남은턴 = RAND(1, 3)
ENDIF
RETURN 1

@EVENT_DAILY_INVITATION_FROM_COUNTRY_ALLOW_WHEN_WANDERING()
RETURN 1