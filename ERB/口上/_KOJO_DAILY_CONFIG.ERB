;-------------------------------------------------
;desc  :구상 데일리의 콘피그 설정
;-------------------------------------------------
@CONFIG_KOJO_DAILY
#DIM 대상
#DIM FIRST_LINE, 2
FIRST_LINE:0 = LINECOUNT

CALL ASK_MULTI("통상의 설정", "전구상 데일리 온", "전구상 데일리 오프", "돌아온다")

SELECTCASE RESULT
	CASE 1
		PRINTFORML 전구상 데일리를 온으로 합니까?
		CALL ASK_YN()
		IF RESULT == 1
			CLEARLINE LINECOUNT - FIRST_LINE
			RESTART
		ENDIF
		CALL SET_ALL_KOJO_DAILY(1)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	CASE 2
		PRINTFORML 전구상 데일리를 오프로 합니까?
		CALL ASK_YN()
		IF RESULT == 1
			CLEARLINE LINECOUNT - FIRST_LINE
			RESTART
		ENDIF
		CALL SET_ALL_KOJO_DAILY(0)
		CLEARLINE LINECOUNT - FIRST_LINE
		RESTART
	CASE 3
		RETURN
ENDSELECT

FIRST_LINE:1 = LINECOUNT
$SHOW_LOOP

CALL SINGLE_DRAWLINE
PRINTFORML 각 구상의 데일리 이벤트의 발생을 제어할 수 있습니다（클릭으로 ON/OFF）
CALL SINGLE_DRAWLINE

CALL SELECT_CHARA_LIST("CONFIG_KOJO_DAILY", "NONE", "DEFAULT")
대상 = RESULT

IF RESULT == -1
	CLEARLINE FIRST_LINE:0 - LINECOUNT
	RESTART
ENDIF

CALL CONFIG_KOJO_DAILY_EACH(대상)

CLEARLINE FIRST_LINE:1 - LINECOUNT
GOTO SHOW_LOOP


@SET_ALL_KOJO_DAILY(온오프)
#DIM 온오프
#DIM 배열요소
#DIMS EVENT_NAME_ENG

배열요소 = 데일리_통상
FOR LOCAL, 0, CHARANUM
	SIF !IS_FIXED_CHARA(LOCAL)
		CONTINUE
	FOR LOCAL:1, 0, VARSIZE("KOJO_DAILY_EVENT_NAME_ENG", 2)
		EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:(NO:LOCAL):배열요소:(LOCAL:1)%
		CALL KOJO_DAILY_SET_DISABLE(LOCAL, EVENT_NAME_ENG, !온오프)
	NEXT
NEXT

;-------------------------------------------------
;desc  :각 캐릭터마다의 구상 데일리 설정
;param :대상:대상의 캐릭터 번호
;-------------------------------------------------
@CONFIG_KOJO_DAILY_EACH(대상)
#DIM FIRST_LINE
#DIM 대상


FIRST_LINE = LINECOUNT

CALL SINGLE_DRAWLINE
PRINTFORML %ANAME(대상)%의 콘피그
CALL SINGLE_DRAWLINE

CALL LISTUP_KOJO_DAILY_EACH(대상, 0)

CALL SINGLE_DRAWLINE
PRINTFORML [97] 모두 온
PRINTFORML [98] 모두 오프
PRINTFORML [99] 돌아온다

INPUT 

IF RESULT == 99
	RETURN
ELSEIF RESULT == 97
	CALL SET_ALL_KOJO_DAILY_EACH(대상, 0)
	GOTO SET_END
ELSEIF RESULT == 98
	CALL SET_ALL_KOJO_DAILY_EACH(대상, 1)
	GOTO SET_END
ENDIF

CALL TOGGLE_KOJO_DAILY(대상, RESULT)

$SET_END

CLEARLINE LINECOUNT - FIRST_LINE
RESTART

@SELECT_CHARA_LIST_SHOW_LOGIC_CONFIG_KOJO_DAILY(대상)
#DIM 대상
RETURN IS_FIXED_CHARA(대상)

@LISTUP_KOJO_DAILY_EACH(대상, 버튼치)
#DIM 대상
#DIM 대상NO
#DIM EVENT_NUM
#DIM 버튼치
#DIM 배열요소
#DIM 오프
#DIMS EVENT_NAME_JAP
#DIMS EVENT_NAME_ENG

대상NO = NO:대상
배열요소 = 데일리_통상
EVENT_NUM = KOJO_DAILY_EVENT_NUM:대상NO:배열요소
 

FOR LOCAL, 0, EVENT_NUM
	EVENT_NAME_JAP = %KOJO_DAILY_EVENT_NAME:대상NO:배열요소:LOCAL%
	EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:대상NO:배열요소:LOCAL%
	오프 = KOJO_DAILY_GET_DISABLE_CONFIG(대상, EVENT_NAME_ENG)
	PRINTFORML [{LOCAL + 버튼치}] - %EVENT_NAME_JAP, 40, LEFT% - \@ 오프 ? 오프 # 온\@
NEXT

@TOGGLE_KOJO_DAILY(대상, 지정NO)
#DIM 대상
#DIM 배열요소
#DIM EVENT_NUM
#DIM 오프
#DIMS EVENT_NAME_ENG
#DIM 대상NO
#DIM 지정NO

배열요소 = 데일리_통상
대상NO = NO:대상
EVENT_NUM = KOJO_DAILY_EVENT_NUM:대상NO:배열요소

SIF !INRANGE(지정NO, 0, EVENT_NUM)
	RETURN 0

EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:대상NO:배열요소:지정NO%

오프 = KOJO_DAILY_GET_DISABLE_CONFIG(대상, EVENT_NAME_ENG)
CALL KOJO_DAILY_SET_DISABLE(대상, EVENT_NAME_ENG, !오프)

RETURN 1

@SET_ALL_KOJO_DAILY_EACH(대상, 온오프)
#DIM 대상
#DIM 배열요소
#DIM EVENT_NUM
#DIMS EVENT_NAME_ENG
#DIM 온오프
#DIM 대상NO

배열요소 = 데일리_통상
대상NO = NO:대상
EVENT_NUM = KOJO_DAILY_EVENT_NUM:대상NO:배열요소

FOR LOCAL, 0, EVENT_NUM
	EVENT_NAME_ENG = %KOJO_DAILY_EVENT_NAME_ENG:대상NO:배열요소:LOCAL%
	CALL KOJO_DAILY_SET_DISABLE(대상, EVENT_NAME_ENG, 온오프)
NEXT

