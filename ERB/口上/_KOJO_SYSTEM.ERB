;구상에 관한 처리

;--------------------------------------------------
;캐릭터 ARG:0의 구상 넘버를 돌려준다
;고정 캐릭터……NO가 그대로 돌아간다
;범용 캐릭터……구상 패턴 번호가 돌아간다
;--------------------------------------------------
@KOJO_NO(ARG:0)
#FUNCTION
IF IS_FIXED_CHARA(ARG:0) || IS_SP_CHARA(ARG:0)
	RETURNF NO:(ARG:0)
ENDIF
IF CFLAG:(ARG:0):범용구상패턴 >= 1000 && CFLAG:(ARG:0):범용구상패턴 <= 1999
	RETURNF CFLAG:(ARG:0):범용구상패턴
ENDIF
RETURNF 999

;--------------------------------------------------
;상황에 따른 부가 문자열을 돌려준다
;ARG:0에 1을 설정하면, 분기가 세세해진다
;--------------------------------------------------
@KOJO_STATE(ARG:0 = 0)
#FUNCTIONS
IF ARG:0
	;만나러간다
	IF FLAG:조교모드 == 0
		RETURNF "A1"
	;침실호출 
	ELSEIF FLAG:조교모드 == 1
		RETURNF "A2"
	;육아
	ELSEIF FLAG:조교모드 == 3
		RETURNF "A3"
	;포로 회화
	ELSEIF FLAG:조교모드 == 6
		RETURNF "A4"
	;포로의조교
	ELSEIF FLAG:조교모드 == 2
		RETURNF "B"
	;포로 역조교(통상)
	ELSEIF FLAG:조교모드 == 5
		RETURNF "C"
	;포로 역조교(외래)
	ELSEIF FLAG:조교모드 == 4
		RETURNF "D"
	ENDIF
ELSE
	;만나러간다·침실호출 ·육아·포로 회화
	IF GROUPMATCH(FLAG:조교모드, 0, 1, 3, 6)
		RETURNF "A"
	;포로의조교
	ELSEIF FLAG:조교모드 == 2
		RETURNF "B"
	;포로 역조교(통상)
	ELSEIF FLAG:조교모드 == 5
		RETURNF "C"
	;포로 역조교(외래)
	ELSEIF FLAG:조교모드 == 4
		RETURNF "D"
	ENDIF
ENDIF

RETURNF "X"

;--------------------------------------------------
;구상의 존재 판정  구상이 존재하면 1을, 존재하지 않으면 0을 돌려준다
;ARG:0에는 캐릭터의 NO를 지정한다
;--------------------------------------------------
@IS_KOJO(ARG:0)
;이벤트 캐릭터는 구상을 표시하지 않는다
IF ARG:0 >= 2000
	RETURN 0
ENDIF

;구상을 표시하지 않는 설정이라면 항상 0을 돌려준다
IF (ARG:0 <= 100 && CONFIG:40 == 1) || (ARG:0 >= 101 && CONFIG:41 == 1)
	RETURN 0
ENDIF

TRYCCALLFORM KOJO_EXIST_K{ARG:0}
	RETURN 1
CATCH
ENDCATCH
RETURN 0

;--------------------------------------------------
;조교 개시시의 구상
;--------------------------------------------------
@EVENTTRAIN
;현재의 타겟을 기록
LOCAL:0 = TARGET

;상대가 한사람의 경우만 구상 호출 포로 조교의 경우는 인원수 제한을 무시해 메인 타겟의 구상을 표시 역조교, 특수 세력 역조교의 경우는 모두 정해진 일
;IF TFLAG:44 == 1 || GROUPMATCH(FLAG:조교 모드, 2, 4, 5)
	FOR LOCAL:0, 0, CHARANUM
		;조교참가플래그가 서 있으면 구상 호출(포로 조교의 경우는 메인 타겟만)
		IF CFLAG:(LOCAL:0):조교참가플래그 && (FLAG:조교모드 != 2 || FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1)
			TARGET = LOCAL:0
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				TRYCALLFORM KOJO_TRAIN_START_K{LOCAL:5}
				TRYCALLFORM KOJO_TRAIN_START_%KOJO_STATE(1)%_K{LOCAL:5}
			ENDIF
		ENDIF
	NEXT
;ENDIF

;타겟을 되돌린다
TARGET = LOCAL:0

;--------------------------------------------------
;조교 종료시의 구상
;--------------------------------------------------
@EVENTEND
;현재의 타겟을 기록
LOCAL:0 = TARGET

;상대가 한사람의 경우만 구상 호출 포로 조교의 경우는 인원수 제한을 무시해 메인 타겟의 구상을 표시 역조교, 특수 세력 역조교의 경우는 모두 정해진 일
IF TFLAG:44 == 1 || GROUPMATCH(FLAG:조교모드,2,4,5)
	FOR LOCAL:0, 0, CHARANUM
		;조교참가플래그가 서 있으면 구상 호출(포로 조교의 경우는 메인 타겟만)
		IF CFLAG:(LOCAL:0):조교참가플래그 && (FLAG:조교모드 != 2 || FINDELEMENT(PRISONER_TARGET, LOCAL:0) != -1)
			TARGET = LOCAL:0
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				TRYCALLFORM KOJO_TRAIN_END_K{LOCAL:5}
				TRYCALLFORM KOJO_TRAIN_END_%KOJO_STATE(1)%_K{LOCAL:5}
			ENDIF
		ENDIF
	NEXT
ENDIF

;타겟을 되돌린다
TARGET = LOCAL:0

;--------------------------------------------------
;커맨드 실행전의 구상
;반환값에 1을 설정하면 지문이 컷 된다
;--------------------------------------------------
@KOJO_COM_BEFORE
;현재의 타겟을 기록
LOCAL:0 = TARGET

;반환값의 Default치를 0으로 설정
LOCAL:1 = 0

;주인공이 플레이어의 경우
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
			SIF TARGET == -1
				CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;주인공이 타겟의 경우
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		SIF TARGET == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_BEFORE_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;플레이어에도 타겟으로도 주인공을 포함하지 않는다
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_BEFORE_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF

;타겟을 되돌린다
TARGET = LOCAL:0

RETURN LOCAL:1

;--------------------------------------------------
;커맨드 실행시의 구상
;--------------------------------------------------
@KOJO_COM
;현재의 타겟을 기록
LOCAL:0 = TARGET

;반환값의 Default치를 0으로 설정
LOCAL:1 = 0

;주인공이 플레이어의 경우
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;주인공이 타겟의 경우
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;플레이어에도 타겟으로도 주인공을 포함하지 않는다
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		SIF TARGET:LOCAL == -1
			CONTINUE
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF

;타겟을 되돌린다
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;커맨드 실행 후의 구상
;파라미터의 변화나 절정의 유무를 참조 가능
;-------------------------------------------------
@KOJO_COM_AFTER
;현재의 타겟을 기록
LOCAL:0 = TARGET

;반환값의 Default치를 0으로 설정
LOCAL:1 = 0

;주인공이 플레이어의 경우
IF IS_MPLY(MASTER)
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:(LOCAL)
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT

;주인공이 타겟의 경우
ELSEIF IS_MTAR(MASTER)
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:(LOCAL)
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)

			CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_AFTER_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
;플레이어에도 타겟으로도 주인공을 포함하지 않는다
ELSE
	FOR LOCAL, 0, MPLY_NUM
		TARGET = MPLY:LOCAL
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_FTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_PLAYER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_PLAYER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
	FOR LOCAL, 0, MTAR_NUM
		TARGET = MTAR:LOCAL
		;실신·수면·이탈 상태라면 구상을 비표시
		IF !(TCVAR:52 || TCVAR:53)
			LOCAL:5 = KOJO_NO(TARGET)
				CALL IS_KOJO(LOCAL:5)
			IF RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_TARGET_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
				RESULT = 0
				TRYCALLFORM KOJO_COM_THIRD_AFTER_TARGET_%KOJO_STATE()%_K{LOCAL:5}
				LOCAL:1 |= RESULT
			ENDIF
		ENDIF
	NEXT
ENDIF


;타겟을 되돌린다
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;각종 이벤트의 구상
;대상이 되는 캐릭터의 번호를 ARG:0에 대입
;내용은 ARG:1에 넣는 수치로 판정
;-------------------------------------------------
@KOJO_EVENT(ARG:0, ARG:1)
;대상 캐릭터가 주인공이라면 표시하지 않고 돌아온다
IF ARG:0 == MASTER
	RETURN
ENDIF

LOCAL:1 = 0

;현재의 타겟을 기록하고 나서 타겟을 ARG:0으로 변경
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_EVENT_K{LOCAL:5}, ARG:1
	IF RESULT
		LOCAL:1 = 1
	ENDIF
ENDIF

IF !LOCAL:1
	;Default 메세지
	CALL KOJO_EVENT_DEFAULT(ARG:1)
ENDIF

;타겟을 원래대로 되돌린다
TARGET = LOCAL:0

;-------------------------------------------------
;단체 엔딩 구상
;대상이 되는 캐릭터의 번호를 ARG:0에 대입
;-------------------------------------------------
@KOJO_SINGLE_ENDING(ARG:0)
LOCAL:1 = 0

;현재의 타겟을 기록하고 나서 타겟을 ARG:0으로 변경
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_SINGLE_ENDING_K{LOCAL:5}
	LOCAL:1 = RESULT
ENDIF

;타겟을 원래대로 되돌린다
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;참수 엔딩 구상
;대상이 되는 캐릭터의 번호를 ARG:0에 대입
;-------------------------------------------------
@KOJO_DEAD_ENDING(ARG:0)
LOCAL:1 = 0

;현재의 타겟을 기록하고 나서 타겟을 ARG:0으로 변경
LOCAL:0 = TARGET
TARGET = ARG:0
LOCAL:5 = KOJO_NO(TARGET)

CALL IS_KOJO(LOCAL:5)
IF RESULT
	RESULT = 0
	TRYCALLFORM KOJO_DEAD_ENDING_K{LOCAL:5}
	LOCAL:1 = RESULT
ENDIF

;타겟을 원래대로 되돌린다
TARGET = LOCAL:0

RETURN LOCAL:1

;-------------------------------------------------
;캐릭터 ARG:0의 커맨드 실행이 끝난 플래그를 ON로 한다(=첫회 판정을 OFF)
;SELECTCOM와 플레이어, 타겟, 주도권을 바탕으로 자동으로 설정
;-------------------------------------------------
@SET_COM_FIRST_FLAG(ARG:0)
LOCAL:0 = SELECTCOM
IF LOCAL:0 < 0 || LOCAL:0 > 999
	RETURN
ENDIF

;커맨드 번호 20～29는 특수 판정(플레이어·타겟의 구별이 없기에, 양쪽 모두의 플래그를 세운다)
IF LOCAL:0 >= 20 && LOCAL:0 <= 29
	IF !IS_MPLY(ARG:0) && !IS_MTAR(ARG:0)
		;플레이어도 타겟도 아닐 때는 무시
		RETURN
	ENDIF

	IF IS_INITIATIVE(ARG:0)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 + 1000

	LOCAL:2 = LOCAL:0 / 50 + 2000
	LOCAL:3 = LOCAL:0 % 50
	LOCAL:4 = LOCAL:1 / 50 + 2000
	LOCAL:5 = LOCAL:1 % 50

	;사용 플래그 2000～2079
	CFLAG:(ARG:0):(LOCAL:2) |= (1 << LOCAL:3)
	CFLAG:(ARG:0):(LOCAL:4) |= (1 << LOCAL:5)

ELSE
	IF IS_MPLY(ARG:0)
		LOCAL:0 += 1000
	ELSEIF IS_MTAR(ARG:0)
		LOCAL:0 += 0
	ELSE
		;플레이어도 타겟도 아닐 때는 무시
		RETURN
	ENDIF

	IF IS_INITIATIVE(ARG:0)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 % 50

	;사용 플래그 2000～2079
	CFLAG:(ARG:0):(LOCAL:1) |= (1 << LOCAL:2)
ENDIF

;-------------------------------------------------
;커맨드의 첫회 판정
;현재의 TARGET의 실행이 끝난 플래그를 조사한다
;ARG:0에 0을 설정하면 주도권별로, 1을 설정하면 주도권에 의하지 않고 판정한다
;-------------------------------------------------
@IS_COM_FIRST(ARG:0 = 0)
#FUNCTION
LOCAL:0 = SELECTCOM
IF LOCAL:0 < 0 || LOCAL:0 > 999
	RETURNF 0
ENDIF

IF IS_MPLY(TARGET)
	LOCAL:0 += 1000
ELSEIF IS_MTAR(TARGET)
	LOCAL:0 += 0
ELSE
	RETURNF 0
ENDIF

IF ARG:0
	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 / 50 + 2040
	LOCAL:3 = LOCAL:0 % 50

	IF (CFLAG:(LOCAL:1) & (1 << LOCAL:3)) || (CFLAG:(LOCAL:2) & (1 << LOCAL:3))
		RETURNF 0
	ENDIF

ELSE
	IF IS_INITIATIVE(TARGET)
		LOCAL:0 += 2000
	ELSE
		LOCAL:0 += 0
	ENDIF

	LOCAL:1 = LOCAL:0 / 50 + 2000
	LOCAL:2 = LOCAL:0 % 50

	IF CFLAG:(LOCAL:1) & (1 << LOCAL:2)
		RETURNF 0
	ENDIF
ENDIF
RETURNF 1

;-------------------------------------------------
;데이트 이벤트용의 함수. ARG:0은 캐릭터 번호로, ARG:1은 행선지.
;불려 간 DATE_EVENT_K내에서, 자작한 이벤트 본체의 함수를 호출해 주세요
;행선지를 참조하는 경우는 DATE_EVENT_K에 건너고 있는 ARG:1을 이용하면 좋을 것입니다.
;-------------------------------------------------	
@KOJO_DATE_EVENT(ARG:0, ARG:1)
LOCAL:5 = KOJO_NO(ARG:0)
CALL IS_KOJO(LOCAL:5)
IF RESULT
	TRYCCALLFORM DATE_EVENT_K{LOCAL:5}(ARG:1)
	RETURN RESULT
	CATCH 
		RETURN 0
	ENDCATCH
ELSE
	RETURN 0
ENDIF

;-------------------------------------------------
;ARG:0이 구상 데일리 이벤트를 실시할 수 있을까의 간이 체크 함수.
;섬세한 조건은 스스로 설정해
;ARG:10 MASTER와 별세력인 1 MASTER와 동일 세력이다  -1어디라도 좋다
;ARG:20 포로가 아닌 1 포로인 -1어디라도 좋다
;ARG:30 안면이 없는 1 안면이 있는 -1어디라도 좋다
;-------------------------------------------------
@CHECK_KOJO_DAILY_HAPPEN(ARG:0, ARG:1 = -1, ARG:2 = -1, ARG:3 = -1)
#FUNCTION
#DIM EVENT_ODDS
EVENT_ODDS = KOJO_DAILY_EVENT_ODDS

;MASTER라면 안 돼
SIF ARG:0 == MASTER
	RETURNF 0

;범위외는 물론 아웃
SIF ARG:0 < 0 || CHARANUM <= ARG:0
	RETURNF 0
;죽고 있으면 안 돼
SIF CFLAG:(ARG:0):특수상태 == 특수상태_사망
	RETURNF 0
;미등장도 안 돼
SIF CFLAG:(ARG:0):소속 == 0 && CFLAG:(ARG:0):특수상태 == 0 && !CFLAG:(ARG:0):포로처
	RETURNF 0
;소속 판정
SELECTCASE ARG:1
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):소속 == CFLAG:MASTER:소속
			RETURNF 0
	CASE 1
		SIF CFLAG:(ARG:0):소속 != CFLAG:MASTER:소속
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
;포로 판정
SELECTCASE ARG:2
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):포로처
			RETURNF 0
	CASE 1
		SIF !CFLAG:(ARG:0):포로처
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
;안면 판정
SELECTCASE ARG:3
	CASE -1
	CASE 0
		SIF CFLAG:(ARG:0):안면
			RETURNF 0
	CASE 1
		SIF !CFLAG:(ARG:0):안면
			RETURNF 0
	CASEELSE
		RETURNF 0
ENDSELECT
RETURNF 1
