@EVENT_KOJO_DAILY()
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_COUNT
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 대상
#DIM 대상NO


SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF FLAG:관전모드
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0

CALL KOJO_DAILY_DERIVATION()

DAILY_CANCEL = 0
EVENT_COUNT = KOJO_DAILY_EVENT_COUNT


;구상용 데일리 MASTER의 것은 발동하지 않는다
;순번 결정
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 1, CHARANUM
	대상 = EVENT_CHARA:LOCAL
	대상NO = NO:대상
	SIF !IS_FIXED_CHARA(대상)
		CONTINUE

	SIF KOJO_DAILY_EVENT_NUM:대상NO:데일리_통상 == 0
		CONTINUE

	EVENT_NUM = KOJO_DAILY_EVENT_NUM:대상NO:데일리_통상

	CALL FISHER_YATES_SHAFFLE(EVENT_NUM)
	ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_ARRAY"

	FOR LOCAL:1, 0, EVENT_NUM
		SIF EVENT_COUNT == 0
			BREAK
		NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:대상NO:데일리_통상:(EVENT_ARRAY:(LOCAL:1))
		NAME_JAP '= KOJO_DAILY_EVENT_NAME:대상NO:데일리_통상:(EVENT_ARRAY:(LOCAL:1))

		SIF KOJO_DAILY_GET_DISABLE_CONFIG(대상, NAME_ENG)
			CONTINUE

		TRYCCALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%_RATE(대상)
			SIF RESULT * KOJO_DAILY_EVENT_ODDS <= RAND:1000
				CONTINUE

			;방랑중의 거점 페이즈데이리는 특수한 함수가 필요
			IF !CFLAG:MASTER:소속
				RESULT = 0
				TRYCALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%_ALLOW_WHEN_WANDERING()
				SIF RESULT == 0
					CONTINUE
			ENDIF

			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%_DECISION(대상)
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%_SETTARGET(대상)
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%_NAME(대상)
			IF RESULTS == ""
				CALL KOJO_DAILY_START(NAME_JAP)
			ELSE
				CALL KOJO_DAILY_START(RESULTS)
			ENDIF
			CALLFORM KOJO_DAILY_K{대상NO}_%NAME_ENG%(대상)

			IF RESULT == 1
				EVENT_COUNT --
			ENDIF
			SIF DAILY_CANCEL
				RETURN
		CATCH
			DEBUGPRINTFORML KOJO_DAILY_{대상NO}_%NAME_ENG%_RATE 발견되지 않고
		ENDCATCH
	NEXT
NEXT



;공통 데일리 이벤트:파생 처리
@KOJO_DAILY_DERIVATION()
#DIM EVENT_NUM
#DIMS NAME_ENG
#DIMS NAME_JAP
#DIM EVENT_CHARA, VARSIZE("SHAFFLE_ARRAY")
#DIM EVENT_ARRAY, VARSIZE("SHAFFLE_ARRAY")
#DIM 대상
#DIM 대상NO
#DIM 라이벌_ID
라이벌_ID = NO_TO_CHARA(RIVAL_ID)

SIF !IS_DAILY_EVENT_HAPPEN(MASTER)
	RETURN 0

SIF FLAG:관전모드
	RETURN 0

SIF DAILY_CANCEL
	RETURN 0


DAILY_CANCEL = 0

;구상용 데일리 MASTER의 것은 발동하지 않는다
;순번 결정
CALL FISHER_YATES_SHAFFLE(CHARANUM)

ARRAYCOPY "SHAFFLE_ARRAY", "EVENT_CHARA"

FOR LOCAL, 1, CHARANUM
	SIF LOCAL == 라이벌_ID
		CONTINUE
	대상 = EVENT_CHARA:LOCAL
	대상NO = NO:대상
	SIF !IS_FIXED_CHARA(대상)
		CONTINUE
	EVENT_NUM = KOJO_DAILY_EVENT_NUM:대상NO:데일리_통상파생

	FOR LOCAL:1, 0, EVENT_NUM
		NAME_ENG '= KOJO_DAILY_EVENT_NAME_ENG:대상NO:데일리_통상파생:(LOCAL:1)
		NAME_JAP '= KOJO_DAILY_EVENT_NAME:대상NO:데일리_통상파생:(LOCAL:1	)

		RESULT = 0
		TRYCCALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_DISABLE(대상)
			SIF RESULT == 1
				CONTINUE

			;방랑중의 거점 페이즈데이리는 특수한 함수가 필요
			IF !CFLAG:MASTER:소속
				RESULT = 0
				TRYCALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_ALLOW_WHEN_WANDERING()
				SIF RESULT == 0
					CONTINUE
			ENDIF


			TRYCALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_DECISION(대상)
			SIF RESULT == 0
				CONTINUE

			VARSET DAILY_TARGET, -1
			VARSET DAILY_TARGET_NUM
			RESULT = 1
			TRYCALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_SETTARGET(대상)
			SIF RESULT == 0
				CONTINUE

			RESULTS = 
			TRYCALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_NAME(대상)
			IF RESULTS == ""
				CALL KOJO_DAILY_START_DERIVATION(NAME_JAP)
			ELSE
				CALL KOJO_DAILY_START_DERIVATION(RESULTS)
			ENDIF

			CALLFORM KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%(대상)
			SIF DAILY_CANCEL
				RETURN
		CATCH
			DEBUGPRINTFORML KOJO_DAILY_K{대상NO}_DERIVATION_%NAME_ENG%_DECISION 발견되지 않고
		ENDCATCH
	NEXT
NEXT
